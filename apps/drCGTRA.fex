-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/26/04    JOY                Added/Changed Norecords logic
-*12/14/04   DEB - CHANGED SORT SO LEVEL1 WOULD PRINT ON EVERY LINE
-*                 AND RENAMED LEVEL1 TO EMPLOYEE ID - PER PATTI
-*12/21/04   DEB   EXCLUDED ADDTL DOD_TYPES NE TRADITIONAL BOOKED TKT
-*7/1/05    DEB  - ADDED CODE FOR SUMMARIZED VERSION FOR SETFILE CGNONCSM
-* 10/11/05 DEB - EXCLUDED DOD_TYPE H FROM EXTRACT
-* ADDED TRANTYPE INCLUDE - JK 12/18/14 STORY ID S-06492
-*01/19/16 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*  09/20/2016  REJ  S-24432 ER5 Testing updates.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.
-* 09/21/2017 CA S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-*                       Backed out changes for S-36530 
-*

-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';
 
 
-SET &&DTSTY = 'DT';
 
-SET &&FIELD = &ROW_LABEL;
-SET &&IDLEVELBREAK = &PARM2;
 
 
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
EX RPTRESET
-RUN
 
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX SETDBLS SETFILE=&&SETF
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DRD1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-INCLUDE GetForDrillDown
-:DRD1
-RUN
 
-INCLUDE OTHPARMS
EX CNTLUPD
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&RPT_TTL1='            TRADITIONALLY BOOKED TICKET DETAIL';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKT,CNT';
-SET &&TICOUNT = 'TKT_ISS AS TKT,ISS';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-IF &&FROMAVRP EQ 'Y' GOTO :DT1;
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-GOTO :DT1_CONT;
-:DT1
-SET &FMM=EDIT(&&FROMDT,'$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'9999');
-SET &TOM=EDIT(&&TODT,'$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'9999');
-:DT1_CONT
 
TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN
 
 
-READ SAVE &LEVEL.2.
 
-IF &&IDLEVELBREAK EQ ' ' GOTO LEV1;
 
-SET &&LCHK = 'WHERE LEVEL_DESC EQ ''&&FIELD.EVAL''';
 
 
-GOTO ARNDLEV1;
 
-LEV1;
-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &&LCHK='WHERE ' | &AROLL_DSC.EVAL | ' CONTAINS ' | '''&&FIELD.EVAL''';
 
-ARNDLEV1;
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DOD1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-RUN
-:DOD1
 
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
 -RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX AIRUSE
-RUN
 
-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
 
-INCLUDE IDLEVROLLDEF
-RUN
 
DEFINE FILE &&EXTRACT
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC1/A80 = &&LDESC;
 
  MXADV/A8  = IF (ADV_PURCH LT 7) THEN '00 - 06' ELSE
                 '7+ DAYS';
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
 NEW_ADV_PURCH/D9    = ADV_PURCH;
 TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
 
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  XPSNGR_NM/A15        =
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
-* DEFINES TO PREVIOUS DEFINES
DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
-INCLUDE TRANTYPE
-INCLUDE IDLEVDEFINE
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT
  VAL_AIR.VAL_AIRLINE AS 'AIRLINE'
  DISSAVINGS
  FARE_PAID
  MXADV
  NET_TKT_CNT
  NEW_ADV_PURCH
  REFUSAL_DESC
  TRN_DATE
  TKT_TYPE
  XDPT_DT
  XTRAN_DT
  XPSNGR_NM
  TKT_NUM
  TKT_SORT
  SEG_COUNT
  SEG_NBR
  SEG_LOWEST
  LEVEL1
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
 WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
 WHERE DOD_TYPE NE 'A' OR 'I' OR 'J' OR 'L' OR 'M' OR 'N' OR 'X' OR 'S' OR 'H'
-*  WHERE CITYPAIR.INTL_DOM EQ 'D'
 WHERE AIR_MAIN.INTL_DOM EQ 'D'
&&WHERE1
&&WHERE2
&&WHERE3
 
&&VOIDCHK
&&LCHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT1'
BY XPSNGR_NM
ON TABLE HOLD AS FIRSTHLD
END
-RUN
 
 
TABLE FILE RPTHOLD
SUM  FARE_PAID
     NET_TKT_CNT
BY XPSNGR_NM
ON TABLE HOLD AS PAXHLD
END
-RUN
 
MATCH FILE PAXHLD
PRINT FARE_PAID
      NET_TKT_CNT
BY XPSNGR_NM
ON TABLE HOLD
RUN
FILE FIRSTHLD
PRINT TTKT1
BY XPSNGR_NM
AFTER MATCH HOLD AS PXHLD OLD-OR-NEW
END
-RUN
 
TABLE FILE PXHLD
SUM FARE_PAID
    NET_TKT_CNT
    TTKT1
BY XPSNGR_NM
ON TABLE HOLD AS PXHLD1
END
-RUN
 
 
TABLE FILE PXHLD1
SUM FARE_PAID
    NET_TKT_CNT
    TTKT1
RANKED BY HIGHEST TTKT1 NOPRINT
BY XPSNGR_NM
ON TABLE HOLD AS PXHLD2
END
-RUN
 
DEFINE FILE PXHLD2
 RANK_LIMIT/I5 = 999;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 LIST/D6 = IF XPSNGR_NM NE LAST XPSNGR_NM THEN (LAST LIST + 1)
    ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999');
 RANK_VALUE/A6 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE FRANK;
 
 
END
-RUN
 
TABLE FILE PXHLD2
SUM FARE_PAID
    NET_TKT_CNT
    TTKT1
BY RANK_VALUE
BY XPSNGR_NM
ON TABLE HOLD AS PXHLD3
END
-RUN
 
 
 
TABLE FILE RPTHOLD
PRINT XDPT_DT
      XTRAN_DT
BY XPSNGR_NM
BY LEVEL1
BY TKT_NUM
BY SEG_COUNT
BY TKT_TYPE
BY TKT_SORT
BY NEW_ADV_PURCH
BY AIRLINE
BY REFUSAL_DESC
WHERE SEG_NBR EQ '01'
ON TABLE HOLD AS XHLD
END
-RUN
 
TABLE FILE XHLD
SUM XDPT_DT
    XTRAN_DT
BY XPSNGR_NM
BY LEVEL1
BY TKT_NUM
BY SEG_COUNT
BY TKT_TYPE
BY TKT_SORT
BY NEW_ADV_PURCH
BY AIRLINE
BY REFUSAL_DESC
ON TABLE HOLD AS XHLD1
END
-RUN
 
TABLE FILE RPTHOLD
SUM FARE_PAID AS 'FARE'
    NET_TKT_CNT AS 'TKT'
    DISSAVINGS AS 'LOST'
    SEG_LOWEST AS 'LOW'
BY XPSNGR_NM
BY LEVEL1
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS XAMT
END
-RUN
 
MATCH FILE XAMT
SUM FARE
    LOST
    LOW
    TKT
BY XPSNGR_NM
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD
RUN
FILE XHLD1
PRINT XDPT_DT
      XTRAN_DT
      TKT_TYPE
      LEVEL1
      TKT_SORT
      AIRLINE
      REFUSAL_DESC
      NEW_ADV_PURCH
BY XPSNGR_NM
BY TKT_NUM
BY SEG_COUNT
AFTER MATCH HOLD AS XAMT1 OLD
END
-RUN
 
TABLE FILE XAMT1
SUM FARE
    LOST
    LOW
    TKT
BY XPSNGR_NM
BY LEVEL1
BY TKT_NUM
BY SEG_COUNT
BY XDPT_DT
BY XTRAN_DT
BY TKT_SORT
BY TKT_TYPE
BY NEW_ADV_PURCH
BY AIRLINE
BY REFUSAL_DESC
ON TABLE HOLD AS XAMT2
END
-RUN
 
 
MATCH FILE PXHLD3
SUM   RANK_VALUE
      TTKT1
BY XPSNGR_NM
ON TABLE HOLD
RUN
FILE XAMT2
PRINT FARE
    LOST
    LOW
    LEVEL1
    SEG_COUNT
    TKT
    XDPT_DT
    XTRAN_DT
    TKT_NUM
    TKT_TYPE
    TKT_SORT
    AIRLINE
    REFUSAL_DESC
    NEW_ADV_PURCH
BY XPSNGR_NM
AFTER MATCH HOLD AS PXHLD5 OLD-AND-NEW
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
JOIN ROLL WITH XPSNGR_NM IN PXHLD5 TO ROLLUP_CODE IN ROLLUP AS J3
-RUN
 
DEFINE FILE PXHLD5
NOWTOD/A8 WITH TKT = HHMMSS (NOWTOD);
ROLL/A8='&&ROLLUP.EVAL';
  LVL/A60='&&LVL_DSC.EVAL';
  RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
  RD/A10 = 'Run Date:';
  RT/A10 = 'Run Time:';
-*   LEV/A25 = EDIT(LEVEL_DESC, '9999999999999999999999999');
  RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
  RN1/A60=TRIM('B', '&&FIELD.EVAL', 60, ' ', 1, 'A60');
  TDATE1/A8MDYY='&MDYY';
  TDATE2/A10=EDIT(TDATE1,'99/99/9999');
  TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
END
-RUN
 
-IF &&SETF EQ 'CGNONCSM' THEN GOTO SUMMARY;
 
TABLE FILE PXHLD5
HEADING
"<+0> Client:<ROLL_NAME"
"<+0> Contact:<ROLL_CONTACT"
"<+0> Phone:<RPHONE "
"<+0> &&DTDESC <+0> &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"<+0> <RN"
"<+0> <RN1"
" "
-*
SUM
LEVEL1 AS 'EMPLOYEE,ID'
TKT AS 'COUNT'
FARE/D12.2CSM AS 'FARE,PAID'
LOW/D12.2CSM AS 'LOW,FARE'
LOST/D12.2CSM AS 'LOST,SVGS'
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY RANK_VALUE NOPRINT
BY XPSNGR_NM AS 'PASSENGER,NAME'
BY LEVEL1 NOPRINT
BY TKT_NUM AS 'TKT,NUM'
BY TKT_SORT NOPRINT
BY TKT_TYPE AS 'TKT,TYPE'
BY XTRAN_DT AS 'TRN,DTE'
BY XDPT_DT AS 'DPT,DTE'
BY AIRLINE AS 'CR'
BY REFUSAL_DESC AS 'REASON'
 
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON XPSNGR_NM RECOMPUTE MULTILINES AS 'TOTAL FOR'
-*
-INCLUDE FOOTERDT
-*
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
 
-GOTO CONTINUE
 
-SUMMARY
 
 
TABLE FILE PXHLD5
HEADING
"<+0> Client:<ROLL_NAME"
"<+0> Contact:<ROLL_CONTACT"
"<+0> Phone:<RPHONE "
"<+0> &&DTDESC <+0> &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"<+0> <RN"
"<+0> <RN1"
" "
-*
SUM
LEVEL1 AS 'EMPLOYEE,ID'
TKT AS 'COUNT'
FARE/D12.2CSM AS 'FARE,PAID'
 
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY RANK_VALUE NOPRINT
BY XPSNGR_NM AS 'PASSENGER,NAME'
 
 
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON XPSNGR_NM RECOMPUTE MULTILINES AS 'TOTAL FOR'
-*
-INCLUDE FOOTERDT
-*
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
 
 
-CONTINUE
 
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.030000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.030000, BOTTOMMARGIN=0.010000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
 
TYPE=REPORT, FOCEXEC=NONE, FONT=ARIAL, SIZE=8, COLOR=BLACK,
    BACKCOLOR=NONE, GRID=OFF, STYLE=NORMAL, $
 
TYPE=TITLE,
     JUSTIFY=LEFT,
     STYLE=BOLD+UNDERLINE,
     FONT=ARIAL,
     COLOR=RGB(5 76 149),
$
 
TYPE=HEADING, SIZE=10, GRID=OFF, STYLE=BOLD, $
 
TYPE=HEADING, HEADALIGN=BODY,$
TYPE=HEADING, LINE=1, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=1, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=4, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=4, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=5, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=5, ITEM=2, COLSPAN=10, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=6, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=6, ITEM=2, COLSPAN=10, JUSTIFY=CENTER, $
 
 
-* TYPE=REPORT, COLUMN=N5, WRAP=.5, $
-* TYPE=REPORT, COLUMN=N8, WRAP=.5, $
-* TYPE=REPORT, COLUMN=N11, WRAP=.5,$
 
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=8,   STYLE=BOLD, $
 
 
TYPE=FOOTING, FONT=ARIAL, SIZE=7, STYLE=BOLD,$
TYPE=REPORT, IMAGE=TSMHEAD.jpg, POSITION=(0 0), $
TYPE=REPORT, IMAGE=FOOTER.jpg, POSITION=(0 8.2), $
 
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;
 
 
-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT
-GOTO NEXT
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
 
-NEXT
