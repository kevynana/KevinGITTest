
-*1/28/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*Replaced 0-6/7-13 target fields with  0-6 AND 7-13 ATPS story id: S06692 - 12/17/14 - JK
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-* Story ID S-10086 Added &&overdrill flag logic for use in 5.0 to choose to for the report to be drillable or non-drillable - 9/14/15 - JM
-*4/25/2017-JEM-S-33318-updated logic for no records report


-**********************************************************************
-* Air
-**********************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON
SET SPACE = 1
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');
-RUN


-*********************************************************************************
-* Gather/Add Air Info to BZADV File
-*********************************************************************************

DEFINE FILE SR_50
  CHG_CD/A1 = EDIT(LEVEL1, '9');
  CHG_CAT/A10 = IF CHG_CD EQ 'B' THEN 'B' ELSE
                IF CHG_CD EQ 'G' THEN 'G' ELSE
                IF CHG_CD EQ 'M' THEN 'M' ELSE 'NON-BMG';
  CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
  PRIOR/A1 = IF TRN_DATE GE &&FXDATE.EVAL AND TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
  CGADV/A8 = IF (ADV_PURCH LT 7) THEN '0-6' ELSE 
             IF (ADV_PURCH GE 7) AND (ADV_PURCH LE 13) THEN '7-13' ELSE '14+';
-*              IF (ADV_PURCH GT 13) AND (ADV_PURCH LE 20) THEN '14-20' ELSE
-*                 '21+';  
  
  BRK_LVL/I2 = &&BRK_LVL;
  LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
  FARE_PAID/P12.2 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/P12    = IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (FARE_PAID GE 0) AND
                          (SEG_COUNT EQ 1) AND 
                          (CONJ_REL EQ 1) THEN 1 ELSE 
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND 
                          (EX_FLAG EQ 'F') THEN (-1) ELSE 
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

  SAVINGS/P12.2     = SEG_STANDARD - FARE_PAID;
  NEW_SEG_MILES/D12 = SEG_MILES;
 -* Defines on Previous Defines
  DISSAVINGS/P12.2 = IF EMBARK EQ 'DT1' THEN 0 ELSE
                   FARE_PAID-SEG_LOWEST;
END
-RUN


TABLEF FILE SR_50
PRINT CGADV
  AROLL_DSC6
  CHG_CAT
  CURRENT
  PRIOR
  DISSAVINGS
  DOD_TYPE
  AIR_MAIN.INTL_DOM AS 'INTDOM'
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR 	
  TKT_NUM
  TRN_DATE
WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)
WHERE VOID_DATE EQ 0
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN

TABLE FILE RPTHOLD
PRINT 
  CGADV
  CHG_CAT
  DISSAVINGS
  DOD_TYPE
  INTDOM
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR 	
  TKT_NUM
  TRN_DATE
BY CURRENT
BY PRIOR
BY LEVEL_DESC
BY CHG_CAT
BY AROLL_DSC6
ON TABLE HOLD AS TRANONE 
END
-RUN


-IF &LINES EQ 0 THEN GOTO NOREC;

-SET &&RPTSUF = 'BZAPSM';
-INCLUDE FDEFRPTS
-RUN


DEFINE FILE TRANONE
CAVOL/P12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
CMILES/P12 = IF CURRENT EQ 'Y' THEN NEW_SEG_MILES ELSE 0;
PAVOL/P12.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CNTKT/P12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CNTKTD/P12 = IF CURRENT EQ 'Y' AND INTDOM EQ 'D' THEN NET_TKT_CNT ELSE 0;
PNTKT/P12 = IF PRIOR EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CLSAV/P12.2 = IF CURRENT EQ 'Y' THEN DISSAVINGS ELSE 0;
CADVT/P12 = IF (CURRENT EQ 'Y') AND  (CGADV EQ '0-6') THEN NET_TKT_CNT ELSE 0;
CADVT1/P12 = IF (CURRENT EQ 'Y') AND (CGADV EQ '7-13') THEN NET_TKT_CNT ELSE 0;
CADVA/P12.2 = IF (CURRENT EQ 'Y') AND (CGADV EQ '0-6') THEN CAVOL ELSE 0;
CADVA1/P12.2 = IF (CURRENT EQ 'Y') AND (CGADV EQ '7-13') THEN CAVOL ELSE 0;
CONL/P12 = IF (CURRENT EQ 'Y') AND (ONLINE_TRADITIONAL EQ 'ONLINE') AND (INTDOM EQ 'D') THEN NET_TKT_CNT ELSE 0;
CNR/P12 = IF (CURRENT EQ 'Y') AND (NONREF_FLAG EQ 'N') THEN NET_TKT_CNT ELSE 0;
                
END
-RUN


TABLE FILE TRANONE
SUM CADVA
    CADVA1
    CADVT
    CADVT1
    CAVOL
    CLSAV
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL   
BY LEVEL_DESC
BY CHG_CAT
BY AROLL_DSC6
ON TABLE HOLD AS TRANTWOA
END
-RUN

TABLE FILE TRANTWOA
SUM CAVOL AS 'TVOL'
    CNTKT AS 'TTKT'
BY LEVEL_DESC 
ON TABLE HOLD AS THLD
END
-RUN

MATCH FILE TRANTWOA
PRINT CADVA
    CADVA1
    CADVT
    CADVT1
    CAVOL
    CLSAV
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL 
    CHG_CAT 
    AROLL_DSC6
BY LEVEL_DESC
ON TABLE HOLD
RUN 
FILE THLD
SUM TVOL
    TTKT
BY LEVEL_DESC
AFTER MATCH HOLD AS TRANTWO OLD
END

TABLE FILE TRANTWO
PRINT CAVOL CMILES CNTKT PNTKT CLSAV PAVOL CADVA CADVA1 CADVT CADVT1 CONL CNTKTD CNR CHG_CAT AROLL_DSC6 TVOL TTKT
RANKED BY HIGHEST TVOL NOPRINT
BY LEVEL_DESC
ON TABLE HOLD AS RNK1
END
-RUN

-INCLUDE OTHPARMS
-RUN
 
DEFINE FILE RNK1
 RANK_LIMIT/I2 = 9999;
-*  RANK_LIMIT/I2 = &&RANK_LIMIT;
 RLIMIT/A2    = EDIT(RANK_LIMIT) ;
 CRANK/D5     = RANK;
 BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
 LIST/D6 = IF LEVEL_DESC NE LAST LEVEL_DESC THEN (LAST LIST + 1) 
           ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999'); 
 RANK_VALUE/A9 = IF RANK_LIMIT EQ 0 THEN BRANK ELSE
                 IF RANK GT RANK_LIMIT THEN 'ALL OTHER' ELSE BRANK;
 LVL_PRINT/A25 =IF RANK_VALUE LT RLIMIT THEN LEVEL_DESC ELSE 'ALL OTHER';
END
-RUN

TABLE FILE RNK1
SUM CAVOL CMILES CNTKT PNTKT CLSAV PAVOL CADVA CADVA1 CADVT CADVT1 CONL CNTKTD CNR TVOL TTKT
COMPUTE CAVGT/P12CM = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE CAVOL/CNTKT;  
COMPUTE CADVP1/P4% = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT/CNTKT)*100); 
COMPUTE CADVP2/P4% = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT1/CNTKT)*100); 
COMPUTE CADVAT/P12.2 = IF CADVT EQ 0 THEN 0 ELSE CADVA/CADVT;  
COMPUTE CADVAT1/P12.2 = IF CADVT1 EQ 0 THEN 0 ELSE CADVA1/CADVT1; 
COMPUTE CADVSV1/P12.2 = IF CADVAT1 EQ 0 THEN 0 ELSE (CADVAT - CADVAT1); 
COMPUTE CADVSV/P12.2 = IF CADVSV1 LT 0 THEN 0 ELSE CADVSV1; 
COMPUTE CADVT25/P12 = CADVT *.25; 
COMPUTE CADVT50/P12 = CADVT *.50; 
COMPUTE APS50/P12CM = CADVSV * CADVT50; 
COMPUTE APS25/P12CM = CADVSV * CADVT25; 
BY RANK_VALUE
BY LVL_PRINT
BY CHG_CAT
BY AROLL_DSC6
ON TABLE HOLD AS RNK2
END
-RUN


JOIN ROLL WITH RANK_VALUE IN RNK2 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN

DEFINE FILE RNK2
ROLL/A8='&&ROLLUP.EVAL';
  RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
  RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
  RN1/A60=TRIM('B', '&&RPT_TTL2.EVAL', 60, ' ', 1, 'A60');
  TDATE1/A8MDYY='&MDYY';
  TDATE2/A10=EDIT(TDATE1,'99/99/9999');
  TIME1/A8=HHMMSS('A8');
  TIME2/A8=EDIT(TIME1,'99$:99$:99');
  RD/A10 = 'Run Date:';
  RT/A10 = 'Run Time:';
  DTDESC/A20='Transaction Dates';
  CUR/A15='Current Period:';
  PRI/A15='Prior Period:';
  SPACE/A1 = 'A';
  KEY/A1 = IF RANK_VALUE EQ 'TOTAL' THEN 'B' ELSE 'A';
  LV/A2 = EDIT(LVL_PRINT, '99');
  NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
  TARGET/A5 = '     ';  
END
-RUN


TABLE FILE RNK2
HEADING 
-IF '&&OUTFMT.EVAL' EQ 'PDF' THEN GOTO HEAD2;    

"Client: <6 <ROLL_NAME "
"Contact: <6 <ROLL_CONTACT"
"Phone: <6 <RPHONE "
" "
"<CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
" <PRI       &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
" "
"<RN "
"<RN1 "

-GOTO SkipHead

-HEAD2


"Client: <6 <ROLL_NAME <42 <RN <90 <DTDESC"
"Contact: <6 <ROLL_CONTACT <40 <RN1 <90 <CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"Phone: <6 <RPHONE <90 <PRI       &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
" "
-SkipHead
SUM CAVOL/P12.2M AS 'Total,Volume' 
  PCT.CAVOL/P4% AS '% of,Total,Volume'
  CNTKT AS 'Net,Tickets'
  CLSAV/P12CM AS 'Total,Lost,Savings'
  CAVGT/P12CM AS  'Avg,Ticket,Price'  
  CADVP1 AS '% Of,Net Tickets,Purchased,0-6 Days' 
  CADVAT AS '0-6 Days,Avg Ticket,Price' 
  CADVP2 AS '% of,Net Tickets,Purchased,7-13 Days' 
  CADVAT1 AS '7-13 Days,Avg Ticket,Price'
CADVAT NOPRINT 
CADVAT1 NOPRINT 
CADVSV1 NOPRINT
CADVSV NOPRINT
CADVT25 NOPRINT
CADVT50 NOPRINT
APS50 AS 'AP Shift Savings,based on,50% shift'
APS25 AS 'AP Shift Savings,based on,25% shift'
BY RANK_VALUE NOPRINT
BY LVL_PRINT AS 'Hierarchy'
BY CHG_CAT AS 'Charge Code Type'
BY AROLL_DSC6 AS 'Employee Level'
ON LVL_PRINT RECOMPUTE CAVOL CNTKT CLSAV APS50 APS25 AS 'TOTAL FOR'
ON TABLE SET PAGE-NUM OFF
&&OUTLINE1
&&OUTLINE2
FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
"</1"
"<15 &M1.EVAL <45 &M2.EVAL <75 &M3.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.050000, RIGHTMARGIN=0.050000,
    TOPMARGIN=0.00000, BOTTOMMARGIN=0.050000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $ 


-IF '&&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO NOB;

TYPE=REPORT, FONT='ARIAL', SIZE=7, STYLE=NORMAL, BACKCOLOR=NONE, JUSTIFY=LEFT, $


TYPE=DATA,
     GRID=OFF,
     SIZE=7,
     STYLE=NORMAL,
     BACKCOLOR=('WHITE' RGB(239 239 239)),
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR=RGB(5 76 149),
     BORDER-BOTTOM-COLOR=RGB(5 76 149),
     BORDER-LEFT-COLOR=RGB(5 76 149),
     BORDER-RIGHT-COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
     WHEN=N1 NE 'ZZ',
$
-NOB 

-IF &&OVERDRILL EQ 'Y' GOTO OVERDRILL;

TYPE=DATA, COLUMN=N5, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drBZapsm' ROW_LABEL=N1 LSEL=LVL_PRINT CHG_CAT=N3 EMP_LVL=N4 PARM1='A'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'),  
$
TYPE=DATA, COLUMN=N7, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drBZapsm' ROW_LABEL=N1 LSEL=LVL_PRINT CHG_CAT=N3 EMP_LVL=N4 PARM1='N'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'),  
$ 
TYPE=DATA, COLUMN=N8, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drBZapsm' ROW_LABEL=N1 LSEL=LVL_PRINT CHG_CAT=N3 EMP_LVL=N4 PARM1='L'     \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'),  
$
TYPE=DATA, COLUMN=N9, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drBZapsm' ROW_LABEL=N1 LSEL=LVL_PRINT CHG_CAT=N3 EMP_LVL=N4 PARM1='T'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'),  
$
TYPE=DATA, COLUMN=N10, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drBZapsm' ROW_LABEL=N1 LSEL=LVL_PRINT CHG_CAT=N3 EMP_LVL=N4 PARM1='6'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'),  
$ 
TYPE=DATA, COLUMN=N12, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drBZapsm' ROW_LABEL=N1 LSEL=LVL_PRINT CHG_CAT=N3 EMP_LVL=N4 PARM1='7'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'),  
$ 

-OVERDRILL;

-IF '&&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO NOB2;

TYPE=HEADING,
     LINE=1,
     OBJECT=TEXT,
     ITEM=1,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=1,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=1,
     OBJECT=FIELD,
     ITEM=2,
     SIZE=10,
     JUSTIFY=CENTER,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD+ITALIC,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=3,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=4,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=2,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     STYLE=BOLD,
     FONT='TIMES NEW ROMAN',
$

TYPE=HEADING,
     LINE=2,
     OBJECT=FIELD,
     ITEM=2,
     SIZE=10,
     JUSTIFY=CENTER,
     COLOR='WHITE',
     STYLE=BOLD+ITALIC,
     FONT='TIMES NEW ROMAN',

$
TYPE=HEADING,
     LINE=3,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     STYLE=BOLD,
     FONT='TIMES NEW ROMAN',
$

TYPE=TITLE,
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
     GRID=ON,
     SIZE=7,
     COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
$
-GOTO PDFFMT

-NOB2
TYPE=REPORT, FONT='ARIAL', SIZE=8, STYLE=NORMAL, BACKCOLOR=NONE, JUSTIFY=LEFT, $

TYPE=TITLE,
     FONT='ARIAL',
     STYLE=BOLD,
     GRID=OFF,
     SIZE=8,
     BACKCOLOR=RGB(170 170 170),
     JUSTIFY=LEFT,
$
TYPE=REPORT, WRAP=1, JUSTIFY=LEFT, $
TYPE=REPORT, WRAP=2, JUSTIFY=LEFT, COLUMN=N2, $
TYPE=REPORT, WRAP=.8, JUSTIFY=LEFT, COLUMN=N3, $
TYPE=REPORT, WRAP=.8, JUSTIFY=LEFT, COLUMN=N4, $
-* TYPE=DATA, COLUMN=N2, WRAP=2, JUSTIFY=LEFT, $
TYPE=DATA, BACKCOLOR=NONE, GRID=OFF, SIZE=8,STYLE=BOLD, JUSTIFY=LEFT, WHEN=N1 EQ 'ZZ',$
TYPE=FOOTING, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=BOLD, COLOR='WHITE', $
TYPE=HEADING, SIZE=10, FONT=ARIAL, STYLE=BOLD, $
TYPE=SUBTOTAL, STYLE=BOLD, SIZE=8, $
TYPE=GRANDTOTAL, STYLE=BOLD, SIZE=8, $
-GOTO XSTY

-PDFFMT

TYPE=DATA, BACKCOLOR=NONE, GRID=OFF, SIZE=8,STYLE=BOLD, FONT='TIMES NEW ROMAN', JUSTIFY=LEFT, WHEN=N1 EQ 'ZZ',$
TYPE=SUBTOTAL, STYLE=BOLD, $
TYPE=FOOTING, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=BOLD, COLOR='WHITE', $
TYPE=REPORT, IMAGE=TSMHEAD.jpg, POSITION=(0 0), $
TYPE=REPORT, IMAGE=FOOTER.jpg, POSITION=(0 8.2), $ 
-* TYPE=REPORT,GRID=OFF, $
-XSTY
ENDSTYLE

END
-RUN
 

SET SPACE = 2

-GOTO XXIT;



-NOREC
-IF &&FROMAVRP NE 'Y' GOTO :AVSF2;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF2

-INCLUDE NORECRPT
-RUN
-EXIT


-XXIT

