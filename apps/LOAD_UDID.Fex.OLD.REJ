-************************************************************************
-*  PROGRAM : LOAD_UDID.FEX                           
-*  Fucntions: This program is used to load UDID data to eTTekReview
-*  from AS400 files. Since we use a single UDID database, this program
-*  will be called from both Quartered and Non-Quartered load process.
-*
-*  Author: Lusheng Si
-*  Date:   09/12/2005
-************************************************************************
-*-SET &ECHO = ALL;
SET ASNAME = ON
USE CLEAR *
-**********************************************
-* Step 1:  Define all files required 
-**********************************************
-SET &FA0=&&AS400_PATH || 'TTPUDFA0.TXT';
-SET &AA0=&&AS400_PATH || 'TTPUDAA0.TXT';
-SET &CA0=&&AS400_PATH || 'TTPUDCA0.TXT';
-SET &HA0=&&AS400_PATH || 'TTPUDHA0.TXT';

FI TTPUDFA0 DISK &FA0
FI TTPUDAA0 DISK &AA0
FI TTPUDCA0 DISK &CA0
FI TTPUDHA0 DISK &HA0

-SET &AIR_UDB  = 'UA' || '&&COMP.EVAL';
-SET &CAR_UDB  = 'UC' || '&&COMP.EVAL';
-SET &HTL_UDB  = 'UH' || '&&COMP.EVAL';
-SET &UDF_UDB   = 'UF' || '&&COMP.EVAL';

-SET &AIRUDB  = &&COM_PATH || &AIR_UDB || '.FOC';
-SET &CARUDB  = &&COM_PATH || &CAR_UDB || '.FOC';
-SET &HTLUDB  = &&COM_PATH || &HTL_UDB || '.FOC';
-SET &UDFUDB  = &&COM_PATH || &UDF_UDB  || '.FOC';

-*Check if the UDID files exist.
-SET &AIRUDB = '&AIRUDB.EVAL';
-RUN


-DOS STATE &AIRUDB
-IF &RETCODE EQ 0 GOTO NOSETUP;
-RUN
EX setupUDF
-RUN
-NOSETUP

USE ADD
  &AIRUDB
  &CARUDB
  &HTLUDB
  &UDFUDB
END

-**********************************************
-* Step 2:  Retrieve data from TTPUDFA0 
-**********************************************

TABLE FILE TTPUDFA0
PRINT UDID UDTYPE UD_DESC UD_GROUP UD_COLH
WHERE ROLLUP_CODE EQ  '&&RUP.EVAL'
ON TABLE SAVE
END
-RUN


-IF &LINES EQ 0 THEN GOTO ChkAirUdid;

MODIFY FILE &UDF_UDB
FIXFORM UDID/3 UDIDTYPE/1 UD_DESC/40 UD_GROUP/3 UD_COLH/A20
MATCH UDID 
  ON MATCH UPDATE UDIDTYPE UD_DESC UD_GROUP UD_COLH
  ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


-ChkAirUdid
-* AIR DATABASE
DEFINE FILE TTPUDAA0
  UDIDTYPE/A1=IF AUD_DATA EQ ' ' THEN 'N' ELSE 'A';   
  OLD_SR/A1 = SUBSTR(16,NUD_DATA,16,16,1,'A1');
  NEW_SR/A1 = DECODE OLD_SR (0 0 1 1 2 2 3 3 4 4 5 5 6 6 7 7 8 8 9 9
                               } 0 J 1 K 2 L 3 M 4 N 5 O 6 P 7 Q 8 R 9);  
  NDATA/D16.8 = IF UDIDTYPE EQ 'A' OR NUD_DATA EQ '0000000000000000' THEN 0 
     ELSE IF OLD_SR GT '9' THEN
     -1 * EDIT(SUBSTR(16,NUD_DATA,2,8,7,'A7')|'.'| SUBSTR(16,NUD_DATA,9,15,7,'A7')|NEW_SR)
     ELSE EDIT(SUBSTR(16,NUD_DATA,2,8,7,'A7')|'.'| SUBSTR(16,NUD_DATA,9,15,7,'A7')|NEW_SR);     
END

TABLE FILE TTPUDAA0
PRINT AIR_KEY UDID AUD_DATA NDATA AS 'NUD_DATA'
BY AIR_KEY NOPRINT
WHERE ROLLUP_CODE EQ  '&&RUP.EVAL'
ON TABLE SAVE
END
-RUN
 

-IF &LINES EQ 0 THEN GOTO ChkCarUdid;

MODIFY FILE &AIR_UDB
FIXFORM AIR_KEY/45 UDID/3 AUD_DATA/64 NUD_DATA/16
MATCH AIR_KEY
  ON NOMATCH INCLUDE
  ON MATCH CONTINUE
  MATCH UDID
    ON NOMATCH INCLUDE
    ON MATCH UPDATE AUD_DATA NUD_DATA
DATA ON SAVE
END
-RUN

-ChkCarUdid
-* CAR DATABASE
DEFINE FILE TTPUDCA0
  UDIDTYPE/A1=IF AUD_DATA EQ ' ' THEN 'N' ELSE 'A';   
  OLD_SR/A1 = SUBSTR(16,NUD_DATA,16,16,1,'A1');
  NEW_SR/A1 = DECODE OLD_SR (0 0 1 1 2 2 3 3 4 4 5 5 6 6 7 7 8 8 9 9
                               } 0 J 1 K 2 L 3 M 4 N 5 O 6 P 7 Q 8 R 9);  
  NDATA/D16.8 = IF UDIDTYPE EQ 'A' OR NUD_DATA EQ '0000000000000000' THEN 0 
     ELSE IF OLD_SR GT '9' THEN
     -1 * EDIT(SUBSTR(16,NUD_DATA,2,8,7,'A7')|'.'| SUBSTR(16,NUD_DATA,9,15,7,'A7')|NEW_SR)
     ELSE EDIT(SUBSTR(16,NUD_DATA,2,8,7,'A7')|'.'| SUBSTR(16,NUD_DATA,9,15,7,'A7')|NEW_SR);     
END

TABLE FILE TTPUDCA0
PRINT CAR_KEY UDID AUD_DATA NDATA AS 'NUD_DATA'
BY CAR_KEY NOPRINT
WHERE ROLLUP_CODE EQ  '&&RUP.EVAL'
ON TABLE SAVE
END
-RUN
-IF &LINES EQ 0 THEN GOTO ChkHtlUdid;

MODIFY FILE &CAR_UDB
FIXFORM CAR_KEY/82 UDID/3 AUD_DATA/64 NUD_DATA/16
MATCH CAR_KEY
  ON NOMATCH INCLUDE
  ON MATCH CONTINUE
  MATCH UDID
    ON NOMATCH INCLUDE
    ON MATCH UPDATE AUD_DATA NUD_DATA
DATA ON SAVE
END
-RUN


-ChkHtlUdid
-* HOTEL DATABASE
DEFINE FILE TTPUDHA0
  UDIDTYPE/A1=IF AUD_DATA EQ ' ' THEN 'N' ELSE 'A';   
  OLD_SR/A1 = SUBSTR(16,NUD_DATA,16,16,1,'A1');
  NEW_SR/A1 = DECODE OLD_SR (0 0 1 1 2 2 3 3 4 4 5 5 6 6 7 7 8 8 9 9
                               } 0 J 1 K 2 L 3 M 4 N 5 O 6 P 7 Q 8 R 9);  
  NDATA/D16.8 = IF UDIDTYPE EQ 'A' OR NUD_DATA EQ '0000000000000000' THEN 0 
     ELSE IF OLD_SR GT '9' THEN
     -1 * EDIT(SUBSTR(16,NUD_DATA,2,8,7,'A7')|'.'| SUBSTR(16,NUD_DATA,9,15,7,'A7')|NEW_SR)
     ELSE EDIT(SUBSTR(16,NUD_DATA,2,8,7,'A7')|'.'| SUBSTR(16,NUD_DATA,9,15,7,'A7')|NEW_SR);     
END

TABLE FILE TTPUDHA0
PRINT HTL_KEY UDID AUD_DATA NDATA AS 'NUD_DATA'
BY HTL_KEY NOPRINT
WHERE ROLLUP_CODE EQ  '&&RUP.EVAL'
ON TABLE SAVE
END
-RUN
-IF &LINES EQ 0 THEN GOTO DONEUdid;

MODIFY FILE &HTL_UDB
FIXFORM HTL_KEY/69 UDID/3 AUD_DATA/64 NUD_DATA/16
MATCH HTL_KEY
  ON NOMATCH INCLUDE
  ON MATCH CONTINUE
  MATCH UDID
    ON NOMATCH INCLUDE
    ON MATCH UPDATE AUD_DATA NUD_DATA
DATA ON SAVE
END
-RUN

-DONEUdid
 
