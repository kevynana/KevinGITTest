-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File CVIEW1EX.FEX
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK

-********************************************************************
-* MODIFICAIONS:
-*SET &ECHO=ALL;
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
  LEVEL_DESC/A60 = &&LDESC;
  NBR_DAYS/P12S = NUM_DAYS;
  RENTAL_DAYS/P12S = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
 
-* ****DEFINES ON PREVIOUS DEFINES****
  MIDBOOK/P12S = IF (DAILY_RATE GE 0) AND 
                  (CAR_CAT EQ 'INTERMEDIATE' OR 'ECONOMY' OR 'COMPACT') THEN 1 
                  ELSE IF (DAILY_RATE LT 0) AND 
                  (CAR_CAT EQ 'INTERMEDIATE' OR 'ECONOMY' OR 
                  'COMPACT') THEN (-1) ELSE 0;
    
  MID_DAYS/P12S = IF (CAR_CAT EQ 'INTERMEDIATE' OR 'ECONOMY' 
                   OR 'COMPACT') THEN RENTAL_DAYS ELSE 0;
    
END
-RUN

TABLEF FILE &&EXTRACT
PRINT BOOKINGS
  CAR_CAT	
  CAR_DESC	
  CAR_KEY	
  CAR_RATE 	
  CAR_TYPE 	
  DAILY_RATE
  INVOICE_DATE 	
  LEVEL_DESC
  MIDBOOK	
  MID_DAYS
  NBR_DAYS 
  RENTAL_AMT
  RENTAL_DAYS
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL)
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS CARHOLD
END
-RUN

DEFINE FILE CARHOLD
CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE &&FXDATE.EVAL AND
               INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
END

TABLE FILE CARHOLD
PRINT BOOKINGS
  CAR_DESC	
  CAR_KEY	
  CAR_RATE 	
  CAR_TYPE 
  CURRENT	
  DAILY_RATE
  MIDBOOK	
  MID_DAYS
  NBR_DAYS 
  PRIOR
  RENTAL_AMT
  RENTAL_DAYS
BY LEVEL_DESC
ON TABLE HOLD AS CARTWO
END
-RUN

DEFINE FILE CARTWO
-* DEFINES FOR HEADINGS
CARDAY/A50 = 'Number of Car Rental Days';
CARAVG/A50 = 'Average Daily Car Rate';
CARMID/A50 = 'Midsize Usage';
CARBOOK/A50 = 'Number of Car Bookings';
FLAG/A1 = 'A';

-* DEFINES FOR CURRENT
CCDAY/D12CS = IF CURRENT EQ 'Y' THEN RENTAL_DAYS ELSE 0;
CCAMT/P12.2 = IF CURRENT EQ 'Y' THEN RENTAL_AMT ELSE 0;
CCMID/P12S = IF CURRENT EQ 'Y' THEN MID_DAYS ELSE 0;
CCBOOK/D12CS = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;

-* DEFINES FOR PRIOR
PCDAY/D12C = IF PRIOR EQ 'Y' THEN RENTAL_DAYS ELSE 0;
PCAMT/P12.2 = IF PRIOR EQ 'Y' THEN RENTAL_AMT ELSE 0;
PCMID/P12S = IF PRIOR EQ 'Y' THEN MID_DAYS ELSE 0;
PCBOOK/D12CS = IF PRIOR EQ 'Y' THEN BOOKINGS ELSE 0;
END

TABLE FILE CARTWO
SUM CCDAY AS 'TCCDAY'
    PCDAY AS 'TPCDAY'
BY LEVEL_DESC
ON TABLE HOLD AS CARMATCH
END
-RUN

TABLE FILE CARTWO
SUM CARBOOK
  CAR_DESC	
  CAR_KEY	
  CAR_RATE 	
  CAR_TYPE 
  CURRENT
  CCBOOK	
  DAILY_RATE 
  FLAG
  MIDBOOK	
  MID_DAYS
  NBR_DAYS 
  PRIOR
  RENTAL_AMT
  RENTAL_DAYS
  PCBOOK
  CARDAY
  CARAVG
  CARMID
  CCDAY
  CCAMT
  CCMID
  PCDAY
  PCAMT
  PCMID
BY LEVEL_DESC
ON TABLE HOLD AS CARMATCH1
END
-RUN

-* JOIN CLEAR *
-* -RUN

MATCH FILE CARMATCH1
PRINT CARBOOK	
  CAR_DESC	
  CAR_KEY	
  CAR_RATE 	
  CAR_TYPE 
  CURRENT
  CCBOOK
  PCBOOK	
  DAILY_RATE 
  FLAG
  MIDBOOK	
  MID_DAYS
  NBR_DAYS 
  PRIOR
  RENTAL_AMT
  RENTAL_DAYS
  CARDAY
  CARAVG
  CARMID
  CCDAY
  CCAMT
  CCMID
  PCDAY
  PCAMT
  PCMID
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE CARMATCH
SUM TCCDAY
    TPCDAY
BY LEVEL_DESC
AFTER MATCH HOLD AS CARMATCH2 OLD-OR-NEW
END
-RUN

TABLE FILE CARMATCH2
SUM CARBOOK
  CARAVG
  CARDAY	
  CAR_DESC	
  CAR_KEY
  CARMID	
  CAR_RATE 	
  CAR_TYPE
  CCBOOK
  CCAMT
  CCDAY
  CCMID
  CURRENT	
  DAILY_RATE 
  MID_DAYS
  NBR_DAYS 
  PCAMT
  PCDAY
  PCMID 
  PCBOOK
  PRIOR
  RENTAL_AMT
  RENTAL_DAYS
COMPUTE CCAVG/P12.2 = IF ((CCDAY LT 1) AND (CCDAY GT (-1))) THEN 0 ELSE
                         (CCAMT/CCDAY);
COMPUTE PCAVG/P12.2 = IF ((PCDAY LT 1) AND (PCDAY GT (-1))) THEN 0 ELSE
                         (PCAMT/PCDAY);
COMPUTE CPCTMID/P12.2S = IF ((TCCDAY LT 1) AND (TCCDAY GT (-1))) THEN 0 ELSE 
                           ((CCMID/TCCDAY)*100);
COMPUTE PPCTMID/P12.2S = IF ((TPCDAY LT 1) AND (TPCDAY GT (-1))) THEN 0 ELSE 
                           ((PCMID/TPCDAY)*100);
-* INCREASE DECREASE COMPUTE
COMPUTE IDCDAY/P12.2 = IF ((PCDAY LT 1) AND (PCDAY GT (-1))) THEN 0 ELSE
                         (((CCDAY-PCDAY)/PCDAY)*100);
COMPUTE IDCAVG/P12.2 = IF ((PCAVG LT 1) AND (PCAVG GT (-1))) THEN 0 ELSE
                         (((CCAVG-PCAVG)/PCAVG)*100);
COMPUTE IDCPCT/P12.2S = (CPCTMID-PPCTMID);

COMPUTE IDCBOOK/P12.2 = IF ((PCBOOK LT 1) AND (PCBOOK GT (-1))) THEN 0 ELSE
                           (((CCBOOK-PCBOOK)/PCBOOK)*100);

BY FLAG
ON TABLE HOLD AS CARTTL FORMAT FOCUS INDEX FLAG
END
-RUN












