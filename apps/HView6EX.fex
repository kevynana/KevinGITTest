-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File HVIEW3EX.FEX
-********************************************************************
-* MODIFICAIONS:
-********************************************************************
-*
-* This program extracts data for the series of basic hotel location listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-* 12/12/01 - JK  ADDED DEFINE FOR ONLINE
-* 04/01/04 - DV  ADDED BOOKINGS DEFINE PER STEVE
-*4/20/04    DV   CHANGED PREF_NON DEFINE TO INCLUDE REFUSE_CD 'T'
-*5/12/04    DV   CHANGED PREF_NON DEFINE TO INCLUDE REFUSE_CD 'CC'
-*5/06/05   DV   ADDED CODE FOR GLOBAL CURRENCY
-*7/13/05   DV   CHANGED PREF_NON DEFINE TO INCLUDE REFUSE_CD 'R'
-*               AND REMOVED REFUSE_CD 'CC'
-* 1/7/13 DV CHANGED PREF_NON DEFINE TO INCLUDE CLIENT_PREF C OR P
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/9/14 Updated Pref_non define to use exclude_flags - jk
-* 9/24/14 Updated to use exclude flags in % - JK


-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT

  CITY_ST/A35 = PROP_CITY||(' '| PROP_STATE);
  LEVEL_DESC/A60  = &&LDESC;
  IN_DATE/MDYY = CHKIN_DATE;
  INV_DATE/MDYY = INVOICE_DATE;
  NEW_ROOM_RATE/D12.2C = ROOM_RATE;
  NEW_STD_RATE/D12.2C = STD_RATE;
  NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;
  NBR_DAYS/D12 =  NUM_DAYS; 
  NNROOMS/D12 = IF (NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)
                THEN ((NBR_DAYS*NUM_ROOMS)*(-1))
                ELSE NBR_DAYS*NUM_ROOMS;
  OUT_DATE/MDYY = CHKOUT_DATE;
  PROP_ZIP5/A5=EDIT(PROP_ZIP, '99999$$$$$');
  REFUSE_CD/A2 = EDIT(REFUSE_KEY,'$$$$$$$$99');	
-* ****DEFINES ON PREVIOUS DEFINES****
  BOOKINGS/D8 = IF NEW_TOTAL_AMT GE 0 THEN 1 ELSE (-1);
-*  PREF_NON/A15 = IF REFUSE_CD EQ 'A' OR 'T' OR 'R' THEN 'PREFERRED' ELSE
-*                   'NON-PREFERRED';
-*  PREF_NON/A15 = IF CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'PREFERRED' ELSE 'NON-PREFERRED';
 PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
 PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
  
 PREF_NON/A15 = IF PNP_FLAG EQ 'I'  THEN 'PREFERRED' ELSE 'NON-PREFERRED';

  TOTAL_STD/D12.2C = IF (NNROOMS LT 0) AND (NEW_STD_RATE LT 0) 
                     THEN ((NNROOMS * NEW_STD_RATE)*(-1))
                     ELSE NNROOMS * NEW_STD_RATE;
-* ***DEFINES ON PREVIOUS DEFINED DEFINES***
   SAVINGS/D12.2CS = TOTAL_STD - TOTAL_AMT;
   TOTAL_SLSAV/D12.2C = TOTAL_STD - NEW_TOTAL_AMT;
   NPREF_AMT/D12.2C = IF PREF_NON EQ 'NON-PREFERRED' THEN TOTAL_AMT ELSE 0;
   NPREF_NGT/D12 = IF PREF_NON EQ 'NON-PREFERRED' THEN NNROOMS ELSE 0;
   PREF_AMT/D12.2C = IF PREF_NON EQ 'PREFERRED' THEN TOTAL_AMT ELSE 0;
   PREF_NGT/D12 = IF PREF_NON EQ 'PREFERRED' THEN NNROOMS ELSE 0;
   TTL_NGT/D12 = NPREF_NGT + PREF_NGT;
   XIN_DOW/WR = IN_DATE;
   XOUT_DOW/WR = OUT_DATE;
   NNROOMSPN/P12CS = IF PNP_FLAG EQ 'I' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 
                   IF PNP_FLAG EQ 'E' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 0;    
      
   NNROOMS3/P12CS = IF PNP_IE EQ 'I' THEN NNROOMS ELSE 0; 
   
END
-RUN

TABLEF FILE &&EXTRACT
PRINT
  BOOKINGS 
  CHAIN_CODE
  CHAIN_NAME
  CITY_ST
  CURR_DATE
  HHL_CONFIRM 	
  HOTEL_PHONE 	
  HTL_KEY	
  IN_DATE 	
  INV_DATE	
  INVOICE_DATE 	
  INVOICE_NUM 	
  LEVEL_DESC	
  NEW_ROOM_RATE
  NEW_STD_RATE
  NEW_TOTAL_AMT
  NBR_DAYS	
  NNROOMS
  NNROOMSPN
  NNROOMS3
  NPREF_AMT
  NPREF_NGT
  NUM_DAYS 	
  NUM_ROOMS	
  OUT_DATE 
  PREF_AMT
  PREF_NGT	
  PROP_ZIP5
  PASSNGR_NAME 	
  PREF_NON
  PROP_ADDRESS 	
  PROP_CITY 	
  PROP_NAME 	
  PROP_STATE_CD
  REFUSE_CD 	
  ROOM_RATE 	
  SAVINGS	
  STD_RATE 	
  TOTAL_AMT 	
  TOTAL_STD 	
  TOTAL_SLSAV
  TTL_NGT
  XIN_DOW
  XOUT_DOW
-INCLUDE &HTLDATES 
-*WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS

ON TABLE HOLD AS HHOLD

END
-RUN


TABLE FILE HHOLD
PRINT BOOKINGS 
  CHAIN_CODE
  CHAIN_NAME
  CITY_ST
  HHL_CONFIRM 	
  HOTEL_PHONE 	
  HTL_KEY	
  IN_DATE 	
  INV_DATE	
  INVOICE_DATE 	
  INVOICE_NUM 	
  LEVEL_DESC	
  NEW_ROOM_RATE
  NEW_STD_RATE
  NEW_TOTAL_AMT
  NBR_DAYS	
  NNROOMS
  NNROOMSPN
  NNROOMS3
  NPREF_AMT
  NPREF_NGT
  NUM_DAYS 	
  NUM_ROOMS	
  OUT_DATE 
  PREF_AMT
  PREF_NGT	
  PROP_ZIP5
  PASSNGR_NAME 	
  PREF_NON
  PROP_ADDRESS 	
  PROP_CITY 	
  PROP_NAME 	
  PROP_STATE_CD
  REFUSE_CD 	
  ROOM_RATE 	
  SAVINGS	
  STD_RATE 	
  TOTAL_AMT 	
  TOTAL_STD 	
  TOTAL_SLSAV
  TTL_NGT
  XIN_DOW
  XOUT_DOW
BY CURR_DATE
ON TABLE HOLD AS HHOLD2 FORMAT FOCUS INDEX CURR_DATE
END
-RUN

-* Global Reporting JOIN
JOIN HHOLD2.CURR_DATE IN HHOLD2 TO GLOBAL.DATE IN GLOBAL AS J1
END

DEFINE FILE HHOLD2
NAMEC/A30 = CNAME;
AMT_TOTAL/D12.2 = NEW_TOTAL_AMT;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;


AMT_TOTALX/D20.6 = AMT_TOTAL * CURR1X;
AMT_PREFX/D20.6 = PREF_AMT * CURR1X;
AMT_NPREFX/D20.6 = NPREF_AMT * CURR1X;
SLSAV_TTLX/D20.6 = TOTAL_SLSAV * CURR1X;


-* DEFINES ON PREVIOUS DEFINES

AMT_TOTL1/D20.6C = AMT_TOTALX;
AMT_PREF1/D20.6 = AMT_PREFX;
AMT_NPREF1/D20.6 = AMT_NPREFX;
SLSAV_TTL1/D20.6 = SLSAV_TTLX;
END

TABLE FILE HHOLD2
PRINT BOOKINGS 
  CHAIN_CODE
  CHAIN_NAME
  CITY_ST
  HHL_CONFIRM 	
  HOTEL_PHONE 	
  HTL_KEY	
  IN_DATE 	
  INV_DATE	
  INVOICE_DATE 	
  INVOICE_NUM 	
  LEVEL_DESC	
  NEW_ROOM_RATE
  NEW_STD_RATE
  NEW_TOTAL_AMT
  NBR_DAYS	
  NNROOMS
  NNROOMSPN
  NNROOMS3
  NPREF_AMT
  NPREF_NGT
  NUM_DAYS 	
  NUM_ROOMS	
  OUT_DATE 
  PREF_AMT
  PREF_NGT	
  PROP_ZIP5
  PASSNGR_NAME 	
  PREF_NON
  PROP_ADDRESS 	
  PROP_CITY 	
  PROP_NAME 	
  PROP_STATE_CD
  REFUSE_CD 	
  ROOM_RATE 	
  SAVINGS	
  STD_RATE 	
  TOTAL_AMT 	
  TOTAL_STD 	
  TOTAL_SLSAV
  TTL_NGT
  XIN_DOW
  XOUT_DOW
  NAMEC
  AMT_TOTL1/D16.2CS
  AMT_PREF1/D16.2CS
  AMT_NPREF1/D16.2CS
  SLSAV_TTL1/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HTLHLD2
END
-RUN

  

-*-EXIT

