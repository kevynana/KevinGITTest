-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File RNKEXTRT.FEX
-********************************************************************
-********************************************************************
-*
-* This program extracts data for a drillable RBT reason code report
-* - DEB - 03/09/10
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14 - DEB - ADDED EX5 TO EX_FL_FLAG DEFINE
-*02/11/16 - DLV - S-13286  Updated to allow reports to sort/subtotal by id-levels
-*06/15/17 DLV S-35586 - Carried through EMP_ID  CC_NUM

-********************************************************************
SET ASNAMES = ON
-INCLUDE SETECHO

-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';
-RUN

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
  BASE_FARE/D12.2CS = SEG_AMT - SEG_COM;
  COMM_SEG/D12.2S   = SEG_COM;
  IFLAG/A11 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE' ELSE 'TRADITIONAL'; 
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;

  F_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 0;
  P_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'P') THEN 1 ELSE 0;
  KILO/D9      = CITYPAIR.SEG_MILES * 1.60934 ;  
  LEVEL_DESC1/A60      = &&LDESC;
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_COMM/D12.2S     = (SEG_AMT - SEG_COM);
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
  NET_TKT_CNT/D8     = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_LOWEST/D9 = SEG_LOWEST;
  NEW_SEG_MILES/D9 = SEG_MILES;
  NEW_STAY/D9 = STAY;
  NEW_TRIP_LENGTH/D12 = TRIP_LENGTH;
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  REF_EXCH_CNT2/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'P') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG EQ 'P') THEN 1 ELSE 0;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND  
                 (SEG_COUNT EQ 1) THEN 1 ELSE 0;                            
  TOTAL_FARE/D12.2S = SEG_AMT + SEG_TAX;
  TRAN_MONTH/MT = TRN_DATE;
  VASAVINGS/D11.2CS = SEG_DISCOUNT - TOTAL_FARE;
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;
  EX_RF_FLAG/A1 = IF (((RF_FLAG EQ 'F' OR 'P') AND
      (EMBARK NE 'EX1' OR 'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1'))
    THEN 'R'
    ELSE IF (((EXSALE NE ' ') AND (SEG_COUNT GE 0)) OR
      ((EMBARK NE 'DT1') AND (EX_FLAG EQ 'F' OR 'P') AND 
      (SEG_COUNT LE 0) AND (RF_FLAG EQ ' ')))
    THEN 'E'
  ELSE ' ';
  
-* ****DEFINES ON PREVIOUS DEFINES****
  CPMILE/D12.2CS = IF ((SEG_MILES LT 1) AND (SEG_MILES GT (-1))) THEN 0 ELSE
                      (FARE_PAID/SEG_MILES);
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  AIRFEE_CNT/D8CS = IF AIRLINE_FEE GT 0 THEN 1 ELSE 0;
  EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
     NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
     IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;  
-INCLUDE TRANTYPE  
-INCLUDE IDLEVDEFINE  

END
-RUN

TABLEF FILE &&EXTRACT
PRINT   
        AIR_KEY
        AIRLINE
        AIRLINE_FEE
        AIRFEE_CNT	
        AIR_MAIN.INTL_DOM
        BASE_FARE
        AR_BRANCH
        BR_CL_IDX
        CC_NUM
        CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
        CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
        CLASS
        CLIENT_NUM	
        COMM_SEG
        EMB_CTY.COUNTRY_CODE AS 'EMB_CNTRY_CODE'
        CPMILE	
        DISSAVINGS
        DPT_TIME
        EMBARK
        EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
        EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
        EMP_ID
        EXCHGS
        EX_RF_FLAG
        F_EXCH_CNT
        FARE_BAS		
        FARE_PAID
        IFLAG
        KILO
        LEVEL_DESC	
        LEVEL1	
        LEVEL2	
        LEVEL3
        MRK_TKT_CNT
        NET_COMM	
        NET_FARE	
        NET_TKT_CNT
        NEW_ADV_PURCH
        NEW_SEG_LOWEST
        NEW_SEG_MILES
        NEW_STAY
        NEW_TRIP_LENGTH
        P_EXCH_CNT
        PASSNGR_NAME
        PNR_LOCATOR
        REF_EXCH_CNT
        REF_EXCH_CNT2
        REFUSAL_DESC
        REFUSE_CODE
        RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
        RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
        SAVINGS	
        SEG_AMT	
        SEG_COUNT	
        SEG_DISCOUNT	
        SEG_LOWEST
        SEG_NBR	
        SEG_STANDARD	
        SEG_TAX	
        STAY
        TICKET_BRANCH	
        TKT_AMOUNT
        TKT_DATE	
        TKT_NUM	
        TKT_PURCH
        TKT_SORT
        TKT_TYPE	
        TOTAL_FARE	
        TRN_DATE
        VAL_AIRLINE	
        VASAVINGS
        XDPT_DT	
        XPSNGR_NM	
        XTRAN_DT
         

-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD 

END
-RUN


