-*12/21/04 -DEB  - ADDED ADDTL DOD_TYPE TO IFLAG DEFINE
-*04/20/10   DEB                CHANGED ORIG_SALE DEFINE
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14 - DEB - ADDED EX5 TO NUMEROUS DEFINES
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
 
-INCLUDE SETECHO
 
CREATE FILE TCTRAN
 
 
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
 
-TYPE TCAIRDB
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCTRNAS
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
DEFINE FILE TCTRAN
COMP/A6 = EDIT(ROLLCD, '999999');
END
TABLE FILE TCTRAN
PRINT *
BY ROLLCD
ON TABLE HOLD AS TCTRAN1 FORMAT FOCUS INDEX ROLLCD
END
-RUN
 
JOIN TCTRAN1.ROLLCD IN TCTRAN1 TO ROLLUP.ROLLUP IN ROLLUP AS J1
 
TABLE FILE TCTRAN1
PRINT
   DOC_TYPE
   DOD_TYPE
   DOD_DESC
   ONLINE_TRADITIONAL
   EMBARK
   EX_FLAG
   EXCHG_SALE
   ROLLCD
   ROLL_NAME
   RF_FLAG
   SEG_AMT
   SEG_COM
   SEG_NBR
   SEG_TAX
   TRN_DATE
   VOID_DATE
   VOID_STATUS
-* NEW FIELDS
   OPS_MANAGER
   CSM_NAME
   PEER_GROUP
   ROLL_HOME_BR
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS TRANONE
END
-RUN
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
DEFINE FILE TRANONE
 
 
EMCO_TAX/D15 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_TAX
  ELSE 0;
EXCH_AMT/D15 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_AMT ELSE 0;
EXCH_MCO/D15 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_AMT
  ELSE 0;
EXCH_TAX/D15 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_TAX ELSE 0;
EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE
	NE ' ') THEN 1 ELSE 0;
FARE_PAID/D15 = SEG_AMT + SEG_TAX;
FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (TKT_NUM
          NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
IFLAG/A33 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE BOOKINGS' ELSE
   IF DOD_TYPE EQ 'M' OR 'X' THEN 'EXCHANGE FROM AN INTERNET BOOKING' ELSE
   IF DOD_TYPE EQ 'A' OR 'L' OR 'N' OR 'S' THEN 'WEB BOOKINGS' ELSE
   IF DOD_TYPE EQ 'C' OR 'D' THEN 'CONSOLIDATOR BOOKINGS' ELSE 'TRADITIONAL BOOKINGS';
 
NEW_SALES/D15 = IF (SEG_AMT GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE
  EQ ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5') THEN SEG_AMT ELSE 0;
NEW_TAX/D15 = IF ((SEG_TAX GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE EQ
  ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5')) THEN SEG_TAX ELSE 0;
NEW_TOTAL/D15 = NEW_SALES + NEW_TAX;
-*ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;
 
 
ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;
 
PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;
REFUND_AMT/D15 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_AMT ELSE 0;
REFUND_TAX/D15 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_TAX ELSE 0;
REFUND_TOTAL/D15 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN FARE_PAID ELSE 0;
REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;
TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT
  EQ 1) THEN 1 ELSE 0;
 
 
TOTL/A15 = 'TOTAL NEW SALES';
 
TRAN_DT/MDYY = TRN_DATE;
VOID_AMT/D15 = IF VOID_DATE NE 0 THEN SEG_AMT ELSE 0;
VOID_TAX/D15 = IF VOID_DATE NE 0 THEN SEG_TAX ELSE 0;
VOID_TOTAL/D15 = IF VOID_DATE NE 0 THEN FARE_PAID ELSE 0;
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01')
 THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) THEN 1 ELSE 0;
 
-* Defines on previous DEFINEs
TOT_ETAX/D15 = EXCH_TAX + EMCO_TAX;
TOT_EXCH/D15 = EXCH_AMT + EXCH_MCO;
EMCO_TOTAL/D15 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN FARE_PAID
  ELSE 0;
EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK
 NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE IF (DOC_TYPE EQ '1') AND
(EXCHG_SALE EQ 'E') THEN 0 ELSE IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE
  ' ') THEN TKT_CNT ELSE 0;
EXCH_TOTAL/D15 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (((EXCHG_SALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EMBARK NE 'DT1' OR
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ') AND (EX_FLAG EQ 'F' OR 'P') AND
  (SEG_COUNT LE 0)) OR (EMBARK EQ 'EX1' OR 'EX5')) THEN FARE_PAID ELSE 0;
MCO_AMT/D15 = IF DOC_TYPE EQ '1' THEN NEW_SALES ELSE 0;
MCO_CNT/D9 = IF DOC_TYPE EQ '1' THEN ORIG_SALE ELSE 0;
MCO_TAX/D15 = IF DOC_TYPE EQ '1' THEN NEW_TAX ELSE 0;
MCO_TOTAL/D15 = IF DOC_TYPE EQ '1' THEN NEW_TOTAL ELSE 0;
MCOS/D9CS           = IF (DOC_TYPE EQ '1') AND
                           (EX_FLAG EQ ' ')  AND
                           (EXCHG_SALE NE ' ') THEN 1 ELSE 0;
NET_AMT/D15 = NEW_SALES + REFUND_AMT + TOT_EXCH - VOID_AMT;
NET_TAX/D15 = NEW_TAX + REFUND_TAX + TOT_ETAX - VOID_TAX;
TOTAL_EXCHG/D15 = EXCH_TOTAL + EMCO_TOTAL;
TOT_EXAMT/D15 = EXCH_AMT + EXCH_MCO;
TOT_EXTAX/D15 = EXCH_TAX + EMCO_TAX;
TOT_ECNT/D9 = EXCHGS + EXMCOS;
TOT_TRANS/D9 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;
CHARGE_TRN/D9 = ORIG_SALE + VOIDS + EXCHGS + REFUNDS;
 
-* Defines on previous DEFINEs
-*NET_AMT/D15 = NEW_SALES + REFUND_AMT + TOT_EXAMT - VOID_AMT;
-*NET_TAX/D15 = NEW_TAX + REFUND_TAX + TOT_EXTAX - VOID_TAX;
-*NET_TOTAL/D15 = NEW_TOTAL + REFUND_TOTAL + TOTAL_EXCHG - VOID_TOTAL;
-*NET_TRAN/D15 = ORIG_SALE + EXCHGS - REFUNDS - VOIDS;
 
-*GROSS_AMT/D15 = NEW_SALES + TOT_EXAMT - VOID_AMT;
-*GROSS_TAX/D15 = NEW_TAX + TOT_EXTAX - VOID_TAX;
-*GROSS_TOTAL/D15 = NEW_TOTAL + TOTAL_EXCHG - VOID_TOTAL;
-*GROSS_TRAN/D15 = ORIG_SALE + EXCHGS - VOIDS;
 
 
NET_AMT/D15 = NEW_SALES + REFUND_AMT + EXCH_AMT - VOID_AMT;
NET_TAX/D15 = NEW_TAX + REFUND_TAX + EXCH_TAX - VOID_TAX;
NET_TOTAL/D15 = NEW_TOTAL + REFUND_TOTAL + EXCH_TOTAL - VOID_TOTAL;
 
NET_TRAN/D15 = ORIG_SALE + EXCHGS - REFUNDS - VOIDS;
 
GROSS_AMT/D15 = NEW_SALES + EXCH_AMT;
GROSS_TAX/D15 = NEW_TAX + EXCH_TAX;
-*GROSS_TOTAL/D15 = NEW_TOTAL + EXCH_TOTAL;
GROSS_TOTAL/D15 = IF VOID_DATE EQ 0 THEN FARE_PAID ELSE 0;
GROSS_TRAN/D15 = ORIG_SALE + EXCHGS;
 
 
END
-RUN
 
 
 
TABLE FILE TRANONE
 SUM
 ORIG_SALE  NEW_TOTAL EXCH_TOTAL
 EXCHGS  TOTAL_EXCHG
 REFUNDS REFUND_TOTAL
 NET_TRAN
 NET_TOTAL GROSS_TRAN
 GROSS_TOTAL
 ROLL_NAME
 OPS_MANAGER
 CSM_NAME
 PEER_GROUP
 ROLL_HOME_BR
 BY ROLLCD
 ON TABLE HOLD AS TRANTWO
 END
-RUN
 
 
 TABLE FILE TRANTWO
 SUM ORIG_SALE  NEW_TOTAL EXCH_TOTAL
 EXCHGS  TOTAL_EXCHG
 REFUNDS REFUND_TOTAL
 NET_TRAN
 NET_TOTAL GROSS_TRAN
 GROSS_TOTAL
 ROLL_NAME
 OPS_MANAGER
 CSM_NAME
 PEER_GROUP
 ROLL_HOME_BR
 RANKED BY HIGHEST GROSS_TOTAL
 BY ROLLCD
 ON TABLE HOLD AS RNKHLD
 END
 -RUN
 
-SET &&RANK_LIMIT = 9999;
 
DEFINE FILE RNKHLD
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = (LAST LIST + 1);
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                  CRANK;
  RANK_VAL/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                IF RANK GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                ARANK;
  RANK_LABEL/A9 = IF RANK_VALUE NE 'ALL OTHER' THEN ROLLCD
                      ELSE 'ALL OTHER';
NOWTOD/A8 WITH RANK = HHMMSS (NOWTOD);
 
END
 
TABLE FILE RNKHLD
 
-INCLUDE HEADER
 
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
 
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
