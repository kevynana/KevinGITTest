-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* CREATED FOR PRINCIPAL HOTEL SUMMARY BY BRANDED PROPERTY
-* CREATED FROM HVIEW1EX
-* File HVIW41EX.FEX
-********************************************************************
-* MODIFICAIONS:
-********************************************************************
-*
-* This program extracts data for the series of basic hotel location listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-*DATE       NAME          DESCRIPTION
-*4/17/01    TANDT-SS      ADDED DOD_TYPE.
-*7/20/01    TANDT-SS      ADDED AGENT_NUM.
-*4/16/02    TANDT-DB      ADDED DEFINE FOR CHKIN_MONTH
-*8/06/02    TANDT-DB      ADDED DEFINE FOR RATE_GRP2
-*11/5/02    TANDT-DB      ADDED DEFINE FOR PROP_ZIP5
-*08/22/03   TANDT-DV      ADDED GSA_GTR
-*10/08/03   TANDT-DV      ADDED AROLL_LEV6 AROLL_LEV7 AROLL_LEV8 AROLL_LEV9
-*12/02/03   TANDT-DV      ADDED AROLL_LEV5  ADDED FOP FOP2 DEFINE
-*12/10/03   TANDT-DV      ADDED AROLL_LEV4
-*03/31/04   TANDT-DV      CHANGED BOOKINGS DEFINE PER STEVE
-*5/11/04    TANDT-JK      ADDED MX_DAY DEFINE
-*5/11/04    TANDT-JK      MODIFIED BOOKINGS DEFINE TO BACK OUT NEGATIVES
-*6/15/04    TANDT-DV      ADDED XLEVEL3 XLEV_3 DEFINES
-*6/22/04    TANDT-SC      ADDED RESV_STATUS 
-*12/28/04   TANDT-DV      ADDED DOD_TYPE J TO IBOOK DEFINE
-*10/11/05   TANDT-DV      ADDED DOD_TYPE H TO IBOOK DEFINE
-*04/29/10   DEB         CHANGED RATE_GRP AND RATE_GRP2 FOR ROUNDING
-*10/11/13   STEVE       CHANGED HTL_BRAND ON SIX CONTINENTS BRAND TO IHG BRAND
-*1/29/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE

-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';


DEFINE FILE &&EXTRACT

  CARD_NUM/A12 = EDIT(CREDIT_CARD, '$$999999999999$$$$');
  CHKIN_MONTH/MT  = CHKIN_DATE;
  CARD1_NUM/A4 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$$$9999');
  CITY_ST/A35 = PROP_CITY||(', '| PROP_STATE);
  FOP/A2 = EDIT(CREDIT_CARD,'99$$$$$$$$$$$$$$$$');
  FOP2/A7 = IF FOP EQ 'AX' THEN EDIT(CREDIT_CARD, '99$$$$$$$$$$99999')
	      ELSE EDIT(CREDIT_CARD, '99$$$$$$$$$$$99999');
  LEV_2/A15 = IF PASSNGR_NAME NE LAST PASSNGR_NAME THEN LEVEL2 ELSE ' ';
  LEVEL_DESC/A60  = &&LDESC;
  IBOOK/A13 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'INTERNET' ELSE
              'NON-INTERNET';
  IN_DATE/MDYY = CHKIN_DATE;
  INV_DATE/MDYY = INVOICE_DATE;
  NBR_DAYS/D12 =  NUM_DAYS;
  NBR_ROOMS/D12 = NUM_ROOMS;
  NEW_ROOM_RATE/D14.2C = ROOM_RATE;
  NEW_STD_RATE/D14.2C = STD_RATE;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
  OUT_DATE/MDYY = CHKOUT_DATE;
-* RATE_GRP/A15 = IF ((ROOM_RATE GE 0 AND ROOM_RATE LT 150) OR
-*                      (ROOM_RATE LE 0 AND ROOM_RATE GT (-150)))
-*                  THEN '$0 - $149' ELSE
-*                  IF ((ROOM_RATE GE 150 AND ROOM_RATE LE 199) OR
-*                      (ROOM_RATE LE (-150) AND ROOM_RATE GE (-199)))
-*                  THEN '$150 - $199' ELSE
-*                  IF ((ROOM_RATE GE 200 AND ROOM_RATE LE 249) OR
-*                      (ROOM_RATE LE (-200) AND ROOM_RATE GE (-249)))
-*                  THEN '$200 - $249' ELSE '$250 OR GREATER';

 RATE_GRP/A15 = IF ((ROOM_RATE GE 0 AND ROOM_RATE LT 149.50) OR
                      (ROOM_RATE LE 0 AND ROOM_RATE GT (-149.50)))
                   THEN '$0 - $149' ELSE
                   IF ((ROOM_RATE GE 149.50 AND ROOM_RATE LT 199.50) OR
                      (ROOM_RATE LE (-149.50) AND ROOM_RATE GT (-199.50)))
                   THEN '$150 - $199' ELSE
                   IF ((ROOM_RATE GE 199.50 AND ROOM_RATE LT 249.50) OR
                      (ROOM_RATE LE (-199.50) AND ROOM_RATE GT (-249.50)))
                   THEN '$200 - $249' ELSE '$250 OR GREATER';


-*   RATE_GRP2/A15 =IF ((ROOM_RATE GE 0 AND ROOM_RATE LT 100) OR
-*                      (ROOM_RATE LE 0 AND ROOM_RATE GT (-100)))
-*                  THEN '$0 - $99' ELSE
-*                  IF ((ROOM_RATE GE 100 AND ROOM_RATE LE 150) OR
-*                      (ROOM_RATE LE (-100) AND ROOM_RATE GE (-150)))
-*                  THEN '$100 - $150' ELSE
-*                  IF ((ROOM_RATE GE 151 AND ROOM_RATE LE 200) OR
-*                      (ROOM_RATE LE (-151) AND ROOM_RATE GE (-200)))
-*                  THEN '$151 - $200' ELSE '$201 OR GREATER';

  RATE_GRP2/A15 =IF ((ROOM_RATE GE 0 AND ROOM_RATE LT 99.50) OR
                       (ROOM_RATE LE 0 AND ROOM_RATE GT (-99.50)))
                   THEN '$0 - $99' ELSE
                   IF ((ROOM_RATE GE 99.50 AND ROOM_RATE LT 150.50) OR
                       (ROOM_RATE LE (-99.50) AND ROOM_RATE GT (-150.50)))
                   THEN '$100 - $150' ELSE
                   IF ((ROOM_RATE GE 150.50 AND ROOM_RATE LT 200.50) OR
                      (ROOM_RATE LE (-150.50) AND ROOM_RATE GT (-200.50)))
                   THEN '$151 - $200' ELSE '$201 OR GREATER';
                   
  REASON_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
  REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
  RSNCDE/A48=REFUSE_CD|| '-' ||REFUSAL_DESC;
  XPSNGR_NM/A15 =EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  PROP_ZIP5/A5=EDIT(PROP_ZIP, '99999$$$$$');
  XLEVEL3/A5 = EDIT(LEVEL3, '99999$$$$$$$$$$');
  XLEV_3/A3 = EDIT(LEVEL3, '$$$$$$999$$$$$$');
  HTL_BRAND/A20 = IF CHAIN_CODE EQ 'BR' OR 'CY' OR 'ET' OR 'FN' OR 'MB' OR 'MC' OR 
                     'NR' OR 'RC' OR 'TO' OR 'VC' OR 'XV' THEN 
                     'MARRIOTT BRAND' ELSE
                  IF CHAIN_CODE EQ 'DT' OR 'ES' OR 'GI' OR 'HG' OR 'HH' OR
                      'HL' OR 'HX' OR 'SH' THEN 'HILTON BRAND' ELSE
                  IF CHAIN_CODE EQ 'CP' OR 'HI' OR 'IC' OR 'YO' OR 'YZ' THEN
                      'IHG BRAND' ELSE
                  IF CHAIN_CODE EQ 'GX' OR 'SI' OR 'WH' OR 'WI' THEN
                      'STARWOOD BRAND' ELSE
                  IF CHAIN_CODE EQ 'HY' THEN 'HYATT BRAND' ELSE
                      'ALL OTHER';
-* ****DEFINES ON PREVIOUS DEFINES****

   HTLBRD_ST/A1 = IF HTL_BRAND EQ 'ALL OTHER' THEN 'B' ELSE 'A';

   BOOKINGS/D8 = IF NEW_TOTAL_AMT GT 0 THEN 1 ELSE (-1);
-*                  IF NEW_TOTAL_AMT LT 0 THEN (-1) ELSE 0;
   MX_DY/I6 = IF NBR_DAYS LT 1 THEN NBR_DAYS * (-1)
              ELSE NBR_DAYS;
   MXREFUSE_CD/A9 = IF REFUSE_CD EQ 'A' OR 'C' THEN 'A OR C'
                    ELSE 'ALL OTHER';
   TOTAL_STD/D12.2C = IF ((NNROOMS LT 0) AND (NEW_STD_RATE LT 0))
                      THEN ((NNROOMS * NEW_STD_RATE) * (-1))
                      ELSE NNROOMS * NEW_STD_RATE;

-* ****DEFINES ON PREVIOUS DEFINED DEFINES***
   TOTAL_SLSAV/D12.2C = TOTAL_STD - NEW_TOTAL_AMT;
   UP_STD/D12.2C = NEW_STD_RATE/NNROOMS;
MIR_SEQ1/A1 = EDIT(MIR_SEQ, '9$$$$$');

RES_SYS/A1 = IF MIR_SEQ1 EQ 'X' THEN 'S' ELSE 'A';

PROP_CODE1/A7 = IF RES_SYS EQ 'S' THEN PROP_SABRE_CODE ELSE PROP_APOLLO_CODE;  

END
-RUN

TABLEF FILE &&EXTRACT
PRINT AIRPORT_CODE
  AGENT_NUM	
  ALPHA_RATE	
  AR_BRANCH
  AROLL_LEV4
  AROLL_LEV5
  AROLL_LEV6
  AROLL_LEV7
  AROLL_LEV8
  AROLL_LEV9
  BOOKINGS	
  BR_CL_IDX	
  CARD_NUM	
  CARD1_NUM	
  CHAIN_CODE 	
  CHAIN_NAME
  CHKIN_MONTH 	
  CITY_ST
  CLIENT_NAME	
  CREDIT_CARD
  DOD_TYPE
  FOP2
  GSA_GTR	
  HHL_CONFIRM 	
  HOTEL_PHONE
  HTL_BRAND
  HTLBRD_ST 	
  HTL_KEY
  IBOOK	
  IN_DATE 	
  INV_DATE	
  INVOICE_DATE 	
  INVOICE_NUM 	
  LEV_2	
  LEVEL_DESC	
  LEVEL1 	
  LEVEL2 	
  LEVEL3 
  MX_DY
  MXREFUSE_CD	
  NBR_DAYS	
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_STD_RATE
  NEW_TOTAL_AMT	
  NNROOMS	
  NUM_DAYS 	
  NUM_ROOMS	
  OUT_DATE 	
  PASSNGR_NAME 	
  PNR_LOCATOR 	
  PROP_ADDRESS 	
  PROP_CITY 
  PROP_CODE1 AS 'PROP_CODE'
  PROP_CNTRY	
  PROP_NAME 	
  PROP_STATE_CD 
  PROP_ZIP5	
  RATE_GRP
  RATE_GRP2	
  REFUSAL_DESC	
  REFUSE_CD	
  REFUSE_KEY
  RESV_BRANCH
  RESV_DATE
  RESV_STATUS	
  ROOM_RATE 	
  ROOM_TYPE
  RSNCDE	
  STD_RATE 	
  TICKET_BRANCH	
  TKT_NUM	
  TOTAL_AMT 	
  TOTAL_SLSAV
  TOTAL_STD 
  UP_STD
  XPSNGR_NM
  XLEVEL3
  XLEV_3
-INCLUDE &HTLDATES    
-* WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL
-* WHERE TOTAL_AMT NE 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD

END
-RUN
-*-EXIT


