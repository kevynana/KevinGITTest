-**************************************************************
-* Date       Author    Modification
-**************************************************************
-* 01/23/2012 GSE       Removed parens from around RLIMIT to
-*                      prevent errors in 7.7.03
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-**************************************************************
-INCLUDE SETECHO
 
CREATE FILE TCTRNK
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCHTLDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCH104EX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-INCLUDE SetParmtime
-RUN
 
-IF '&&SETF.EVAL' EQ 'TCTH10D' OR 'TCTH10I' THEN GOTO DOMINT;
-RUN
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
-SET &&CTY_LMT = &&RANK_LIMIT;
-SET &&HTL_LMT = &&RANK_LIMIT2;
-SET &&RANK_METH = IF &&RANK_METH EQ 'ROOM_AMT' THEN 'ROOM_AMT'
- ELSE IF &&RANK_METH EQ 'NMBR_DAYS' THEN 'NMBR_DAYS'
- ELSE 'NMBR_DAYS';
 
 
 
 
TABLE FILE TCTRNK
SUM BOOKINGS
    NNROOMS
    NNROOMS/D8 AS 'NMBR_DAYS'
    TOTAL_AMT/D12.2 AS 'ROOM_AMT'
BY ROLLCD
BY CHAIN
ON TABLE HOLD AS HOLD1
END
-RUN
 
TABLE FILE HOLD1
PRINT CHAIN
  BOOKINGS
  NMBR_DAYS
  NNROOMS
  ROOM_AMT/D12.2
BY ROLLCD
RANKED BY HIGHEST &&RANK_METH NOPRINT
ON TABLE HOLD AS HOLD2
END
-RUN
 
DEFINE FILE HOLD2
 RANK_LIMIT/I5 = &&HTL_LMT;
 CRANK/D5 =   RANK;
 BRANK/A6 =   FTOA(CRANK, '(D5)' , 'A6');
 LIST/D6 = IF CHAIN NE LAST CHAIN THEN (LAST LIST + 1) ELSE LAST LIST;
 FRANKX/A6 = FTOA(LIST, '(D5)', 'A6');
 FRANK/A5=EDIT(FRANKX,'$99999');
 RANK_VALUE/A9 = IF (RANK_LIMIT EQ 0) THEN BRANK ELSE
                 IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE BRANK;
END
-RUN
 
TABLE FILE HOLD2
SUM
  BOOKINGS
  NMBR_DAYS
  NNROOMS
  ROOM_AMT
BY ROLLCD
BY RANK_VALUE
BY CHAIN
ON TABLE HOLD AS HOLD3
END
-RUN
 
DEFINE FILE HOLD3
 CHN_NAME/A35 =IF RANK_VALUE LT '&&HTL_LMT' THEN CHAIN ELSE 'ALL OTHER';
END
-RUN
 
TABLE FILE HOLD3
SUM
  BOOKINGS
  NMBR_DAYS
  NNROOMS
  ROOM_AMT
BY ROLLCD
 BY RANK_VALUE
BY CHN_NAME
ON TABLE HOLD AS HOLD4
END
-RUN
 
TABLE FILE HOLD1
SUM
  BOOKINGS
  NMBR_DAYS
  NNROOMS
  ROOM_AMT
BY ROLLCD
ON TABLE HOLD AS RHOLD1
END
-RUN
 
TABLE FILE RHOLD1
SUM BOOKINGS
    NMBR_DAYS
    NNROOMS
    ROOM_AMT
RANKED BY HIGHEST &&RANK_METH AS 'DAYS_RANK'
BY ROLLCD
ON TABLE HOLD AS RHOLD2
END
-RUN
 
MATCH FILE HOLD4
PRINT BOOKINGS
      CHN_NAME
      NMBR_DAYS
      NNROOMS
      RANK_VALUE
      ROOM_AMT
BY ROLLCD
ON TABLE HOLD
RUN
FILE RHOLD2
SUM DAYS_RANK
BY ROLLCD
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN
 
TABLE FILE HOLD5
PRINT BOOKINGS
      CHN_NAME
      NMBR_DAYS
      NNROOMS
      RANK_VALUE
      ROOM_AMT
RANKED BY HIGHEST DAYS_RANK
BY ROLLCD
ON TABLE HOLD AS HOLD6
END
-RUN
 
DEFINE FILE HOLD6
  ROLL_LIMIT/I4 = &&CTY_LMT;
  RLIMIT/A4    = EDIT(ROLL_LIMIT);
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF ROLLCD NE LAST ROLLCD THEN (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6=EDIT(FRANKX,'$999999');
  ROLL_VALUE/A9 = IF ROLL_LIMIT EQ 0 THEN BRANK ELSE
                 IF RANK GT ROLL_LIMIT THEN 'ALL OTHER' ELSE FRANK;
END
-RUN
 
TABLE FILE HOLD6
PRINT
  BOOKINGS
  RLIMIT
  ROLL_LIMIT
  DAYS_RANK
  NMBR_DAYS
  NNROOMS
  ROOM_AMT
BY ROLL_VALUE
BY ROLLCD
BY RANK_VALUE
BY CHN_NAME
ON TABLE HOLD AS TCR1
END
-RUN
 
 
 
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
DEFINE FILE TCR1
ROLL_PRINT/A35 = IF ROLL_VALUE LT RLIMIT THEN ROLLCD ELSE ' ';
CHN_PRINT/A35 = IF ROLL_VALUE LT RLIMIT THEN CHN_NAME ELSE ' ';
CHN_RANK/A9 = IF ROLL_VALUE LT RLIMIT THEN RANK_VALUE ELSE ' ';
NOWTOD/A8 WITH ROLL_VALUE = HHMMSS (NOWTOD);
END
 
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE TCR1
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
-*
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
 
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-GOTO XXIT;
 
-DOMINT;
 
TABLE FILE TCTRNK
SUM NNROOMS/D8 AS 'NIGHTS'
BY ROLLCD
BY CHAIN
ON TABLE HOLD AS HOLD1
END
-RUN
 
TABLE FILE HOLD1
SUM NIGHTS
BY ROLLCD
RANKED BY HIGHEST NIGHTS NOPRINT
BY CHAIN
ON TABLE HOLD AS HOLD2
END
-RUN
 
 
DEFINE FILE HOLD2
 RANK_LIMIT/I5 = 10;
 DRANK/D9 = RANK;
 ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
 ARANK/A9 = EDIT(ARANKX,'$$999999999');
 LIST/D9 = IF (ROLLCD NE LAST ROLLCD) AND (CHAIN NE LAST CHAIN) THEN (LAST LIST + 1) ELSE LAST LIST;
 CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
 CRANK/A9 = EDIT(CRANKX,'$$999999999');
 RANK1/D9 = IF RANK EQ LAST RANK THEN RANK + 1 ELSE RANK;
 CRANKX/A11 = FTOA(RANK1, '(D9)', 'A11');
 CRANK/A9 = EDIT(CRANKX,'$$999999999');
 RANK_VALUE/A9 = IF (RANK_LIMIT EQ 0) THEN ARANK ELSE
                 IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
 CHN_NAME/A35 = IF RANK_VALUE LE '10' THEN CHAIN ELSE 'ALL OTHER';
END
-RUN
 
 
TABLE FILE HOLD2
SUM NIGHTS
BY ROLLCD
BY RANK_VALUE
BY CHN_NAME
ON TABLE HOLD AS HOLD4
END
-RUN
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
DEFINE FILE HOLD4
NOWTOD/A8 WITH ROLLCD = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE HOLD4
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
SUM CHN_NAME AS 'HOTEL' NIGHTS AS '# NIGHTS'
BY ROLLCD AS 'CLIENT'
ACROSS RANK_VALUE NOPRINT
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
 
 
 
 
 
-XXIT
