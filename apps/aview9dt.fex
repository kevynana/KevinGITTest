-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO
-*                     FOCEXEC: ERPT
-**************************************************************************
-*DATE           NAME               DESCRIPTION
-*11/10/00      KASSOC-DK            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-*11/10/00      KASSOC-DK            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*05/09/01      TANDT-DB            ADDED STDTIME
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*1/19/05    DEB                ADDED CODE FOR GLOBAL CURRENCY
-*
-**************************************************************************


SET ASNAMES = ON
-RUN

-IF &&CURRENCY EQ 'USD' THEN GOTO NEXT;
-SET &&SUBHEAD = <NAMEC;
-SET &RSN_CUR = 'MISC,FARE, (' | &&CURRENCY || ')';
-SET &MSSV_CUR = 'MISC,SAVINGS, (' | &&CURRENCY || ')';
-SET &&PRINT8='GRSN_FARE/D16.2CS AS ' | '''&RSN_CUR.EVAL''';
-SET &&PRINT10='GMISC_AMT/D16.2CS AS ' | '''&MSSV_CUR.EVAL''';





-NEXT;



TABLEF FILE MSSAV
PRINT   
    AIR_KEY
    INTL_DOM
    AIRLINE    
    ARR_TIME
    BR_CL_IDX
    EMB_APT_CD
    RTE_APT_CD
    CLASS	
    COMM_SEG	
    DOC_TYPE	
    DPT_TIME
    EMB_APT_NM	
    EMB_CTY_NM	
    EMB_CTY_CD	
    FARE_BAS
    FARE_PAID	
    INT_CODE	
    LEVEL_DESC
    MISC_SAV
    MRK_TKT_CNT
    NAMEC
    NET_FARE
    NET_TKT_CNT	
    NEW_SEG_DISC
    PAID_FAREX/D16.2CS	
    REF_EXCH_CNT	
    REFUSE_KEY        	
    REFUSE_CODE        	
    RESV_BRANCH
    RSN_SEGX/D16.2CS	
    RTE_APT_NM	
    RTE_CTY_NM	
    RTE_CTY_CD
    SAV_MISCX/D16.2CS
    SEG_DESIG
    SEG_NBR	
    SEG_REASON
    SEG_RSN
    TAX
    TKT_NUM
    TKT_PURCH
    TKT_SORT
    TKT_TYPE
    TRN_DATE
    VAL_AIR	
    VASAVINGS	
    XDPT_DT	
    XTRAN_DT	
    XPSNGR_NM
ON TABLE HOLD AS HLD1
END
-RUN


JOIN 
HLD1.VAL_AIR IN HLD1 TO ALL AIRLINE.AIRLINE IN AIRLINE
AS JOINCT
 END
-RUN



TABLE FILE HLD1
SUM 
  FARE_PAID AS 'TFARE'
  PAID_FAREX/D16.2CS AS 'GTFARE'	
  MISC_SAV AS 'TMISC'
  SAV_MISCX/D16.2CS AS 'GTMISC'
  MRK_TKT_CNT AS 'MTKT'
  NET_TKT_CNT AS 'TTKT'
  SEG_RSN AS 'TSRSN'
  RSN_SEGX/D16.2CS AS 'GTSRSN'	
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE HLD1
PRINT 
  AIRLINE	
  ARR_TIME
  BR_CL_IDX 
  DPT_TIME	
  EMB_APT_CD	
  EMB_APT_NM	
  EMB_CTY_NM
  FARE_BAS	
  FARE_PAID
  MISC_SAV
  MRK_TKT_CNT
  NAMEC
  PAID_FAREX/D16.2CS
  RSN_SEGX/D16.2CS
  SAV_MISCX/D16.2CS
  SEG_RSN
  REFUSE_CODE	
  RTE_APT_CD	
  RTE_APT_NM	
  RTE_CTY_NM	
  TKT_NUM	
  TKT_TYPE	
  AIRLINE_NAME
  VAL_AIR
  XDPT_DT	
  XTRAN_DT	
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT 
  AIRLINE	
  ARR_TIME
  BR_CL_IDX 
  DPT_TIME	
  EMB_APT_CD	
  EMB_APT_NM	
  EMB_CTY_NM	
  FARE_BAS
  FARE_PAID	
  MISC_SAV
  MRK_TKT_CNT
  NAMEC
  PAID_FAREX
  RSN_SEGX
  SAV_MISCX
  SEG_RSN
  REFUSE_CODE	
  RTE_APT_CD	
  RTE_APT_NM	
  RTE_CTY_NM	
  TKT_NUM	
  TKT_TYPE	
  AIRLINE_NAME
  VAL_AIR
  XDPT_DT	
  XTRAN_DT	
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT GTFARE
  GTMISC
  GTSRSN
  MTKT
  TFARE
  TMISC	
  TSRSN	
  TTKT	
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

TABLE FILE AIRHOLD2
PRINT 
  AIRLINE	
  ARR_TIME
  BR_CL_IDX 
  DPT_TIME	
  EMB_APT_CD	
  EMB_APT_NM	
  EMB_CTY_NM	
  FARE_BAS
  FARE_PAID
  GTFARE
  GTMISC
  GTSRSN	
  LEVEL_DESC	
  MISC_SAV
  MRK_TKT_CNT
  MTKT
  NAMEC
  PAID_FAREX
  RSN_SEGX
  SAV_MISCX
  SEG_RSN	
  REFUSE_CODE	
  RTE_APT_CD	
  RTE_APT_NM	
  RTE_CTY_NM
  TFARE	
  TKT_NUM	
  TKT_TYPE
  TMISC	
  TSRSN	
  TRN_DATE	
  TTKT	
  AIRLINE_NAME
  VAL_AIR
  XDPT_DT	
  XPSNGR_NM	
  XTRAN_DT	
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN

DEFINE FILE AIRHOLD3
-INCLUDE STDTIME
  PASNGR_NAME/A15 = IF TKT_NUM NE LAST TKT_NUM THEN XPSNGR_NM ELSE ' ';
  RREFUSE_CODE/A2 = IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE '';
  TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE '';
  XT_DTEX/A8YYMD=TRN_DATE;
  XT_DTE/A8=XT_DTEX;
  XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
     
-* ****DEFINES ON PREVIOUS DEFINES****
  SRT_DT/A100 = TKT_SORT||XT_DTE||LEVEL_DESC||XPSNGR_NM;
  NET_TKT_CNT/D12CS = IF SRT_DT NE LAST SRT_DT THEN TTKT ELSE 0;
  SEG_CNT/D12CS = IF SRT_DT NE LAST SRT_DT THEN MTKT ELSE 0;
  REASON_FARE/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSRSN ELSE 0;
  GRSN_FARE/D16.2CS = IF SRT_DT NE LAST SRT_DT THEN GTSRSN ELSE 0;
  FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
  GFARE_AMT/D16.2CS = IF SRT_DT NE LAST SRT_DT THEN GTFARE ELSE 0;
  MISC_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TMISC ELSE 0;
  GMISC_AMT/D16.2CS = IF SRT_DT NE LAST SRT_DT THEN GTMISC ELSE 0;
 -**************************************************************************
-*DATE           NAME               DESCRIPTION
-*11/10/00      KASSOC-DK            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-**************************************************************************
  NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END

-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_DRIL;
-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'AIRHOLD3';
-INCLUDE DRILWHRS
-************************************************************

-SKIP_DRIL

TABLE FILE AIRHOLD3
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*
&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*11/10/00      KASSOC-DK            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-*
-**********************************************************************
-* Where statements for Drilled Report
-**********************************************************************
-*
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
-*
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10
-*
-**********************************************************************
-* Report specific footer(s)
-**********************************************************************
-*
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*11/10/00      KASSOC-DK            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&DTSTY
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
