-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO

-* File DRSAVING.FEX
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*----           ----                 ---------------------
-*8/31/00        SAC                   Initial Creation
-*****************************************************************  

-*************************************************************************
-*DATE         NAME             DESCRIPTION
-*09/27/00    IBISTL-SB         ADDED SET STATEMENTS TO GET THE VALUE
-*                              INTO THIS DRILLED FEX
-*09/27/00     IBISTL-SB        ADDED THE LOGIC FOR LOCATING THE SET FILE
-*                              IN THE WEBPATH AND THEN EXECUTING THE SET
-*                              FILE
-*09/27/00    IBISTL-SB         REMOVED THE ';' FROM THE SET ASNAMES 
-*                              STATEMENT
-*09/27/00    IBISTL-SB         CHANGED THE &&FIELD PARAMETERS TO MIXED
-*                              CASE
-* 3/07/01   TANDT-SS           CHANGE ALL NUMERIC FORMATS FROM P (PACKED) TO
-*                              D (FLOATING-POINT DOUBLE PRECISION) TO PROVIDE
-*                              CONSISTENCY IN ROUNDING AND PERCENTAGES.
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*7/26/04    JOY                Added/Changed Norecords logic
-* ADDED TRANTYPE INCLUDE - JK 12/19/14 STORY ID S-06492
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

-*************************************************************************

-DEFAULT &&CPFLAG = 'XX';
-DEFAULT &&MONTH = 'XX';
-DEFAULT &&SETF = 'XX';

-SET &&CPFLAG = IF &PERIOD EQ 'CURRENT' THEN 'CP' ELSE 'PP';
-SET &&MONTH = &DMNTH;

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;

-**** the following two lines added 11/03/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
-*
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT


  
-*EX TSSETDB SETFILE=DRSAVE
-* EX SETDBLS SETFILE=&&SETF
-*RUN
-********************************IBISTL-SB-ADD*************************
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN


EX TSSETDB SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-* CP (CURRENT PERIOD) OR PP (PRIOR PERIOD)
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&SUBHEAD = <MONTH;
-SET &&STD = 'STD_FARE AS STD,FARE';
-SET &&SAVE = 'SAVE_AMT AS SVGS,AMT';
-SET &&LOW = 'LOW_AMT AS LOW,FARE';
-SET &&LOSTS = 'LOST_SAV_AMT AS LOST,SVGS';


-IF &&CPFLAG EQ 'CP' THEN GOTO CURPERIOD
-  ELSE IF &&CPFLAG EQ 'PP' THEN GOTO PRPERIOD
-  ELSE GOTO CURMONTH; 

-CURPERIOD
-SET &&DCHK='WHERE TRN_DATE GE '&&CYFMDT.EVAL' AND TRN_DATE LE '&&CYTODT.EVAL'';
-SET &&FROMDT = &&CYFMDT;
-SET &&TODT = &&CYTODT;
-GOTO ENDDATE

-PRPERIOD
-SET &&DCHK='WHERE TRN_DATE GE '&&PYFMDT.EVAL' AND TRN_DATE LE '&&PYTODT.EVAL'';
-SET &&FROMDT = &&PYFMDT;
-SET &&TODT = &&PYTODT;
-GOTO ENDDATE

-ENDDATE

-*********************IBISTL-SB CHANGES MADE *********************
-*  BYPASS FIELDS; ALL FIELDS WILL BE PRINTED NOW
-*****************************************************************
-GOTO OUTSIDE;
-FIELDS
-IF &&FIELD EQ 'Standard Fare' THEN GOTO STDFARE
-  ELSE IF &&FIELD EQ 'Fare Paid' THEN GOTO FAREP
-  ELSE IF &&FIELD EQ 'Lowest Fare' THEN GOTO LOWFARE
-  ELSE IF &&FIELD EQ 'Savings Amount' THEN GOTO SAVAMT
-  ELSE IF &&FIELD EQ 'Lost Savings' THEN GOTO LSAVINGS
-  ELSE GOTO STDFARE;

-GOTO OUTSIDE

-STDFARE  
-SET &&RPT_TTL1 = '                   STANDARD FARE DETAIL    ';
-SET &&LOW = '';
-SET &&LOSTS = '';
-GOTO OUTSIDE

-FAREP 
-SET &&RPT_TTL1 = '                      FARE PAID DETAIL     ';
-SET &&STD = '';
-SET &&SAVE = '';
-SET &&LOW = '';
-SET &&LOSTS = '';
-GOTO OUTSIDE

-LOWFARE
-SET &&RPT_TTL1 = '                     LOWEST FARE DETAIL    ';
-SET &&STD = '';
-SET &&SAVE = '';
-GOTO OUTSIDE

-SAVAMT
-SET &&RPT_TTL1 = '                   SAVINGS AMOUNT DETAIL   ';
-SET &&LOW = '';
-SET &&LOSTS = '';
-GOTO OUTSIDE

-LSAVINGS
-SET &&RPT_TTL1 = '                    LOST SAVINGS DETAIL    ';
-SET &&STD = '';
-SET &&SAVE = '';
-GOTO OUTSIDE

-OUTSIDE
-SET &&RPT_TTL1 = '                  SAVINGS ANALYSIS DETAIL  ';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN

-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
-* EX AIRUSE
EX TSAIRUSE
-RUN

-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
  
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

  TKT_TYPE/A4         = IF (SEG_COUNT LE 0) AND
                           (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                           (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                           (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                           (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  MONTH/MT = TRN_DATE;
  SAVINGS/D11.2CS = SEG_STANDARD - FARE_PAID;
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
-INCLUDE TRANTYPE                         
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  AIRLINE	
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
  SEG_AMT
  SEG_TAX
  FARE_PAID	
  NET_TKT_CNT
  PASSNGR_NAME        
  REFUSE_CODE	
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  TKT_NUM
  TKT_TYPE       
  TRN_DATE	
  DPT_TIME
  XDPT_DT
  XTRAN_DT      
  MONTH
  TKT_SORT
  SEG_NBR
  SEG_STANDARD
  SAVINGS
  SEG_LOWEST
  DISSAVINGS
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

&&DCHK
-INCLUDE RPTPARMS
WHERE MONTH EQ &&MONTH
&&VOIDCHK
ON TABLE HOLD AS RPTHOLD
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

-TYPE After Extract

-TYPE Before Report
TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT'
FARE_PAID AS 'TFARE'
SEG_STANDARD AS 'TSTD'
SAVINGS AS 'TSAVE'
SEG_LOWEST AS 'TLOW'
DISSAVINGS AS 'DTSAVE'
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE RPTHOLD
PRINT AIRLINE   
      DISSAVINGS  
      EMB_APT_NM
      FARE_PAID   
      REFUSE_CODE 
      RTE_APT_NM  
      SEG_LOWEST
      SEG_STANDARD
      SAVINGS
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      MONTH
      XDPT_DT
      XTRAN_DT    
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT AIRLINE     
      DISSAVINGS
      EMB_APT_NM
      FARE_PAID   
      REFUSE_CODE 
      RTE_APT_NM  
      SAVINGS
      SEG_LOWEST
      SEG_STANDARD
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      MONTH
      XDPT_DT     
      XTRAN_DT
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TFARE
      TTKT 
      TSTD
-*corrected spelling of field name -SS
-*    TSAV
      TSAVE 
      TLOW
      DTSAVE
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

TABLE FILE AIRHOLD2
PRINT AIRLINE     
      DTSAVE
      EMB_APT_NM
      FARE_PAID   
      PASSNGR_NAME
      REFUSE_CODE 
      RTE_APT_NM
      TFARE
      TLOW
      TKT_NUM     
      TKT_TYPE	
      TRN_DATE
      TSTD
      TTKT
-*corrected spelling of fieldname -SS
-*    TSAV
      TSAVE
      XDPT_DT     
      DPT_TIME
      MONTH
      XTRAN_DT
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
DEFINE FILE AIRHOLD3
NET_TKT_CNT/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN PASSNGR_NAME ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
LOST_SAV_AMT/D9.2CS = IF SRT_DT NE LAST SRT_DT THEN DTSAVE ELSE 0;
LOW_AMT/D10.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;
-*corrected spelling of fieldname
-* SAVE_AMT/P12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSAV ELSE 0;
SAVE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSAVE ELSE 0;
STD_FARE/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSTD ELSE 0;
  NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END
-RUN

TABLE FILE AIRHOLD3
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'RSN,CDE'
XDPT_DT AS 'DPT,DTE'
EMB_APT_NM AS 'ORG'
RTE_APT_NM AS 'DST'
AIRLINE AS 'CR'
&&STD
FARE_AMT AS 'FARE,PAID'
&&SAVE
&&LOW
&&LOSTS
NET_TKT_CNT AS 'NET,TKT,CNT'
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY TICKET_NMBR AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'

-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
ON XTKT_SRT SKIP-LINE
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
"&&FOOTER"
"eTTek Review &DATE AT <NOWTOD <90 &&FOOTR"

-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2

END
-RUN
-*
-GOTO XXIT;


-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;


-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END


-XXIT

