-***************************************************************************
-* REPORT NAME: ADVANCED PURCHASE REPORT (AIR EXTRACT)
-* TOTAL COMPANY REPORTING
-***************************************************************************

-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================
DEFINE FILE SR_50   
   FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
   ADV_CAT/A8       = IF (ADV_PURCH LT 7) THEN '0 - 6' ELSE
                         IF (ADV_PURCH LT 14) THEN '07 - 13' ELSE
                         IF (ADV_PURCH LT 21) THEN '14 - 20' ELSE '21 +';                                     
   NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                            (SEG_NBR EQ '01') AND
                            (FARE_PAID GE 0) AND
                            (SEG_COUNT EQ 1) AND
                            (CONJ_REL EQ 1) THEN 1 ELSE
                         IF (TICKET_CODE EQ '1') AND
                            (SEG_NBR EQ '01') AND
                            (EX_FLAG EQ 'F') THEN (-1) ELSE
                         IF (TICKET_CODE EQ '1') AND
                            (SEG_NBR EQ '01') AND
                            (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
   NEW_ADV_PURCH1/P8 = IF SEG_NBR EQ '01' THEN ADV_PURCH ELSE 0; 
  
   REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                             (SEG_NBR EQ '01') AND
                             (FARE_PAID GE 0) AND
                             (SEG_COUNT EQ 1) AND
                             (CONJ_REL EQ 1) THEN 0 ELSE
                          IF (TICKET_CODE EQ '1') AND
                             (SEG_NBR EQ '01') AND
                             (EX_FLAG EQ 'F') THEN 1 ELSE
                          IF (TICKET_CODE EQ '1') AND
                             (SEG_NBR EQ '01') AND
                             (RF_FLAG EQ 'F') THEN 1 ELSE 0;  
    TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                             (SEG_NBR EQ '01') AND
                             (FARE_PAID GE 0) AND
                             (SEG_COUNT EQ 1) AND
                             (CONJ_REL EQ 1) THEN 1 ELSE 0;
    XAP/A8 = EDIT(ADV_PURCH);                             
    XARR_DOW/W           = ARR_DATE;
    XARR_DT/MDYY         = ARR_DATE;
    XDPT_DOW/W           = DPT_DATE;
    XDPT_DT/MDYY         = DPT_DATE;
    XTRAN_DT/MDYY        = &&WHATDATE;
    ADV_DAYS/A8 =  IF ADV_PURCH LT 21 THEN XAP ELSE ADV_CAT;
-*     NEW_ADV_PURCH/D9 = IF RF_FLAG EQ ' ' THEN NEW_ADV_PURCH1 ELSE
-*                       IF EX_FLAG EQ ' ' THEN NEW_ADV_PURCH1 ELSE (NEW_ADV_PURCH1 * (-1));
    
END
-RUN

TABLEF FILE SR_50
PRINT FARE_PAID
      ADV_CAT
      NET_TKT_CNT
      REF_EXCH_CNT
      TKT_PURCH
      NEW_ADV_PURCH1
      TKT_NUM
WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) 
WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCADVP
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*                <---------------- STEP5 ---------------->
-*========================================================================
-*   PROCESS EXTRACT DATA FROM STEP4 - HOLD TO FOCUS DATABASE TCAREV       
-*========================================================================
DEFINE FILE TCADVP
ROLLCD/A8 = '&&ROLL';
NEW_ADV_PURCH/P8 = IF NET_TKT_CNT LT 0 THEN (NEW_ADV_PURCH1 * (-1)) ELSE NEW_ADV_PURCH1;
END
-RUN
TABLE FILE TCADVP
SUM FARE_PAID
    NET_TKT_CNT
    REF_EXCH_CNT
    TKT_PURCH
    NEW_ADV_PURCH AS 'ADV_PURCH'
BY TKT_NUM    
BY ROLLCD
BY ADV_CAT
ON TABLE HOLD AS HOLD2
END
-RUN

TABLE FILE HOLD2
SUM FARE_PAID
    NET_TKT_CNT
    REF_EXCH_CNT
    TKT_PURCH
    ADV_PURCH
BY ROLLCD
BY ADV_CAT
ON TABLE HOLD AS TCADVP1
END
-RUN

MODIFY FILE TCADV
FIXFORM FROM TCADVP1
MATCH ROLLCD ADV_CAT 
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON TCADVP1
END
-RUN


-NEXTONE



 
  
 
