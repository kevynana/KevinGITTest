-**********************************************************************************
-* Created for Principal
-**********************************************************************************
-* Changed PRTRA_TKT Define to exclude WN bookings per Jami JK 9/16/08
-* Changed REFUNDALE to exclude NF/LE Tickets per Jami 9/16/08
-* 06/27/2017 - RSB - S-36530 Report stream to be updated with standard footer
-* 09/13/2017 CA S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-*                       Backed out changes for S-36530 
-*
-*
-*

SET ASNAMES = ON
-* SET SPACE = 1
-SET HOLDATTR = ON;
-RUN
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT 
  PRTRAD/A1 = IF (DOD_TYPE NE 'H' OR 'I' OR 'J' OR 'M' OR 'X' OR 'A' OR 'L' OR
                  'N' OR 'S') THEN 'Y' ELSE 'N';
  LEVEL_DESC/A60 = &&LDESC;
  FARE_PAID/D12.2CS = SEG_AMT + SEG_TAX;
  FLAG/A1 = 'A';
  NET_TKT_CNT/P5CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
 XDPT_DT/A8MDYY         = DPT_DATE;
-*  XDPT_DT/A8MDYY = IF SEG_NBR EQ '01' THEN XDPT_DT1 ELSE ' ';
 XTRND/A8MDYY = TRN_DATE;
-*  XTRND/MDYY = IF SEG_NBR EQ '01' THEN XTRND1 ELSE ' ';
-* DEFINES ON PREVIOUS DEFINES
 PRADV/A1 = IF (ADV_PURCH LT 21) THEN 'Y' ELSE 'N';
 PRADV_TKT/P8CS = IF (PRADV EQ 'Y') THEN NET_TKT_CNT ELSE 0;
 PRADV_TKT1/P8CS = IF (PRADV EQ 'N') THEN NET_TKT_CNT ELSE 0;
 PRTRA_TKT/P8CS = IF (PRTRAD EQ 'Y') AND (AIRLINE NE 'WN' OR 'XH') THEN NET_TKT_CNT ELSE 0;
 PRONL_TKT/P8CS = IF (PRTRAD EQ 'N') THEN NET_TKT_CNT ELSE 0;
 REFUNDABLE/P8CS = IF NONREF_FLAG EQ 'R' OR ' ' AND REFUSE_CODE NE 'NF' OR 'LE' THEN NET_TKT_CNT ELSE 0;
 NONREF/P8CS = IF NONREF_FLAG EQ 'N' THEN NET_TKT_CNT ELSE 0;
 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT AROLL_DSC4
  DST_APC
  FARE_PAID
  LEVEL_DESC
  PRADV_TKT
  PRADV_TKT1
  PRTRA_TKT
  NET_TKT_CNT
  NONREF
  PASSNGR_NAME
  PRONL_TKT
  REFUNDABLE
  SEG_NBR
  TKT_DATE
  TKT_NUM
  TKT_SORT
  XDPT_DT
  XTRND
WHERE VOID.VOID_DATE EQ 0
-INCLUDE &AIRDATES
WHERE EMBARK NE 'DT1'
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS GRPTHOLD
END
-RUN

TABLE FILE GRPTHOLD
PRINT AROLL_DSC4
  DST_APC
  FARE_PAID
  LEVEL_DESC
  PRADV_TKT
  PRADV_TKT1
  PRTRA_TKT
  PRONL_TKT
  NET_TKT_CNT
  NONREF
  PASSNGR_NAME
  REFUNDABLE
  SEG_NBR
  TKT_NUM
  TKT_SORT
  XDPT_DT
  XTRND
BY TKT_DATE
ON TABLE HOLD AS GRPTHLD2 FORMAT FOCUS INDEX TKT_DATE
END
-RUN


-* Global Reporting JOIN
JOIN GRPTHLD2.TKT_DATE IN GRPTHLD2 TO GLOBAL.DATE IN GLOBAL AS J1


DEFINE FILE GRPTHLD2
NAMEC/A30 = CNAME;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
PAID_FAREX/D20.6 = FARE_PAID * CURR1X;

END
-RUN

TABLE FILE GRPTHLD2
PRINT AROLL_DSC4
  DST_APC
  FARE_PAID
  LEVEL_DESC
  PRADV_TKT
  PRADV_TKT1
  PRTRA_TKT
  PRONL_TKT
  NET_TKT_CNT
  NONREF
  PASSNGR_NAME
  REFUNDABLE
  TKT_DATE
  NAMEC
  PAID_FAREX/D16.2CS
  XDPT_DT
  SEG_NBR
  TKT_NUM
  TKT_SORT
  XTRND
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS RPTHOLD
END
-RUN

TABLE FILE RPTHOLD
PRINT AROLL_DSC4
  FARE_PAID
  LEVEL_DESC
  PRADV_TKT
  PRADV_TKT1
  PRTRA_TKT
  PRONL_TKT
  NET_TKT_CNT
  NONREF
  PASSNGR_NAME
  REFUNDABLE
  TKT_DATE
  NAMEC
  PAID_FAREX/D16.2CS
  XDPT_DT
  SEG_NBR
  TKT_NUM
  TKT_SORT
  XTRND
BY DST_APC
ON TABLE HOLD AS AIR1
END
-RUN

TABLE FILE AIRPORT
PRINT AIRPORT_NAME
BY AIRPORT_CODE AS 'DST_APC'
ON TABLE HOLD AS AIR2
END
-RUN

MATCH FILE AIR1
PRINT AROLL_DSC4
  FARE_PAID
  LEVEL_DESC
  PRADV_TKT
  PRADV_TKT1
  PRTRA_TKT  
  PRONL_TKT
  NET_TKT_CNT
  NONREF
  PASSNGR_NAME
  REFUNDABLE
  TKT_DATE
  NAMEC
  PAID_FAREX
  XDPT_DT
  SEG_NBR
  TKT_NUM
  TKT_SORT
  XTRND
BY DST_APC
ON TABLE HOLD
RUN 
FILE AIR2
SUM AIRPORT_NAME
BY DST_APC
AFTER MATCH HOLD AS RPTHOLD1 OLD-AND-NEW
END
-RUN


TABLE FILE RPTHOLD1
PRINT AIRPORT_NAME
  FARE_PAID
  PRADV_TKT
  PRADV_TKT1
  PRTRA_TKT
  PRONL_TKT
  NET_TKT_CNT
  NONREF
  REFUNDABLE
  PASSNGR_NAME
  PAID_FAREX/D16.2CS
  NAMEC
  XDPT_DT
  SEG_NBR
  TKT_NUM
  TKT_SORT
  XTRND
BY LEVEL_DESC
BY AROLL_DSC4
ON TABLE HOLD AS ABNONHLD
END
-RUN

-****************************************************
-* Departmental Goals
-****************************************************
TABLE FILE ABNONHLD
SUM PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID 
COMPUTE ADVG/P12S = (PRADV_TKT/NET_TKT_CNT)*100;
COMPUTE REFG/P12S = (REFUNDABLE/NET_TKT_CNT)*100;
COMPUTE TRAG/P12S = (PRTRA_TKT/NET_TKT_CNT)*100;
WHERE TOTAL ADVG GT 50 OR REFG GT 0 OR TRAG GT 10
BY AROLL_DSC4
ON TABLE HOLD AS GOALH1
END
-RUN


TABLE FILE ABNONHLD
SUM PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID
BY AROLL_DSC4
BY PASSNGR_NAME
BY TKT_NUM
ON TABLE HOLD AS GOALH2
END
-RUN

MATCH FILE GOALH2
PRINT PASSNGR_NAME PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID TKT_NUM
BY AROLL_DSC4
ON TABLE HOLD
RUN
FILE GOALH1
SUM ADVG REFG TRAG
BY AROLL_DSC4
AFTER MATCH HOLD AS GOALH3 OLD-AND-NEW
END
-RUN

TABLE FILE GOALH3
SUM PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID 
BY AROLL_DSC4
BY PASSNGR_NAME
BY TKT_NUM
WHERE PRADV_TKT NE ' ' OR PRTRA_TKT NE ' ' OR REFUNDABLE NE ' '
WHERE FARE_PAID GT 0
ON TABLE HOLD AS GOALH3
END
-RUN


TABLE FILE ABNONHLD
PRINT XDPT_DT XTRND AIRPORT_NAME LEVEL_DESC
BY AROLL_DSC4
BY PASSNGR_NAME
BY TKT_NUM
WHERE SEG_NBR EQ '01'
ON TABLE HOLD AS G1
END
-RUN

MATCH FILE GOALH3
PRINT PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID
BY AROLL_DSC4
BY PASSNGR_NAME
BY TKT_NUM
ON TABLE HOLD
RUN
FILE G1
SUM XDPT_DT XTRND AIRPORT_NAME LEVEL_DESC
BY AROLL_DSC4
BY PASSNGR_NAME
BY TKT_NUM
AFTER MATCH HOLD AS GOAL4 OLD-AND-NEW
END
-RUN

TABLE FILE GOAL4
SUM PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID
BY LEVEL_DESC
BY AROLL_DSC4
BY PASSNGR_NAME
BY TKT_NUM 
BY XDPT_DT
BY XTRND
BY AIRPORT_NAME
ON TABLE HOLD AS GOAL5
END
-RUN

DEFINE FILE GOAL5
PAX_NAME/A33 = PASSNGR_NAME;
TKT/A10 = TKT_NUM;
XDPT/A8MDYY = XDPT_DT;
XTRN/A8MDYY = XTRND;
DEST/A25 = AIRPORT_NAME;
PRADV_TKT1/P8CS = PRADV_TKT;
PRTRA_TKT1/P8CS = PRTRA_TKT;
REF_TKT1/P5CS = REFUNDABLE;
FARE/D12.2CS = FARE_PAID;
LEVD/A60 = LEVEL_DESC;
AROLLD4/A60 = AROLL_DSC4;
END
-RUN

TABLE FILE GOAL5
SUM PRADV_TKT1 PRTRA_TKT1 REF_TKT1 FARE
COMPUTE ADVPA/D3S% = 0;
COMPUTE TRAPA/D3S% = 0;
COMPUTE REFPA/D3S% = 0;
BY LEVD
BY AROLLD4
BY PAX_NAME
BY TKT
BY XDPT
BY XTRN
BY DEST
ON TABLE HOLD AS GOAL6A
END
-RUN

TABLE FILE GOAL6A 
PRINT PRADV_TKT1 PRTRA_TKT1 REF_TKT1 FARE
      ADVPA TRAPA REFPA 
BY LEVD
BY AROLLD4
BY PAX_NAME
BY TKT
BY XDPT
BY XTRN
BY DEST
ON TABLE HOLD AS GOAL6 FORMAT FOCUS
END
-RUN

-***********************************************************************
-* Totals Use Add
-***********************************************************************
-* Departments
-***********************************************************************
TABLE FILE ABNONHLD
SUM PRONL_TKT AS 'ONL'
    PRADV_TKT1 AS 'ADV'
    NONREF AS 'NREF'
    NET_TKT_CNT AS 'NT1'
    FARE_PAID
    REFUNDABLE
BY AROLL_DSC4 
BY TKT_NUM
ON TABLE HOLD AS DP1
END
-RUN

TABLE FILE DP1
SUM ONL ADV NREF NT1 REFUNDABLE
BY AROLL_DSC4
WHERE FARE_PAID GT 0
ON TABLE HOLD AS DP2
END
-RUN

TABLE FILE GOAL5
PRINT PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID
      LEVEL_DESC PASSNGR_NAME TKT_NUM XDPT_DT XTRND AIRPORT_NAME
BY AROLL_DSC4
ON TABLE HOLD AS G5
END
-RUN


MATCH FILE G5
SUM PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID
    LEVEL_DESC PASSNGR_NAME TKT_NUM XDPT_DT XTRND AIRPORT_NAME
BY AROLL_DSC4
ON TABLE HOLD
RUN
FILE DP2
SUM NT1 ONL ADV NREF
BY AROLL_DSC4
AFTER MATCH HOLD AS TDP1 OLD-AND-NEW
END
-RUN


DEFINE FILE TDP1
PAX_NAME/A33 = 'DEPT TOTAL';
TKT/A10 = ' ';
XDPT/A8MDYY = ' ';
XTRN/A8MDYY = ' ';
DEST/A25 = ' ';
PRADV_TKT1/P8CS = 0;
PRTRA_TKT1/P8CS = 0;
REF_TKT1/P5CS = 0;
FARE/D12.2CS = 0;
LEVD/A60 = LEVEL_DESC;
AROLLD4/A60 = AROLL_DSC4;
END
-RUN

TABLE FILE TDP1
SUM PRADV_TKT1 PRTRA_TKT1 REF_TKT1 FARE LEVD
    ADV ONL NREF NT1
COMPUTE ADVPA/D3% = IF (NT1-ADV) EQ 0 THEN 100 ELSE 
                     IF ADV EQ 0 THEN 0 ELSE (ADV/NT1)*100;
COMPUTE TRAPA/D3% = IF (NT1-ONL) EQ 0 THEN 100 ELSE
                     IF ONL EQ 0 THEN 0 ELSE (ONL/NT1)*100;
COMPUTE REFPA/D3% = IF (NT1-NREF) EQ 0 THEN 100 ELSE
                     IF NREF EQ 0 THEN 0 ELSE (NREF/NT1)*100;
BY AROLLD4
BY PAX_NAME
BY TKT
BY XDPT
BY XTRN
BY DEST
ON TABLE HOLD AS TG1
END
-RUN


TABLE FILE TG1
PRINT PRADV_TKT1 PRTRA_TKT1 REF_TKT1 FARE 
      ADVPA TRAPA REFPA
BY LEVD
BY AROLLD4
BY PAX_NAME
BY TKT
BY XDPT
BY XTRN
BY DEST
ON TABLE HOLD AS GOAL7A FORMAT FOCUS
END
-RUN


-**********************************************************************
-* BU Total
-**********************************************************************

TABLE FILE ABNONHLD
SUM PRONL_TKT AS 'ONL'
    PRADV_TKT1 AS 'ADV'
    NONREF AS 'NREF'
    NET_TKT_CNT AS 'NT1'
    FARE_PAID
BY LEVEL_DESC
BY TKT_NUM
ON TABLE HOLD AS BU1
END
-RUN

TABLE FILE BU1
SUM ONL ADV NREF NT1
BY LEVEL_DESC
WHERE FARE_PAID GT 0
ON TABLE HOLD AS BU2A
END
-RUN

TABLE FILE GOAL5
PRINT PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID
      AROLL_DSC4 PASSNGR_NAME TKT_NUM XDPT_DT XTRND AIRPORT_NAME
BY LEVEL_DESC
ON TABLE HOLD AS BU2
END
-RUN


MATCH FILE BU2
SUM PRADV_TKT PRTRA_TKT NET_TKT_CNT REFUNDABLE FARE_PAID
    AROLL_DSC4 PASSNGR_NAME TKT_NUM XDPT_DT XTRND AIRPORT_NAME
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE BU2A
SUM NT1 ONL ADV NREF
BY LEVEL_DESC
AFTER MATCH HOLD AS TBU1 OLD-AND-NEW
END
-RUN


DEFINE FILE TBU1
PAX_NAME/A33 = ' ';
TKT/A10 = ' ';
XDPT/A8MDYY = ' ';
XTRN/A8MDYY = ' ';
DEST/A25 = ' ';
PRADV_TKT1/P8CS = 0;
PRTRA_TKT1/P8CS = 0;
REF_TKT1/P5CS = 0;
FARE/D12.2CS = 0;
LEVD/A60 = LEVEL_DESC;
AROLLD4/A60 = ' ';
END
-RUN

TABLE FILE TBU1
SUM PRADV_TKT1 PRTRA_TKT1 REF_TKT1 FARE
COMPUTE ADVPA/D3% = IF (NT1-ADV) EQ 0 THEN 100 ELSE 
                     IF ADV EQ 0 THEN 0 ELSE (ADV/NT1)*100;
COMPUTE TRAPA/D3% = IF (NT1-ONL) EQ 0 THEN 100 ELSE
                     IF ONL EQ 0 THEN 0 ELSE (ONL/NT1)*100;
COMPUTE REFPA/D3% = IF (NT1-NREF) EQ 0 THEN 100 ELSE
                     IF NREF EQ 0 THEN 0 ELSE (NREF/NT1)*100;
BY LEVD
BY AROLLD4
BY PAX_NAME
BY TKT
BY XDPT
BY XTRN
BY DEST
ON TABLE HOLD AS GOALT1
END
-RUN

TABLE FILE GOALT1
PRINT PRADV_TKT1 PRTRA_TKT1 REF_TKT1 FARE
      ADVPA TRAPA REFPA
BY LEVD
BY AROLLD4
BY PAX_NAME
BY TKT
BY XDPT
BY XTRN
BY DEST
ON TABLE HOLD AS GOAL7 FORMAT FOCUS
END
-RUN


-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &NONH1=&HLDPATH || 'GOAL6.FOC';
-SET &NONH2=&HLDPATH || 'GOAL7.FOC';
-SET &NONH3=&HLDPATH || 'GOAL7A.FOC';
-RUN

USE CLEAR *
-RUN

EX SUUSES
-RUN

USE ADD 
&NONH1 AS GOAL6
&NONH2 AS GOAL6
&NONH3 AS GOAL6
END
-RUN

DEFINE FILE GOAL6
ARD4/A60 = IF AROLLD4 EQ ' ' THEN 'BU TOTAL' ELSE AROLLD4;
FLAG2/A1 = IF ARD4 EQ 'BU TOTAL' THEN '3' ELSE 
           IF PAX_NAME EQ 'DEPT TOTAL' AND AROLLD4 NE ' ' THEN '2' ELSE '1';
END
-RUN

TABLE FILE GOAL6
PRINT PRADV_TKT1 PRTRA_TKT1 REF_TKT1 FARE
      ADVPA TRAPA REFPA 
BY LEVD
BY ARD4
BY FLAG2
BY PAX_NAME
BY TKT
BY XDPT
BY XTRN
BY DEST
ON TABLE HOLD AS GOALT
END
-RUN

-SET &&RPTSUF = 'NONCOMPLIANCE';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');

-INCLUDE FDEFRPTS
-RUN


-IF &&CURRENCY EQ 'USD' THEN GOTO NEXT;

-SET &&SUBHEAD = <NAMEC;

-NEXT;
-*
-SET &&PAHDR  = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL'   ELSE &&DTSTY;
-*
DEFINE FILE GOALT
NOWTOD/A8 WITH LEVD = HHMMSS (NOWTOD);
BLANK/A1 = ' ';
ADVPA1/A4 = FTOA(ADVPA, '(D3%)', 'A4');
ADVPA11/A4 = FTOA(ADVPA, '(D3S)', 'A4');
ADVPA1A/A4 = IF FLAG2 EQ '2' OR '3' THEN ADVPA1 ELSE ADVPA11;
TRAPA1/A4 = FTOA(TRAPA, '(D3%)', 'A4');
TRAPA11/A4 = FTOA(TRAPA, '(D3S)', 'A4');
TRAPA1A/A4 = IF FLAG2 EQ '2' OR '3' THEN TRAPA1 ELSE TRAPA11;
REFPA1/A4 = FTOA(REFPA, '(D3%)', 'A4');
REFPA11/A4 = FTOA(REFPA, '(D3S)', 'A4');
REFPA1A/A4 = IF FLAG2 EQ '2' OR '3' THEN REFPA1 ELSE REFPA11;
END
-RUN


TABLE FILE GOALT
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPCID;
" "
-NOSPCID;

SUM FARE AS 'FARE,PAID' REF_TKT1 AS 'NON-REF,GOAL,100%'
    PRADV_TKT1 AS '21+,ADV PURCH,GOAL >50%'
    PRTRA_TKT1 AS 'TRAVEL,PORT,GOAL >90%'
    REFPA1A AS 'NON-REF %'
    ADVPA1A AS '21+ ADV %'
    TRAPA1A AS 'TRAVELPORT %'    
BY LEVD AS 'BUSINESS UNIT'
BY ARD4 AS 'DEPARTMENT'
BY FLAG2 NOPRINT
BY PAX_NAME AS 'PASSENGER,NAME'
BY TKT 
BY XTRN AS 'TRAN,DATE'
BY XDPT AS 'DEPART,DATE'
BY DEST AS 'DESTINATION'
-*
-INCLUDE FOOTERDT
-*
ON TABLE SET PAGE-NUM OFF
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=7, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, LINE=4, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=13, SIZE=10, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
-* TYPE=REPORT, WRAP=1,$
TYPE=REPORT, COLUMN=N1, WRAP=1,$
TYPE=REPORT, COLUMN=N2, WRAP=1,$
TYPE=REPORT, COLUMN=N4, WRAP=1,$
TYPE=REPORT, COLUMN=N6, WRAP=.7,$
TYPE=REPORT, COLUMN=N7, WRAP=.7,$
TYPE=REPORT, COLUMN=N8, WRAP=1,$
TYPE=REPORT,COLUMN=N9, WRAP=.7,$
TYPE=REPORT,COLUMN=N10, WRAP=.7,$
TYPE=REPORT,COLUMN=N11, WRAP=.7,$
TYPE=REPORT,COLUMN=N12, WRAP=.7,$
TYPE=REPORT,COLUMN=N13, WRAP=.7,$
TYPE=REPORT,COLUMN=N14, WRAP=.7,$
TYPE=REPORT,COLUMN=N15, WRAP=.7,$
TYPE=REPORT, STYLE=BOLD, WHEN=PAX_NAME EQ 'DEPT TOTAL', $
TYPE=REPORT, STYLE=BOLD, WHEN=ARD4 EQ 'BU TOTAL', $

TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $
-* TYPE=REPORT, IMAGE=&&TNTPIC,  POSITION=(&&TNTDTPOS), SIZE=(&&TNTDTSIZ), $
-* TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLDTPOS), SIZE=(&&ROLDTSIZ), $
-*
-INCLUDE &&PASTYD
-*TYPE=FOOTING, LINE=4, STYLE=BOLD,POSITION=(&FTR_DATEPOS),$ 
-*
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN
-*
-SET &&AIRREC = IF &LINES EQ 0 THEN 'N' ELSE 'Y';
-*
-* SET SPACE = 2


