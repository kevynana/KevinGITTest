
-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-********************************************************************
-* Modifications:

-********************************************************************
SET ASNAMES=ON
-RUN


TABLE FILE &&RPT_HOLD
SUM FARE_PAID AS 'TFARE'
    NET_TKT_CNT AS 'TNET'
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE &&RPT_HOLD
PRINT CLASS
  DPT_TIME
  EMB_APT_NM
  FARE_PAID
  NET_TKT_CNT
  RTE_APT_NM
  SEG_NBR
  TKT_NUM
  TKT_TYPE
  XPSNGR_NM
  XDPT_DT
  XTRAN_DT
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT CLASS
  DPT_TIME
  EMB_APT_NM
  FARE_PAID
  NET_TKT_CNT
  RTE_APT_NM
  SEG_NBR
  TKT_NUM
  TKT_TYPE
  XDPT_DT
  XPSNGR_NM
  XTRAN_DT
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY SEG_NBR
BY TRN_DATE
ON TABLE HOLD
RUN
FILE AIRHOLD1
SUM TFARE
    TNET
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AMTMATCH OLD-OR-NEW
END
-RUN


TABLE FILE &&RPT_HOLD
PRINT CLASS_CAT 
BY LEVEL_DESC
BY XPSNGR_NM
WHERE INTDOM EQ 'I'
ON TABLE HOLD AS XTEST
END
-RUN

MATCH FILE AMTMATCH
PRINT CLASS
  DPT_TIME
  EMB_APT_NM
  FARE_PAID
  NET_TKT_CNT
  RTE_APT_NM
  SEG_NBR
  TKT_NUM
  TKT_SORT
  TKT_TYPE
  TFARE
  TNET
  TRN_DATE
  XDPT_DT
  XPSNGR_NM
  XTRAN_DT
BY LEVEL_DESC
BY XPSNGR_NM
ON TABLE HOLD
RUN 
FILE XTEST
SUM CLASS_CAT
BY LEVEL_DESC
BY XPSNGR_NM
AFTER MATCH HOLD AS JOYTEST OLD-AND-NEW
END
-RUN

TABLE FILE JOYTEST
PRINT CLASS
  CLASS_CAT
  DPT_TIME
  EMB_APT_NM
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  RTE_APT_NM
  TFARE
  TKT_NUM
  TKT_SORT
  TKT_TYPE
  TNET
  TRN_DATE
  XDPT_DT
  XPSNGR_NM
  XTRAN_DT
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN


DEFINE FILE AIRHOLD3
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||LEVEL_DESC||XPSNGR_NM;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
NET_CNT/D8CS     = IF SRT_DT NE LAST SRT_DT THEN TNET ELSE 0;
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*7/31/00     IBISTL-KR             ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-**************************************************************************
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END

-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_DRIL;
-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'AIRHOLD3';
-INCLUDE DRILWHRS
-************************************************************
-SKIP_DRIL

TABLE FILE AIRHOLD3
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*
&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-*
-**********************************************************************
-* Where statements for Drilled Report
-**********************************************************************
-*
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
-*
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10
-*
-**********************************************************************
-* Report specific footer(s)
-**********************************************************************
-*
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT 

ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
