-**************************************************************
-* Date       Author    Modification
-**************************************************************
-* 01/23/2012 GSE       Removed parens from around OLIMIT to
-*                      prevent errors in 7.7.03
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-**************************************************************
-INCLUDE SETECHO
 
CREATE FILE TCLVND
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCLIMDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCL102EX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-*========================================================================
-*  GET THE FINAL REPORT
-*========================================================================
-SET &&RANK_METH = IF &&RANK_METH EQ 'CAR_AMT' THEN 'CAR_AMT'
- ELSE IF &&RANK_METH EQ 'NMBR_DAYS' THEN 'NMBR_DAYS'
- ELSE 'NMBR_DAYS';
 
-SET &&RANK_LIMIT2 = 10;
 
TABLE FILE TCLVND
SUM BOOKINGS
    NBR_DAYS/I8 AS 'NMBR_DAYS'
    RENTAL_AMT/D12.2 AS 'CAR_AMT'
BY VENDOR_CODE
BY VENDOR_NAME
BY ROLLCD
BY GZONE
ON TABLE HOLD AS LHOLD1
END
-RUN
 
 
 
TABLE FILE LHOLD1
PRINT BOOKINGS
    CAR_AMT
    NMBR_DAYS
    VENDOR_CODE
    GZONE
BY VENDOR_NAME
RANKED BY HIGHEST &&RANK_METH
BY ROLLCD
ON TABLE HOLD AS LHOLD2
END
-RUN
 
-SET &&RANK_LIMIT = 50;
 
DEFINE FILE LHOLD2
  DRANK/D5      = RANK ;
  ARANKX/A6     = FTOA(DRANK, '(D5)', 'A6');
  ARANK/A5      = EDIT(ARANKX,'$99999');
  RANK_LIMIT/I5 = &&RANK_LIMIT2 ;
  RNK_VAL/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                 IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
 
 
TABLE FILE LHOLD2
PRINT ROLLCD
   BOOKINGS
    CAR_AMT
    NMBR_DAYS
    VENDOR_CODE
    GZONE
BY VENDOR_NAME
BY RNK_VAL
ON TABLE HOLD AS LHOLD3
END
-RUN
 
DEFINE FILE LHOLD3
 
ROLL_PRINT/A8 = IF RNK_VAL LE '&&RANK_LIMIT2' THEN ROLLCD ELSE 'OTHER';
 
 
END
 
 
TABLE FILE LHOLD3
SUM ROLLCD
    BOOKINGS
    CAR_AMT
    NMBR_DAYS
    VENDOR_CODE
BY VENDOR_NAME
BY RNK_VAL
BY ROLL_PRINT
BY GZONE
ON TABLE HOLD AS LHOLD4
END
-RUN
 
 
TABLE FILE LHOLD1
SUM &&RANK_METH
BY VENDOR_NAME
ON TABLE HOLD AS RHOLD1
END
-RUN
 
TABLE FILE RHOLD1
PRINT &&RANK_METH
RANKED BY HIGHEST &&RANK_METH AS 'TDAYS'
BY VENDOR_NAME
ON TABLE HOLD AS RHOLD2
END
-RUN
 
 
MATCH FILE LHOLD4
PRINT ROLLCD
    BOOKINGS
    CAR_AMT
    NMBR_DAYS
    VENDOR_CODE
    RNK_VAL
    ROLL_PRINT
    GZONE
BY VENDOR_NAME
ON TABLE HOLD
RUN
FILE RHOLD2
SUM TDAYS
BY VENDOR_NAME
AFTER MATCH HOLD AS LHOLD5 OLD-OR-NEW
END
-RUN
 
 
TABLE FILE LHOLD5
PRINT ROLLCD
    BOOKINGS
    CAR_AMT
    NMBR_DAYS
    VENDOR_CODE
    RNK_VAL
    ROLL_PRINT
   VENDOR_NAME
   GZONE
RANKED BY HIGHEST TDAYS
ON TABLE HOLD AS LHOLD6
END
-RUN
 
DEFINE FILE LHOLD6
  ORG_LIMIT/I3 = &&RANK_LIMIT ;
  OLIMIT/A3    = EDIT(ORG_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF VENDOR_NAME NE LAST VENDOR_NAME THEN (LAST LIST + 1)
    ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999');
  ORG_VALUE/A6 = IF ORG_LIMIT EQ 0 THEN BRANK ELSE
               IF LIST GT ORG_LIMIT THEN 'OTHER' ELSE FRANK;
END
 
TABLE FILE LHOLD6
SUM ROLLCD
    BOOKINGS
    CAR_AMT
    NMBR_DAYS
    VENDOR_CODE
    RNK_VAL
    ROLL_PRINT
   VENDOR_NAME
   TDAYS
     OLIMIT
    ORG_LIMIT
BY ORG_VALUE
BY VENDOR_NAME
BY RNK_VAL
BY ROLL_PRINT
BY GZONE
ON TABLE HOLD AS LHOLD7
END
-RUN
 
 
DEFINE FILE LHOLD7
VND_NM/A12 = IF ORG_VALUE LT OLIMIT THEN VENDOR_NAME ELSE 'ALL OTHER';
NOWTOD/A8 WITH ORG_VALUE = HHMMSS (NOWTOD);
END
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
 
 
TABLE FILE LHOLD7
-INCLUDE HEADER
"&&SUBHEAD"
"</2"
 &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
 
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
FOOTING BOTTOM
"&&FOOTER1"
"&&FOOTER2"
"Blank vendor codes with $0 rate excluded from this report"
" "
 
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
