-**********************************************************************
-* This program will retrieve all selected ROLLUP_CODEs from BRPTFLDS.
-* If no company was selected, it assumes all companies selected.
-* Date: Feb 09, 2004
-* 4/27/15 - DEB S-08598 - ADDED WHERE CLAUSE FOR TCTRNCSR
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  10/13/2016  REJ  S-25863 Updated to exclude DEMO-R.
-*  12/28/2016  REJ  S-26045 Updated for ER5.  Tried to make the code more
-*                           clear for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-**********************************************************************
-* -INCLUDE SETECHO
 
-IF &&FROMER5 EQ 'Y' GOTO :ER5LOGIC;
 
TABLE FILE BRPTFLDS
PRINT ROLLUP
BY INST_KEY NOPRINT
WHERE INST_KEY EQ &&IKEY
WHERE ROLLUP NE ' '
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
-SET &WHATROLLS = '';
-SET &OFACCT2 = 0;
 
-*check to see if it is included or excluded, OF ACCOUNT is checked
TABLE FILE BRPTFLDS
PRINT ROLL1.INCL1 OF_ACCT
BY INST_KEY NOPRINT
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE AS ROLL111
END
-RUN
 
-IF &LINES EQ 0 GOTO NOROLL1;
-READ ROLL111 &INCL1.1. &OFACCT2.1.
 
-CLOSE ROLL111
-NOROLL1
 
-SET &WHATROLLS = IF &OFACCT2 EQ 1 THEN 'WHERE OF_ACCOUNT EQ ' | '''Y''' ELSE
-                 IF &OFACCT2 EQ 2 THEN 'WHERE OF_ACCOUNT NE ' | '''Y''' ELSE ' ';
 
-IF &TOTROLLS EQ 0 THEN GOTO SelectAll;
-IF &INCL1 EQ 1 THEN GOTO GetCount;
-*Exclude condition
DEFINE FILE BRPTFLDS
ROLL/A10 = '''' | ROLLUP | '''';
END
 
TABLE FILE BRPTFLDS
PRINT ROLL
BY ROLLUP NOPRINT
ON TABLE SAVE AS ROLLEXCL
END
-RUN
 
TABLE FILE ROLLUP
PRINT ROLLUP_CODE
BY ROLLUP_CODE NOPRINT
WHERE UPD_ENABLE NE 'Z'
WHERE ROLLUP_CODE NE 'TOTAL-R' OR 'ABCD-R' OR 'ABCD2-R' OR 'ABCD3-R' OR 'DEMO-R'
WHERE COMP NE ' '
&WHATROLLS
WHERE NOT ROLLUP_CODE IN FILE ROLLEXCL
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
-GOTO GetCount;
 
-SelectAll
-IF '&&SETF.EVAL' EQ 'TCTRNCSR' GOTO CSRonly;
TABLE FILE ROLLUP
PRINT ROLLUP_CODE
BY ROLLUP_CODE NOPRINT
IF UPD_ENABLE NE 'Z'
-*IF UPD_ENABLE EQ 'X'
WHERE ROLLUP_CODE NE 'TOTAL-R' OR 'ABCD-R' OR 'ABCD2-R' OR 'ABCD3-R' OR 'DEMO-R'
WHERE COMP NE ' '
&WHATROLLS
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
 
 
-GOTO GetCount
 
-CSRonly
TABLE FILE  ROLLUP
PRINT ROLLUP_CODE
BY ROLLUP_CODE NOPRINT
IF UPD_ENABLE NE 'Z'
-*IF UPD_ENABLE EQ 'X'
WHERE ROLLUP_CODE NE 'TOTAL-R' OR 'ABCD-R' OR 'ABCD2-R' OR 'ABCD3-R' OR 'DEMO-R'
WHERE CSM_NAME EQ 'CSR' OR 'CSR TEAM'
WHERE COMP NE ' '
&WHATROLLS
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
-GOTO GetCount
 
 
-:ER5LOGIC
-* Need to look for the parameters TOT_ROLLUP_CODE and OF_ACCOUNT.  These are the only two that will drive
-* which rollup codes are going to be used, other parameters are data driven like 'branch', etc.
-DEFAULT &OFWHERE='';
 
TABLE FILE BRPT_FLDS_PARM
PRINT *
WHERE DB_FIELD EQ 'OF_ACCOUNT' OR 'TOT_ROLLUP_CODE'
ON TABLE HOLD AS HCHKPARM1
END
-RUN
-SET &ER5ALL=IF &RECORDS EQ 0 THEN 'Y' ELSE 'N';
 
JOIN CLEAR *
JOIN PARM_SEQ IN BRPT_FLDS_PARM TO ALL PARM_SEQ IN BRPT_FLDS_PARM_VALUES AS J1
-RUN
DEFINE FILE BRPT_FLDS_PARM
WHERE_OF/A200=IF DATA_VALUE_DESCRIPTION EQ 'No' THEN  'WHERE OF_ACCOUNT ' | OPERATOR ||  (' ' | '''' | 'N' | '''') ELSE
              'WHERE OF_ACCOUNT ' | OPERATOR ||  (' ' | '''' | 'Y' | '''');
END
TABLE FILE BRPT_FLDS_PARM
PRINT WHERE_OF
WHERE BRPT_FLDS_PARM.INST_KEY EQ BRPT_FLDS_PARM_VALUES.INST_KEY
WHERE DB_FIELD EQ 'OF_ACCOUNT'
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE SAVE AS HCHKPARM2
END
-RUN
-SET &OFWH=&RECORDS;
 
-READ HCHKPARM2 &OFWHERE.200
-RUN
JOIN CLEAR *
JOIN PARM_SEQ IN BRPT_FLDS_PARM TO ALL PARM_SEQ IN BRPT_FLDS_PARM_VALUES AS J1
JOIN INST_KEY IN BRPT_FLDS_PARM TO INST_KEY IN BRPT_FLDS_MISC AS J2
-RUN
DEFINE FILE BRPT_FLDS_PARM
ROLL/A10 = '''' || EDIT(DATA_VALUE,'99999999') || '''';
WHERER/A50=IF &OFWH EQ 0 THEN 'WHERE (ROLLUP_CODE ' | OPERATOR ELSE ' ' | SELECTION_AND_OR | ' (ROLLUP_CODE ' | OPERATOR;
END
TABLE FILE BRPT_FLDS_PARM
PRINT WHERER ROLL
WHERE BRPT_FLDS_PARM.INST_KEY EQ BRPT_FLDS_PARM_VALUES.INST_KEY
WHERE DB_FIELD EQ 'TOT_ROLLUP_CODE'
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE SAVE AS HCHKPARM3
END
-RUN
-SET &ROWH=&RECORDS;
 
TABLE FILE ROLLUP_SQL
PRINT ROLLUP_CODE
BY ROLLUP_CODE NOPRINT
IF UPD_ENABLE NE 'Z'
WHERE ROLLUP_CODE NE 'TOTAL-R' OR 'ABCD-R' OR 'ABCD2-R' OR 'ABCD3-R' OR 'DEMO-R'
WHERE COMP NE ' '
-IF &ER5ALL EQ 'Y' GOTO :ER5SKIP;
-SET &CNT = 0;
&OFWHERE.EVAL
-IF &ROWH EQ 0 GOTO :ER5SKIP;
-REPEAT RLoop1 &ROWH TIMES
-READ HCHKPARM3 NOCLOSE &ROWHERE.50, &RVALUE.10
-SET &CNT = &CNT + 1;
-IF &CNT GT 1 GOTO :RCONT;
&ROWHERE.EVAL &RVALUE
-GOTO RLoop1;
-:RCONT
OR &RVALUE
-RLoop1
)
-:ER5SKIP
 
-IF '&&SETF.EVAL' NE 'TCTRNCSR' GOTO :CSRonlyX;
WHERE CSM_NAME EQ 'CSR' OR 'CSR TEAM'
-:CSRonlyX
 
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
 
-GetCount
 
-SET &CNT = 0;
-REPEAT Loop1 &TOTROLLS TIMES
-READ ROLLSEL NOCLOSE &VAL.8.
-IF &VAL EQ ' ' THEN GOTO Loop1;
-SET &CNT = &CNT + 1;
-SET &ROLL.&CNT = &VAL;
-Loop1
 
