-*========================================================================
-*  bkupsu2.fex
-*  Changed to backup SU databases at every 2 hours.  
-*  Don't back up after 11:00pm and before 5:00am. 
-*  Only keep the most recent 3 days backups.
-*
-*  Date: 05/06/2010
-*
-*========================================================================

-INCLUDE SETECHO

-* Create variables for current run
-SET &DOW = DOWK(&YYMD, 'A4');

-SET &HOUR=EDIT(&TOD,'99');
-IF &HOUR GT 23 OR &HOUR LT 5 THEN GOTO DontBackUP;

DEFINE FILE ROLLUP
  THISDAY/YYMD = &YYMD;
  B3D/YYMD = DATEADD(THISDAY, 'D', (-4));
END
TABLE FILE ROLLUP
PRINT B3D
BY ROLLUP_CODE NOPRINT
WHERE RECORDLIMIT EQ 1
ON TABLE SAVE
END
-RUN
-READ SAVE &Before3Days.8.

-TYPE &Before3Days
-SET &DELDOW = DOWK(&Before3Days,'A4')

-TYPE &DELDOW
-SET &DAILYFOLDER = 'D:\DailyBackUps\' || '&DELDOW.EVAL' || '\';

-SET &CNT = 0;
-REPEAT TOEND 24 TIMES
-SET &HF = IF &CNT LT 10 THEN '0' | &CNT ELSE EDIT(&CNT);
-SET &SUBFOLDER = &DAILYFOLDER || '&HF.EVAL';
-SET &SUBFILES = &SUBFOLDER || '\*.*';

-SET &CHKFILE = &SUBFOLDER || '\RPT_FLDS.FOC';
DOS STATE &CHKFILE
-RUN
-IF &RETCODE NE 0 THEN GOTO INCREASECNT;

-DOS DEL /F /Q &SUBFILES
-DOS RD &SUBFOLDER

-INCREASECNT
-SET &CNT = &CNT + 1;
-TOEND
-DOS RD &DAILYFOLDER
-RUN

-SET &BKPATH='D:\DailyBackUps\' || &DOW || '\' || &HOUR || '\';

-* Make directory to store backup file
-DOS MKDIR &BKPATH

-SET &TOFILES = &BKPATH || '*.FOC';
-DOS XCOPY D:\TNT\TTracker\SUData\*.foc /y /d &TOFILES
-RUN

-DontBackUP