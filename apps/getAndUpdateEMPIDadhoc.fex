-**************************************************************************************************
-* getAndUpdateEMPIDadhoc.fex
-* this program is a one time task to query the rollup codes which used employee id in adhoc
-*
-* lsi 08/15/2016
-**************************************************************************************************
-INCLUDE SETECHO
SET ASNAME = ON
SET HOLDLIST = PRINTONLY

-SET &DIR = 'D:\TEMP\LSI\';

DEFINE FILE EMPIDRollup
  LVL/A11 = EDIT(EMPIDLEVEL);
  EMPIDFLD/A10='LEVEL' | EDIT(LVL,'$$$$$$$$$$9');
  LEVEL/A1= EDIT(LVL,'$$$$$$$$$$9');
END
TABLE FILE EMPIDRollup
BY LEVEL 
ON TABLE SAVE AS 'LEVELGRP'
END
-RUN

-SET &GROUPS = &LINES;
-REPEAT TOALLGROUPS &GROUPS TIMES
-READ LEVELGRP NOCLOSE &LEVEL.1.

-SET &FIELDNAME = 'LEVEL' | &LEVEL;

-SET &FILENAME = 'ROLLUP' | &LEVEL;
-SET &SAVEFILE = &DIR | '&FILENAME.EVAL' | '.TXT';

FILEDEF &FILENAME DISK &SAVEFILE
-RUN

DEFINE FILE EMPIDRollup
	ROLL/A10 = '''' | ROLLUP_CODE | '''';
END
TABLE FILE EMPIDRollup
PRINT ROLL 
BY ROLLUP_CODE NOPRINT
WHERE EMPIDLEVEL EQ &LEVEL
ON TABLE SAVE AS &FILENAME.EVAL
END
-RUN


-SET &HOLDFILE = 'HOLDF' | &LEVEL;
-SET &FPATH2 = &DIR | '&HOLDFILE.EVAL' | '.xls';
FILEDEF &HOLDFILE DISK &FPATH2
-RUN
-SET &TEMPFILE = 'THOLD' | &LEVEL;

-*GET ALL REPORT WHICH CONTAINS LEVEL# USED AS EMPLOYEE NUMBER
JOIN CLEAR *
JOIN SET_KEY IN RPT_INST TO SET_KEY IN RPT_SET AS J1
JOIN INST_KEY IN RPT_INST TO INST_KEY IN ADHOCREPORTFIELDS AS J2
END

TABLE FILE RPT_INST
PRINT COLUMN_NAME
BY ROLLUP_CODE AS 'ROLLUP'
BY USER_NAME
BY SET_KEY 
BY SET_NAME
BY INST_KEY
BY RPT_NAME
BY COLUMN_ORDER
WHERE ROLLUP_CODE IN FILE &FILENAME.EVAL
WHERE COLUMN_NAME EQ '&FIELDNAME.EVAL'
ON TABLE HOLD AS &TEMPFILE.EVAL
END
-RUN
-IF &LINES EQ 0 THEN GOTO TOALLGROUPS;

-*GENERATE EXCEL FILE FOR VALIDATION
TABLE FILE &TEMPFILE
PRINT COLUMN_NAME
BY ROLLUP
BY USER_NAME
BY SET_KEY 
BY SET_NAME
BY INST_KEY
BY RPT_NAME
BY COLUMN_ORDER
ON TABLE HOLD AS  &HOLDFILE FORMAT EXL2K
END
-RUN

-*CHANGE VALUE TO EMP_ID
DEFINE FILE &TEMPFILE
 NEWCOLNAME/A20 = 'EMP_ID';
END
TABLE FILE &TEMPFILE
PRINT NEWCOLNAME AS 'COLUMN_NAME'
BY INST_KEY
BY COLUMN_ORDER
ON TABLE HOLD 
END
-RUN

MODIFY FILE ADHOCREPORTFIELDS
FIXFORM FROM HOLD
MATCH INST_KEY COLUMN_ORDER
  ON MATCH UPDATE COLUMN_NAME
DATA ON HOLD
END
-RUN

-TOALLGROUPS

