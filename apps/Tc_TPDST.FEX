-***************************************************************************
-* REPORT NAME: TOP DESTINATION SUMMARY
-* TOTAL COMPANY REPORTING
-***************************************************************************
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO
CREATE FILE TCDST
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCDSTEX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
-SET &&RANK_METH   = NET_TKT_CNT;
 
-INCLUDE SetParmtime
-RUN
 
TABLE FILE TCDST
SUM FARE
    MILES
    NET_TKT_CNT
BY DST_APC
BY DST_APCX
ON TABLE HOLD AS HOLD1
END
-RUN
 
TABLE FILE HOLD1
PRINT FARE
      MILES
      NET_TKT_CNT
BY DST_APC
BY DST_APCX
ON TABLE HOLD AS HOLD2
END
-RUN
 
JOIN HOLD2.DST_APCX IN HOLD2 TO AIRPORT.APCODEX IN AIRPORT AS J2
-RUN
 
TABLE FILE HOLD2
SUM
    FARE
    MILES
    NET_TKT_CNT
    AIRPORT_NAME AS 'DAP_NAME'
 
RANKED BY HIGHEST &&RANK_METH
BY DST_APC
ON TABLE HOLD AS RNKHLD2
END
-RUN
 
 
-SET &&RANK_LIMIT  = 20;
DEFINE FILE RNKHLD2
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = IF DST_APC NE LAST DST_APC THEN (LAST LIST + 1) ELSE LAST LIST;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                  CRANK;
  RANK_VAL/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                IF RANK GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                ARANK;
  RANK_LAB1/A25 = IF RANK_VAL NE 'ALL OTHER' THEN &&RANK_LABEL1
                  ELSE 'ALL OTHER';
  RANK_LABEL1/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL1
                      ELSE ' ';
  RANK_LABEL2/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL2
                      ELSE ' ';
  RANK_LABEL3/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL3
                      ELSE ' ';
DAP_NM/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN DAP_NAME ELSE ' ';
END
 
TABLE FILE RNKHLD2
SUM DAP_NM AS 'DAP_NAME'
      DST_APC
      NET_TKT_CNT
      FARE
      MILES
      NET_TKT_CNT
 
BY RANK_VALUE
BY RANK_LABEL1
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
 
JOIN CLEAR *
-RUN
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
-TYPE BEFORE DEFINES
DEFINE FILE AIRHOLD3
NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
 
TABLE FILE AIRHOLD3
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
