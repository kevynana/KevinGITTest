-*1/30/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*  03/31/2015  REJ  S-05681 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-* 5/1/2015 - DEB - S-08674 CARRIED THROUGH NBR_DAYS IN FINAL HOLD FILE
-*10/16/15 - S-11851 Added logic for ABACHLOD (DOMESTIC ONLY) ABACHLOI (INTERNATIONAL ONLY) - JM
-*12/30/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels


-INCLUDE SETECHO
 
FILEDEF RPTPARMS CLEAR
-RUN
  
EX CARUSE
-RUN
 
-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN
 
DEFINE FILE BRPTINST
  RT/A3 = 'CAR';
END
TABLE FILE BRPTINST
PRINT RT AS 'RPT_GROUP'
BY INST_KEY
ON TABLE SAVE AS AIRACH00
END
-RUN
MODIFY FILE BRPTINST
FIXFORM INST_KEY/7 RPT_GROUP/3
MATCH INST_KEY
  ON MATCH UPDATE RPT_GROUP
DATA ON AIRACH00
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 CAR.ID/1 CAR.INCL1/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG CAR
  ON NOMATCH INCLUDE
  ON MATCH UPDATE CAR.INCL1
DATA ON AIRACH00
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :OTC1;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-GOTO :OTC2;
-:OTC1
EX CODEGEN
-RUN
-:OTC2

-SET &&IDLEVELBREAK = '';

 
-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN
 
DEFINE FILE &&EXTRACT
CAR_AMT/D12.2CS = RENTAL_AMT;
LEVEL_DESC1/A80      = &&LDESC;
CBOOK/P8CS = IF DAILY_RATE GE 0 THEN 1 ELSE (-1);
NBR_DAYS/D12 = NUM_DAYS;
CAR_DAYS/D12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
 
OTFLAG/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';
 
CTKTN/A10 = IF TKT_NUM EQ '0000000000' THEN 'CAR ONLY' ELSE TKT_NUM;
 
TRN_DATE/YYMD = INVOICE_DATE;
 
CI_AMT/D12.2CS = IF OTFLAG EQ 'Y' THEN CAR_AMT ELSE 0;
CIBOOK/D8CS = IF OTFLAG EQ 'Y' THEN CBOOK ELSE 0;
CT_AMT/D12.2CS = IF OTFLAG EQ 'N' THEN CAR_AMT ELSE 0;
CTBOOK/D8CS = IF OTFLAG EQ 'N' THEN CBOOK ELSE 0;
CIDAYS/D8CS = IF OTFLAG EQ 'Y' THEN CAR_DAYS ELSE 0;
CTDAYS/D8CS = IF OTFLAG EQ 'N' THEN CAR_DAYS ELSE 0;
FLAG/A5 = 'CAR';
-INCLUDE IDLEVDEFINE 
END
TABLE FILE &&EXTRACT
SUM CAR_AMT CBOOK CIBOOK CTBOOK CI_AMT CT_AMT CIDAYS CTDAYS NBR_DAYS AS 'TOTCDAY'
BY LEVEL_DESC
BY FLAG
-INCLUDE &CARDATES
-INCLUDE RPTPARMS
-IF '&&SETF.EVAL' EQ 'ABACHLD' GOTO INTD;
-IF '&&SETF.EVAL' EQ 'ABACHLI' GOTO INTI;
-GOTO INTDONE;
-INTD;
WHERE PICK_CTY.INTL_DOM EQ 'D'
-GOTO INTDONE;
-INTI;
WHERE PICK_CTY.INTL_DOM EQ 'I'
-GOTO INTDONE;
-INTDONE;
ON TABLE HOLD AS CARHLD
END
-RUN
