-TYPE  drSMair1
-INCLUDE SETECHO
-*1/28/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-* Corrected Drilldown Error found while working on Story ID S-06692 12/17/14 - JK
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

-DEFAULT &&OTFLAG ='XX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&LSEL = 'XX';
-DEFAULT &&CHG_CAT = 'XX';
-DEFAULT &&EMP_LVL = 'XX';


-SET &&TITLECK = 'N'; 
-SET &&DTSTY = 'DT';
-DEFAULT &FMO = ' ';
-DEFAULT &TMO = ' ';
-SET &&LSEL = &LSEL;
-SET &&DIFLAGCHK = ' ';
-SET &&FIELD = &PARM1;
-SET &&CHG_CAT = &CHG_CAT;
-SET &&EMP_LVL = &EMP_LVL;

-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';


 
-IF '&&LSEL.EVAL' EQ 'ALL OTHER' THEN GOTO FIRST;
 


-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-SET &&UNAME = &user;
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
EX RESET
-RUN


********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-**********************************************************************
-*
-**********************************************************************
-CONT


-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX SETDBLS SETFILE=&&SETF
-RUN

EX CNTLUPD
-RUN
-INCLUDE OTHPARMS
 
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   

-IF &&LSEL NE 'TOTAL' THEN GOTO NotTotal;

-SET &LCHK= ' ';
-SET &&TITLECK = 'Y';
-GOTO Next;

-NotTotal;
-SET &&RPT_TTL2 = '&LSEL.EVAL';

-SET &LEN2=
- ARGLEN(60, '&&RPT_TTL2.EVAL', 'I2');
-SET &&RPT_TTL2 = IF &LEN2 EQ 0 THEN ' ' ELSE
- SUBSTR(60,'&&RPT_TTL2.EVAL',1,&LEN2, &LEN2, 'A&LEN2.EVAL');


-SET &BK = '';
-IF &LEN2 EQ 0 THEN GOTO AddSp2;
-SET &Spaces = IF &LEN2 LT 60 THEN INT((60-&LEN2)/2) ELSE 0;
-REPEAT AddSp2 &Spaces TIMES
-SET &BK = ' ' | '&BK.EVAL';
-AddSp2
-SET &&RPT_TTL2 = &BK | '&&RPT_TTL2.EVAL';


 
TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN


-READ SAVE &LEVEL.2.

-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &LCHK='WHERE ' | &AROLL_DSC.EVAL | ' CONTAINS ' | '''&&LSEL.EVAL''';

 
-Next


-****
-* &&ITFLAG WILL EQUAL ONE OF THE FOLLOWING:
-* O (ONLINE), T (TRADITIONAL 2)
-****
-* &&DIFLAG WILL EQUAL ONE OF THE FOLLOWING:\
-* D (DOMESTIC), I (INTERNATIONAL), B (INTERNATIONAL AND DOMESTIC)
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&LSCHK = ' ';
-SET &&DIFLAGCHK = ' ';
-SET &&FARBAS = ' ';
-SET &&SUBHEAD = ' ';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NT_TKT_CNT AS NET,TKT,CNT';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&LOWEST = ' ';
-SET &&LSTSVGS = ' ';
-SET &&AVGTKT = ' ';
-SET &&ADVCHK= ' ';
-SET &&REFCHK = ' ';
-SET &&OTFLAGCHK = ' ';
-SET &&DCHK='WHERE TRN_DATE GE '&&FROMDT.EVAL' AND TRN_DATE LE '&&TODT.EVAL'';
-SET &&CC = 'WHERE CHG_CAT EQ ''&&CHG_CAT.EVAL''';
-SET &&EL = 'WHERE AROLL_DSC6 EQ ''&&EMP_LVL.EVAL''';

-SET &&SUBHEAD = '   ';


-GOTO FIELDS;
-IF &&DIFLAG EQ 'D' THEN GOTO DOMESTIC  
-  ELSE IF &&DIFLAG EQ 'I' THEN GOTO INTERNTL
-  ELSE GOTO FIELDS;

-DOMESTIC
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&SUBHEAD = 'DOMESTIC ONLY';
-GOTO FIELDS

-INTERNTL
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&SUBHEAD = 'INTERNATIONAL ONLY';


-FIELDS

-IF &&FIELD EQ 'A' THEN GOTO VOLUME ELSE 
-IF &&FIELD EQ 'N' THEN GOTO NET ELSE
-IF &&FIELD EQ 'L' THEN GOTO LOST ELSE
-IF &&FIELD EQ 'T' THEN GOTO ATP ELSE
-IF &&FIELD EQ '6' THEN GOTO ADV ELSE 
-IF &&FIELD EQ '7' THEN GOTO ADV2 ELSE 
-IF &&FIELD EQ 'O' THEN GOTO DONLINE ELSE 
-IF &&FIELD EQ 'R' THEN GOTO NON ELSE GOTO ENDOT;


-VOLUME
-SET &&RPT_TTL1 = '                     AIR VOLUME DETAIL     ';
-GOTO ENDOT

-NET
-SET &&RPT_TTL1 = '                     NET TICKET DETAIL     ';
-GOTO ENDOT

-LOST
-SET &&RPT_TTL1 = '                  LOST SAVINGS DETAIL';
-SET &&LSTKT = 'WHERE (LSTSVNGS GE .001) OR (LSTSVNGS LE -.001)'; 
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SAVINGS';
-SET &&LOWEST = 'LOW_TOT AS LOW,AMT';

-GOTO ENDOT

-ATP
-SET &&RPT_TTL1 = '                  AVERAGE TICKET DETAIL     ';
-SET &&AVGTKT = 'COMPUTE ATP/P12.2CSM = FARE_AMT/NT_TKT_CNT; AS ''AVG,TKT,COST''';
-GOTO ENDOT

-ADV
-SET &&RPT_TTL1='                 0-6 ADVANCED PURCHASE DETAIL';
-SET &&ADVCHK = 'WHERE CGADV EQ ''0-6''';
-SET &&AVGTKT = 'COMPUTE ATP/P12.2CSM = FARE_AMT/NT_TKT_CNT; AS ''AVG,TKT,COST''';
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SVGS';

-GOTO ENDOT

-ADV2
-SET &&RPT_TTL1='                 7-13 ADVANCED PURCHASE DETAIL';
-SET &&ADVCHK = 'WHERE CGADV EQ ''7-13''';
-SET &&AVGTKT = 'COMPUTE ATP/P12.2CSM = FARE_AMT/NT_TKT_CNT; AS ''AVG,TKT,COST''';
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SVGS';

-GOTO ENDOT

-DONLINE
-SET &&RPT_TTL1 = '       DOMESTIC ONLINE AIR VOLUME DETAIL     ';
-SET &&OTFLAGCHK = 'WHERE IFLAG EQ ''I''';
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';

-GOTO ENDOT

-NON
-SET &&RPT_TTL1 = '           NON-REFUNDABLE TICKET DETAIL     ';
-SET &&REFCHK = 'WHERE NON_FLAG EQ ''NON-REFUNDABLE''';
-SET &&FARBAS = 'FARE_BAS AS FARE,BASIS';

-*-SET &&SUBHEAD = 'NON-REFUNDABLE ONLY';
-ENDOT
 

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

EX AIRUSE
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
 
DEFINE FILE SR_50
  CHG_CD/A1 = EDIT(LEVEL1, '9');
  CHG_CAT/A10 = IF CHG_CD EQ 'B' THEN 'B' ELSE
                  IF CHG_CD EQ 'G' THEN 'G' ELSE
                IF CHG_CD EQ 'M' THEN 'M' ELSE 'NON-BMG';
  FARE_PAID/P12.2  = SEG_AMT + SEG_TAX;
  IFLAG/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'I' ELSE 'N';
  NET_TKT_CNT/P12CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
-*   MXADV/A8  = IF (ADV_PURCH LT 7) THEN '00 - 06' ELSE
-*               IF (ADV_PURCH LT 14) THEN '07 - 13' ELSE '14 +'; 
  CGADV/A8 = IF (ADV_PURCH LT 7) THEN '0-6' ELSE 
             IF (ADV_PURCH GE 7) AND (ADV_PURCH LE 13) THEN '7-13' ELSE '14+';
  NON_FLAG/A15 = IF NONREF_FLAG EQ 'N' THEN 'NON-REFUNDABLE' ELSE 
                                            'REFUNDABLE';
  TKT_TYPE/A4         = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  MONTH/MT = TRN_DATE;
  LSTSVNGS/P12.2C    = IF EMBARK EQ 'DT1' THEN 0 ELSE FARE_PAID - SEG_LOWEST;
  LEVEL_DESC/A60 = &&LDESC.EVAL;                          

END
-RUN

 
TABLEF FILE SR_50
PRINT 
  AIRLINE
  AROLL_DSC6
  CHG_CAT
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  FARE_PAID
  FARE_BAS
  IFLAG
  LEVEL_DESC
  LSTSVNGS
  NET_TKT_CNT
  NON_FLAG
  CGADV
  PASSNGR_NAME
  REFUSE_CODE
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  SEG_LOWEST
  TKT_NUM
  TKT_TYPE
  TRN_DATE
  DPT_TIME
  XDPT_DT
  XTRAN_DT
  MONTH
  TKT_SORT
  SEG_NBR
  TKT_DATE
  ROLLUP_CODE
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
 
&&DCHK
&&VOIDCHK
&&DIFLAGCHK
&LCHK
&&ADVCHK
-* &&OTFLAGCHK
-* &&LSCHK
-* &&REFCHK
&&CC
&&EL
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN


-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT'
   FARE_PAID AS 'TFARE'
   LSTSVNGS AS 'TLOST'
   SEG_LOWEST AS 'TLOW'
BY LEVEL_DESC   
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN
 
TABLE FILE RPTHOLD
PRINT AIRLINE
  AROLL_DSC6
  CHG_CAT
  DPT_TIME
  EMB_APT_NM
  FARE_BAS
  FARE_PAID
  LSTSVNGS
  NET_TKT_CNT
  CGADV
  REFUSE_CODE
  RTE_APT_NM
  SEG_LOWEST
  TKT_NUM
  TKT_TYPE
  TRN_DATE
  XDPT_DT
  XTRAN_DT
  ROLLUP_CODE
BY LEVEL_DESC  
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN
 
MATCH FILE XTWO
PRINT AIRLINE
  AROLL_DSC6
  CHG_CAT
  DPT_TIME
  EMB_APT_NM
  FARE_BAS
  FARE_PAID
  LSTSVNGS
  CGADV
  LEVEL_DESC
  NET_TKT_CNT
  REFUSE_CODE
  RTE_APT_NM
  SEG_LOWEST
  TKT_NUM
  TKT_TYPE
  XDPT_DT
  XTRAN_DT
  ROLLUP_CODE
BY LEVEL_DESC  
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TFARE
      TTKT
      TLOST
      TLOW
BY LEVEL_DESC      
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN
 
TABLE FILE AIRHOLD2
PRINT AIRLINE
  AROLL_DSC6
  CHG_CAT
  DPT_TIME
  EMB_APT_NM
  FARE_BAS
  FARE_PAID
  LSTSVNGS
  CGADV
  LEVEL_DESC
  NET_TKT_CNT
  PASSNGR_NAME
  REFUSE_CODE
  RTE_APT_NM
  SEG_LOWEST
  TFARE
  TKT_NUM
  TKT_TYPE
  TLOST
  TLOW
  TRN_DATE
  TTKT
  XDPT_DT
  XTRAN_DT
  ROLLUP_CODE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN


JOIN ROLL WITH TKT_SORT IN AIRHOLD3 TO ROLLUP_CODE IN ROLLUP AS J1.
-RUN

-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');
-RUN

 


DEFINE FILE AIRHOLD3
NT_TKT_CNT/P9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN PASSNGR_NAME ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/P12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
TAVG/D12.2CS=IF (TFARE LT 0) AND (TTKT LT 0) THEN ((TFARE/TTKT) * (-1))
                       ELSE IF ((TTKT LT 1) AND (TTKT GT (-1))) THEN 0 ELSE
                       (TFARE/TTKT); 
LOST_TOT/P12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TLOST ELSE 0;
LOW_TOT/P12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
ROLL/A8='&&ROLLUP.EVAL';


RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
RN1/A60=TRIM('B', '&&RPT_TTL2.EVAL', 60, ' ', 1, 'A60');
-*LVL/A60=  IF '&&LSEL.EVAL' EQ 'TOTAL' THEN '&&RPT_TTL2.EVAL' ELSE RN1;
LVL/A60 = '&&RPT_TTL2.EVAL';
TDATE1/A8MDYY='&MDYY';
TDATE2/A10=EDIT(TDATE1,'99/99/9999');
TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
RD/A10 = 'Run Date:';
RT/A10 = 'Run Time:';
CUR/A15='Current Period:';
PRI/A15='Prior Period:';
FLAG/A1 = ' ';
END
-RUN
 



TABLE FILE AIRHOLD3
HEADING
-IF '&&OUTFMT.EVAL' EQ 'PDF' THEN GOTO HEAD2;    

"Client: <6 <ROLL_NAME "
"Contact: <6 <ROLL_CONTACT"
"Phone: <6 <RPHONE "
" "
"<CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
" <PRI       &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
" "
"<RN "
"Hierarchy: <LVL "

-GOTO SkipHead

-HEAD2
"Client: <6 <ROLL_NAME <38 <RN <90 <RD <TDATE1"
"Contact: <6 <ROLL_CONTACT <32 <LVL <90 <CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"Phone: <6 <RPHONE <90 <PRI       &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
 

-SkipHead
" "
" "
"&&SUBHEAD"
" "
-*
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'RSN,CDE'
&&DEPARTD
EMB_APT_NM/A17 AS 'ORG'
RTE_APT_NM/A17 AS 'DST'
AIRLINE AS 'CR'
&&FARBAS
&&FAREAMT
&&AVGTKT
&&LOWEST 
&&LSTSVGS
&&NTCOUNT
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY LEVEL_DESC AS 'HIERARCHY'
BY CHG_CAT AS 'CHARGE,CODE,TYPE'
BY AROLL_DSC6 AS 'EMPLOYEE,LEVEL'
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY SEG_NBR NOPRINT
BY TKT_NUM AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'

-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON LEVEL_DESC RECOMPUTE MULTILINES AS 'TOTAL FOR'
ON XTKT_SRT SKIP-LINE
 

-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
"</4"
"<15 &M1.EVAL <45 &M2.EVAL <75 &M3.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.050000, RIGHTMARGIN=0.050000,
    TOPMARGIN=0.000000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FOCEXEC=NONE, FONT=ARIAL, SIZE=7, COLOR=BLACK,
    BACKCOLOR=NONE, STYLE=NORMAL, $ 
 

-IF '&&OUTFMT.EVAL' NE 'PDF' THEN GOTO DIFFFMT;    
TYPE=DATA, 
     GRID=OFF,
     BACKCOLOR=( RGB(239 239 239) 'WHITE' ),
$
-*-DIFFFMT
TYPE=HEADING,
     LINE=1,
     OBJECT=TEXT,
     ITEM=1,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,

$
TYPE=HEADING,
     LINE=2,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$     
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=1,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=1,
     OBJECT=FIELD,
     ITEM=2,
     SIZE=10,
     JUSTIFY=CENTER,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD+ITALIC,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=3,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=4,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=2,
     OBJECT=TEXT,
     ITEM=1,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=2,
     OBJECT=FIELD,
     ITEM=1,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=2,
     OBJECT=FIELD,
     ITEM=2,
     SIZE=10,
     JUSTIFY=CENTER,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD+ITALIC,
$
TYPE=HEADING,   
     LINE=2,
     OBJECT=FIELD,
     ITEM=3,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=2,
     OBJECT=FIELD,
     ITEM=4,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,

$     
TYPE=HEADING,
     LINE=3,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=6,
     SIZE=10,
     FONT='ARIAL',
     STYLE=BOLD+ITALIC,
     COLOR=RGB(0 0 138),
$
TYPE=TITLE,
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
     COLOR=RGB(0 0 138),
$     
TYPE=FOOTING,
     SIZE=8,
     FONT='ARIAL',
     COLOR=WHITE, 
$
TYPE=GRANDTOTAL,
     SIZE=9,
     STYLE=BOLD,
$  
-GOTO PDFFMT;
-DIFFFMT
TYPE=HEADING,
     SIZE=9,
     JUSTIFY=LEFT,
     FONT='ARIAL',
     STYLE=BOLD,
$
TYPE=TITLE,
     FONT='ARIAL',
     STYLE=BOLD,
$     

TYPE=GRANDTOTAL,
     SIZE=8,
     STYLE=BOLD,

$
-PDFFMT

TYPE=REPORT, IMAGE=TSMHEAD.jpg, POSITION=(0 0), $
TYPE=REPORT, IMAGE=FOOTER.jpg, POSITION=(0 8.2), $ 
ENDSTYLE
   

&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;


-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-XXIT

