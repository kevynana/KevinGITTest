-*5/5/15         DEB  - S-08783 created new extract for TCAIRGEO
-*5/8/15         DEB  - S-08783 corrected issue with WN showing incorrect geo 
-*                            zone
-*5/12/15        DEB - S-08783 - VAL_AIRLINE to extract 
-*09/08/2017 DLV  S-37268   - Expanded EMBAPC RTEAPC define to A9
-INCLUDE SETECHO 
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
 
BASE_FARE/D12.2CS   = SEG_AMT ;
FARE_PAID/D12.2CS = SEG_AMT + SEG_TAX;
MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
 
END

TABLEF FILE &&EXTRACT
PRINT 
  AIRLINE
  VAL_AIRLINE AS 'VAL_AIR'
  EMBARKX
  BASE_FARE
  MRK_TKT_CNT
  ROUTEX
  TKT_NUM
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCAGEO
END
-RUN




TABLE FILE TCAGEO
BY EMBARKX
BY ROUTEX
ON TABLE HOLD
END


DEFINE FILE HOLD
EMBAPC/A9='''' | EMBARKX || '''';
END  
TABLE FILE HOLD
BY EMBAPC
ON TABLE SAVE AS EMBAPCF
END
-RUN


DEFINE FILE HOLD
RTEAPC/A9='''' | ROUTEX || '''';
END  
TABLE FILE HOLD
BY RTEAPC
ON TABLE SAVE AS RTEAPCF
END
-RUN

JOIN CLEAR *
JOIN COUNTRY_CODE IN AIRPORT TO COUNTRY_CODE IN COUNTRY AS J1
END

TABLE FILE AIRPORT
PRINT 
AIRPORT_CODE  GEO_ZONE AS 'EMB_GEO'
BY APCODEX 
WHERE APCODEX IN FILE EMBAPCF
ON TABLE HOLD AS CNCTYAPT1 FORMAT FOCUS INDEX APCODEX 
END
-RUN


TABLE FILE AIRPORT
PRINT 
AIRPORT_CODE  GEO_ZONE AS 'RTE_GEO'
BY APCODEX 
WHERE APCODEX IN FILE RTEAPCF
ON TABLE HOLD AS CNCTYAPT2 FORMAT FOCUS INDEX APCODEX 
END
-RUN


JOIN CLEAR *
JOIN EMBARKX IN TCAGEO TO APCODEX IN CNCTYAPT1 AS JEA1
JOIN ROUTEX IN TCAGEO TO APCODEX IN CNCTYAPT2 AS JRA1
-RUN

TABLE FILE TCAGEO
PRINT BASE_FARE
      MRK_TKT_CNT
BY VAL_AIR
BY AIRLINE 
BY TKT_NUM
BY EMB_GEO
BY RTE_GEO
ON TABLE HOLD AS TCGEO1
END
-RUN


-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
DEFINE FILE TCGEO1
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCGEO1
SUM  
    BASE_FARE/D14.2CS
    MRK_TKT_CNT
BY ROLLCD
BY EMB_GEO
BY RTE_GEO
BY VAL_AIR
BY AIRLINE
WHERE RTE_GEO NE ' '
WHERE EMB_GEO NE ' '
ON TABLE HOLD AS TCAG
END
-RUN








DEFINE FILE TCAG
 NMB/I5 = NMB + 1;
END
TABLE FILE TCAG
SUM BASE_FARE
    MRK_TKT_CNT
BY ROLLCD
BY NMB
BY EMB_GEO
BY RTE_GEO
BY VAL_AIR
BY AIRLINE
ON TABLE HOLD AS TCAG1
END
-RUN





MODIFY FILE TC_AGEO
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM TCAG1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCAG1
END
-RUN


-NEXTONE

 
