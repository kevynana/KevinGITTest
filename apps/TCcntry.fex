-***************************************************************************
-* REPORT NAME: TOP NON DIRECTIONAL MARKET PAIR SUMMARY
-* TOTAL COMPANY REPORTING
-***************************************************************************
-INCLUDE SETECHO
CREATE FILE TCCNTRY
-RUN

-INCLUDE TCGETROLL
-RUN

-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;

EX TCCNEX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2

-INCLUDE SetParmtime
-RUN

-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
TABLE FILE TCMARK
SUM FARE
    MILES	
    NET_TKT_CNT	
BY ORGDST
BY CP_AIRLINE
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
PRINT CP_AIRLINE	
    FARE
    MILES	
    NET_TKT_CNT	
BY ORGDST
RANKED BY HIGHEST &&RANK_METH   
ON TABLE HOLD AS HOLD2
END
-RUN

DEFINE FILE HOLD2
  DRANK/D5      = RANK ;
  ARANKX/A6     = FTOA(DRANK, '(D5)', 'A6');
  ARANK/A5      = EDIT(ARANKX,'$99999');
  RANK_LIMIT/I5 = &&RANK_LIMIT2 ;
  RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                  IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN

TABLE FILE HOLD2
PRINT CP_AIRLINE
    FARE
    MILES	
    NET_TKT_CNT	
BY ORGDST  
BY RANK_VALUE
ON TABLE HOLD AS HOLD3
END
-RUN

DEFINE FILE HOLD3
AIRLINE_PRINT/A12 = IF RANK_VALUE LE '&&RANK_LIMIT2' THEN CP_AIRLINE ELSE 'OTHER';
END
-RUN

TABLE FILE HOLD3
SUM CP_AIRLINE
    FARE
    MILES	
    NET_TKT_CNT	
BY ORGDST 
BY RANK_VALUE 
BY AIRLINE_PRINT 
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE HOLD1
SUM &&RANK_METH 
BY ORGDST
ON TABLE HOLD AS RHOLD1 
END
-RUN

TABLE FILE RHOLD1
PRINT &&RANK_METH
RANKED BY HIGHEST &&RANK_METH AS 'SEG_RANK'
BY ORGDST
ON TABLE HOLD AS RHOLD2
END
-RUN

MATCH FILE HOLD4
PRINT AIRLINE_PRINT
    CP_AIRLINE
    FARE
    MILES	
    NET_TKT_CNT	
    RANK_VALUE
BY ORGDST 
ON TABLE HOLD
RUN
FILE RHOLD2
SUM SEG_RANK
BY ORGDST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN

TABLE FILE HOLD5
PRINT AIRLINE_PRINT
    CP_AIRLINE	
    FARE
    MILES	
    NET_TKT_CNT	
    ORGDST
    RANK_VALUE
RANKED BY HIGHEST SEG_RANK 
ON TABLE HOLD AS HOLD6
END
-RUN

DEFINE FILE HOLD6
  ORG_LIMIT/I2 = &&RANK_LIMIT;
  OLIMIT/A2    = EDIT(ORG_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF ORGDST NE LAST ORGDST THEN (LAST LIST + 1) 
    ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999'); 
  ORG_VALUE/A6 = IF ORG_LIMIT EQ 0 THEN BRANK ELSE
               IF LIST GT ORG_LIMIT THEN 'OTHER' ELSE FRANK;
END
-RUN

TABLE FILE HOLD6
SUM CP_AIRLINE
    FARE
    MILES	
    NET_TKT_CNT	
    OLIMIT
BY ORG_VALUE
BY ORGDST
BY RANK_VALUE
BY AIRLINE_PRINT
ON TABLE HOLD AS HOLD7
END
-RUN

DEFINE FILE HOLD7
  ORG_PRINT/A55 =IF ORG_VALUE LT (OLIMIT) THEN ORGDST ELSE 'ALL OTHER';
  RO_PRINT/A12   =IF ORG_VALUE LT (OLIMIT) THEN AIRLINE_PRINT ELSE 
                  IF ORG_PRINT EQ 'ALL OTHER' THEN CP_AIRLINE ELSE ' ';
END
-RUN

TABLE FILE HOLD7
SUM FARE
    MILES	
    NET_TKT_CNT	
BY ORG_VALUE     
BY ORG_PRINT  
BY RANK_VALUE
BY RO_PRINT
ON TABLE HOLD AS HOLD8
END
-RUN

TABLE FILE HOLD7
SUM MILES AS 'C_MILES'
    FARE AS 'C_FARE' 
    NET_TKT_CNT AS 'C_SEGMT'
BY ORG_PRINT  
ON TABLE HOLD AS HOLD9
END
-RUN

MATCH FILE HOLD8
PRINT FARE
      MILES	
      NET_TKT_CNT 	
      ORG_VALUE	
      RANK_VALUE	
      RO_PRINT	
BY ORG_PRINT 
ON TABLE HOLD
RUN
FILE HOLD9
SUM C_FARE
    C_MILES
    C_SEGMT
BY ORG_PRINT 
AFTER MATCH HOLD AS HOLD10 OLD-OR-NEW
END
-RUN

-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN

-TYPE BEFORE DEFINES
DEFINE FILE HOLD10 
  RNK_VAL/A5 = IF ORG_PRINT EQ 'ALL OTHER' THEN '1' ELSE RANK_VALUE;
  PER_FARE/D8.1=    IF ((C_FARE LT 1) AND (C_FARE GT (-1))) THEN 0
                    ELSE ((FARE/C_FARE)*100);
  SEGPER/D8.1 =     IF ((C_SEGMT LT 1) AND (C_SEGMT GT (-1))) THEN 0
                    ELSE ((NET_TKT_CNT/C_SEGMT)*100);
  MILE_PCT/D8.1=    IF ((C_MILES LT 1) AND (C_MILES GT (-1))) THEN 0
                    ELSE ((MILES/C_MILES)*100);
  NOWTOD/A8 WITH ORG_VALUE = HHMMSS (NOWTOD);
END

TABLE FILE HOLD10
-INCLUDE HEADER
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
   
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT 
ON TABLE SET STYLE *
-INCLUDE &&SMSTY 
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
