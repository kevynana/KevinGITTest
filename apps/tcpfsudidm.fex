-*07/05/17-S-18138-JEM-Creation of ROUTINE FOR PFS UDIDS 

SET ASNAMES = ON 
-RUN

-INCLUDE SETECHO


-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';


DEFINE FILE SR_50

  FARE_PAID/P15.2CS   = SEG_AMT + SEG_TAX;   
  NET_TKT_CNT/P12CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TRNMY/MTY = TRN_DATE;  
                           
END

TABLE FILE SR_50
PRINT AIR_KEY
  EMBARK
  FARE_PAID
  ROUTE
  SEG_NBR
  TKT_SORT
  TRNMY
-* -INCLUDE &AIRDATES
WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS REPHOLD1
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;

TABLE FILE REPHOLD1
PRINT TRNMY 
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS TCAIRHM
END
-RUN


-* Access FSXXXX database


TABLE FILE &&COMPFS
SUM FEE_AMOUNT CNT.FEE_AMOUNT/P12S AS 'FEE_CNT'
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS TCPFSHM
END
-RUN


MATCH FILE TCAIRHM
PRINT TRNMY
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD
RUN
FILE TCPFSHM
SUM FEE_AMOUNT FEE_CNT
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
AFTER MATCH HOLD AS TCPFSH1M OLD-AND-NEW
END
-RUN

DEFINE FILE TCPFSH1M
FEE_AMT/P12.2CSM = FEE_AMOUNT;
ROLLCD/A8 = '&&ROLL';
END
-RUN
TABLE FILE TCPFSH1M
SUM FEE_AMT FEE_CNT
BY TRNMY
BY ROLLCD
ON TABLE HOLD AS TCPFSH2M
END
-RUN


TABLE FILE TCPFSH2M
SUM FEE_AMT FEE_CNT
BY TRNMY
BY ROLLCD
ON TABLE HOLD AS TCPFSH3M
END
-RUN


MODIFY FILE TC_VSV2M
FIXFORM FROM TCPFSH3M
MATCH TRNMY ROLLCD 
ON MATCH UPDATE FEE_AMT FEE_CNT
ON NOMATCH INCLUDE
DATA ON TCPFSH3M
END
-RUN




-NEXTONE

 


