-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File AIREXTRT.FEX
-********************************************************************
-* This program extracts data for the group report
-********************************************************************
-*12/21/04   DEB  CHANGED CGBOOK DEFINE TO INCLUDE DOD_TYPE 'J'
-*1/11/05   DEB  CHANGED CGBOOK DEFINE TO INCLUDE DOD_TYPE 'H'
-*1/27/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-* 3/17/14 - DEB - ADDED FLIGHT
-* ADDED TRANTYPE INCLUDE - JK 12/15/14 STORY ID S-06492
-*12/17/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*09/28/2017 - JEM - S-35461 Place the footer date/time stamp text in a separate file to be -INCLUDED in the stylesheets

-********************************************************************
-INCLUDE SETECHO

SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-SET &&SMD = 'D';
-RUN

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE DR_50
BASE_FARE/D12.2CS = SEG_AMT;
-* CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'   
-*                               'D' 'DISCOUNT'  'B' 'BUSINESS'
-*                               ' ' 'OTHER');

CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			       'D' 'DEFAULT'   'B' 'BUSINESS'
			       'E' 'ECONOMY'   'U'  'UPGRADE' 
                               ' ' 'DEFAULT'); 

CGBOOK/A20 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE' ELSE 'TRADITIONAL';  
FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
LEVEL_DESC1/A80 = &&LDESC;
MILEAGE/A12 = IF (SEG_MILES GE 0) AND (SEG_MILES LE 2549) OR
                 (SEG_MILES LE 0) AND (SEG_MILES GE (-2549)) THEN '0 - 2549'
         ELSE IF (SEG_MILES GE 2550) AND (SEG_MILES LE 3549) OR
                 (SEG_MILES LE (-2550)) AND (SEG_MILES GE (-3549)) THEN 
                 '2550 - 3549' 
                 ELSE '3500 +';
MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
NEW_SEG_AMT/D12.2CS = SEG_AMT;
NET_COMM/D12.2S = (SEG_AMT - SEG_COM);
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
SEG_CNT/D8CS         = SEG_COUNT;
TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                           (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' '; 
TRAN_MONTH/MT        = TRN_DATE;
TRAN_MY/MTYY         = TRN_DATE;
XARR_DT/MDYY         = ARR_DATE; 
XDPT_DT/MDYY         = DPT_DATE;
XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
XTRAN_DT/MDYY        = TRN_DATE;
-* DEFINES ON PREVIOUS DEFINES

DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;

FLAG/A1 = IF SEG_NBR EQ '01' OR RF_FLAG EQ 'P' OR EX_FLAG EQ 'P'
          OR DOC_TYPE EQ '1' THEN 'Y' ELSE 'N';
          
-INCLUDE TRANTYPE          
-INCLUDE IDLEVDEFINE 

END
-RUN


TABLEF FILE DR_50
PRINT
  AGENT_NUM
  AIR_KEY	
  AIR_MAIN.INTL_DOM
  AIRLINE	
  AIRLINE_NAME
  ARR_TIME
  BR_CL_IDX
  CGBOOK
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'		
  CL_CAT	
  CLASS	
  CLASS_CAT
  DISSAVINGS
  DOC_TYPE
  DPT_TIME
  DST_APC
  DST_APCX
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
  EX_FLAG
  FARE_PAID
  FARE_BAS
  FLIGHT
  LEVEL_DESC
  LEVEL3
  MILEAGE
  MRK_TKT_CNT
  NET_COMM
  NEW_SEG_AMT
  NET_TKT_CNT
  ORG_APC
  ORG_APCX
  PASSNGR_NAME
  REFUSE_CODE
  RF_FLAG
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'
  SEG_CNT
  SEG_COM
  SEG_COUNT
  SEG_MILES
  SEG_NBR
  TOT_TNT_FEE
  TRAN_MONTH
  TRAN_MY	
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE
  XARR_DT	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT	

-INCLUDE &AIRDATES

WHERE VOID_DATE EQ 0
WHERE FLAG EQ 'Y'
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD

END
-RUN

