-*======================================================================
-*  DATE        PROGRAMMER  CHANGE
-*  ----        ----------  ---------------------------------
-*  05/23/2017  GSE         S-22584 Initial version
-*======================================================================
-* File nonair_pnr_delete.fex.fex
-TYPE nonair_pnr_delete.fex
-SET &LOOPSTART = 0;
-SET &LOOPEND = 11;
-SET &NOW = HHMMSS('A8');
-RUN
-TYPE BEGINNING &NOW
-RUN
-*
-DEFAULT &ROLLUP = ' ';
-DEFAULT &PNR = ' ';
-DEFAULT &TASF = ' ';
-DEFAULT &TYPE = ' ';
-*
-IF '&ROLLUP.EVAL' EQ ' ' GOTO PARAMERROR;
-IF '&PNR.EVAL' EQ ' ' GOTO PARAMERROR;
-IF ('&TASF.EVAL' NE 'M' OR 'D' OR 'N') GOTO PARAMERROR;
-IF ('&TYPE.EVAL' NE 'H' OR 'C' OR 'L') GOTO PARAMERROR;
-*
-SET &FTYPE = IF &TYPE EQ 'H' THEN 'HOTEL' ELSE
-             IF &TYPE EQ 'C' THEN 'CAR' ELSE
-             IF &TYPE EQ 'L' THEN 'LIMO';
-*
-SET &MFD = IF &TYPE EQ 'H' THEN 'HR_50' ELSE
-           IF &TYPE EQ 'C' THEN 'CR_50' ELSE
-           IF &TYPE EQ 'L' THEN 'LR_50';
-*
-SET &BY1 = IF &TYPE EQ 'H' THEN 'HTL_KEY' ELSE
-           IF &TYPE EQ 'C' OR 'L' THEN 'CAR_KEY';
-*
SET HOLDLIST = PRINTONLY
SET ASNAMES = ON
SET PCOMMA = ON
SET FIXFRMINPUT = NONCOND
SET MESSAGE = OFF
-*
EX REVIEW_50_SQL_LOADS/concat_all_dbs_bench ROLL=&ROLLUP.EVAL,FID=&FTYPE.EVAL,CLEAR=Y
-*
TABLE FILE &MFD
SUM
      LEVEL1          LEVEL2          LEVEL3          LEVEL4          LEVEL5          LEVEL6          LEVEL7
      INT_CODE        AROLL_LEV1      AROLL_LEV2      AROLL_LEV3      AROLL_LEV4      AROLL_LEV5      AROLL_LEV6
      AROLL_LEV7      AROLL_LEV8      AROLL_LEV9      AROLL_LEV10     AROLL_LEV11     AROLL_LEV12     AROLL_LEV13
      AROLL_LEV14     AROLL_LEV15
BY &BY1
WHERE PNR_LOCATOR EQ '&PNR.EVAL'
ON TABLE HOLD AS PNRDATA
END
-RUN
-IF &LINES EQ 0 GOTO NOPROC;
-*
TABLE FILE &MFD
SUM
      MIN.NUD_DATA    MIN.AUD_DATA
BY &BY1
BY UDID
WHERE PNR_LOCATOR EQ '&PNR.EVAL'
ON TABLE HOLD AS UDIDDATA
END
-RUN
-*
-SET &UDRECS = &LINES;
-*
 
-*
USE CLEAR *
-RUN
-*
TABLE FILE PNRDATA
BY &BY1
ON TABLE HOLD AS DELKEYS
END
-RUN
-*
TABLE FILE ROLLUP_SQL
PRINT COMP QTR_ENABLE
BY ROLLUP_CODE
WHERE ROLLUP_CODE EQ '&ROLLUP.EVAL'
WHERE ROLLUP_CODE NE 'ABCD2-R' OR 'ABCD3-R' OR 'TOTAL-R'
-*WHERE UPD_ENABLE EQ 'X'
WHERE COMP NE ' '
ON TABLE HOLD AS ROLLLIST
END
-RUN
-*
-READROLL
 
-READFILE ROLLLIST
-RUN
-IF &IORETURN NE 0 GOTO XXIT;
-*
 
-*
-QTRD
-SET &NOW = HHMMSS('A8');
-RUN
-TYPE &NOW &ROLLUP_CODE
-RUN
-*
-SET &AUDIDFIL = 'UA' | '&COMP.EVAL';
-SET &HUDIDFIL = 'UH' | '&COMP.EVAL';
-SET &CUDIDFIL = 'UC' | '&COMP.EVAL';
-SET &LUDIDFIL = 'UL' | '&COMP.EVAL';
-SET &TUDIDFIL = 'UT' | '&COMP.EVAL';
-SET &PFEEFILE = 'PF' | '&COMP.EVAL';
-SET &PFSVFILE = 'FS' | '&COMP.EVAL';
-*
-SET &UDIDMFD = IF &TYPE EQ 'H' THEN &HUDIDFIL ELSE
-               IF &TYPE EQ 'C' THEN &CUDIDFIL ELSE
-               IF &TYPE EQ 'L' THEN &LUDIDFIL;
-PROCTASF
-*
-IF '&TASF.EVAL' NE 'D' GOTO ENDDELTASF;
-*
-DELTASF
-TYPE DELETE TASF PROCESSING
-*
TABLE FILE &PFEEFILE
BY SEQNBR
WHERE &BY1 EQ (DELKEYS)
ON TABLE HOLD AS PFEEDEL
END
-RUN
-*
-TYPE &PFEEFILE &LINES DELETE
-*
SET MESSAGE = ON
MODIFY FILE &PFEEFILE
FIXFORM FROM PFEEDEL
MATCH SEQNBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON PFEEDEL
END
SET MESSAGE = OFF
-RUN
-*
-TYPE &UDIDMFD &UDRECS + 1 KEY DELETE
-*
SET MESSAGE = ON
MODIFY FILE &UDIDMFD
FIXFORM FROM DELKEYS
MATCH &BY1
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DELKEYS
END
SET MESSAGE = OFF
-RUN
-*
-GOTO DONETASF
-*
-ENDDELTASF
-*
-IF '&TASF.EVAL' NE 'M' GOTO ENDMOVETASF;
-*
-MOVETASF
-*
-TYPE MOVE TASF PROCESSING
-*
-SET &BY_KEY_FMT = IF &TYPE EQ 'H' THEN 69 ELSE
-                  IF &TYPE EQ 'C' OR 'L' THEN 82;
-*
JOIN &BY1 IN PNRDATA TO MULTIPLE &BY1 IN &PFEEFILE TAG J0 AS J0
-*
TABLE FILE PNRDATA
PRINT
      MISC_KEY NOPRINT
      PNRDATA.&BY1.EVAL AS MISC_KEY
      COMPUTE NEW_BY_KEY/A&BY_KEY_FMT.EVAL = ' '; AS &BY1
      ID_LEVEL1 NOPRINT
      PNRDATA.LEVEL1 AS ID_LEVEL1
      ID_LEVEL2 NOPRINT
      PNRDATA.LEVEL2 AS ID_LEVEL2
      ID_LEVEL3 NOPRINT
      PNRDATA.LEVEL3 AS ID_LEVEL3
      ID_LEVEL4 NOPRINT
      PNRDATA.LEVEL4 AS ID_LEVEL4
      ID_LEVEL5 NOPRINT
      PNRDATA.LEVEL5 AS ID_LEVEL5
      ID_LEVEL6 NOPRINT
      PNRDATA.LEVEL6 AS ID_LEVEL6
      ID_LEVEL7 NOPRINT
      PNRDATA.LEVEL7 AS ID_LEVEL7
      ID_DASH NOPRINT
      PNRDATA.INT_CODE AS ID_DASH
      AROLL_LEV1 NOPRINT
      PNRDATA.AROLL_LEV1
      AROLL_LEV2 NOPRINT
      PNRDATA.AROLL_LEV2
      AROLL_LEV3 NOPRINT
      PNRDATA.AROLL_LEV3
      AROLL_LEV4 NOPRINT
      PNRDATA.AROLL_LEV4
      AROLL_LEV5 NOPRINT
      PNRDATA.AROLL_LEV5
      AROLL_LEV6 NOPRINT
      PNRDATA.AROLL_LEV6
      AROLL_LEV7 NOPRINT
      PNRDATA.AROLL_LEV7
      AROLL_LEV8 NOPRINT
      PNRDATA.AROLL_LEV8
      AROLL_LEV9 NOPRINT
      PNRDATA.AROLL_LEV9
      AROLL_LEV10 NOPRINT
      PNRDATA.AROLL_LEV10
      AROLL_LEV11 NOPRINT
      PNRDATA.AROLL_LEV11
      AROLL_LEV12 NOPRINT
      PNRDATA.AROLL_LEV12
      AROLL_LEV13 NOPRINT
      PNRDATA.AROLL_LEV13
      AROLL_LEV14 NOPRINT
      PNRDATA.AROLL_LEV14
      AROLL_LEV15 NOPRINT
      PNRDATA.AROLL_LEV15
BY SEQNBR
ON TABLE HOLD AS FEEUPDT
END
-RUN
-*
-TYPE &PFEEFILE &LINES UPDATE
JOIN CLEAR J0
-*
SET MESSAGE = ON
MODIFY FILE &PFEEFILE
FIXFORM FROM FEEUPDT
MATCH SEQNBR
ON MATCH UPDATE &BY1 MISC_KEY
ON MATCH UPDATE ID_DASH ID_LEVEL1 ID_LEVEL2 ID_LEVEL3 ID_LEVEL4 ID_LEVEL5 ID_LEVEL6 ID_LEVEL7
ON MATCH UPDATE AROLL_LEV1 AROLL_LEV2 AROLL_LEV3 AROLL_LEV4 AROLL_LEV5 AROLL_LEV6 AROLL_LEV7
ON MATCH UPDATE AROLL_LEV8 AROLL_LEV9 AROLL_LEV10 AROLL_LEV11 AROLL_LEV12 AROLL_LEV13 AROLL_LEV14 AROLL_LEV15
ON NOMATCH REJECT
DATA ON FEEUPDT
END
SET MESSAGE = OFF
-RUN
-*
TABLE FILE UDIDDATA
PRINT
      NUD_DATA
      AUD_DATA
BY TOTAL COMPUTE MISC_KEY/A114 = &BY1;
BY UDID
ON TABLE HOLD AS UDIDUPDT
END
-RUN
-*
-TYPE &TUDIDFIL &LINES + 1 KEY INSERT
-*
SET MESSAGE = ON
MODIFY FILE &TUDIDFIL
FIXFORM FROM UDIDUPDT
MATCH MISC_KEY UDID
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON UDIDUPDT
END
SET MESSAGE = OFF
-RUN
-*
-TYPE &UDIDMFD &UDRECS + 1 KEY DELETE
-*
SET MESSAGE = ON
MODIFY FILE &UDIDMFD
FIXFORM FROM DELKEYS
MATCH &BY1
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DELKEYS
END
SET MESSAGE = OFF
-RUN
-*
-GOTO DONETASF
-*
-ENDMOVETASF
-*
-IF '&TASF.EVAL' NE 'N' GOTO ENDNOTASF;
-*
-TYPE NO TASF PROCESSING
-*
-TYPE &UDIDMFD &UDRECS + 1 KEY DELETE
-*
SET MESSAGE = ON
MODIFY FILE &UDIDMFD
FIXFORM FROM DELKEYS
MATCH &BY1
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DELKEYS
END
SET MESSAGE = OFF
-RUN
-*
-GOTO DONETASF
-*
-ENDNOTASF
-*
-DONETASF
-*
-SET &QTRCTR = &LOOPSTART.EVAL;
-SET &ENDCTR = IF &QTR_ENABLE NE 'X' THEN 1 ELSE (&LOOPEND.EVAL - &LOOPSTART.EVAL + 1);
-*TYPE &ENDCTR Times
-TYPE -----
-*
-REPEAT QTRMOD &ENDCTR TIMES;
-SET &QTR = IF &QTR_ENABLE NE 'X' THEN '_' ELSE
-           IF &QTRCTR EQ 10 THEN 'Y' ELSE
-           IF &QTRCTR EQ 11 THEN 'Z' ELSE &QTRCTR;
-SET &SMODFILE = 'S' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &SFOCFILE = '&SMODFILE.EVAL' || '.foc';
-SET &MMODFILE = 'M' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &MFOCFILE = '&MMODFILE.EVAL' || '.foc';
-SET &DMODFILE = 'D' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &DFOCFILE = '&DMODFILE.EVAL' || '.foc';
-SET &TMODFILE = 'T' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &AMODFILE = 'A' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &AFOCFILE = 'A' | '&QTR.EVAL' | '&COMP.EVAL' || '.foc';
-SET &HMODFILE = 'H' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &CMODFILE = 'C' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &LMODFILE = 'L' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &QTRCTR = &QTRCTR + 1;
-*
-SET &QTRMFD = IF &TYPE EQ 'H' THEN &HMODFILE ELSE
-              IF &TYPE EQ 'C' THEN &CMODFILE ELSE
-              IF &TYPE EQ 'L' THEN &LMODFILE;
-*
TABLE FILE &QTRMFD
BY &BY1
WHERE &BY1 EQ (DELKEYS)
ON TABLE HOLD AS KEYCNT
END
-RUN
-IF &RECORDS EQ 0 GOTO QTRMOD;
-*
-SET &KEYRECS = &LINES;
-*
-TYPE &QTRMFD &KEYRECS DELETE
-*
SET MESSAGE = ON
MODIFY FILE &QTRMFD
FIXFORM FROM DELKEYS
MATCH &BY1
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DELKEYS
END
SET MESSAGE = OFF
-RUN
-*
-QTRMOD
-*
-GOTO XXIT
 
-PARAMERROR
-IF '&ROLLUP.EVAL' GT ' ' GOTO OKROLL;
-TYPE You must enter a value for ROLLUP
-OKROLL
-*
-IF '&PNR.EVAL' GT ' ' GOTO OKTKT;
-TYPE You must enter a value for PNR
-OKTKT
-*
-IF ('&TASF.EVAL' EQ 'M' OR 'D' OR 'N') GOTO OKTASF;
-TYPE TASF must be 'M' or 'D' OR 'N'
-OKTASF
-*
-IF ('&TYPE.EVAL' EQ 'H' OR 'C' OR 'L') GOTO OKTYPE;
-TYPE TYPE must be 'H' OR 'C' OR 'L'
-OKTYPE
-*
-GOTO XXIT
-*
-NOPROC
-TYPE No Air records are found for TKT_NUM &TKT_NUM
-XXIT
