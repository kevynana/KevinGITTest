-*2/22/05        JK                 ADDED TNT_COMPANY WHERE STATEMENT

-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;

END

TABLEF FILE &&EXTRACT
PRINT BR_CL_IDX
  CLASS
  FARE_PAID
  NET_TKT_CNT
  PASSNGR_NAME
  PNR_LOC
  REF_EXCH_CNT
  TKT_NUM
  TKT_PURCH
  TICKET_AGENCY
  VAL_AIRLINE  
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCARC 
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;

-IF &&SETF.EVAL EQ 'TCAIRDL' THEN GOTO Delta;
-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
DEFINE FILE TCARC
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCARC
SUM FARE_PAID/D14.2CS
    NET_TKT_CNT
    PNR_LOC
    TICKET_AGENCY
    TKT_PURCH
    REF_EXCH_CNT
    PASSNGR_NAME
    VAL_AIRLINE
BY ROLLCD
BY TKT_NUM
BY CLASS
ON TABLE HOLD AS TCARC1
END
-RUN


DEFINE FILE TCARC1
 NMB/I5 = NMB + 1;
END
TABLE FILE TCARC1
PRINT CLASS
     FARE_PAID
      NET_TKT_CNT
      PASSNGR_NAME
      PNR_LOC
      REF_EXCH_CNT
      TICKET_AGENCY
      TKT_NUM
      TKT_PURCH
      VAL_AIRLINE
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCARC2
END
-RUN



MODIFY FILE TC_AJL
FIXFORM FROM TCARC2
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCARC2
END
-RUN


-GOTO NEXTONE

-Delta



DEFINE FILE TCARC
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCARC
SUM FARE_PAID/D14.2CS
    NET_TKT_CNT
    PNR_LOC
    TICKET_AGENCY
    TKT_PURCH
    REF_EXCH_CNT
    PASSNGR_NAME
    
BY ROLLCD
BY BR_CL_IDX
BY TKT_NUM
ON TABLE HOLD AS TCHOLD
END
-RUN



DEFINE FILE TCHOLD
 NMB/I5 = NMB + 1;
END
TABLE FILE TCHOLD
PRINT BR_CL_IDX
     FARE_PAID
      NET_TKT_CNT
      PASSNGR_NAME
      PNR_LOC
      REF_EXCH_CNT
      TICKET_AGENCY
      TKT_NUM
      TKT_PURCH
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCHOLD2
END
-RUN



MODIFY FILE TC_ADL
FIXFORM FROM TCHOLD2
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCHOLD2
END
-RUN


TABLE FILE TC_ADL
PRINT *
ON TABLE HOLD AS TC_ADL2 FORMAT FOCUS
END
-RUN




-NEXTONE
