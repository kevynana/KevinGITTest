-*11/20/17 - Creation of new report TCTPAIR


-INCLUDE SETECHO
SET ASNAMES = ON 

-*======================================================================== 
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE SR_50

  FARE_PAID/P15.2CS   = SEG_AMT + SEG_TAX;
  NET_TKTS/P12CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;  

ONL_TKTS/P12CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN NET_TKTS ELSE 0;
                    
ROLLUP_CODE/A8 = '&&ROLL';   
RO_FLAG/A10 = IF RNDT_FLAG EQ 'Y' THEN 'Roundtrip' ELSE 'Oneway';   
ALN_NAME/A12 = VAL_AIR.AIRLINE_NAME;

END


TABLE FILE SR_50
PRINT FARE_PAID
      NET_TKTS
      ONL_TKTS
BY AIR_KEY      
BY ROLLUP_CODE
BY ROLL_NAME
BY PASSNGR_NAME
BY ALN_NAME
BY RO_FLAG
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0 
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS

ON TABLE HOLD AS TCTH1
END
-RUN


TABLE FILE TCTH1
PRINT ALN_NAME
      FARE_PAID
      NET_TKTS
      ONL_TKTS
      PASSNGR_NAME
      ROLLUP_CODE
      ROLL_NAME
      RO_FLAG
BY AIR_KEY
ON TABLE HOLD AS TCTH2 FORMAT FOCUS INDEX AIR_KEY
END
-RUN


-INCLUDE TCUDAUSE
-RUN

TABLE FILE UDAIR
PRINT AUD_DATA/A2 AS 'DTA_FLAG'
BY AIR_KEY
WHERE UDID EQ 'DTA'
ON TABLE HOLD AS UDH1 
END
-RUN

JOIN AIR_KEY IN TCTH2 TO AIR_KEY IN UDH1 AS UDJ1
-RUN

TABLE FILE TCTH2
SUM NET_TKTS AS 'TNTKTS'
BY ROLLUP_CODE
BY ROLL_NAME
BY PASSNGR_NAME
ON TABLE HOLD AS TCTHT1
END
-RUN


MATCH FILE TCTH2
SUM FARE_PAID
      NET_TKTS
      ONL_TKTS
BY ROLLUP_CODE
BY ROLL_NAME
BY PASSNGR_NAME
BY ALN_NAME
BY RO_FLAG
ON TABLE HOLD
RUN
FILE TCTHT1
SUM TNTKTS
BY ROLLUP_CODE
BY ROLL_NAME
BY PASSNGR_NAME
AFTER MATCH HOLD AS TCTHT2 OLD-OR-NEW
END
-RUN


TABLE FILE TCTHT2
SUM FARE_PAID
    NET_TKTS
    ONL_TKTS
    TNTKTS
BY ROLLUP_CODE
BY ROLL_NAME
BY PASSNGR_NAME
BY ALN_NAME
BY RO_FLAG
-* WHERE DTA_FLAG EQ 'Y'
ON TABLE HOLD AS TCTH3
END
-RUN


TABLE FILE TCTH3
PRINT *
BY ROLLUP_CODE
BY ROLL_NAME
RANKED BY HIGHEST TNTKTS NOPRINT
ON TABLE HOLD AS TCTT3
END
-RUN

-SET &&RANK_LIMIT = 300;

DEFINE FILE TCTT3
RANK_LIMIT/I5 = &&RANK_LIMIT;
 CRANK/D5 =   RANK;
 BRANK/A6 =   FTOA(CRANK, '(D5)' , 'A6');
 LIST/D6 = IF PASSNGR_NAME NE LAST PASSNGR_NAME THEN (LAST LIST + 1) ELSE LAST LIST;
 FRANKX/A6 = FTOA(LIST, '(D5)', 'A6');
 FRANK/A5=EDIT(FRANKX,'$99999');
 RANK_VALUE/A9 = IF (RANK_LIMIT EQ 0) THEN BRANK ELSE
                 IF LIST GT RANK_LIMIT THEN 'OTHER' ELSE FRANK;
 RANK_LABEL/A33 = IF RANK_VALUE NE 'OTHER' THEN PASSNGR_NAME ELSE ' ';
END
-RUN


TABLE FILE TCTT3
PRINT FARE_PAID
      NET_TKTS
      ONL_TKTS
      ALN_NAME
      TNTKTS
BY ROLLUP_CODE
BY ROLL_NAME
BY RANK_VALUE 
BY RANK_LABEL 
BY ALN_NAME
BY RO_FLAG
WHERE RANK_VALUE NE 'OTHER'
ON TABLE HOLD AS TCTT4
END
-RUN



-IF &LINES EQ 0 THEN GOTO NEXTONE;

-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
TABLE FILE TCTT4
PRINT FARE_PAID
      NET_TKTS
      ONL_TKTS
      ALN_NAME
      TNTKTS
BY ROLLUP_CODE
BY ROLL_NAME
BY RANK_VALUE
BY RANK_LABEL AS 'PASSNGR_NAME'
BY ALN_NAME
BY RO_FLAG
ON TABLE HOLD AS TCTT4
END
-RUN


MODIFY FILE TCTPAIR
FIXFORM FROM TCTT4
MATCH ROLLUP_CODE ROLL_NAME RANK_VALUE PASSNGR_NAME ALN_NAME RO_FLAG
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON TCTT4
END
-RUN





-NEXTONE

