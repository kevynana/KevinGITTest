-*-INCLUDE SETECHO
-* File rollup_summary_driver.fex
-*
-* Purpose:  This focexec is used to recreate the ROLLUP_SUMMARY file.  It will
-*           load data for all Rollups.
-*
-*           Update the ROLLUP_SUMMARY2.MAS file with changes and the Air, Hotel, and
-*           Car focexecs.  The Rollup_Summary*0.fex focexecs are used by this program,
-*           the ones without a zero on the end are used by the Load programs, so any
-*           changes will need to be made to both sets of focexecs.
-*           This program will create the ROLLUP_SUMMARY2.FOC focus file and then rename
-*           it to ROLLUP_SUMMARY at the end of the job.
-*
-SET &SRC='D:\TNT\TTRACKER\DATA\';
-*-GOTO A;
CREATE FILE ROLLUP_SUMMARY2
 
TABLE FILE ROLLUP
PRINT COMP QTR_ENABLE
BY ROLLUP_CODE
WHERE COMP GT ''
WHERE ROLLUP_CODE NE 'ABCD-R'
WHERE ROLLUP_CODE NE 'ABCD2-R' OR 'ABCD3-R' OR 'TOTAL-R';
-*WHERE ROLLUP_CODE EQ 'ABCD-R' OR 'KIEWIT-R'
-*WHERE ROLLUP_CODE EQ 'ABCD-R' OR 'METX-R' OR 'NYL-R'
-*WHERE UPD_ENABLE EQ 'X'
-*WHERE RECORDLIMIT EQ 1
ON TABLE SAVE AS ALLROLL
END
-RUN
-SET &TOTROLLS = &LINES;
-REPEAT TOEND1 &TOTROLLS TIMES
-READ ALLROLL NOCLOSE &ROLL.8. &COMP.6. &QTR.1.
-SET &&RUP='&ROLL.EVAL';
 
-SET &GWHERE=IF '&ROLL.EVAL' EQ 'KIEWIT-R' THEN '-*' ELSE 'WHERE CATEGORY NE ' | '''' | 'GLOBAL' | '''';
 
-IF '&QTR.EVAL' EQ 'X' THEN GOTO QUARTED;
 
-SINGLEDB
 
-SET &SR_50='&SRC.EVAL' || 'S_' || '&COMP.EVAL' || '.FOC AS SR_50';
-SET &MR_50='&SRC.EVAL' || 'M_' || '&COMP.EVAL' || '.FOC AS MR_50';
-SET &AIRMAIN='&SRC.EVAL' || 'A_' || '&COMP.EVAL' || '.FOC AS AIR_MAIN';
-SET &CITYSEG='&SRC.EVAL' || 'S_' || '&COMP.EVAL' || '.FOC AS CITYSEG';
-SET &CITYPAIR='&SRC.EVAL' || 'S_' || '&COMP.EVAL' || '.FOC AS CITYPAIR';
-SET &CITYMRK='&SRC.EVAL' || 'M_' || '&COMP.EVAL' || '.FOC AS CITYMRK';
-SET &CITYDST='&SRC.EVAL' || 'D_' || '&COMP.EVAL' || '.FOC AS CITYDST';
-SET &CR_50='&SRC.EVAL' || 'C_' || '&COMP.EVAL' || '.FOC AS CR_50';
-SET &LR_50='&SRC.EVAL' || 'L_' || '&COMP.EVAL' || '.FOC AS LR_50';
-SET &HR_50='&SRC.EVAL' || 'H_' || '&COMP.EVAL' || '.FOC AS HR_50';
 
 
USE CLEAR *
USE
&SR_50.EVAL
&MR_50.EVAL
&AIRMAIN.EVAL
&CITYSEG.EVAL
&CITYPAIR.EVAL
&CITYMRK.EVAL
&CITYDST.EVAL
&CR_50.EVAL
&LR_50.EVAL
&HR_50.EVAL
END
-RUN
 
-INCLUDE ROLLUP_SUMMARY_AIR0
-INCLUDE ROLLUP_SUMMARY_CAR0
-INCLUDE ROLLUP_SUMMARY_LIMO0
-INCLUDE ROLLUP_SUMMARY_HTL0
 
-GOTO TOEND1;
 
-QUARTED
 
-SET &CNT = 0;
-REPEAT INNER 12 TIMES
 
-SET &CNTX = IF &CNT EQ 10 THEN 'Y' ELSE
-            IF &CNT EQ 11 THEN 'Z' ELSE &CNT;
 
-SET &SR_50='&SRC.EVAL' || 'S' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS SR_50';
-SET &MR_50='&SRC.EVAL' || 'M' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS MR_50';
-SET &AIRMAIN='&SRC.EVAL' || 'A' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS AIR_MAIN';
-SET &CITYSEG='&SRC.EVAL' || 'S' || '&CNTX.EVAL' ||'&COMP.EVAL' || '.FOC AS CITYSEG';
-SET &CITYPAIR='&SRC.EVAL' || 'S' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS CITYPAIR';
-SET &CITYMRK='&SRC.EVAL' || 'M' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS CITYMRK';
-SET &CITYDST='&SRC.EVAL' || 'D' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS CITYDST';
-SET &CR_50='&SRC.EVAL' || 'C' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS CR_50';
-SET &LR_50='&SRC.EVAL' || 'L' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS LR_50';
-SET &HR_50='&SRC.EVAL' || 'H' || '&CNTX.EVAL' || '&COMP.EVAL' || '.FOC AS HR_50';
 
USE CLEAR *
USE
&SR_50.EVAL
&MR_50.EVAL
&AIRMAIN.EVAL
&CITYSEG.EVAL
&CITYPAIR.EVAL
&CITYMRK.EVAL
&CITYDST.EVAL
&CR_50.EVAL
&LR_50.EVAL
&HR_50.EVAL
END
-RUN
 
-INCLUDE ROLLUP_SUMMARY_AIR0
-INCLUDE ROLLUP_SUMMARY_CAR0
-INCLUDE ROLLUP_SUMMARY_LIMO0
-INCLUDE ROLLUP_SUMMARY_HTL0
 
-SET &CNT = &CNT + 1;
-INNER
 
-TOEND1
 
-SET &HLDPATH=TEMPPATH(50,'A50');
-*-EXIT
-A
-*-SET &RSFOC=&HLDPATH || 'ROLLUP_SUMMARY2.FOC';
-SET &RSFOC=&SRC || 'ROLLUP_SUMMARY2.FOC';
-SET &RSMAS=&SRC || 'ROLLUP_SUMMARY2.MAS';
COPY &RSFOC.EVAL D:\TNT\TTracker\data\ROLLUP_SUMMARY.FOC
COPY &RSMAS.EVAL D:\TNT\TTracker\data\ROLLUP_SUMMARY.MAS
-RUN
