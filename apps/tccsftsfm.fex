-*12/22/16-S-27948-JEM-Creation of ROUTINE FOR CSF/TSF UDIDS 

-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED 
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE SR_50

  FARE_PAID/P15.2CS   = SEG_AMT + SEG_TAX;   
  NET_TKT_CNT/P12CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TRNMY/MTY = TRN_DATE;  
                    
END

TABLE FILE SR_50
PRINT AIR_KEY	
  FARE_PAID	
  NET_TKT_CNT	
  TKT_NUM
  SEG_NBR
  TRNMY
-* -INCLUDE &AIRDATES
WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS

-* ON TABLE HOLD AS &&RPT_HOLD
ON TABLE HOLD AS REPHOLD
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;

TABLE FILE REPHOLD
PRINT FARE_PAID NET_TKT_CNT TKT_NUM SEG_NBR TRNMY
BY AIR_KEY
ON TABLE HOLD AS TCSD3X FORMAT FOCUS INDEX AIR_KEY
END
-RUN

-INCLUDE TCUDAUSE
-RUN

-* CSF UDID

TABLE FILE UDAIR
PRINT AUD_DATA AS 'CSF'
BY AIR_KEY
WHERE UDID EQ 'CSF'
ON TABLE HOLD AS TCCSF
END
-RUN


DEFINE FILE TCCSF
CSF1/A5 = GETTOK(CSF, 64, 1, '/', 5, CSF1);
CSF_AMT/D12.2CSM = EDIT(CSF1);
END
TABLE FILE TCCSF
SUM CSF_AMT
BY AIR_KEY
ON TABLE HOLD AS TCCSF2
END
-RUN


-* TSF UDID

TABLE FILE UDAIR
SUM AUD_DATA AS 'TSF'
BY AIR_KEY
WHERE UDID EQ 'TSF'
ON TABLE HOLD AS TCTSF
END
-RUN


DEFINE FILE TCTSF
TSF1/A5 = GETTOK(TSF, 64, 1, '/', 5, TSF1);
TSF_AMT/D12.2CSM = EDIT(TSF1);
END
TABLE FILE TCTSF
SUM TSF_AMT
BY AIR_KEY
ON TABLE HOLD AS TCTSF2
END
-RUN

MATCH FILE TCCSF2
SUM CSF_AMT
BY AIR_KEY
ON TABLE HOLD
RUN
FILE TCTSF2
SUM TSF_AMT
BY AIR_KEY
AFTER MATCH HOLD AS TCSFTSFH1 OLD-OR-NEW
END


TABLE FILE TCSFTSFH1
SUM CSF_AMT TSF_AMT
BY AIR_KEY
ON TABLE HOLD AS TCSFTSFH2 
END


JOIN AIR_KEY IN TCSD3X TO AIR_KEY IN TCSFTSFH2 AS JJ
-RUN

DEFINE FILE TCSD3X
CSF_AMT1A/P12.2CSM = IF SEG_NBR EQ '01' THEN CSF_AMT ELSE 0; 
TSF_AMT1A/P12.2CSM = IF SEG_NBR EQ '01' THEN TSF_AMT ELSE 0;
CSF_AMT1/P12.2CSM = IF FARE_PAID LT 0 THEN 0 ELSE CSF_AMT1A;
TSF_AMT1/P12.2CSM = IF FARE_PAID LT 0 THEN 0 ELSE TSF_AMT1A;
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCSD3X
SUM FARE_PAID NET_TKT_CNT CSF_AMT1 AS 'CSF_AMT' TSF_AMT1 AS 'TSF_AMT'
BY TRNMY
BY ROLLCD
ON TABLE HOLD AS TCSD4X 
END
-RUN


DEFINE FILE TCSD4X
 NMB/I5 = NMB + 1;
END
TABLE FILE TCSD4X
PRINT CSF_AMT 
      TSF_AMT
BY TRNMY      
BY ROLLCD
ON TABLE HOLD AS TCSD3
END
-RUN


MODIFY FILE TC_VSV2M
FIXFORM FROM TCSD3
MATCH TRNMY ROLLCD
ON MATCH UPDATE CSF_AMT TSF_AMT
ON NOMATCH INCLUDE
DATA ON TCSD3
END
-RUN



-NEXTONE

 


