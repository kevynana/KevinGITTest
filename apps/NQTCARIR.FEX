-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the new quarterly review -  DV
-******************************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN


-SET &XY1 = ' ';
-SET &XY2 = ' ' ;
-SET &PAAMT1 = 0;
-SET &TOTAMT= 0;
-SET &PAPCT = 0;
-SET &CARIRL1= ' ';
-SET &PYFLG = 'N';

TABLE FILE QRCARIR
SUM CMAMT
    CMCNT
    PCT.CMCNT/D6.2 AS 'CMPCT'
    CMILE
    PMAMT
    PMCNT
    PCT.PMCNT/D6.2 AS 'PMPCT'
    PMILE
COMPUTE CAMF/D6M = IF ((CMCNT LT 1) AND (CMCNT GT (-1))) THEN 0 ELSE
                       (CMAMT/CMCNT);
COMPUTE CCPM/D6.2 = IF ((CMILE LT 1) AND (CMILE GT (-1))) THEN 0 ELSE
                        (CMAMT/CMILE); 
COMPUTE PAMF/D6M = IF ((PMCNT LT 1) AND (PMCNT GT (-1))) THEN 0 ELSE
                       (PMAMT/PMCNT);
COMPUTE PCPM/D6.2 = IF ((PMILE LT 1) AND (PMILE GT (-1))) THEN 0 ELSE
                        (PMAMT/PMILE);                         
BY ROLLUP_CODE
BY RANK_VALUE
BY AIRLINEN
ON TABLE HOLD AS BASEMKT
END
-RUN


DEFINE FILE QRPCARIR
ROLL/A8 = IF ROLLUP_CODE EQ '&&BASEROLL.EVAL' THEN '&&BASEROLL.EVAL' ELSE 'PEER-R';
END
TABLE FILE QRPCARIR
SUM CMAMT
    CMCNT
    CMILE
    PMAMT
    PMCNT
    PMILE
COMPUTE PCAMF/D6M = IF ((CMCNT LT 1) AND (CMCNT GT (-1))) THEN 0 ELSE
                       (CMAMT/CMCNT);
COMPUTE PCCPM/D6.2 = IF ((CMILE LT 1) AND (CMILE GT (-1))) THEN 0 ELSE
                        (CMAMT/CMILE); 
COMPUTE PPAMF/D6M = IF ((PMCNT LT 1) AND (PMCNT GT (-1))) THEN 0 ELSE
                       (PMAMT/PMCNT);
COMPUTE PPCPM/D6.2 = IF ((PMILE LT 1) AND (PMILE GT (-1))) THEN 0 ELSE
                        (PMAMT/PMILE);                         
BY ROLL AS 'ROLLUP_CODE'
BY AIRLINEN
WHERE ROLL NE '&&BASEROLL.EVAL'
ON TABLE HOLD AS PEERMKT
END
-RUN

MATCH FILE BASEMKT
PRINT CMAMT
      CMCNT
      CMPCT
      CMILE
      PMAMT
      PMCNT
      PMPCT
      PMILE
      CAMF
      CCPM
      PAMF
      PCPM
      RANK_VALUE
BY AIRLINEN
ON TABLE HOLD
RUN
FILE PEERMKT
PRINT PCAMF
      PCCPM
      PPAMF
      PPCPM
BY AIRLINEN
AFTER MATCH HOLD AS ALLMKT OLD-OR-NEW
END
-RUN


DEFINE FILE QRCARIR
AIRLINE/A15 = 'Total';
RANK/A9 = 'Total'
END
TABLE FILE QRCARIR
SUM CMAMT
    CMCNT
    PCT.CMCNT/D6.2 AS 'CMPCT'
    CMILE
    PMAMT
    PMCNT
    PCT.PMCNT/D6.2 AS 'PMPCT'
    PMILE
COMPUTE CAMF/D6M = IF ((CMCNT LT 1) AND (CMCNT GT (-1))) THEN 0 ELSE
                       (CMAMT/CMCNT);
COMPUTE CCPM/D6.2 = IF ((CMILE LT 1) AND (CMILE GT (-1))) THEN 0 ELSE
                        (CMAMT/CMILE); 
COMPUTE PAMF/D6M = IF ((PMCNT LT 1) AND (PMCNT GT (-1))) THEN 0 ELSE
                       (PMAMT/PMCNT);
COMPUTE PCPM/D6.2 = IF ((PMILE LT 1) AND (PMILE GT (-1))) THEN 0 ELSE
                        (PMAMT/PMILE);                         
BY RANK AS 'RANK_VALUE'
BY AIRLINE AS 'AIRLINEN'
ON TABLE HOLD AS BMKTYTD
END
-RUN


DEFINE FILE QRPCARIR
AIRLINE/A15 = 'Total';
END
TABLE FILE QRPCARIR
SUM CMAMT
    CMCNT
    CMILE
    PMAMT
    PMCNT
    PMILE
COMPUTE PCAMF/D6M = IF ((CMCNT LT 1) AND (CMCNT GT (-1))) THEN 0 ELSE
                       (CMAMT/CMCNT);
COMPUTE PCCPM/D6.2 = IF ((CMILE LT 1) AND (CMILE GT (-1))) THEN 0 ELSE
                        (CMAMT/CMILE); 
COMPUTE PPAMF/D6M = IF ((PMCNT LT 1) AND (PMCNT GT (-1))) THEN 0 ELSE
                       (PMAMT/PMCNT);
COMPUTE PPCPM/D6.2 = IF ((PMILE LT 1) AND (PMILE GT (-1))) THEN 0 ELSE
                        (PMAMT/PMILE);                         
BY AIRLINE AS 'AIRLINEN'
ON TABLE HOLD AS PMKTYTD
END
-RUN


MATCH FILE BMKTYTD
PRINT CMAMT
    CMCNT
    CMPCT
    CMILE
    PMAMT
    PMCNT
    PMPCT
    PMILE
    CAMF
    CCPM
    PAMF
    PCPM
    RANK_VALUE
BY AIRLINEN
ON TABLE HOLD
RUN
FILE PMKTYTD
PRINT PCAMF
      PCCPM
      PPAMF
      PPCPM
BY AIRLINEN
AFTER MATCH HOLD AS ALLMKTYTD OLD-OR-NEW
END
-RUN


TABLE FILE ALLMKT 
PRINT CMAMT
      CMCNT
      CMPCT
      PMAMT
      PMCNT
      PMPCT
      CAMF
      CCPM
      PAMF
      PCPM
      PCAMF
      PCCPM
      PPAMF
      PPCPM
BY RANK_VALUE
BY AIRLINEN
ON TABLE HOLD AS ALLMKT1 FORMAT FOCUS
END
-RUN

TABLE FILE ALLMKTYTD 
PRINT CMAMT
      CMCNT
      CMPCT
      PMAMT
      PMCNT
      PMPCT
      CAMF
      CCPM
      PAMF
      PCPM
      PCAMF
      PCCPM
      PPAMF
      PPCPM
BY RANK_VALUE
BY AIRLINEN
ON TABLE HOLD AS AMYTD1 FORMAT FOCUS
END
-RUN


-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &HRANK1=&HLDPATH || 'ALLMKT1.FOC';
-SET &HRANK2=&HLDPATH || 'AMYTD1.FOC';
-RUN

USE CLEAR *

USE ADD 
&HRANK1 AS ALLMKT1
&HRANK2 AS ALLMKT1
END
-RUN




-* Set up year variables for report title
TABLE FILE DCARI
BY YEAR
ON TABLE SAVE
END
-RUN

-SET &CNT = IF &LINES EQ 1 THEN 2 ELSE 1;
-REPEAT TOEND &LINES TIMES
-READ SAVE &XY.4.
-SET &XY.&CNT = '&XY.EVAL';
-SET &CNT = &CNT + 1;
-TOEND
-CLOSE SAVE
 


-IF &LINES EQ 2 THEN GOTO TWOYEARS;
DEFINE FILE DCARI
-INCLUDE QRGET1YR
END
  
TABLE FILE DCARI
SUM YEAR1
BY YEAR NOPRINT
ON TABLE SAVE AS SAVE1YR
END
-RUN
-READ SAVE1YR &XY1.4.
-TYPE &XY1
-TWOYEARS

TABLE FILE ALLMKT1
BY AIRLINEN
ON TABLE SAVE
END
-RUN

-SET &SKIPLINES = IF &LINES EQ 12 THEN 1 ELSE (12 - &LINES);
-SET &FTLNS = &SKIPLINES + 1;

TABLE FILE DOMCARI
BY TRAN
WHERE PRIOR EQ 'Y'
ON TABLE SAVE
END
-RUN

-SET &PYFLG = IF &LINES LT 4 THEN 'N' ELSE 'Y';

TABLE FILE ALLMKT1
SUM PMAMT
ON TABLE SAVE
END
-RUN

-READ SAVE &PMAMT.12.
-SET &PMAMT1 = &PMAMT;
-CLOSE SAVE

DEFINE FILE ALLMKT1
PAAMT/D12 = IF RANK_VALUE EQ '        1' OR '        2' OR '        3' THEN CMAMT ELSE 0;
END
TABLE FILE ALLMKT1
SUM PAAMT
ON TABLE SAVE
END
-RUN

-READ SAVE &PAAMT.12.
-SET &PAAMT1 = &PAAMT;
-CLOSE SAVE

TABLE FILE ALLMKT1
PRINT CMAMT
IF AIRLINEN EQ 'Total'
ON TABLE SAVE
END
-RUN

-READ SAVE &CMAMT.12.
-SET &TOTAMT = &CMAMT;
-CLOSE SAVE


-SET &PAPCT = IF ((&TOTAMT LT 1) AND (&PAAMT1 GT (-1))) THEN 0 ELSE ((&PAAMT1/&TOTAMT) * 100);
-SET &CARIRTL1 = 'Top 3 Airlines represent ' | &PAPCT.EVAL% | ' of total spend.';

DEFINE FILE ALLMKT1
ALABEL1/A18 = 'Year to Date ' | '&XY2.EVAL';
ALABEL2/A18 = 'Year to Date ' | '&XY1.EVAL';
ALABEL3/A10 = 'Variance';
ACMPCT/A6 = FTOA(CMPCT,'(D6.2)',ACMPCT);
ACMPCT1/A7 = ACMPCT | '%';
APMPCT/A6 = FTOA(PMPCT,'(D6.2)',APMPCT);
APMPCT1/A7 = APMPCT | '%';
OTH_VAL/A9 = IF AIRLINEN EQ 'Other' THEN 'Other' ELSE RANK_VALUE;
RANK_VAL/A9 = IF OTH_VAL EQ 'Other' OR 'Total' THEN '         ' ELSE RANK_VALUE ;
FLAG/A1 = ' ';
END
TABLE FILE ALLMKT1
HEADING
"</1"
"<26<ALABEL1 <62<ALABEL2 <88<ALABEL3"
SUM RANK_VAL AS ' ' IN 1
      AIRLINEN AS 'Carrier' IN 5
      CMCNT AS 'Market,Pairs' IN 13
      ACMPCT1 AS '% of,Total,Markets' IN 18
      CMAMT/D12M AS 'Fare Paid,   &XY2.EVAL' IN 23
      CAMF AS 'Avg,Market,Fare' IN 29
      CCPM AS 'Cost,Per,Mile,(CPM)' IN 33
      PCAMF AS 'Peer,Group,Avg,Fare' IN 38
      PCCPM AS 'Peer,Group,CPM' IN 42
      PMCNT AS 'Market,Pairs' IN 48
      APMPCT1 AS '% of,Total,Markets' IN 53
      PMAMT/D12M AS 'Fare Paid,   &XY1.EVAL' IN 58
      PAMF AS 'Avg,Market,Fare' IN 65
      PCPM AS 'Cost,Per,Mile,(CPM)' IN 69
      PPAMF AS 'Peer,Group,Avg,Fare' IN 74
      PPCPM AS 'Peer,Group,CPM' IN 79
-IF '&PYFLG.EVAL' EQ 'N' THEN GOTO NoPYear;
COMPUTE AMFVAR/D6M = (CAMF-PAMF); AS 'Average,Market,Fare' IN 86
COMPUTE CPMVAR/D6.2 = (CCPM-PCPM); AS 'CPM' IN 91
-GOTO FinRpt;
-NoPYear;
     FLAG AS 'Average,Market,Fare' IN 86
     FLAG AS 'CPM' IN 91
-FinRpt;     
BY OTH_VAL NOPRINT
BY AIRLINEN NOPRINT

ON TABLE SET PAGE-NUM OFF
&&OUTLINE1
&&OUTLINE2
FOOTING
"</&SKIPLINES <5 &CARIRTL1" 
" "
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.100000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $   

TYPE=HEADING, SIZE=8, LINE=3, OBJECT=FIELD, STYLE=BOLD,$ 
TYPE=TITLE, STYLE=BOLD,$
TYPE=REPORT, IMAGE=bgcgreyH.GIF,POSITION=(0.70 0.85), SIZE=(9.5 0.20), $
TYPE=REPORT, IMAGE=bgcgrey.GIF,POSITION=(5.1 0.1), SIZE=(0.10  3.5), $
TYPE=REPORT, IMAGE=bgcgrey.GIF,POSITION=(9.1 0.1), SIZE=(0.10  3.5), $
TYPE=FOOTING,FONT=COURIER NEW, COLOR=BLACK, BACKCOLOR=WHITE, $ 
TYPE=FOOTING, SIZE=15, LINE=&FTLNS, OBJECT=TEXT, FONT=TIMES NEW ROMAN, $

TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD,  $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=REPORT,GRID=OFF, $
TYPE=REPORT, IMAGE=CATotal.GIF,POSITION=(0.1 0.1), SIZE=(10.75 .75), $
-IF '&&SKIPGRAPH.EVAL' EQ 'Y' THEN GOTO NOIMAGE;
-* -IF &&G_CARID EQ 'N' GOTO SKIP_CARID;
-IF &&GIF.&SVGFILE EQ 'N' GOTO SKIP_CARID;
 TYPE=REPORT,
-*  IMAGE=CARID.SVG,
 IMAGE=&&CARIG,
  POSITION=(0.05  4.25), SIZE=(6.0 3.75), $
-* POSITION=(0.05  3.5), SIZE=(6.0 3.75), $

-SKIP_CARID
-* -IF &&G_CARIDP EQ 'N' GOTO SKIP_CARIPD;
-IF &&GIF.&SVGFILE2 EQ 'N' GOTO SKIP_CARID;

 TYPE=REPORT,
-*  IMAGE=CARIPD.SVG,
 IMAGE=&&CARIG2,
 POSITION=(5.5  4.25), SIZE=(6.0 3.75), $
 
-* POSITION=(5.05  3.5), SIZE=(6.0 3.75), $

-SKIP_CARIPD
-NOIMAGE

ENDSTYLE
END
-RUN
