SET ASNAMES=ON

-INCLUDE SETECHO
SET ASNAMES=ON

-DEFAULT &RANK = 10;
-DEFAULT &FROMDT='2000/01/01';
-DEFAULT &TODT= '2000/01/31';
-DEFAULT &ROLLUP = ' ';
-DEFAULT &REQUESTER = ' ';
-DEFAULT &OUTFMT = ' ';
-DEFAULT &ICFLAG = ' ';
-************************************************
-* Date Range Selection
-IF '&FROMDT.EVAL' EQ '2000/01/01' THEN GOTO NODTE;
-SET &DATE ='WHERE (DATE_COMPLETED GE ''&FROMDT.EVAL'') AND (DATE_COMPLETED LE ''&TODT.EVAL'')';
-GOTO ROLL;
-NODTE;
-SET &DATE = ' ';

-*************************************************

-************************************************
-* Rollup Selection
-ROLL
-IF '&ROLLUP.EVAL' EQ ' ' THEN GOTO NOROLL;
-SET &ROLLUP = 'WHERE AREA_REQ EQ ''&ROLLUP.EVAL''';
-GOTO REQUESTER;
-NOROLL
-SET &ROLLUP = ' ';
-RUN
-************************************************

-************************************************
-************************************************
-REQUESTER
-*Requester Selection
-IF '&REQUESTER.EVAL' EQ ' ' THEN GOTO NOREQUEST;
-SET &REQUESTERA = 'WHERE REQUESTER1 EQ ''&REQUESTER.EVAL''';
-SET &REQUESTER1 = '&REQUESTER.EVAL';
-GOTO OUTPUT;
-NOREQUEST
-SET &REQUESTERA = ' ';
-SET &REQUESTER1 = 'NONE';
-RUN
-*************************************************

-*************************************************
-OUTPUT 
-* Output format
-IF '&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO EXCEL ELSE
-IF '&OUTFMT.EVAL' EQ 'PDF' THEN GOTO PDF ELSE GOTO DEFAULT;
-RUN
-EXCEL
-SET &OUTFMT1 = '.xls';
-GOTO BTYPE;
-PDF
-SET &OUTFMT1 = '.pdf';
-GOTO BTYPE;
-DEFAULT
-SET &OUTFMT = 'PDF';
-SET &OUTFMT1 = '.pdf';
-RUN

-****************************************************

-************************************************
-BTYPE
-* Client/Internal Billing Type
-IF '&ICFLAG.EVAL' EQ ' ' THEN GOTO NOIC;
-SET &ICFLAGA = 'WHERE ICFLAG EQ ''&ICFLAG.EVAL''';
-SET &ICFLAG1 = '&ICFLAG.EVAL';
-GOTO BEGIN;
-NOIC
-SET &ICFLAGA = 'WHERE ICFLAG EQ ''C'' OR ''I'' OR '' ''';
-SET &ICFLAG1 = 'CI';
-RUN
-***********************************************


-BEGIN;
DEFINE FILE HOURTRAK
REQUESTER1/A20 = UPCASE(20, REQUESTOR, 'A20');
END
TABLE FILE HOURTRAK
PRINT *
BY AREA_REQ 
&DATE
&ROLLUP
&REQUESTERA
&ICFLAGA
ON TABLE HOLD AS H1 FORMAT FOCUS INDEX AREA_REQ
END
-RUN

JOIN AREA_REQ IN H1 TO ROLLUP_CODE IN ROLLUP 
-RUN

DEFINE FILE H1
CODE/A1 = EDIT(AREA_REQ, '$$9$$$$$');
BRANCH_CODE/A2 = IF AREA_REQ CONTAINS '-R' THEN ' ' ELSE 
                 IF CODE EQ ' ' THEN EDIT (AREA_REQ, '99$$$$$$') ELSE ' ';
END
-RUN

TABLE FILE H1
PRINT AREA_REQ ROLL_NAME COMPLETED_BY REQUESTOR DATE_COMPLETED COMPLETED_BY SHORT_DESC
      BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS KEY AREA
BY BRANCH_CODE
ON TABLE HOLD AS H2 FORMAT FOCUS INDEX BRANCH_CODE
END
-RUN


JOIN BRANCH_CODE IN H2 TO BRANCH_CODE IN BRANCH
-RUN

TABLE FILE H2
PRINT AREA_REQ ROLL_NAME COMPLETED_BY REQUESTOR DATE_COMPLETED SHORT_DESC BRANCH_CODE ROLL_NAME
      BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS BRANCH_NAME KEY AREA
BY COMPLETED_BY
ON TABLE HOLD AS H3
END
-RUN

DEFINE FILE H3
BNAME/A25 = IF BRANCH_CODE EQ ' ' AND ROLL_NAME NE ' ' THEN ROLL_NAME ELSE 
            IF BRANCH_CODE EQ ' ' AND ROLL_NAME EQ ' ' THEN AREA_REQ ELSE BRANCH_NAME;
END
-RUN

TABLE FILE H3
SUM TOTAL_HOURS
BY REQUESTOR
ON TABLE HOLD AS H3A
END
-RUN

TABLE FILE H3A
PRINT *
RANKED BY HIGHEST TOTAL_HOURS
ON TABLE HOLD AS H4
END
-RUN


DEFINE FILE H4
DRANK/D5 = RANK;
ARANKX/A6     = FTOA(DRANK, '(D5)', 'A6');
ARANK/A5      = EDIT(ARANKX,'$99999');
RANK_LIMIT/I6 = &RANK.EVAL;
RNK_VAL/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
             IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
RANK_LABEL/A20 = IF RNK_VAL NE 'OTHER' THEN REQUESTOR ELSE 'All Other';
END
-RUN

-SET &RPTSUF = 'HOURTRAK3';
-SET &FMD=EDIT(&FROMDT, '$$$$$$$$99');
-SET &FMY=EDIT(&FROMDT, '9999$$$$$$');
-SET &FMM=EDIT(&FROMDT, '$$$$$99$$$');
-SET &TOD=EDIT(&TODT, '$$$$$$$$99');
-SET &TOY=EDIT(&TODT, '9999$$$$$$');
-SET &TOM=EDIT(&TODT, '$$$$$99$$$');
-SET &DATE1 = IF &DATE EQ ' ' THEN 'No Period Selected - All Included' ELSE '&DATE';
-SET &NOWTM = HHMMSS ('A8');
-SET &NOWTM = EDIT(&NOWTM,'99$99$99');
-SET &NOWDT = &YYMD;
-SET &NOWDTTM = &NOWDT || '_' || &NOWTM;
-SET &DTTMIID = &RPTSUF || '_' || &NOWDTTM;
-SET &RPTPATH = 'D:/HOURTRAK/';
-* -SET &XTN = '.PDF';
-SET &SAVTRAK = &RPTPATH||&DTTMIID||&OUTFMT1;
-* -SET &SAVTRAK1 = &RPTPATH||&DTTMIID;
-RUN

FILEDEF SAVREP DISK -
&SAVTRAK


TABLE FILE H4
HEADING CENTER
"<30 Hour Tracking by Most Frequent Requester"
-IF &DATE1 EQ 'No Period Selected - All Included' THEN GOTO NOHEAD;
"<30 Completion Period: &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
-GOTO RPT;
-NOHEAD
"<30 Completion Period: &DATE1"
-RPT;
"</2"
SUM TOTAL_HOURS AS 'Total,Hours' 
BY RNK_VAL NOPRINT
BY RANK_LABEL AS 'Requester'
ON RANK_LABEL SKIP-LINE
ON TABLE COLUMN-TOTAL
ON TABLE SAVE AS SAVREP FORMAT &OUTFMT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
     PAGESIZE='Letter',
     LEFTMARGIN=0.250000,
     RIGHTMARGIN=0.250000,
     TOPMARGIN=0.250000,
     BOTTOMMARGIN=0.250000,
     SQUEEZE=ON,
     ORIENTATION=PORTRAIT,
$
TYPE=REPORT,
     GRID=OFF,
     FONT='TIMES NEW ROMAN',
     SIZE=7,
     BACKCOLOR='NONE',
     STYLE=NORMAL,
$     
    
TYPE=HEADING, 
     FONT='TIMES NEW ROMAN',
     SIZE=8,
     BACKCOLOR='NONE',
     STYLE=BOLD,
$
TYPE=TITLE,
     FONT='TIMES NEW ROMAN',
     SIZE=7,
     BACKCOLOR='NONE',
     STYLE=BOLD,
$
TYPE=DATA, COLUMN=N2, STYLE=UNDERLINE,WHEN=RANK_LABEL NE 'All Other',
     URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
     (IBIF_ex='DRHTRK3' RANK_LABEL=N2 FROM='&FROMDT.EVAL' TO='&TODT.EVAL' ICFLAG1='&ICFLAG1.EVAL'\
      FPARM='&DTTMIID.EVAL'), $ 
      
TYPE=REPORT,IMAGE=REVIEW,POSITION=(.3 .3), SIZE=(1.3 .44),$
TYPE=REPORT,IMAGE=ISERIES,POSITION=(6.7 .15), SIZE=(1.0 1.0), $
ENDSTYLE
END




