-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/9/14 Updated Pref_non define to use exclude_flags - jk
-* 9/23/14 Updated to use exclude flags in % - JK
-* Story ID S-10086 Added &&overdrill flag logic for use in 5.0 to choose to for the report to be drillable or non-drillable - 9/15/15 - JM
-*4/27/2017-JEM-S-33318-updated logic for no records report
-* 08/28/2017 RSB S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets

-INCLUDE SETECHO

TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN


-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT 
  CITY_ST/A35 = PROP_CITY||(' '| PROP_STATE);
  LEVEL_DESC/A60  = &&LDESC;
  NBR_DAYS/D12=NUM_DAYS;
  NBR_ROOMS/D12 = NUM_ROOMS;
  
 PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
 PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');  
  
  TOTAL_RATE/D14.2C = TOTAL_AMT;
  NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
  REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
  -*PR_FLAG/A1 = IF CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'Y' ELSE 'N';
  PR_FLAG/A1 = IF PNP_FLAG EQ 'I' THEN 'Y' ELSE 'N';
-* NPR_FLAG/A1 = IF CLIENT_PREF NE 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'Y' ELSE 'N';
  NPR_FLAG/A1 = IF PNP_FLAG NE 'I' THEN 'Y' ELSE 'N';
  TTL_FLAG/A1 = IF REFUSE_CODE NE 'M' OR 'MT' OR 'MV' OR 'NT' OR 'NV' OR 'S' OR 'ST' OR 'SV' THEN 'Y' ELSE 'N';
  HDP_FLAG/A1 = IF REFUSE_CODE EQ 'N' OR 'I' OR 'V' THEN 'N' ELSE 'Y';
  

-* Defines on Previous Defines
BOOKINGS/D8 = IF TOTAL_RATE GE 0 THEN 1 ELSE (-1);

-* Defines on previously defines defines

NONPRM/P8CS = IF NPR_FLAG EQ 'Y' THEN NNROOMS ELSE 0;
PREFRM/P8CS = IF PR_FLAG EQ 'Y' THEN NNROOMS ELSE 0;
TTLRM/P8CS = IF TTL_FLAG EQ 'Y' THEN NNROOMS ELSE 0;

NONPAM/D12.2CS = IF NPR_FLAG EQ 'Y' THEN TOTAL_RATE ELSE 0;
PREFAM/D12.2CS = IF PR_FLAG EQ 'Y' THEN TOTAL_RATE ELSE 0;
TTLAM/D12.2CS = IF TTL_FLAG EQ 'Y' THEN TOTAL_RATE ELSE 0;

NONPBK/P8CS = IF NPR_FLAG EQ 'Y' THEN BOOKINGS ELSE 0;
NONPBK1/P8CS = IF PNP_FLAG EQ 'E' AND PNP_IE EQ 'I' THEN BOOKINGS ELSE 0;

PREFBK/P8CS = IF PR_FLAG EQ 'Y' THEN BOOKINGS ELSE 0;
TTLBK/P8CS = IF TTL_FLAG EQ 'Y' THEN BOOKINGS ELSE 0;
TTLBK1/P12 = IF PNP_IE EQ 'I' THEN BOOKINGS ELSE 0;

END
-RUN

TABLEF FILE &&EXTRACT
PRINT LEVEL_DESC
      LEVEL2
      PASSNGR_NAME
      NONPRM
      PREFRM
      TTLRM
      NONPAM
      PREFAM
      TTLAM
      NONPBK
      NONPBK1
      PREFBK
      TTLBK
      TTLBK1
      TOTAL_AMT
      HTL_KEY
      REFUSE_CODE
-INCLUDE &HTLDATES  
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
WHERE HDP_FLAG EQ 'Y'
-INCLUDE RPTPARMS
ON TABLE HOLD AS HHOLD
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;

TABLE FILE HHOLD
PRINT NONPRM
    PREFRM
    TTLRM
    NONPAM
    PREFAM
    TTLAM
    NONPBK
    NONPBK1
    PREFBK
    TTLBK
    TTLBK1
    TOTAL_AMT
    PASSNGR_NAME
    LEVEL_DESC
    LEVEL2
BY HTL_KEY    
ON TABLE HOLD AS RPTHOLD
END
-RUN



-* Join to UDIDs to add Employee Number

-INCLUDE RETUDID
-RUN

-IF '&&ROLLUP.EVAL' EQ 'HDRR-R' THEN GOTO EXHDR;

-EXHDR

EX UDHHDR
-RUN

DEFINE FILE HTLHLD2
EMP_NUMA/A15 = EDIT(EMP_NUM1, '999999999999999');
END
TABLE FILE HTLHLD2
SUM NONPRM
    PREFRM
    TTLRM
    NONPAM
    PREFAM
    TTLAM
    NONPBK
    NONPBK1
    PREFBK
    TTLBK
    TTLBK1
    EMP_NUMA
    LEVEL2
BY LEVEL_DESC    
BY PASSNGR_NAME
-* BY EMP_NUMA
ON TABLE HOLD AS HTLHLD3
END
-RUN


TABLE FILE HTLHLD3
SUM NONPRM
    PREFRM
    TTLRM
    NONPAM
    PREFAM
    TTLAM
    NONPBK
    NONPBK1
    PREFBK
    TTLBK
    TTLBK1
BY LEVEL_DESC    
RANKED BY HIGHEST 50 TOTAL NONPBK
BY PASSNGR_NAME
BY LEVEL2
ON TABLE HOLD AS RANKH
END
-RUN


DEFINE FILE RANKH
 CNT/I2 = CNT + 1;
 CNT2/I2 = IF CNT LE 50 THEN CNT ELSE 51;
 PAX/A33 = IF CNT2 LE 50 THEN PASSNGR_NAME ELSE 'ALL OTHERS';
 EMP_NUM/A15 = IF CNT2 LE 50 THEN LEVEL2 ELSE 'ALL OTHERS';
END

TABLE FILE RANKH
SUM NONPRM
    PREFRM
    TTLRM
    NONPAM
    PREFAM
    TTLAM
    NONPBK
    NONPBK1
    PREFBK
    TTLBK
    TTLBK1
BY LEVEL_DESC 
BY CNT2 
BY PAX 
BY EMP_NUM
ON TABLE HOLD AS HTLHOLD
END
-RUN


-SET &&RPTSUF = 'NONPREFHTL';

-INCLUDE FDEFRPTS
-RUN


DEFINE FILE HTLHOLD
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
FLAG/A5 = 'TOTAL';
END
-RUN


TABLE FILE HTLHOLD
-INCLUDE HEADER
SUM TTLBK AS 'TTL Number of,Hotel Bookings'
    PREFBK AS 'Number of,Preferred Hotel,Bookings'
COMPUTE PREFAR/D12.2CSM = IF ((PREFRM LT 1) AND (PREFRM GT (-1))) THEN 0 ELSE (PREFAM/PREFRM);  AS 'Preferred Hotel,Average Nightly,Rate'
    NONPBK AS 'Number of,NonPreferred,Hotel Bookings'
COMPUTE NPREFAR/D12.2CSM = IF ((NONPRM LT 1) AND (NONPRM GT (-1))) THEN 0 ELSE (NONPAM/NONPRM); AS 'NonPreferred Hotel,Average Nightly,Rate'
COMPUTE NPPCT/P8% = IF ((TTLBK1 LT 1) AND (TTLBK1 GT (-1))) THEN 0 ELSE (NONPBK1/TTLBK1)*100; AS '% Of,NonPreferred,Hotel Bookings,to Total'
BY LEVEL_DESC NOPRINT
BY CNT2 NOPRINT
BY PAX AS 'Hotel Guest'
BY EMP_NUM AS 'Employee,Number'
&&BREAKOPTION
ON LEVEL_DESC RECOMPUTE MULTILINES AS 'Total For'
ON TABLE NOTOTAL
ON TABLE SET PAGE-NUM OFF
-*
-INCLUDE FOOTERSM
-*
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
-*
-INCLUDE &&SMSTY
-*
-IF &&OVERDRILL EQ 'Y' GOTO OVERDRILL;

TYPE=DATA, COLUMN=N3, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRHDPNP' ROW_LABEL=N3 LSEL=LEVEL_DESC\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

-**********
-OVERDRILL
-**********
-IF &&OUTFMT EQ 'PDF' THEN GOTO NOWRAP;
TYPE=REPORT, WRAP= 1,$
-* TYPE=REPORT, COLUMN=N2, WRAP=2,$
TYPE=REPORT, COLUMN=N3, WRAP=0,$
-NOWRAP;
ENDSTYLE

&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN


-NORECORDS;


