-*=======================================================================
-* Add Column Heading into UFXXX.FOC files
-* 
-*=======================================================================

FI CUSTS DISK D:\LOAD\FOCTEMP\CUSTS.DAT
SET MESSAGE=ON
-*=======================================================================
SET ASNAMES=ON
DEFINE FILE ROLLUP
XL01/A1 = 'W' ;
END
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP AS 'ROLLUP_CODE'
      XL01   AS 'PR_STAMP'
WHERE ROLLUP_CODE NE 'ABCD2-R' OR 'TOTAL-R' OR 'RELTY-R' OR 'UNIG-R'
ON TABLE HOLD
END
MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-*======================================================================
USE CLEAR *
-RUN
SET TEMPDIR=D:\LOAD\FOCTEMP
SET MESSAGE=ON
SET WARNING=OFF
SET YRTHRESH=30
SET DEFCENT=19
-RUN
-*=======================================================================
-TOP
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP COMP
BY ROLLUP_CODE NOPRINT
IF PR_STAMP EQ 'W'
IF RECORDLIMIT EQ 1
WHERE ROLLUP_CODE NE 'ABCD2-R' OR 'TOTAL-R' OR 'RELTY-R' OR 'UNIG-R'
ON TABLE SAVE AS CUSTS
END
-RUN

-IF &RECORDS EQ 0 GOTO STOP;
-RUN
-READ CUSTS &&ROLLUP.A8 &&COMP.A6.
-RUN
-*=======================================================================
-*                 CREATE DATA FILES FOR PROCESSING
-*=======================================================================
-SET &&COMP = &&COMP;

-SET &&DBASE = 'D:\TNT\TTRACKER\DATA\'||'UF'||&&COMP|||'.FOC';
-SET &&DBMAS = 'D:\TNT\TTRACKER\DATA\'||'UF'||&&COMP|||'.MAS';
-SET &&CP1  = 'COPY D:\TNT\TTRACKER\DATA\UF_SYSTEM.MAS';
-SET &&FOCNAME = 'UF'||&&COMP;

-RUN
USE &&DBASE
END

-*========================================================================
REBUILD
REORG
DUMP
&&FOCNAME
NO
&&CP1 &&DBMAS
CREATE FILE &&FOCNAME
-RUN
REBUILD
REORG
LOAD
&&FOCNAME
YES
-*=======================================================================
SET ASNAMES=ON
DEFINE FILE ROLLUP
XL01/A1 = 'Z' ;
END
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP AS 'ROLLUP_CODE'
      XL01   AS 'PR_STAMP'
WHERE ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE HOLD
END
MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-GOTO TOP
-RUN
-*======================================================================
-STOP


