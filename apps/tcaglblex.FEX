-*6/29/16-JEM-S-16738 Created for new report that shows global data
-*11/7/16-JEM-S-26142-Adding counts/annualized spend fields per Patti 
-*3/1/17-JEM-S-31069-Updating Prior year to date to be Prior annual year 
-*8/29/17-JEM-S-34936 Replaced COMPANY_NAME WITH LEVEL_DESC

-INCLUDE SETECHO
SET ASNAMES = ON


-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED

-*========================================================================


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT

ALN_CODE/A2 = EDIT(AIRLINE, '99');              
              
FARE/D12.2CS = IF VOID_DATE EQ 0 THEN SEG_AMT + SEG_TAX ELSE 0;
FARE_PAID/D12.2CS = SEG_AMT + SEG_TAX;


NET_TKT_CNT/P8CS    = IF (TICKET_CODE EQ '1') AND
                         (SEG_NBR EQ '01') AND
                         (FARE_PAID GE 0) AND
                         (SEG_COUNT EQ 1) AND 
                         (CONJ_REL EQ 1) THEN 1 ELSE 
                      IF (TICKET_CODE EQ '1') AND
                         (SEG_NBR EQ '01') AND 
                         (EX_FLAG EQ 'F') THEN (-1) ELSE 
                      IF (TICKET_CODE EQ '1') AND
                         (SEG_NBR EQ '01') AND
                         (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
                         
NET_TKTS/P12CS = IF VOID_DATE EQ 0 THEN NET_TKT_CNT ELSE 0;                           
                         
ROLLCD/A8 = '&&ROLL'; 
-* Added for Total Trans

ORIG_SALE/P12 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
          (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND (RF_FLAG EQ ' ') THEN 1 ELSE 0;	
          
          
TKT_CNT/P12CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;	
  
FULL_RFNDS/P12 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (DOC_TYPE NE '1') AND (SEG_NBR EQ '01') AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
                           
  
PART_RFNDS/P12 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;


REFUNDS/P12 = FULL_RFNDS + PART_RFNDS;	
  

EXCHGS/P12 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
 NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE IF (DOC_TYPE EQ '1') AND 
(EXCHG_SALE EQ 'E') THEN 0 ELSE IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE 
  ' ') THEN TKT_CNT ELSE 0; 
  
EXMCOS/P12 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE 
	NE ' ') THEN 1 ELSE 0;  
	
VOIDS/P12 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01')
 THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) THEN 1 ELSE 0;	
	

TOT_ECNT/P12 = EXCHGS + EXMCOS;	

TOT_TRANS/P12 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;

-* DEFINES ON PREVIOUS

LEVEL_DESC/A40 = IF ROLLCD EQ 'CERN-R' THEN AROLL_LEV3 ELSE AROLL_LEV2;

END
-RUN
TABLE FILE &&EXTRACT
PRINT ALN_CODE
   FARE
   FARE_PAID
   LEVEL_DESC
   NET_TKTS
   ORIG_SYS
   ROLLCD
   TOT_TRANS
   TRN_DATE
   TNT_COMPANY
BY ALN_CODE
-* -INCLUDE &AIRDATES
-* WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
-*       (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)
-* WHERE VOID_DATE EQ 0    
WHERE CATEGORY EQ 'GLOBAL'
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TC_TRANA FORMAT FOCUS INDEX ALN_CODE
END

TABLE FILE RAIL_CODES
PRINT RAIL_CARRIER_CODE 
BY RAIL_CARRIER_CODE AS 'RAIL_CODE'
ON TABLE HOLD AS RH1
END

TABLE FILE RH1
PRINT RAIL_CARRIER_CODE
BY RAIL_CODE
ON TABLE HOLD AS RH2 FORMAT FOCUS INDEX RAIL_CODE
END

JOIN ALN_CODE IN TC_TRANA TO RAIL_CODE IN RH2 AS T1
-RUN


DEFINE FILE TC_TRANA
BCDATE/YYMD=&&FROMDT.EVAL;
BCDATE1/MT=BCDATE;
BC_MONTH/MDYY=BCDATE;
BC_YR1/MDYY=DATEMOV(BC_MONTH, 'BOY');
BC_YR/YYMD=BC_YR1;
BCD_NUM/P2 = IF BCDATE1 EQ 'JAN' THEN 1 ELSE
             IF BCDATE1 EQ 'FEB' THEN 2 ELSE
             IF BCDATE1 EQ 'MAR' THEN 3 ELSE
             IF BCDATE1 EQ 'APR' THEN 4 ELSE
             IF BCDATE1 EQ 'MAY' THEN 5 ELSE 
             IF BCDATE1 EQ 'JUN' THEN 6 ELSE
             IF BCDATE1 EQ 'JUL' THEN 7 ELSE
             IF BCDATE1 EQ 'AUG' THEN 8 ELSE
             IF BCDATE1 EQ 'SEP' THEN 9 ELSE
             IF BCDATE1 EQ 'OCT' THEN 10 ELSE
             IF BCDATE1 EQ 'NOV' THEN 11 ELSE 
             IF BCDATE1 EQ 'DEC' THEN 12 ELSE 0;

BPDATE/YYMD=&&FXDATE.EVAL;
BPDATE1/MT=BPDATE;
BP_MONTH/MDYY=BPDATE;
BP_YR1/MDYY=DATEMOV(BP_MONTH, 'BOY');
BP_YR1A/MDYY=DATEMOV(BP_MONTH, 'EOY');
BP_YR/YYMD=BP_YR1;
BP_YRA/YYMD=BP_YR1A;
BPD_NUM/P2 = IF BPDATE1 EQ 'JAN' THEN 1 ELSE
             IF BPDATE1 EQ 'FEB' THEN 2 ELSE
             IF BPDATE1 EQ 'MAR' THEN 3 ELSE
             IF BPDATE1 EQ 'APR' THEN 4 ELSE
             IF BPDATE1 EQ 'MAY' THEN 5 ELSE 
             IF BPDATE1 EQ 'JUN' THEN 6 ELSE
             IF BPDATE1 EQ 'JUL' THEN 7 ELSE
             IF BPDATE1 EQ 'AUG' THEN 8 ELSE
             IF BPDATE1 EQ 'SEP' THEN 9 ELSE
             IF BPDATE1 EQ 'OCT' THEN 10 ELSE
             IF BPDATE1 EQ 'NOV' THEN 11 ELSE 
             IF BPDATE1 EQ 'DEC' THEN 12 ELSE 0;
             
CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
                
CYTD/A1 = IF TRN_DATE GE BC_YR AND TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';               
                
PRIOR/A1 = IF TRN_DATE GE &&FXDATE.EVAL AND
              TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';

PYTD/A1 = IF TRN_DATE GE BP_YR AND TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';        
PYR/A1 = IF TRN_DATE GE BP_YR AND TRN_DATE LE BP_YRA THEN 'Y' ELSE 'N';
             
              
CAVOL/D12.2CS = IF CURRENT EQ 'Y' AND RAIL_CODE EQ ' ' THEN FARE ELSE 0;
CYAVOL1/D12.2CS = IF CYTD EQ 'Y' AND RAIL_CODE EQ ' ' THEN FARE ELSE 0;
CATRNS/P12CS = IF CURRENT EQ 'Y' AND RAIL_CODE EQ ' ' THEN TOT_TRANS ELSE 0;
CANETT/P12CS = IF CURRENT EQ 'Y' AND RAIL_CODE EQ ' ' THEN NET_TKTS ELSE 0;
CTVOL/D12.2CS = IF CURRENT EQ 'Y' AND RAIL_CODE NE ' ' THEN FARE ELSE 0;
CTTRNS/P12CS = IF CURRENT EQ 'Y' AND RAIL_CODE NE ' ' THEN TOT_TRANS ELSE 0;
CTNETT/P12CS = IF CURRENT EQ 'Y' AND RAIL_CODE NE ' ' THEN NET_TKTS ELSE 0;
CYTVOL1/D12.2CS = IF CYTD EQ 'Y' AND RAIL_CODE NE ' ' THEN FARE ELSE 0;
CYAVOL/D12.2CS = (CYAVOL1/BCD_NUM)*12;
CYTVOL/D12.2CS = (CYTVOL1/BCD_NUM)*12;

PAVOL/D12.2CS = IF PRIOR EQ 'Y' AND RAIL_CODE EQ ' ' THEN FARE ELSE 0;
PYAVOL1/D12.2CS = IF PYTD EQ 'Y' AND RAIL_CODE EQ ' ' THEN FARE ELSE 0;
PYRAVOL/D12.2CS = IF PYR EQ 'Y' AND RAIL_CODE EQ ' ' THEN FARE ELSE 0;
PATRNS/P12CS = IF PRIOR EQ 'Y' AND RAIL_CODE EQ ' ' THEN TOT_TRANS ELSE 0;
PYRATRNS/P12CS = IF PYR EQ 'Y' AND RAIL_CODE EQ ' ' THEN TOT_TRANS ELSE 0;
PANETT/P12CS = IF PRIOR EQ 'Y' AND RAIL_CODE EQ ' ' THEN NET_TKTS ELSE 0;
PYRANETT/P12CS = IF PYR EQ 'Y' AND RAIL_CODE EQ ' ' THEN NET_TKTS ELSE 0;
PTVOL/D12.2CS = IF PRIOR EQ 'Y' AND RAIL_CODE NE ' ' THEN FARE ELSE 0;
PTTRNS/P12CS = IF PRIOR EQ 'Y' AND RAIL_CODE NE ' ' THEN TOT_TRANS ELSE 0;
PYRTTRNS/P12CS = IF PYR EQ 'Y' AND RAIL_CODE NE ' ' THEN TOT_TRANS ELSE 0;
PTNETT/P12CS = IF PRIOR EQ 'Y' AND RAIL_CODE NE ' ' THEN NET_TKT ELSE 0;
PYRTNETT/P12CS = IF PYR EQ 'Y' AND RAIL_CODE NE ' ' THEN NET_TKT ELSE 0;
PYTVOL1/D12.2CS = IF PYTD EQ 'Y' AND RAIL_CODE NE ' ' THEN FARE ELSE 0;
PYRTVOL/D12.2CS = IF PYR EQ 'Y' AND RAIL_CODE NE ' ' THEN FARE ELSE 0;
PYAVOL/D12.2CS = (PYAVOL1/BPD_NUM)*12;
PYTVOL/D12.2CS = (PYTVOL1/BPD_NUM)*12;

ROLLCD/A8 = '&&ROLL'; 
RNR_FLAG/A15 = IF ORIG_SYS EQ 'RA' OR 'R3' THEN 'Radius' ELSE 'Non-radius';
END

TABLE FILE TC_TRANA
SUM CAVOL
    CANETT
    CATRNS
    CTVOL
    CTNETT
    CTTRNS
    CYAVOL
    CYAVOL1
    CYTVOL    
    PAVOL
    PANETT
    PATRNS
    PTVOL
    PTNETT
    PTTRNS
    PYAVOL1
    PYAVOL
    PYTVOL 
    PYTVOL1
-*NEW PRIOR 12 MONTHS FIELDS
    PYRAVOL
    PYRATRNS
    PYRANETT
    PYRTVOL
    PYRTTRNS
    PYRTNETT
BY ROLLCD    
BY LEVEL_DESC
BY RNR_FLAG
ON TABLE HOLD AS TCT1A 
END
-RUN


DEFINE FILE TCT1A
COMP_NAME/A40 = LCWORD(40, LEVEL_DESC, COMP_NAME);
END
TABLE FILE TCT1A
SUM CAVOL
    CANETT
    CATRNS
    CYAVOL
    CTVOL
    CTNETT
    CTTRNS
    CYTVOL
    PAVOL
    PANETT
    PATRNS
    PYAVOL
    PTVOL
    PTNETT
    PTTRNS
    PYTVOL    
-*NEW PRIOR 12 MONTHS FIELDS
    PYRAVOL
    PYRATRNS
    PYRANETT
    PYRTVOL
    PYRTTRNS
    PYRTNETT    
BY ROLLCD
BY RNR_FLAG
BY COMP_NAME
ON TABLE HOLD AS TCT1
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;


-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================


MODIFY FILE TCAGLOBAL
FIXFORM FROM TCT1
DATA ON TCT1
END
-RUN



-NEXTONE

