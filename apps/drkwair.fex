-* Kiewit 
-* ADDED TRANTYPE INCLUDE - JK 12/18/14 STORY ID S-06492
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-DEFAULT &&LSEL = 'XX';
-DEFAULT &&SETF = 'XX';

-SET &&DTSTY = 'DT';
-SET &&LSEL = &ROW_LABEL;

-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-SET &&UNAME = &user;
EX RPTRESET
-RUN

EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;

-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE GetForDrillDown
-RUN

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';  



FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN

-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN

-INCLUDE SELCATQT
-RUN

EX &&CATQTRPR
 -RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

EX AIRUSE
-RUN

-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN

-TYPE Before Extract
SET ASNAMES = ON 
-SET HOLDATTR = ON;
-RUN

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
DEFINE FILE &&EXTRACT
  CCARD6/A6 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$999999');
  LEVEL_DESC/A60 = &&LDESC;
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LSTSVNGS/D12.2CS    = SEG_AMT + SEG_TAX - SEG_LOWEST;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
 NEW_ADV_PURCH/D9    = ADV_PURCH;
 NEW_SEG_LOW/D12.2CS = SEG_LOWEST;
 TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';  
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
-INCLUDE TRANTYPE
END
-RUN

TABLEF FILE &&EXTRACT
PRINT AIRLINE
  CCARD6
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  DPT_TIME
  FARE_PAID  
  FARE_BAS   
  LSTSVNGS
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_LOW
  REFUSE_CODE
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  SEG_COUNT
  SEG_NBR   
  TRN_DATE
  TKT_NUM
  TKT_SORT  
  TKT_TYPE
  XDPT_DT
  XPSNGR_NM
-INCLUDE &AIRDATES
WHERE REFUSE_CODE EQ 'SS'
WHERE LEVEL_DESC EQ '&&LSEL.EVAL'
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3

-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT'
FARE_PAID AS 'TFARE'
NEW_SEG_LOW AS 'TLOW'
LSTSVNGS AS 'TLOST'
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE RPTHOLD
PRINT AIRLINE
  DPT_TIME
  CCARD6
  EMB_APT_NM
  FARE_PAID  
  FARE_BAS   
  LSTSVNGS
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_LOW
  REFUSE_CODE
  RTE_APT_NM
  TKT_TYPE
  XDPT_DT
  TKT_NUM
  SEG_COUNT
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT AIRLINE
  CCARD6
  DPT_TIME
  EMB_APT_NM
  FARE_PAID  
  FARE_BAS   
  LSTSVNGS
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_LOW
  REFUSE_CODE
  RTE_APT_NM
  TKT_TYPE
  XDPT_DT
  TKT_NUM
  SEG_COUNT
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TTKT
      TFARE
      TLOW
      TLOST
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

TABLE FILE AIRHOLD2
PRINT AIRLINE
  CCARD6
  DPT_TIME
  EMB_APT_NM
  FARE_PAID  
  FARE_BAS   
  LSTSVNGS
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_LOW
  REFUSE_CODE
  RTE_APT_NM
  TKT_TYPE
  TRN_DATE
  XDPT_DT
  XPSNGR_NM
  TKT_NUM
  SEG_COUNT
  TTKT
  TFARE
  TLOW
  TLOST
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN

 
DEFINE FILE AIRHOLD3
NET_TKT_CNT1/D8CS = IF TKT_NUM NE LAST TKT_NUM THEN TTKT ELSE 0;
CCR/A6 = IF TKT_NUM NE LAST TKT_NUM THEN CCARD6 ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||XPSNGR_NM;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
LOST_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOST ELSE 0;
LOW_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END
-RUN

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
-RUN

TABLE FILE AIRHOLD3
-INCLUDE &&PAHDR
"&&SUBHEAD"

PRINT
TICKET_NMBR AS 'TKT,NBR'
TKT_TYPE AS 'TKT,TYPE'
XDPT_DT AS 'DEPART,DATE'
EMB_APT_NM AS 'ORG'
RTE_APT_NM AS 'DST'
AIRLINE AS 'CR'
FARE_BAS AS 'FARE,BASIS'
FARE_AMT AS 'FARE,PAID'
LOW_TOT AS 'LOW,FARE'
LOST_TOT AS 'LOST,SAVINGS'
NET_TKT_CNT1 AS 'NET,TICKETS'
CCR AS 'FOP'
REASON_CD AS 'RSN,CDE'

BY XPSNGR_NM AS 'PASSENGER,NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT

ON TABLE COLUMN-TOTAL
ON XTKT_SRT SKIP-LINE

FOOTING BOTTOM
-INCLUDE FOOTERDT
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYD
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-XXIT
-GOTO NEXT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-NEXT
