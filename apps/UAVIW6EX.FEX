-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File RNKEXTRT.FEX
-********************************************************************
-********************************************************************
-*
-* This program extracts data for a drillable RBT reason code report
-* - DEB - 03/09/10
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14 - DEB - ADDED EX5 TO EX_FL_FLAG DEFINE
-*7/11/16                   S-20630 DLV  Added EMP_ID to extract
-*01/10/2017 - S-27026 -DEB - Added HOTEL_FLAG and SEG_HTL_FLAG to extract
 
-********************************************************************
SET ASNAMES = ON

-SET HOLDATTR = ON;

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
  BASE_FARE/D12.2CS = SEG_AMT - SEG_COM;
  BOOK_DT/MDYY        = RESV_DATE;
  COMM_SEG/D12.2S   = SEG_COM;
  IFLAG/A11 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE' ELSE 'TRADITIONAL'; 
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  FOP/A2 = EDIT(CC_NUM,'99$$$$$$$$$$$$$$$$');
  FOP2/A7 = IF FOP EQ 'AX' THEN EDIT(CC_NUM, '99$$$$$$$$$$99999')
          ELSE EDIT(CC_NUM, '99$$$$$$$$$$$$9999');    
  F_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 0;
  P_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'P') THEN 1 ELSE 0;
  KILO/D9      = CITYPAIR.SEG_MILES * 1.60934 ;  
  LEVEL_DESC/A60      = &&LDESC;
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_COMM/D12.2S     = (SEG_AMT - SEG_COM);
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
  NET_TKT_CNT/D8     = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_LOWEST/D9 = SEG_LOWEST;
  NEW_SEG_MILES/D9 = SEG_MILES;
  NEW_STAY/D9 = STAY;
  NEW_TRIP_LENGTH/D12 = TRIP_LENGTH;
  NOHR_KEY/A2          = EDIT(NHR_KEY, '$$$$$$$$99');
  NOHTL_KEY/A40        = IF NOHR_KEY NE ' ' THEN NHR_DESC ELSE 'NONE GIVEN';
  NON_REF/A15 = DECODE NONREF_FLAG ('N' 'NON-REFUNDABLE'
                                    'R' 'REFUNDABLE'
                                   ' ' 'REFUNDABLE');
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  REF_EXCH_CNT2/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'P') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG EQ 'P') THEN 1 ELSE 0;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND  
                 (SEG_COUNT EQ 1) THEN 1 ELSE 0;                            
  TOTAL_FARE/D12.2S = SEG_AMT + SEG_TAX;
  TRAN_MONTH/MT = TRN_DATE;
  TRIP_KEY/A2   = EDIT(TRP_KEY, '$$$$$$$$99');
  TRIPRSN/A10 = IF TRIP_KEY NE ' ' THEN TRIP_KEY ELSE 'ZZZZZZZZZZ';
  TRIP_RSN/A10 = IF TRIPRSN EQ 'ZZZZZZZZZZ' THEN 'NONE GIVEN' ELSE TRIPRSN;
  VASAVINGS/D11.2CS = SEG_DISCOUNT - TOTAL_FARE;
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;
  EX_RF_FLAG/A1 = IF (((RF_FLAG EQ 'F' OR 'P') AND
      (EMBARK NE 'EX1' OR 'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1'))
    THEN 'R'
    ELSE IF (((EXSALE NE ' ') AND (SEG_COUNT GE 0)) OR
      ((EMBARK NE 'DT1') AND (EX_FLAG EQ 'F' OR 'P') AND 
      (SEG_COUNT LE 0) AND (RF_FLAG EQ ' ')))
    THEN 'E'
  ELSE ' ';
  TL_TKT/A1 = EDIT(AUX_TKTLS_NUM, '9');
  TKT_NBR/A10 = IF TL_TKT EQ '2' AND AUX_TKTLS_NUM NE '2828282828' THEN AUX_TKTLS_NUM ELSE TKT_NUM;
-* ****DEFINES ON PREVIOUS DEFINES****
  BAG_FEE/D12.2CS  = 0 ;
  NOHOTEL/A1 = ' ';
  CPMILE/D12.2CS = IF ((SEG_MILES LT 1) AND (SEG_MILES GT (-1))) THEN 0 ELSE
                      (FARE_PAID/SEG_MILES);
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  AIRFEE_CNT/D8CS = IF AIRLINE_FEE GT 0 THEN 1 ELSE 0;
  EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
     NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
     IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;  
-INCLUDE TRANTYPE  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT   
        AIR_KEY
        AIRLINE
        AIRLINE_FEE
        AIRFEE_CNT	
        AIR_MAIN.INTL_DOM
        AROLL_LEV5
        AROLL_LEV6
        BAG_FEE
        BASE_FARE
        BOOK_DT
        AR_BRANCH
        BR_CL_IDX
        CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
        CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
        C_ITIN
        CLASS
        CLIENT_NUM	
        COMM_SEG
        EMB_CTY.COUNTRY_CODE AS 'EMB_CNTRY_CODE'
        CPMILE	
        DISSAVINGS
        DOC_NUMBER
        DOD_TYPE
        DOD_PAX_CAT
        DPT_TIME
        EMBARK
        EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
        EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
        EMP_ID
        EXCHGS
        EX_RF_FLAG
        EX_FLAG
        EXCHG_SALE
        EXCH_NET_FARE
        FOP2
        F_EXCH_CNT
        FARE_BAS		
        FARE_PAID
        HOTEL_FLAG
        IFLAG
        KILO
        LEVEL_DESC	
        LEVEL1	
        LEVEL2	
        LEVEL3
        MRK_TKT_CNT
        NET_COMM	
        NET_FARE	
        NET_TKT_CNT
        NEW_ADV_PURCH
        NEW_SEG_LOWEST
        NEW_SEG_MILES
        NEW_STAY
        NEW_TRIP_LENGTH
        NOHR_KEY
        NOHTL_KEY  
        NONREF_FLAG
        P_EXCH_CNT
        PASSNGR_NAME
        PNR_LOCATOR
        REF_EXCH_CNT
        REF_EXCH_CNT2
        REFUSAL_DESC
        REFUSE_CODE
        RESV_DATE
        RF_FLAG
        RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
        RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
        SAVINGS	
        SEG_AMT	
        SEG_COUNT	
        SEG_DISCOUNT
        SEG_HTL_FLAG
        SEG_LOWEST
        SEG_NBR	
        SEG_STANDARD	
        SEG_TAX	
        STAY
        TICKET_BRANCH	
        TKT_AMOUNT
        TKT_DATE
        TKT_NBR
        TKT_NUM	
        TKT_PURCH
        TKT_SORT
        TKT_TYPE	
        TOTAL_FARE
        TRIP_KEY AS 'TRP_PRP'
        TRIP_RSN
        TRN_DATE
        VAL_AIRLINE	
        VASAVINGS
        VOID_DATE
        XDPT_DT	
        XPSNGR_NM	
        XTRAN_DT
         

-INCLUDE &AIRDATES
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD 

END
-RUN


