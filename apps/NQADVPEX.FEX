-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the new quarterly review - JK 8/2/05
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492

-******************************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;

DEFINE FILE &&EXTRACT 
  ADV/A1=IF (ADV_PURCH LE 6) THEN 'A' ELSE
         IF (ADV_PURCH LE 13) THEN 'B' ELSE 'C';
  FARE_PAID/D12 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D12=IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT EQ 1) THEN 1 ELSE
                  IF (SEG_NBR EQ '01') AND (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1) THEN (-1) ELSE
                  IF (DOC_TYPE NE '1') AND (SEG_NBR EQ '01') AND (RF_FLAG EQ 'F') AND (SEG_COUNT EQ -1)
                     THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D12 = ADV_PURCH;
-* Defines on Previous
 ADV_FPA/D12 = IF ADV EQ 'A' THEN FARE_PAID ELSE 0;
 ADV_FPB/D12 = IF ADV EQ 'B' THEN FARE_PAID ELSE 0;
 ADV_FPC/D12 = IF ADV EQ 'C' THEN FARE_PAID ELSE 0;
 ADV_NTA/D12 = IF ADV EQ 'A' THEN NET_TKT_CNT ELSE 0;
 ADV_NTB/D12 = IF ADV EQ 'B' THEN NET_TKT_CNT ELSE 0;
 ADV_NTC/D12 = IF ADV EQ 'C' THEN NET_TKT_CNT ELSE 0;
 NON_FP/D12 = IF NONREF_FLAG EQ 'N' THEN FARE_PAID ELSE 0;
 NON_NT/D12 = IF NONREF_FLAG EQ 'N' THEN NET_TKT_CNT ELSE 0;
 REF_FP/D12 = IF NONREF_FLAG EQ 'R' THEN FARE_PAID ELSE 0;
 REF_NT/D12 = IF NONREF_FLAG EQ 'R' THEN NET_TKT_CNT ELSE 0;
 REC_CNT/D12 = IF NET_TKT_CNT EQ 1 THEN 1 ELSE 0;
-* DEFINES ON PREVIOUS
 TTLDAYS/D12 = NEW_ADV_PURCH * REC_CNT;
-INCLUDE QRFTRANA
-INCLUDE TRANTYPE
END
-RUN 

TABLEF FILE &&EXTRACT
PRINT ADV_FPA
      ADV_FPB
      ADV_FPC
      ADV_NTA
      ADV_NTB
      ADV_NTC
      FARE_PAID
      NET_TKT_CNT
      NON_FP
      NON_NT
      REC_CNT
      REF_FP
      REF_NT
      ROLLUP_CODE
      YEAR
      TRAN   
      TTLDAYS
      CURRENT PYTD PRIOR CQ
WHERE (TRN_DATE GE '&&FROMDT.EVAL') AND (TRN_DATE LE '&&TODT.EVAL') OR
      (TRN_DATE GE '&&FXDATE.EVAL') AND (TRN_DATE LE '&&TXDATE.EVAL')
WHERE VOID_DATE EQ 0
-IF &&INTDOM EQ 'T' THEN GOTO SKIP;
WHERE AIR_MAIN.INTL_DOM EQ '&&INTDOM.EVAL'
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
-SKIP
ON TABLE HOLD AS HOLDEX1
END
-RUN
-SET &&NOADV = IF &LINES EQ 0 THEN 'N' ELSE 'Y';
-IF &LINES EQ 0 THEN GOTO NOADV;

TABLE FILE HOLDEX1
SUM ADV_FPA
    ADV_FPB
    ADV_FPC
    ADV_NTA
    ADV_NTB
    ADV_NTC
    FARE_PAID
    NET_TKT_CNT 
    NON_FP
    NON_NT
    REC_CNT
    REF_FP
    REF_NT
    TTLDAYS
    CURRENT PYTD PRIOR CQ
BY ROLLUP_CODE
BY YEAR
BY TRAN
ON TABLE HOLD
END
-RUN

TABLE FILE HOLD
PRINT ROLLUP_CODE YEAR TRAN ADV_FPA ADV_FPB ADV_FPC ADV_NTA ADV_NTB ADV_NTC FARE_PAID 
      NET_TKT_CNT NON_FP NON_NT REF_FP REF_NT TTLDAYS REC_CNT CURRENT PYTD PRIOR CQ
ON TABLE SAVE 
END
-RUN

MODIFY FILE QRADVPUR
FIXFORM ROLLUP_CODE/8 YEAR/4 TRAN/7 ADV_FPA/12 ADV_FPB/12 ADV_FPC/12 ADV_NTA/12 ADV_NTB/12 ADV_NTC/12 FARE_PAID/12 NET_TKT_CNT/12 NON_FP/12 NON_NT/12 REF_FP/12 REF_NT/12 TTLDAYS/12 REC_CNT/12 CURRENT/1 PYTD/1 PRIOR/1 CQ/1
MATCH ROLLUP_CODE YEAR TRAN
  ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN

-NOADV