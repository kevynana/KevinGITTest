-*11/25/03   STEVE              Added -INCLUDE RPTPARMS
-*3/23/04    jk                 Due to market file format update changed
-*                              city_st define to have an extra space
-*7/26/04    JOY                Added/Changed Norecords logic
-* ADDED TRANTYPE INCLUDE - JK 12/19/14 STORY ID S-06492
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.

-INCLUDE SETECHO

-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';

-SET &&DTSTY = 'DT';

-SET &&FIELD = &ROW_LABEL;

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 11/03/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT


-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX SETDBLS SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****

-SET &&NONCHK = 'WHERE REFUSE_CD NE ''A''';
-SET &&LCHK = 'WHERE LEVEL2 EQ ''&&FIELD.EVAL''';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN

EX HTLUSE
-RUN

-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN

-TYPE Before Extract
SET ASNAMES = ON 
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
CITY_ST/A40 = PROP_STATE||(' '| ' '|PROP_CITY);
IN_DATE/MDYY = CHKIN_DATE;
NBR_DAYS/D12 = NUM_DAYS;
NBR_ROOMS/D12 = NUM_ROOMS;
NEW_ROOM_RATE/D12.2C = ROOM_RATE;
NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;

NNROOMS/D12 = IF (NUM_DAYS LT 0) AND
                 (NUM_ROOMS LT 0) THEN
                 (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                 (NUM_DAYS * NUM_ROOMS);

OUT_DATE/MDYY = CHKOUT_DATE;
REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
-INCLUDE TRANTYPE
END
-RUN

TABLEF FILE &&EXTRACT
PRINT CITY_ST  
  IN_DATE 	
  NBR_DAYS    
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT       
  NNROOMS	
  OUT_DATE    
  PASSNGR_NAME 	
  PROP_NAME 
  REFUSE_CD
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

-INCLUDE RPTPARMS
WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL
&&NONCHK
&&LCHK
ON TABLE HOLD AS HHOLD
END
-RUN


-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

DEFINE FILE METXHTL
HTL_MKT/A130 = IF H_MARKET NE ' ' THEN H_MARKET ELSE ST_CITY;
END

TABLE FILE METXHTL
PRINT HTL_MKT
BY ST_CITY AS 'CITY_ST'
WHERE ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS MKTFILE
END
-RUN

TABLE FILE HHOLD
PRINT IN_DATE
      NBR_ROOMS
      NEW_TOTAL_AMT
      NNROOMS
      OUT_DATE
      PASSNGR_NAME
      PROP_NAME
      REFUSE_CD
BY CITY_ST
ON TABLE HOLD AS MKTHLD
END
-RUN

MATCH FILE MKTHLD
PRINT IN_DATE
      NBR_ROOMS
      NEW_TOTAL_AMT
      NNROOMS
      OUT_DATE
      PASSNGR_NAME
      PROP_NAME
      REFUSE_CD
BY CITY_ST
ON TABLE HOLD
RUN
FILE MKTFILE
SUM HTL_MKT
BY CITY_ST
AFTER MATCH HOLD AS RPTHOLD OLD-AND-NEW
END
-RUN


-TYPE After Extract
DEFINE FILE RPTHOLD
  NOWTOD/A8 WITH NNROOMS = HHMMSS (NOWTOD);
END
-RUN
-TYPE Before Report

TABLE FILE RPTHOLD
-INCLUDE HEADAIR
"&&FIELD"
"</2"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM 
NNROOMS AS 'NUMBER,NIGHTS'
NBR_ROOMS AS 'NUMBER,ROOMS'
NEW_TOTAL_AMT AS 'TOTAL,AMOUNT'
COMPUTE AVGDR/D12.2CS = IF NEW_TOTAL_AMT LT 0 AND NNROOMS LT 0
 THEN ((NEW_TOTAL_AMT/NNROOMS) * (-1)) 
 ELSE NEW_TOTAL_AMT/NNROOMS; AS 'AVERAGE,DAILY,RATE'

-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY REFUSE_CD AS 'REASON,CODE'
BY PROP_NAME AS 'HOTEL'
BY CITY_ST AS 'CITY/STATE'
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME RECOMPUTE AS 'TOTAL FOR'
-*
-INCLUDE FOOTERDT

ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-GOTO ENDIT;

-XXIT
-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO ENDIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-ENDIT
-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END


-XXIT
