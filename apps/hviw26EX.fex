-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
-* File HVIEW3EX.FEX
-********************************************************************
-* MODIFICAIONS:
-********************************************************************
-*
-* This program extracts data for the series of basic hotel location listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-* 12/12/01 - JK  ADDED DEFINE FOR ONLINE
-* 03/31/04 - DV  ADDED BOOKINGS DEFINE PER STEVE
-* 08/16/05 - DV CHANGED ONLINE DEFINE TO USE DOD_TYPE INSTEAD OF
-* ONLINE/A1 = IF AGENT EQ '00444' THEN 'Y' ELSE 'N';  
-* 10/11/05 - DEB - ADDED DOD_TYPE H TO ONLINE DEFINE
-* 07/05/06 - STEVE - Added code for ABTOPHTC (by City Name vs Property City)
-* 09/07/06 - STEVE - Added code for Geo Zone
-* 10/04/06 - JOY - Added Prop_Code
-*  4/29/10   DEB         CHANGED RATE_GRP FOR ROUNDING
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/9/14 Updated Pref_non define to use exclude_flags - jk

-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT
  CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                  INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
  PRIOR/A1 =   IF INVOICE_DATE GE &&FXDATE.EVAL AND
                INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
  PROP_C/A25 = LCWORD(25, PROP_CITY, PROP_C); 
  PROP_N/A30 = LCWORD(30, PROP_NAME, PROP_N);
                
  CITY_ST/A35 = PROP_C||(' '| PROP_STATE);
  LEVEL_DESC/A60  = &&LDESC;
  IN_DATE/MDYY = CHKIN_DATE;
  INV_DATE/MDYY = INVOICE_DATE;
  NEW_ROOM_RATE/D12.2C = ROOM_RATE;
  NEW_STD_RATE/D12.2C = STD_RATE;
  TH_AMT/D12.2C = TOTAL_AMT;
  NBR_DAYS/D12 =  NUM_DAYS; 
  NNROOMS/D12 = IF (NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)
                THEN ((NBR_DAYS*NUM_ROOMS)*(-1))
                ELSE NBR_DAYS*NUM_ROOMS;
  OUT_DATE/MDYY = CHKOUT_DATE;
  INV_DT/MDYY = INVOICE_DATE;
  PROP_ZIP5/A5=EDIT(PROP_ZIP, '99999$$$$$');
-*  PREF_NON/A1 = IF CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'P' ELSE 'N';
 PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
 PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
 PREF_NON/A1 = IF PNP_FLAG EQ 'I' THEN 'P' ELSE 'N';
  PERIOD/A7 = IF CURRENT EQ 'Y' THEN 'PERIOD1' ELSE 
              IF PRIOR EQ 'Y' THEN 'PERIOD2' ELSE ' ';


  RATE_GRP/A15 = IF ((ROOM_RATE GE 0 AND ROOM_RATE LT 149.50) OR (ROOM_RATE LE 0 AND ROOM_RATE GT (-149.50))) THEN '$0 - $149' ELSE
                 IF ((ROOM_RATE GE 149.50 AND ROOM_RATE LT 199.50) OR (ROOM_RATE LE (-149.50) AND ROOM_RATE GT (-199.50))) THEN '$150 - $199' ELSE
                 IF ((ROOM_RATE GE 199.50 AND ROOM_RATE LT 249.50) OR (ROOM_RATE LE (-199.50) AND ROOM_RATE GT (-249.50))) THEN '$200 - $249' ELSE '$250 OR GREATER';



  REASON_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
  REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
  RSNCDE/A48=REFUSE_CD|| '-' ||REFUSAL_DESC;
  RES_DT/MDY = RESV_DATE;

-* ****DEFINES ON PREVIOUS DEFINES****

  TOTAL_STD/D12.2C = IF (NNROOMS LT 0) AND (NEW_STD_RATE LT 0) 
                     THEN ((NNROOMS * NEW_STD_RATE)*(-1))
                     ELSE NNROOMS * NEW_STD_RATE;
  BOOKINGS/D8 = IF TH_AMT GE 0 THEN 1 ELSE (-1);
  SAVINGS/D12.2CSM = TOTAL_STD - TH_AMT;
  TOTAL_SLSAV/D12.2CM = TOTAL_STD - TH_AMT;
  XIN_DOW/WR = IN_DATE;
  XOUT_DOW/WR = OUT_DATE;   

CDAY/P12C = IF CURRENT EQ 'Y' THEN NNROOMS ELSE 0;
PDAY/P12C = IF PRIOR EQ 'Y' THEN NNROOMS ELSE 0;
CAMT/P12.2 = IF CURRENT EQ 'Y' THEN TH_AMT ELSE 0;
PAMT/P12.2 = IF PRIOR EQ 'Y' THEN TH_AMT ELSE 0;
CBK/P12C = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
PBK/P12C = IF PRIOR EQ 'Y' THEN BOOKINGS ELSE 0;
CSVG/P12.2 = IF CURRENT EQ 'Y' THEN TOTAL_SLSAV ELSE 0;
PSVG/P12.2 = IF PRIOR EQ 'Y' THEN TOTAL_SLSAV ELSE 0;
MIR_SEQ1/A1 = EDIT(MIR_SEQ, '9$$$$$');

RES_SYS/A1 = IF MIR_SEQ1 EQ 'X' THEN 'S' ELSE 'A';

PROP_CODE1/A7 = IF RES_SYS EQ 'S' THEN PROP_SABRE_CODE ELSE PROP_APOLLO_CODE;
   

END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT BOOKINGS
  CHAIN_CODE 	
  CITY_ST
  HOTEL_PHONE
  IN_DATE 
  INVOICE_NUM
  INVOICE_DATE
  INV_DT
  LEVEL_DESC	  
  NNROOMS  	
  OUT_DATE 
  PROP_CODE1 AS 'PROP_CODE'
  PROP_ZIP5
  PASSNGR_NAME 	
  PERIOD
  PROP_ADDRESS 	   	
  PROP_N AS 'PROP_NAME'
  RES_DT  
  PREF_NON
  CDAY
  PDAY
  CAMT
  PAMT
  CBK
  PBK
  CSVG
  PSVG
  SAVINGS
  TH_AMT
  TOTAL_SLSAV
  -* New Property Master fields
  PROP_LANYON_ID
  PROP_LATITUDE
  PROP_LONGITUDE
  PROP_MOBIL_STARS
  PROP_AAA_DIAMONDS
  PROP_OHG_CLASS
  
WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL OR
      INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS &&RPT_HOLD
END




