-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Due to hotel market format changes, changed city_st to have an extra space
-* JK 3/23/04
-*7/13/05    DEB                ADDED REFUSE_CD 'R' AND 'T' TO APP_FLAG DEFINE
-*7/18/05    STEVE              Took out REFUSE_CD 'T' TO APP_FLAG DEFINE - per Robin Lewis



TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.

SET ASNAMES = ON

DEFINE FILE CURHTLM1
CITY_ST/A40 = PROP_STATE||(' '| ' '|PROP_CITY);
NNROOMS/D10 = IF  (NUM_DAYS LT 0) AND
                  (NUM_ROOMS LT 0) THEN
                  (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                  (NUM_DAYS * NUM_ROOMS);

TH_AMT/D12.2 = TOTAL_AMT;

CY/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
           INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 =   IF INVOICE_DATE GE &&FXDATE.EVAL AND
             INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
APP_FLAG/A30 = IF REFUSE_CODE EQ 'A' OR 'R' THEN 'PREFERRED'
               ELSE 'NON-PREFERRED';

END

TABLE FILE CURHTLM1
PRINT APP_FLAG NNROOMS
BY CITY_ST
ON TABLE HOLD AS HMARK
END
-RUN


DEFINE FILE METXHTL
HTL_MKT/A130 = IF H_MARKET NE ' ' THEN H_MARKET ELSE ST_CITY;
END

TABLE FILE METXHTL
PRINT HTL_MKT
BY ST_CITY AS 'CITY_ST'
WHERE ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS MKTFILE
END
-RUN

MATCH FILE HMARK
PRINT APP_FLAG
      NNROOMS
BY CITY_ST
ON TABLE HOLD
RUN
FILE MKTFILE
SUM HTL_MKT
BY CITY_ST
AFTER MATCH HOLD AS TTLMRK OLD-AND-NEW
END
-RUN


TABLE FILE TTLMRK
SUM NNROOMS
BY APP_FLAG
ON TABLE HOLD AS CTTLHTL 
END
-RUN


DEFINE FILE PRIORHTLM1
CITY_ST/A40 = PROP_STATE||(' '| PROP_CITY);
NNROOMS/D10 = IF  (NUM_DAYS LT 0) AND
                  (NUM_ROOMS LT 0) THEN
                  (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                  (NUM_DAYS * NUM_ROOMS);

TH_AMT/D12.2 = TOTAL_AMT;

CY/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
           INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 =   IF INVOICE_DATE GE &&FXDATE.EVAL AND
             INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
APP_FLAG/A30 = IF REFUSE_CODE EQ 'A' THEN 'PREFERRED'
               ELSE 'NON-PREFERRED';

END

TABLE FILE PRIORHTLM1
PRINT APP_FLAG
      NNROOMS
BY CITY_ST
ON TABLE HOLD AS PHMRK
END
-RUN

DEFINE FILE METXHTL
HTL_MKT/A40 = IF H_MARKET NE ' ' THEN H_MARKET ELSE ST_CITY;
END

TABLE FILE METXHTL
PRINT HTL_MKT
BY ST_CITY AS 'CITY_ST'
WHERE ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS PMKTFILE
END
-RUN

MATCH FILE PHMRK
PRINT APP_FLAG 
      NNROOMS
BY CITY_ST
ON TABLE HOLD
RUN
FILE PMKTFILE
SUM HTL_MKT
BY CITY_ST
AFTER MATCH HOLD AS PTTLMRK OLD-AND-NEW
END
-RUN


TABLE FILE PTTLMRK
SUM NNROOMS AS 'NNROOMS1'
BY APP_FLAG
ON TABLE HOLD AS PTTLHTL
END
-RUN

MATCH FILE CTTLHTL
PRINT NNROOMS
BY APP_FLAG
ON TABLE HOLD
RUN
FILE PTTLHTL
SUM NNROOMS1
BY APP_FLAG
AFTER MATCH HOLD AS TTHTL OLD-OR-NEW
END
-RUN

TABLE FILE TTHTL
SUM NNROOMS
    PCT.NNROOMS AS 'PCTNR'
    NNROOMS1
    PCT.NNROOMS1 AS 'PCTNR1'
BY APP_FLAG
ON TABLE HOLD AS LASTHLD
END
-RUN

DEFINE FILE LASTHLD
CATEGORY/A30 = 'PREFERRED HOTELS';
CAT_ORDER/I2 = 5;
END
-RUN

TABLE FILE LASTHLD
PRINT CATEGORY/A30 AND APP_FLAG/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND NNROOMS/D15 AS 'VOL_CURR' AND PCTNR/D15 AS 'TKT_CURR' AND 
NNROOMS1/D15 AS 'VOL_PRIOR' AND PCTNR1/D15 AS 'TKT_PRIOR'
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS APCHLD1
END
-RUN

TABLE FILE APCHLD1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS APCHLD2
END
MODIFY FILE OV1_MAIN
FIXFORM FROM APCHLD2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON APCHLD2
END
-RUN







