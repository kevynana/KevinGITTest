
-INCLUDE SETECHO
SET ASNAMES = ON

DEFINE FILE &&EXTRACT
FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;

NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;    
                           
ORG_DST/A6 = ORG_APC||DST_APC;    

-* Current Month
CMFM_DT/YYMD = &&FROMDT.EVAL;
CMTO_DT/YYMD = &&TODT.EVAL;

-* Prior Month
CPFM_DT/YYMD = DATEADD(CMFM_DT, 'M', (-12));
CPTO_DTA/YYMD = DATEADD(CMTO_DT, 'M', (-12));
CPTO_DT/YYMD = DATEMOV(CPTO_DTA, 'EOM');

-* Current Year From Date
CYFM_DT/YYMD = DATEADD(CMFM_DT, 'M', (-11));

-* Prior Year From Date & To Date
PYFM_DT/YYMD = DATEADD(CYFM_DT, 'M', (-12));
-* PYTO_DT/YYMD = DATEADD(CPTO_DT, 'M', (-11));

-* Time Period Flags
-* Current Month
CM/A1 = IF TRN_DATE GE CMFM_DT AND TRN_DATE LE CMTO_DT THEN 'Y' ELSE 'N';
PM/A1 = IF TRN_DATE GE CPFM_DT AND TRN_DATE LE CPTO_DT THEN 'Y' ELSE 'N';
CY/A1 = IF TRN_DATE GE CYFM_DT AND TRN_DATE LE CMTO_DT THEN 'Y' ELSE 'N';
PY/A1 = IF TRN_DATE GE PYFM_DT AND TRN_DATE LE CPTO_DT THEN 'Y' ELSE 'N';


-* Time Period Fares/Tickets
CM_NET_TKTS/P12 =    IF CM EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CM_FARE_PAID/D12.2 = IF CM EQ 'Y' THEN FARE_PAID ELSE 0;
PM_NET_TKTS/P12    = IF PM EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PM_FARE_PAID/D12.2 = IF PM EQ 'Y' THEN FARE_PAID ELSE 0;
CY_NET_TKTS/P12 =    IF CY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CY_FARE_PAID/D12.2 = IF CY EQ 'Y' THEN FARE_PAID ELSE 0;
PY_NET_TKTS/P12    = IF PY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PY_FARE_PAID/D12.2 = IF PY EQ 'Y' THEN FARE_PAID ELSE 0;

END
-RUN

TABLE FILE &&EXTRACT
PRINT CM_NET_TKTS
  CM_FARE_PAID
  PM_NET_TKTS
  PM_FARE_PAID
  CY_NET_TKTS
  CY_FARE_PAID
  PY_NET_TKTS
  PY_FARE_PAID
  ORG_DST
  ORG_APC
  DST_APC
  RNDT_FLAG
  C_ITIN
  NET_TKT_CNT
  CMFM_DT
  CMTO_DT
  CPFM_DT
  CPTO_DT
  CYFM_DT
  PYFM_DT
WHERE TRN_DATE GE PYFM_DT AND TRN_DATE LE CMTO_DT
WHERE AIR_MAIN.INTL_DOM EQ 'D'
WHERE VOID_DATE EQ 0
WHERE AIRLINE NE '2V'
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-IF '&&ROLL.EVAL' NE '&&ROLLUP.EVAL' GOTO NOPARMS;
-INCLUDE RPTPARMS
-NOPARMS;
ON TABLE HOLD AS TC_ORG 
END
-RUN

-INCLUDE CATDATEHDR
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACT DATA FROM STEP4 - HOLD TO FOCUS DATABASE TCRANK        
-*========================================================================
DEFINE FILE TC_ORG
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TC_ORG
SUM CM_NET_TKTS
    CM_FARE_PAID
    PM_NET_TKTS
    PM_FARE_PAID
    CY_NET_TKTS
    CY_FARE_PAID
    PY_NET_TKTS
    PY_FARE_PAID     
BY ROLLCD
BY ORG_DST
BY ORG_APC
BY DST_APC
BY RNDT_FLAG
BY C_ITIN
ON TABLE HOLD AS HOLD2 
END
-RUN


TABLE FILE HOLD2
PRINT ORG_APC
    DST_APC
    RNDT_FLAG
   CM_NET_TKTS
   CM_FARE_PAID
   PM_NET_TKTS
   PM_FARE_PAID
   CY_NET_TKTS
   CY_FARE_PAID
   PY_NET_TKTS
   PY_FARE_PAID    
BY ROLLCD
BY ORG_DST
BY C_ITIN
ON TABLE HOLD 
END
-RUN


MODIFY FILE ALL_IND
LOG DUPL MSG OFF
FIXFORM FROM HOLD
DATA ON HOLD
END
-RUN




-NEXTONE;
-XXIT;

