-* File DrillToRep.fex
SET ASNAMES=ON
-*SET TEMPERASE = OFF
JOIN AIR_KEY IN AYMETX TO ALL AIR_KEY IN SYMETX AS J1
END

DEFINE FILE AYMETX
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
END

TABLE FILE AYMETX
PRINT FARE_PAID DISSAVINGS NET_TKT_CNT REFUSE_CODE
BY REFUSE_KEY
ON TABLE HOLD AS 1H
END
-RUN

TABLE FILE AIR_REF
PRINT REFUSAL_DESC
BY REFUSE_KEY
ON TABLE HOLD AS 2H
END
-RUN

MATCH FILE 1H
PRINT FARE_PAID DISSAVINGS NET_TKT_CNT REFUSE_CODE
BY REFUSE_KEY
ON TABLE HOLD
RUN
FILE 2H
SUM REFUSAL_DESC
BY REFUSE_KEY
AFTER MATCH HOLD AS AYMETX1 OLD-AND-NEW
END
-RUN


TABLE FILE AYMETX1
SUM FARE_PAID AS 'Volume' DISSAVINGS AS 'Lost,Savings' NET_TKT_CNT AS 'Tickets'
BY REFUSE_CODE AS 'Reason,Code'
BY REFUSAL_DESC AS 'Reason'
WHERE REFUSE_CODE EQ '&REFUSE_CODE.EVAL';
ON TABLE COLUMN-TOTAL
ON TABLE PCHOLD FORMAT PDF
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.500000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=PORTRAIT, $
TYPE=TITLE, FONT=TIMES NEW ROMAN, SIZE=8, STYLE=BOLD, $
TYPE=GRANDTOTAL, FONT=TIMES NEW ROMAN, SIZE=8, STYLE=BOLD, $
$
TYPE=DATA,
     GRID=OFF,
     FONT='TIMES NEW ROMAN',
     SIZE=7,
     COLOR='BLACK',
     BACKCOLOR='NONE',
     STYLE=NORMAL,
$

ENDSTYLE
END
