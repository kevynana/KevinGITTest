-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* -SET &BITMP4 = 'HTLPREF.GIF';
-* 2/25/05 - DEB - ADDED CODE FOR GLOBAL CURRENCY
-*7/13/05    DEB                ADDED REFUSE_CD 'R' AND 'T' TO APP_FLAG DEFINE
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/9/14 Updated Pref_non define to use exclude_flags - jk



DEFINE FILE GNONHHLD
NNROOMS/D10 = IF  (NUM_DAYS LT 0) AND
                  (NUM_ROOMS LT 0) THEN
                  (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                  (NUM_DAYS * NUM_ROOMS);

TH_AMT/D12.2 = TOTAL_AMT;
GTH_AMT/D16.2 = AMT_TOTALX;

CY/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
           INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 = IF INVOICE_DATE GE &&FXDATE.EVAL AND
           INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
-* APP_FLAG1/A30 = IF REFUSE_CODE EQ 'A' OR 'R' OR 'T' THEN 'PREFERRED'
-*                ELSE 'NON-PREFERRED';
 PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
 PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
APP_FLAG1/A30 = IF PNP_FLAG EQ 'I' THEN ' ' ELSE 'NON-PREFERRED';
APP_FLAG/A30 = LCWORD(30, APP_FLAG1, APP_FLAG);
BITMAP/A12 WITH NNROOMS = 'HTLNGTS.SVG';
FLAG/A1 = 'A';
MONTH/Mtr = INVOICE_DATE;

CNNROOMS/D12 = IF CY EQ 'Y' THEN NNROOMS ELSE 0;
PNNROOMS/D12 = IF PY EQ 'Y' THEN NNROOMS ELSE 0;
END


TABLE FILE GNONHHLD
SUM NNROOMS
    BITMAP
    NAMEC
BY FLAG  
BY MONTH
BY APP_FLAG
WHERE CY EQ 'Y'
WHERE APP_FLAG NE ' '
ON TABLE HOLD AS APCHLD 
END
-RUN

TABLE FILE GNONHHLD
SUM CNNROOMS PNNROOMS
BY FLAG
BY MONTH
ON TABLE HOLD AS TRMHLD
END
-RUN

TABLE FILE GNONHHLD
SUM NNROOMS AS 'NNROOMS1'
    BITMAP
    NAMEC
BY FLAG
BY MONTH
BY APP_FLAG
WHERE PY EQ 'Y'
WHERE APP_FLAG NE ' '
ON TABLE HOLD AS APPHLD1 
END
-RUN


MATCH FILE APCHLD
PRINT NNROOMS
      BITMAP
      NAMEC
      FLAG
BY MONTH      
BY APP_FLAG
ON TABLE HOLD
RUN
FILE APPHLD1
SUM NNROOMS1
    BITMAP
    NAMEC
    FLAG
BY MONTH    
BY APP_FLAG 
AFTER MATCH HOLD AS TTLHLD OLD-OR-NEW
END
-RUN

TABLE FILE TTLHLD
PRINT NNROOMS NNROOMS1 BITMAP NAMEC APP_FLAG
BY FLAG
BY MONTH
ON TABLE HOLD AS TTLHLD1
END
-RUN

MATCH FILE TTLHLD1
PRINT NNROOMS NNROOMS1 BITMAP NAMEC APP_FLAG
BY FLAG
BY MONTH
ON TABLE HOLD
RUN
FILE TRMHLD
SUM CNNROOMS PNNROOMS
BY FLAG
BY MONTH
AFTER MATCH HOLD AS TTLHLD2 OLD-AND-NEW
END
-RUN

TABLE FILE TTLHLD2
SUM NNROOMS NNROOMS1 CNNROOMS PNNROOMS
BY FLAG
BY MONTH
BY APP_FLAG
ON TABLE HOLD AS TTLHLD2A
END
-RUN


-SET &RECFOUND = &RECORDS;
-IF &RECFOUND EQ 0 GOTO NOGRAPH;
-RUN

GRAPH FILE TTLHLD2A
SUM
COMPUTE CPCT/D8 = IF ((CNNROOMS LT 1) AND (CNNROOMS GT (-1))) THEN 0 ELSE (NNROOMS/CNNROOMS); AS 'Period 1'
COMPUTE PPCT/D8 = IF ((PNNROOMS LT 1) AND (PNNROOMS GT (-1))) THEN 0 ELSE (NNROOMS1/PNNROOMS); AS 'Period 2'
BY FLAG NOPRINT
BY MONTH AS ' '
ON GRAPH SET LOOKGRAPH VLINE
ON GRAPH SET GRAPHEDIT SERVER
ON GRAPH SET BARNUMB OFF
ON GRAPH SET GRID ON
ON GRAPH SET VAXIS 140
ON GRAPH SET HAXIS 390
ON GRAPH SET UNITS POINTS
ON GRAPH SET GRMERGE ON
ON GRAPH SAVE AS HTLPREF FORMAT SVG
ON TABLE SET GRAPHSTYLE *
setFillColor(getFrame(), new Color(239,239,239));
setO1AxisSide(0);
setO1MajorGridDisplay(true);
setO1MajorGridStyle(0);
setO1MinorGridDisplay(false);
setAxisAssignment(0,0);
setUseSeriesShapes(true);

setO1LabelDisplay(true);
setO1LabelRotate(3);
setFontSizeAbsolute(getO1Label(),true);
setAutofit(getO1Label(),false); 
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),8);
setFontName(getO1Label(),"Arial");
setFillColor(getO1Label(),new Color(5,76,149));
setTextJustHoriz(getO1Label(),2);



setY1MustIncludeZero(true);
setY1LabelDisplay(true);
setTextFormatPreset(getY1Label(),2);
setTextJustHoriz(getY1Label(),0);
setFontName(getY1Label(),"Times New Roman");
setFontSizeAbsolute(getY1Label(),true);
setFillColor(getY1Label(),new Color(5,76,149));
setFontSize(getY1Label(),9);
setFontStyle(getY1Label(),1);
setY1MajorGridDisplay(false);

setTitleString(" ");
setTextJustHoriz(getSubtitle(), 1);
setSubtitleString("Non Preferred Hotel Nights %");
setFontSizeAbsolute(getSubtitle(),true);
setAutofit(getSubtitle(),false);
setFontSize(getSubtitle(),10);
setFontStyle(getSubtitle(),0);
setFontName(getSubtitle(),"Times New Roman");
 

setTextJustHoriz(getLegendText(),1);
setFontSizeAbsolute(getLegendText(),true);
setFontSize(getLegendText(),9);
setFontStyle(getLegendText(),0);
setFontName(getLegendText(),"Times New Roman");
setTextRotation(getLegendText(),0);
setTextWrap(getLegendText(),false);
setFillColor(getLegendText(),new Color(5,76,149));
setLegendDisplay(true);
setLegendPosition(2);
 
setSeriesFillColor(0, new Color, 0 0 138));
setSeriesFillColor(1, new Color, 115 162 247));
ENDSTYLE
END
-RUN

-IF &&OUTFMT NE 'PDF' GOTO NOGRAPH ELSE GOTO GRAPH;
-GRAPH
-SET &&H_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';
-GOTO HLDF
-NOGRAPH
-SET &&H_GIF='N';

-HLDF


TABLE FILE TTLHLD2
SUM NNROOMS
    NNROOMS1
    CNNROOMS
    PNNROOMS
    NAMEC
COMPUTE CPCT/D16 = IF ((CNNROOMS LT 1) AND (CNNROOMS GT (-1))) THEN 0 ELSE (NNROOMS/CNNROOMS)*100; 
COMPUTE PPCT/D16 = IF ((PNNROOMS LT 1) AND (PNNROOMS GT (-1))) THEN 0 ELSE (NNROOMS1/PNNROOMS)*100;  
BY APP_FLAG
ON TABLE HOLD AS PTTL
END
-RUN

DEFINE FILE PTTL
BITMAP/A12 WITH APP_FLAG = 'HTLPREF';
LABEL/A30 = 'Non-Preferred Hotels';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 5;
END
-RUN

TABLE FILE PTTL
PRINT CATEGORY/A61 AND APP_FLAG/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND NNROOMS/D16 AS 'VOL_CURR' AND CPCT/D16 AS 'TKT_CURR' AND 
NNROOMS1/D16 AS 'VOL_PRIOR' AND PPCT/D16 AS 'TKT_PRIOR'
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;   
-* WHERE APP_FLAG EQ 'Non-Preferred' 
ON TABLE HOLD AS PTTL2
END
-RUN

TABLE FILE PTTL2
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS PTTL3
END
-RUN

MODIFY FILE OV_OFF
LOG DUPL MSG OFF
FIXFORM FROM PTTL3
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON PTTL3
END
-RUN






