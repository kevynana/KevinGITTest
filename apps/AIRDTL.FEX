-* 4/27/04  SAC   ADDED TRP_REF AND NHR_REF
-* 12/21/04 DV   ADDED ADDTL DOD_TYPE TO BOOKING TYPE DEFINES
-* 10/11/05 DV   ADDED DOD_TYPE H TO BOOKING TYPE DEFINES
-*03/18/13  CHANGED CLASS_CAT DEFINE DUE TO CLASS CATEGORY CHANGES - DV

-INCLUDE SETECHO
EXPIRE_REPORTS=1

-SET &&COMPNY = 'ABCD' ;
-SET &&AH = AH_50;
-SET &&XHIER = HIER50;
-SET &&DAH = AH_50 || '.';
-SET &&DHIER = HIER50 || '.';
-SET &&EXTRACT = SR_50;
-SET &&EXTJOIN = SR_JOIN;

-SET &&AIR_PATH = 'D:\TNT\TTRACKER\DATA\';
-SET &&COM_PATH = 'D:\TNT\TTRACKER\SYSTEM\DATA\';
-SET &&SU_PATH = 'D:\TNT\TTRACKER\SUDATA\';
-SET &&AH_DB = 'AL' || &&COMPNY || '.FOC';
-SET &&HIER = 'HP' || &&COMPNY || '.FOC';
-SET &&USE_AH = &&AIR_PATH || &&AH_DB;
-SET &&USE_AREF = &&COM_PATH || 'AIR_REF.FOC';
-SET &&USE_TREF = &&COM_PATH || 'TRP_REF.FOC';
-SET &&USE_NREF = &&COM_PATH || 'NHR_REF.FOC';
-SET &&USE_CLNT = &&COM_PATH || 'CLIENT.FOC';
-SET &&USE_BRCH = &&COM_PATH || 'BRANCH.FOC';
-SET &&USE_HIER = &&AIR_PATH || &&HIER;
-SET &&USE_ROLL = &&COM_PATH || 'ROLLUPX.FOC';
-*SET &&USE_ROLL = &&SU_PATH || 'ROLLUP.FOC';
-SET &&USE_CCAT = &&COM_PATH || 'CLS_CAT.FOC';
-SET &&USE_ARPT = &&COM_PATH || 'AIRPORT.FOC';
-SET &&USE_AEMB = &&COM_PATH || 'EMBARPT.FOC';
-SET &&USE_ARTE = &&COM_PATH || 'RTEARPT.FOC';
-SET &&USE_ANDO = &&COM_PATH || 'NDOARPT.FOC';
-SET &&USE_ANDD = &&COM_PATH || 'NDDARPT.FOC';
-SET &&USE_MILG = &&COM_PATH || 'MILEAGE.FOC';
-SET &&USE_ARLN = &&COM_PATH || 'AIRLINE.FOC';
-SET &&USE_CITY = &&COM_PATH || 'CITY.FOC';
-SET &&USE_CEMB = &&COM_PATH || 'EMBCITY.FOC';
-SET &&USE_CRTE = &&COM_PATH || 'RTECITY.FOC';
-SET &&USE_CNDO = &&COM_PATH || 'NDOCITY.FOC';
-SET &&USE_CNDD = &&COM_PATH || 'NDDCITY.FOC';
-SET &&USE_VAIR = &&COM_PATH || 'VAL_AIR.FOC';

-*****************************************************************

-SET &&COMP1S = 'S_' ||&&COMPNY || '.FOC';
-SET &&COMP1A = 'A_' ||&&COMPNY || '.FOC';
USE CLEAR *
-RUN
USE

 D:\TNT\TTRACKER\SYSTEM\DATA\ORGLIST.FOC
 D:\TNT\TTRACKER\SYSTEM\DATA\RTELIST.FOC
 D:\TNT\TTRACKER\SYSTEM\DATA\REFLIST.FOC
 D:\TNT\TTRACKER\SYSTEM\DATA\CRRLIST.FOC
 D:\TNT\TTRACKER\DATA\&&COMP1S AS &&EXTRACT
 D:\TNT\TTRACKER\DATA\&&COMP1A AS AIR_MAIN
  &&USE_ROLL
  &&USE_ARLN
  &&USE_MILG
  &&USE_CCAT
  &&USE_AEMB
  &&USE_CEMB
  &&USE_ARTE
  &&USE_CRTE
  &&USE_ANDO
  &&USE_CNDO
  &&USE_ANDD
  &&USE_CNDD
  &&USE_VAIR
  &&USE_AH AS &&AH
  &&USE_HIER AS &&XHIER
  &&USE_AREF
  &&USE_TREF
  &&USE_NREF
  &&USE_CLNT
END
-RUN
SET EMPTYREPORT=ON
SET TARGETFRAME=MAIN1
DEFINE FILE SR_50
  BASE_FARE/D12.2CS = SEG_AMT - SEG_COM;
  BOOK_DT/MDYY         = RESV_DATE;
  CARD_NUM/A12=EDIT(CREDIT_CARD, '$$999999999999$$$$');
  CARD1_NUM/A4=EDIT(CREDIT_CARD, '$$$$$$$$$$$$$$9999');
  CARD_NUM1/A16=EDIT(CREDIT_CARD,'$$9999999999999999');
  CCARD/A4 = EDIT(CREDIT_CARD, '$$9999$$$$$$$$$$$$');
  CCARD1/A4 =EDIT(CREDIT_CARD, '$$$$$$9999$$$$$$$$');
  CCARD2/A6 =EDIT(CREDIT_CARD, '$$999999$$$$$$$$$$');
  CCARD3/A6 =EDIT(CREDIT_CARD, '$$999999$$$$$$$$$$');
  CCARD5/A6 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$999999');
  CCARD6/A6 = EDIT(CREDIT_CARD, '999999$$$$$$$$$$$$');
  CGC/A6 = EDIT(CREDIT_CARD, '999999$$$$$$$$$$$$');
  CGC1/A3 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$$$$999');
  CGC2/A9 = 'XXXXXXXXX';
-*  CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'
-*                              'D' 'DISCOUNT'  'B' 'BUSINESS');
  CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE' 
                                ' ' 'DEFAULT'); 
  COM_AMT/D12.2CS = SEG_COM;
  CRD_CRD/A2=EDIT(CREDIT_CARD, '99$$$$$$$$$$$$$$$');
  DEPT_MONTH/MT        = DPT_DATE;
  DEPT_YEAR/YY         = DPT_DATE;
  DIR_CP_CD/A50        = CITYPAIR.EMBARK||'-'||CITYPAIR.ROUTE;
-****************************************************************************
-* 10/3/00  IBISTL-RJ    FIELD WAS BEING TRUNCATED IN FOCUS 6, NEED TO RUN A
-*                       SUBROUTINE IN WEBFOCUS TO TRUNCATE.
  DIR_CP_NMX/A72 = '(' || CITYPAIR.EMBARK || '/'
           || CITYPAIR.ROUTE || ') ' |
           EMB_APT.AIRPORT_NAME ||'---' || RTE_APT.AIRPORT_NAME;
  DIR_CP_NM/A50 = SUBSTR(72,DIR_CP_NMX,1,50,50,'A50');
-*
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  GROSS_FARE/D12.2CS  = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
  IBOOK/A13 = IF DOD_TYPE EQ 'H' OR 'I' OR 'J' THEN 'INTERNET' ELSE
              IF DOD_TYPE EQ 'M' OR 'X' THEN 'EINTERNET' ELSE
              'NON-INTERNET';
  MIR_TYPE/A24 = DECODE AUX_NON_MIR ('X' 'X-DUAL MIR'
                                    'N' 'N-NON-TICKETING MIR'
                                    'J' 'J-NON-FARING MIR'
                                    'A' 'A-NON-ACCOUNTING MIR'
                                    'B' 'B-ACCOUNTING MIR'
                                    'H' 'H-REGULAR TICKET MIR'
                                    'V' 'V-VOID'
                                    'E' 'E-ELECTRONIC TKT NETWORK'
                                    'L' 'L-ELECTRONIC TKT MIR'
                                    ' ' ' -MANUALLY ENTERED TRANS');
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  MTG_NUM/A7 = EDIT(LEVEL3, '$$$$$$$$9999999');
  ND_CITY_NM/A50= NDO_CTY.CITY_NAME | NDD_CTY.CITY_NAME;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NET_TKT_CNT1/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_COUNT/D8CS = SEG_COUNT;
  NEW_SEG_DISC/D12.2CS = SEG_DISC;
  NEW_SEG_MILES/D9= SEG_MILES;
  NOREF_SEG_COUNT/D8CS = IF (RF_FLAG EQ ' ') AND
                            (EX_FLAG EQ ' ') THEN
                           NEW_SEG_COUNT ELSE 0;
  REF_E_CNT/D8CS      = IF (RF_FLAG NE ' ') OR
                           (EX_FLAG NE ' ') THEN
                            1 ELSE 0;
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 0 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  REFEXCH/D12.2CS     = IF (RF_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE
                        IF (EX_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  TRAN_MY/MTYY = TRN_DATE;
  UPCCR/A6 = EDIT(ICODEX,'999999$$$$$$$$$$');
  UPCOSTC/A1 = IF LEVEL1 EQ '0EM999' OR '0EM299' OR '0EM199' OR '0EM167' OR
  '0EM166' OR '0EM165' OR '0EM163' OR '0EM161' OR '0EM112' OR '0EM093' OR
  '0EM092' OR '0EM089' OR '0EM085' OR '0EM079' OR '0EM072' OR '0EM069' OR
  '0EM061' OR '0EM057' OR '0EM056' OR '0EM055' OR '0EM054' OR '0EM053' OR
  '0EM053' OR '0EM051' OR '0EM033' OR '0EM032' OR '0EM027' OR '0EM026' OR
  '0EM022' THEN 'Y' ELSE 'N';
  VALUA/A1 = IF (VAL_AIRLINE EQ 'UA') AND
                (FLIGHT GE '5000')    AND
                (FLIGHT LE '5499') THEN 'Y' ELSE
             IF (VAL_AIRLINE EQ 'UA') AND
                (FLIGHT GE '5500')    AND
                (FLIGHT LE '5899') THEN 'Y' ELSE
             IF (VAL_AIRLINE EQ 'UA') AND
                (FLIGHT GE '6800')    AND
                (FLIGHT LE '7019') THEN 'Y' ELSE
             IF (VAL_AIRLINE EQ 'UA') AND
                (FLIGHT GE '7020')    AND
                (FLIGHT LE '7149') THEN 'Y' ELSE
             IF (VAL_AIRLINE EQ 'UA') AND
                (FLIGHT GE '7150')    AND
                (FLIGHT LE '7899') THEN 'Y' ELSE
             IF (VAL_AIRLINE EQ 'UA') AND
                (FLIGHT GE '7900')    AND
                (FLIGHT LE '7899') THEN 'Y' ELSE 'N';
  XARR_DOW/W           = ARR_DATE;
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XDPT_DATE/MDY        = DPT_DATE;
  XPSNGR_NM/A15        =
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTKT_DT/MDYY         = TKT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;


-* ****DEFINES ON PREVIOUS DEFINES****

  ABOOK_CNT/D8CS = IF DOD_TYPE NE 'H' OR 'I' OR 'J' OR 'M' OR 'X' THEN 
                         NET_TKT_CNT ELSE 0;
  ABOOK_FARE/D12.2CS = IF DOD_TYPE NE 'H' OR 'I' OR 'J' THEN FARE_PAID ELSE 0;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3'
     THEN FARE_PAID ELSE 0;
  ARR_DAY/A3           = DECODE XARR_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  CGCARD/A18 = CGC||CGC2||CGC1;
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  DPT_DAY/A3           = DECODE XDPT_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  GROSS_DSAVE/D12.2CS = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                           DISSAVINGS ELSE 0;
  IBOOK_CNT/D8CS = IF DOD_TYPE EQ 'H' OR 'I' OR 'J' THEN NET_TKT_CNT ELSE 0;
  IEBOOK_CNT/D8CS = IF DOD_TYPE EQ 'M' OR 'X' THEN NET_TKT_CNT ELSE 0;
  IBOOK_FARE/D12.2CS = IF DOD_TYPE EQ 'H' OR 'I' OR 'J' THEN FARE_PAID ELSE 0;
  IEBOOK_FARE/D12.2CS = IF DOD_TYPE EQ 'M' OR 'X' THEN FARE_PAID ELSE 0;
  TRIPS/D8CS = IF STAY GT 0 THEN 1 ELSE
               IF STAY LT 0 THEN (-1) ELSE 0;
END
SET FILENAME=ON
SET ASNAMES=ON
TABLEF FILE SR_50
" "
" &DATE1 ---------- &DATE2"
" "
PRINT
  ABOOK_CNT
  ABOOK_FARE
  AGENT_NUM
  AIR_KEY
  AIR_MAIN.INTL_DOM
  AIRLINE
  AIRLINE_FEE
  AIRLINE_NAME
  AR_BRANCH
  ARR_DAY
  ARR_TIME
  AUX_NON_MIR
  BASE_FARE
  BOOK_DT
  BR_CL_IDX
  CARD_NUM
  CARD_NUM1
  CARD1_NUM
  CCARD
  CCARD1
  CCARD2
  CCARD3
  CCARD5
  CCARD6
  CGCARD
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'
  CL_CAT
  CLASS
  CLASS_CAT
  CLIENT_NAME
  CLIENT_NUM
  COM_AMT
  CRD_CRD
  CREDIT_CARD
  DEPT_MONTH
  DEPT_YEAR
  DIR_CP_CD
  DIR_CP_NM
  DISSAVINGS
  DOC_TYPE
  DOD_PAX_CAT
  DPT_DAY
  DPT_TIME
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  EMB_APT.STATE_CODE AS 'EMB_STATE'
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
  EMB_CTY.COUNTRY_CODE AS 'EMB_CNTRY_CODE'
  EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'
  FARE_BAS
  FARE_PAID
  FLIGHT
  GROSS_DSAVE
  GROSS_FARE
  GROUP_CODE
  IBOOK
  IBOOK_CNT
  IBOOK_FARE
  IEBOOK_CNT
  IEBOOK_FARE
  INT_CODE
  INVOICE_NUM
-*  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  MIR_TYPE
  MRK_TKT_CNT
  MTG_NUM
  ND_CITY_NM
  NET_TKT_CNT
  NET_TKT_CNT1
  NEW_ADV_PURCH
  NEW_SEG_COUNT
  NEW_SEG_DISC
  NEW_SEG_MILES
  NOREF_SEG_COUNT
  ORG_DST
  PASSNGR_NAME
  PNR_LOCATOR
  REF_E_CNT
  REF_EXCH_CNT
  REFEXCH
  REFUSAL_DESC
  REFUSE_CODE
  REFUSE_KEY
-*  ROLL_KEY
-*  ROLL_LEV
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'
  RTE_CTY.COUNTRY_CODE AS 'RTE_CNTRY_CODE'
  RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'
  SAVINGS
  SEG_AMT
  SEG_COM
  SEG_COUNT
  SEG_DESIG
  SEG_DISCOUNT
  SEG_LOWEST
  SEG_MILES
  SEG_NBR
  SEG_STANDARD
  SEG_TAX
  STAY
  TICKET_BRANCH
  TKT_AMOUNT
  TKT_DATE
  TKT_NUM
  TKT_PURCH
  TKT_SORT
  TKT_TYPE
  TOUR_CODE
  TRAN_MONTH
  TRAN_MY
  TRIPS
  TRIP_LENGTH
  TRN_DATE
  UPCCR
  UPCOSTC
  VAL_AIRLINE
  VALUA
  VOID_DATE
  XARR_DT
  XDPT_DT
  XDPT_DATE
  XPSNGR_NM
  XTKT_DT
  XTRAN_DT
-*IF TRN_DATE GE &DATE1
-*IF TRN_DATE LE &DATE2
-*WHERE TRN_DATE GE '2001/12/01' AND TRN_DATE LE '2002/01/31'
-*WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
WHERE VOID_DATE EQ 0
ON TABLE HOLD AS PASS1
END
-*========================================================================
-*              GET REFUSAL KEY INFORMATION FOR AIR DETAIL
-*========================================================================
SET WARNING=OFF
JOIN REFUSE_CODE IN PASS1 TO REFUSE_KEY IN AIR_REF

CREATE FILE REFLIST
-RUN

DEFINE FILE PASS1
XL01/A10 = LJUST(10,REFUSE_KEY,XL01) ;
XL02/A40 = LJUST(40,REFUSAL_DESC,XL02);
XL03/A12 ='('||XL01||')';
XL04/A60 = XL03||('    '|XL02);
END
TABLE FILE PASS1
BY REFUSE_CODE NOPRINT
SUM XL04 AS 'LIST'
ON TABLE HOLD AS REFUSE1
END
MODIFY FILE REFLIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM REFUSE1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON REFUSE1
END

DEFINE FILE REFLIST
XL00/A25 =  ' ----- NO SELECTION -----';
END
TABLE FILE REFLIST
BY LIST NOPRINT
PRINT XL00 AS 'LIST'
ON TABLE HOLD AS REFUSE1
END
MODIFY FILE REFLIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM REFUSE1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON REFUSE1
END
-*========================================================================
-*              GET CITY-ORG CODE INFORMATION FOR AIR DETAIL
-*========================================================================
SET WARNING=OFF

CREATE FILE ORGLIST
-RUN

DEFINE FILE PASS1
XL01/A3  = LJUST(3,EMB_CTY_CD,XL01) ;
XL02/A25 = LJUST(25,EMB_CTY_NM,XL02);
XL03/A12 ='('||XL01||')';
XL04/A45 = XL03||('    '|XL02);
END
TABLE FILE PASS1
BY EMB_CTY_CD NOPRINT
SUM XL04 AS 'LIST'
ON TABLE HOLD AS ORG1
END
MODIFY FILE ORGLIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM ORG1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON ORG1
END

DEFINE FILE ORGLIST
XL00/A25 =  ' ----- NO SELECTION -----';
END
TABLE FILE ORGLIST
BY LIST NOPRINT
PRINT XL00 AS 'LIST'
ON TABLE HOLD AS ORG1
END
MODIFY FILE ORGLIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM ORG1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON ORG1
END
-*========================================================================
-*              GET CITY-DST CODE INFORMATION FOR AIR DETAIL
-*========================================================================
SET WARNING=OFF

CREATE FILE RTELIST
-RUN

DEFINE FILE PASS1
XL01/A3  = LJUST(3,RTE_CTY_CD,XL01) ;
XL02/A25 = LJUST(25,RTE_CTY_NM,XL02);
XL03/A12 ='('||XL01||')';
XL04/A45 = XL03||('    '|XL02);
END
TABLE FILE PASS1
BY RTE_CTY_CD NOPRINT
SUM XL04 AS 'LIST'
ON TABLE HOLD AS RTE1
END
MODIFY FILE RTELIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM RTE1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON RTE1
END

DEFINE FILE RTELIST
XL00/A25 =  ' ----- NO SELECTION -----';
END
TABLE FILE RTELIST
BY LIST NOPRINT
PRINT XL00 AS 'LIST'
ON TABLE HOLD AS RTE1
END
MODIFY FILE RTELIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM RTE1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON RTE1
END
-*========================================================================
-*              GET CARRIER CODE INFORMATION FOR AIR DETAIL
-*========================================================================
SET WARNING=OFF

JOIN AIRLINE IN PASS1 TO AIRLINE IN AIRLINE

CREATE FILE CRRLIST
-RUN

DEFINE FILE PASS1
XL01/A3  = LJUST(3,AIRLINE,XL01) ;
XL02/A12 = LJUST(12,AIRLINE_NAME,XL02);
XL03/A12 ='('||XL01||')';
XL04/A30 = XL03||('    '|XL02);
END
TABLE FILE PASS1
BY AIRLINE NOPRINT
SUM XL04 AS 'LIST'
ON TABLE HOLD AS CRE1
END
MODIFY FILE CRRLIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM RTE1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON CRE1
END

DEFINE FILE CRRLIST
XL00/A25 =  ' ----- NO SELECTION -----';
END
TABLE FILE CRRLIST
BY LIST NOPRINT
PRINT XL00 AS 'LIST'
ON TABLE HOLD AS CRE1
END
MODIFY FILE CRRLIST
LOG INVALID MSG OFF
LOG DUPL MSG OFF
LOG FORMAT MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM CRE1
MATCH LIST
ON NOMATCH INCLUDE
DATA ON CRE1
END
-*========================================================================
-SET &FMM=EDIT(&DATE1,'$$$$$$99');
-SET &FMD=EDIT(&DATE1,'$$$$$$$$$99');
-SET &FMY=EDIT(&DATE1,'$9999');
-SET &TOM=EDIT(&DATE2,'$$$$$$99');
-SET &TOD=EDIT(&DATE2,'$$$$$$$$$99');
-SET &TOY=EDIT(&DATE2,'$9999');
-RUN


TABLE FILE PASS1
" "
" "
" "
"                                <74 ABCD DEMO COMPANY"
"TRANSACTION PERIOD:             <74 TRAVEL AND TRANSPORT INC"
"&FMM/&FMD/&FMY - &TOM/&TOD/&TOY <74 OMAHA, NE"
"                                <74 STEVE CORDS"
"                                <74 1-402-399-4510"


" "
"                                <74 PAGE: <TABPAGENO"

"<44 DETAILED TICKET LISTING"
"<50 ABCD BUSINESS"
"</1"

BY PASSNGR NOPRINT
BY    PASSNGR_NAME AS 'PASSENGER,NAME'
BY    TKT_NUM      AS 'TKT,NMBR'
BY    TKT_TYPE     AS 'TKT,TYPE'
BY	  TRN_DATE     AS 'TRAN,DATE'
BY	  REFUSE_CODE  AS 'RSN,CDE'
BY    XDPT_DT      AS 'DEPART,DATE'
SUM	  DPT_TIME     AS 'TIME'
	  EMB_CTY_NM   AS 'ORG'
	  RTE_CTY_NM   AS 'DST'
	  AIRLINE      AS 'CR'
	  GROSS_FARE   AS 'FAIR,PAID'
	  NET_TKT_CNT  AS 'TKT,CNT'

ON TABLE SET ONLINE-FMT PDF
FOOTING BOTTOM
" "
" "
"eTTek Review &TOD"
"&&CRIGHT"
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=7, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, $
TYPE=HEADING, LINE=4, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=REPORT, IMAGE=&&TNTPIC,  POSITION=(8.4 .5), SIZE=(1.3 .44), $
TYPE=REPORT, IMAGE=ABCD.GIF, POSITION=(.5 .3), SIZE=(2.0 .4),  $
END

