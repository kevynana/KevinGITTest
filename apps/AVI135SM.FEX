 
-**********************************************************************
-* Summary by Hierarchy - Air
-*1/9/14    UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*3/19/14   Carried through Online/Traditional Field JK
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-*  03/31/2015  REJ  S-05681 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-*  02/01/2016  REJ  S-13228 ER5 changes so the selection criteria would show on the subfoot page.
-*  03/08/2016  JEM  S-13285 Updated date selections to allow to run for transaction or travel not just transaction
-*02/2417  DLV S-22486 - updated position of header and footer NEW_HDR NEW_FTR
-*4/25/2017-JEM-S-33318-updated logic for no records report
-* 05/24/2017 JEM S-34216 - updating to make styling/heading/positioning more consistent
-* 08/04/2017 RSB S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-*09/01/2017 DLV S-39470 - removed tick marks around car and hotel extract due to report erring out in excel
-*                         and omitting car hotel data in pdf
-*09/08/17 JEM - S-39671- Updating to use &&rank_limit so uses screen parameters
-*10/10/2017 RSB S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-**********************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON 
SET SPACE = 1
-SET HOLDATTR = ON;
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');


 
 



-SET &&SP = '|';
-SET &&SP1A = IF &&ROLLNME EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP1 = IF &&ROLLCS EQ ' ' THEN ' ' ELSE &&SP1A;
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&REL = &&ROLLEMAIL.LENGTH;

-SET &&RCSL = &&RCNCT.EVAL + &&CSL.EVAL;
-SET &&ROLLCS11 = TRUNCATE(&&ROLLCS);
-SET &&RCNCT1 = TRUNCATE(&&ROLLCNCT);
 
-SET &&CSLPA = IF '&&ROLLUP.EVAL' EQ 'ABCD-R' THEN 80 ELSE 
-              IF '&&ROLLEMAIL.EVAL' NE ' ' AND &&REL GT 20 THEN 68 ELSE
-              IF &&RCSL LE 21 THEN 80 ELSE 
-              IF &&RCSL LE 31 THEN 75 ELSE 68;

-* -SET &&CSLPA = IF &&RCNCT.EVAL GE 15 OR &&CSL.EVAL GE 10 THEN 68 ELSE 75;
-*-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' THEN 88 ELSE &&CSLPA;

-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' AND &&ORIENTATION LIKE 'L%' THEN 88 
- ELSE IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' AND &&ORIENTATION LIKE 'P%' THEN 68 
- ELSE IF '&&ROLLUP.EVAL' EQ 'HDRR-R' AND &&ORIENTATION LIKE 'L%' THEN 80
- ELSE IF &&ORIENTATION LIKE 'P%' THEN &&CSLPA-15 ELSE &&CSLPA;
-SET &&BLANK = ' ';
-SET &&ROLLPHNEML = IF '&&ROLLEMAIL.EVAL' GT ' ' THEN '&&ROLLEMAIL.EVAL' ELSE '&&ROLLPHN.EVAL';
 
-SET &WHATDATE = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'TRN_DATE' ELSE 'FST_DPT_DATE';
 
 
 
-* -IF &&SETF.EVAL EQ 'CHSMHIER' THEN GOTO CHEP;
 
CREATE FILE QRHIERC
-RUN
 
-*********************************************************************************
-* Gather/Add Air Info to QRHIER File
-*********************************************************************************
 
DEFINE FILE SR_50
  CURRENT/A1 = IF &WHATDATE.EVAL GE &&FROMDT.EVAL AND &WHATDATE.EVAL LE &&TODT.EVAL THEN 'Y' ELSE 'N';
  PRIOR/A1 = IF &WHATDATE.EVAL GE &&FXDATE.EVAL AND &WHATDATE.EVAL LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
  CGADV/A8 = IF (ADV_PURCH LT 7) THEN '0-6' ELSE
             IF (ADV_PURCH GE 7) AND (ADV_PURCH LE 13) THEN '7-13' ELSE
             IF (ADV_PURCH GE 14) AND (ADV_PURCH LE 20) THEN '14-20' ELSE
                '21+';
 
  BRK_LVL/I2 = &&BRK_LVL;
  LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A23 = EDIT(LEVEL_DESC1, '99999999999999999999999');
  FARE_PAID/P12.2 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/P12    = IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (FARE_PAID GE 0) AND
                          (SEG_COUNT EQ 1) AND
                          (CONJ_REL EQ 1) THEN 1 ELSE
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (EX_FLAG EQ 'F') THEN (-1) ELSE
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  SAVINGS/P12.2     = SEG_STANDARD - FARE_PAID;
  NEW_SEG_MILES/D12 = SEG_MILES;
 -* Defines on Previous Defines
  DISSAVINGS/P12.2 = IF EMBARK EQ 'DT1' THEN 0 ELSE
                   FARE_PAID-SEG_LOWEST;
 TON_C02/P12.2 =
   IF ((CITYPAIR.SEG_MILES GE 1) AND (CITYPAIR.MILES LE 300) OR
            (CITYPAIR.SEG_MILES LE (-1)) AND (CITYPAIR.MILES GE (-300))) THEN
             (CITYPAIR.SEG_MILES * .00029) ELSE
   IF ((CITYPAIR.SEG_MILES GE 301) AND (CITYPAIR.SEG_MILES LE 1000) OR
            (CITYPAIR.SEG_MILES LE (-301)) AND (CITYPAIR.SEG_MILES GE (-1001))) THEN
            (CITYPAIR.SEG_MILES * .00020) ELSE
   IF ((CITYPAIR.SEG_MILES GE 1001) AND (CITYPAIR.SEG_MILES LE 99999) OR
            (CITYPAIR.SEG_MILES LE (-1001)) AND (CITYPAIR.SEG_MILES GE (-99999))) THEN
           (CITYPAIR.SEG_MILES * .00018) ELSE 0;
END
 
 
 
TABLEF FILE SR_50
PRINT CGADV
  CURRENT
  PRIOR
  DISSAVINGS
  DOD_TYPE
  AIR_MAIN.INTL_DOM AS 'INTDOM'
  FARE_PAID
  LEVEL_DESC
  MRK_TKT_CNT
  NET_TKT_CNT
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR
  TKT_NUM
  TON_C02
  TRN_DATE
WHERE (&WHATDATE.EVAL GE &&FROMDT.EVAL) AND (&WHATDATE.EVAL LE &&TODT.EVAL) OR
      (&WHATDATE.EVAL GE &&FXDATE.EVAL) AND (&WHATDATE.EVAL LE &&TXDATE.EVAL)
WHERE VOID_DATE EQ 0
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
 
 
TABLE FILE RPTHOLD
PRINT CGADV
  DISSAVINGS
  DOD_TYPE
  INTDOM
  FARE_PAID
  LEVEL_DESC
  MRK_TKT_CNT
  NET_TKT_CNT
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR
  TKT_NUM
  TON_C02
  TRN_DATE
BY CURRENT
BY PRIOR
BY LEVEL_DESC
-IF &&SETF NE 'ABSMHDC' GOTO SKIPDom;
WHERE INTDOM NE 'I'
-SKIPDom;
-IF &&SETF NE 'ABSMHIC' GOTO SKIPInt;
WHERE INTDOM EQ 'I'
-SKIPInt;
ON TABLE HOLD AS TRANONE
END
-RUN
 
-IF &LINES EQ 0 THEN GOTO NOREC;
 
 
-SET &&RPTSUF = 'HIERSUM';
-INCLUDE FDEFRPTS
-RUN
 
DEFINE FILE TRANONE
CAVOL/P12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
CMILES/P12 = IF CURRENT EQ 'Y' THEN NEW_SEG_MILES ELSE 0;
PAVOL/P12.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CNTKT/P12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CNTKTD/P12 = IF CURRENT EQ 'Y' AND INTDOM EQ 'D' THEN NET_TKT_CNT ELSE 0;
PNTKT/P12 = IF PRIOR EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CLSAV/P12.2 = IF CURRENT EQ 'Y' THEN DISSAVINGS ELSE 0;
CADVT/P12 = IF (CURRENT EQ 'Y') AND
                (CGADV EQ '0-6') THEN NET_TKT_CNT ELSE 0;
CADVT1/P12 = IF (CURRENT EQ 'Y') AND
                 (CGADV EQ '7-13') THEN NET_TKT_CNT ELSE 0;
 
CONL/P12 = IF (CURRENT EQ 'Y') AND (ONLINE_TRADITIONAL EQ 'ONLINE') AND (INTDOM EQ 'D') THEN NET_TKT_CNT ELSE 0;
 
CNR/P12 = IF (CURRENT EQ 'Y') AND (NONREF_FLAG EQ 'N') THEN
              NET_TKT_CNT ELSE 0;
CMKT/P12 = IF CURRENT EQ 'Y' THEN MRK_TKT_CNT ELSE 0;
CMKTD/P12 = IF CURRENT EQ 'Y' AND INTDOM EQ 'D' THEN MRK_TKT_CNT ELSE 0;
PMKT/P12 = IF PRIOR EQ 'Y' THEN MRK_TKT_CNT ELSE 0;
CC02/D12.2 = IF CURRENT EQ 'Y' THEN TON_C02 ELSE 0;
PC02/D12.2 = IF PRIOR EQ 'Y' THEN TON_C02 ELSE 0;
END
TABLE FILE TRANONE
SUM CADVT
    CADVT1
    CAVOL
    CLSAV
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL
    CMKT
    CMKTD
    PMKT
    CC02
    PC02
BY LEVEL_DESC
ON TABLE HOLD AS TRANTWO
END
-RUN
 
TABLE FILE TRANTWO
PRINT CAVOL CMILES CNTKT PNTKT CLSAV PAVOL CADVT CADVT1 CONL CNTKTD CNR LEVEL_DESC CMKT CC02 CMILES
RANKED BY HIGHEST CAVOL
ON TABLE HOLD AS RNK1
END
-RUN
 
DEFINE FILE RNK1
 -*RANK_LIMIT/I2 = 9999;
 RANK_LIMIT/I2 = &&RANK_LIMIT;
 
 RLIMIT/A2    = EDIT(RANK_LIMIT) ;
 CRANK/D5     = RANK;
 BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
 LIST/D6 = IF LEVEL_DESC NE LAST LEVEL_DESC THEN (LAST LIST + 1)
           ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999');
 RANK_VALUE/A9 = IF RANK_LIMIT EQ 0 THEN BRANK ELSE
                IF LIST GT RANK_LIMIT THEN 'ALL OTHER' ELSE FRANK;
 LVL_PRINT/A23 =IF RANK_VALUE LT RLIMIT THEN LEVEL_DESC ELSE 'ALL OTHER';
END
-RUN
 
TABLE FILE RNK1
SUM CAVOL CMILES CNTKT PNTKT CLSAV PAVOL CADVT CADVT1 CONL CNTKTD CNR
  CMKT CC02
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS RNK2
END
-RUN
 
TABLE FILE RNK2
PRINT LVL_PRINT RANK_VALUE CADVT CADVT1 CAVOL CLSAV CMILES CNR CNTKT PNTKT CONL CNTKTD PAVOL
        CMKT CC02/D12.2
ON TABLE SAVE
END
-RUN
 
 
MODIFY FILE QRHIERC
FIXFORM LVL_PRINT/23 RANK_VALUE/9 CADVT/12 CADVT1/12 CAVOL/12 CLSAV/12 CMILES/12 CNR/12 CNTKT/12 PNTKT/12 CONL/12 CNTKTD/12 PAVOL/12 CMKT/12 CMKTD/12 CC02/12
MATCH RANK_VALUE LVL_PRINT
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
 
 
DEFINE FILE RNK2
LVLP/A25 = ''''||LVL_PRINT||'''';
END
-RUN
TABLE FILE RNK2
PRINT LVLP
BY LVL_PRINT NOPRINT
ON TABLE SAVE AS LEVELF
END
-RUN
 
 
-*********************************************************************************
-* Gather/Add Car Info to QRHIER File Based upon
-*********************************************************************************
-IF &&FROMAVRP NE 'Y' GOTO :AV1;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:AV1
 
EX CARUSE
-RUN
 
-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN

-INCLUDE RESETCPM
-RUN
 
 
-SET &WHATDATEC = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'INVOICE_DATE' ELSE 'PICKUP_DATE';
 
 
DEFINE FILE CR_50
 LEVEL_DESC1/A60 = &&LDESC;
 LEVEL_DESC/A23 = EDIT(LEVEL_DESC1, '99999999999999999999999');
 RENTAL_DAYS/P12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
END
 
-RUN
TABLE FILE CR_50
SUM RENTAL_DAYS
    RENTAL_AMT
BY LEVEL_DESC
WHERE (&WHATDATEC.EVAL GE &&FROMDT.EVAL) AND (&WHATDATEC.EVAL LE &&TODT.EVAL)
-IF &&SETF NE 'ABSMHID' GOTO NoDomonly;
WHERE PICK_CTY.INTL_DOM NE 'I'
-NoDomonly;
-IF &&SETF NE 'ABSMHII' GOTO NoIntonly;
WHERE PICK_CTY.INTL_DOM EQ 'I'
-NoIntonly;
-INCLUDE RPTPARMS
ON TABLE HOLD AS CAR1
END
-RUN

 
TABLE FILE CAR1
SUM RENTAL_AMT/P12.2 AS 'CARVOL'
    RENTAL_DAYS/P12 AS 'CARDAY'
BY LEVEL_DESC
ON TABLE HOLD AS CAR2
END
-RUN
 
-******************************************************
-* Matching Hierarchy Levels
-******************************************************
 
TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARMREC
END
-RUN
 
TABLE FILE CARMREC
SUM CARVOL
    CARDAY
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS CARMREC1
END
-RUN
 
TABLE FILE CARMREC1
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIERC
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/23 CARVOL/12 CARDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL CARDAY
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
 
-**********************************************************************
-* Non Matching information ALL OTHER
-**********************************************************************
 
TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARNREC
END
-RUN
 
DEFINE FILE CARNREC
LVL_PRINT/A23 = 'ALL OTHER';
END
-RUN
TABLE FILE CARNREC
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIERC
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/23 CARVOL/12 CARDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL CARDAY
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
 
-*********************************************************************************
-* Gather/Add HTL Info to QRHIER File
-*********************************************************************************
-IF &&FROMAVRP NE 'Y' GOTO :AV2;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:AV2
 
EX HTLUSE
-RUN
 
-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN

-INCLUDE RESETHPM
-RUN
 
-SET &WHATDATEH = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'INVOICE_DATE' ELSE 'CHKIN_DATE';
 
 
DEFINE FILE HR_50
 LEVEL_DESC1/A60 = &&LDESC;
 LEVEL_DESC/A23 = EDIT(LEVEL_DESC1, '99999999999999999999999');
 NBR_DAYS/P12=NUM_DAYS;
 NEW_TOTAL_AMT/P12.2 = TOTAL_AMT;
 NNROOMS/P12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
END
-RUN
 
TABLE FILE HR_50
SUM NEW_TOTAL_AMT
    NNROOMS
BY LEVEL_DESC
WHERE (&WHATDATEH.EVAL GE &&FROMDT.EVAL) AND (&WHATDATEH.EVAL LE &&TODT.EVAL)
-IF &&SETF NE 'ABSMHID' GOTO NoHDomonly;
WHERE CITY.INTL_DOM NE 'I'
-NoHDomonly;
-IF &&SETF NE 'ABSMHII' GOTO NoHIntonly;
WHERE CITY.INTL_DOM EQ 'I'
-NoHIntonly;
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTL1
END
-RUN
 
-**************************************************************************
-* Matching Hotel Records
-**************************************************************************
TABLE FILE HTL1
SUM NEW_TOTAL_AMT AS 'HTLVOL'
    NNROOMS AS 'HTLDAY'
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLMREC
END
-RUN
 
TABLE FILE HTLMREC
SUM HTLVOL HTLDAY
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS HTLMREC1
END
-RUN
 
TABLE FILE HTLMREC1
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIERC
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/23 HTLVOL/12 HTLDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE HTLDAY HTLVOL
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
 
-**********************************************************************
-* Non Matching ALL OTHER Records
-**********************************************************************
TABLE FILE HTL1
SUM NEW_TOTAL_AMT AS 'HTLVOL'
    NNROOMS AS 'HTLDAY'
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLNREC
END
-RUN
 
DEFINE FILE HTLNREC
LVL_PRINT/A23 = 'ALL OTHER';
END
-RUN
TABLE FILE HTLNREC
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE HOLD AS HTLNREC1
END
-RUN
 
TABLE FILE HTLNREC1
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIERC
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/23 HTLVOL/12 HTLDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE HTLDAY HTLVOL
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
 
DEFINE FILE QRHIERC
RANK_VALUEA/A9 = 'TOTAL';
LVL_PRINTA/A23 = 'TOTAL';
END
-RUN
 
TABLE FILE QRHIERC
SUM CAVOL/P12.2M
  PCT.CAVOL/P4 AS 'PCTVOL'
  CNTKT
  CLSAV/P12.2M
  CMKT/P12
  CC02/D12.2
  CMILES/P12
COMPUTE CTLS/P12 =  IF CAVOL EQ 0 THEN 0 ELSE ((CLSAV/CAVOL)*100);
COMPUTE CAVGT/P12.2M = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE CAVOL/CNTKT;
COMPUTE PAVGT/P12.2M = IF ((PNTKT LT 1) AND (PNTKT GT (-1))) THEN 0 ELSE PAVOL/PNTKT; NOPRINT
COMPUTE AAVGV/P12.2M = CAVGT - PAVGT; NOPRINT
COMPUTE AAVGV1/P12 = IF PAVGT EQ 0 THEN 0 ELSE (((CAVGT-PAVGT)/PAVGT)*100);
COMPUTE CADVP1/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT/CNTKT)*100);
COMPUTE CADVP2/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT1/CNTKT)*100);
COMPUTE CONLP/P12  = IF CNTKTD EQ 0 THEN 0 ELSE ((CONL/CNTKTD)*100);
COMPUTE CNRP/P12  = IF CNTKT EQ 0 THEN 0 ELSE ((CNR/CNTKT)*100);
COMPUTE CPMILE/P10.2 = IF CMILES EQ 0 THEN 0 ELSE (CAVOL/CMILES);
COMPUTE CARAR/P12.2M = IF ((CARDAY LT 1) AND (CARDAY GT (-1))) THEN 0 ELSE CARVOL/CARDAY;
COMPUTE HTLARR/P12.2M = IF ((HTLDAY LT 1) AND (HTLDAY GT (-1))) THEN 0 ELSE HTLVOL/HTLDAY;
COMPUTE TACH/P12.2M = CAVOL + CARVOL + HTLVOL;
BY RANK_VALUEA AS 'RANK_VALUE'
-* BY LVL_PRINTA AS 'LVL_PRINT'
BY LVL_PRINTA
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS TTL1
END
-RUN
 
TABLE FILE TTL1
SUM CAVOL/P12.2M PCTVOL CNTKT/P12 CLSAV/P12 CTLS/P12 CAVGT/P12 AAVGV1/P12
    CADVP1/P12 CADVP2/P12 CONLP/P12 CNRP/P12 CPMILE/P10.2 CARAR/P12 HTLARR/P12 TACH/P12 CMKT/P12 CC02/D12.2 CMILES/P12
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TTL2
END
-RUN
 
 
 
TABLE FILE TTL2
PRINT CAVOL PCTVOL CNTKT CLSAV CTLS CAVGT AAVGV1 CADVP1 CADVP2
      CONLP CNRP CPMILE CARAR HTLARR TACH CMKT CC02 CMILES
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TTL3 FORMAT FOCUS
END
-RUN
 
 
TABLE FILE QRHIERC
SUM CAVOL/P12.2M
  PCT.CAVOL/P4 AS 'PCTVOL'
  CNTKT
  CLSAV/P12.2M
  CMKT
  CC02/D12.2
  CMILES
COMPUTE CTLS/P12 =  IF CAVOL EQ 0 THEN 0 ELSE ((CLSAV/CAVOL)*100);
COMPUTE CAVGT/P12.2M = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE CAVOL/CNTKT;
COMPUTE PAVGT/P12.2M = IF ((PNTKT LT 1) AND (PNTKT GT (-1))) THEN 0 ELSE PAVOL/PNTKT; NOPRINT
COMPUTE AAVGV/P12.2M = CAVGT - PAVGT; NOPRINT
COMPUTE AAVGV1/P12 = IF PAVGT EQ 0 THEN 0 ELSE (((CAVGT-PAVGT)/PAVGT)*100);
COMPUTE CADVP1/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT/CNTKT)*100);
COMPUTE CADVP2/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT1/CNTKT)*100);
COMPUTE CONLP/P12  = IF CNTKTD EQ 0 THEN 0 ELSE ((CONL/CNTKTD)*100);
COMPUTE CNRP/P12  = IF CNTKT EQ 0 THEN 0 ELSE ((CNR/CNTKT)*100);
COMPUTE CPMILE/P10.2 = IF CMILES EQ 0 THEN 0 ELSE (CAVOL/CMILES);
COMPUTE CARAR/P12.2M = IF ((CARDAY LT 1) AND (CARDAY GT (-1))) THEN 0 ELSE CARVOL/CARDAY;
COMPUTE HTLARR/P12.2M = IF ((HTLDAY LT 1) AND (HTLDAY GT (-1))) THEN 0 ELSE HTLVOL/HTLDAY;
COMPUTE TACH/P12.2M = CAVOL + CARVOL + HTLVOL;
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV1
END
-RUN
 
TABLE FILE TLEV1
PRINT CAVOL/P12 PCTVOL/P4 CNTKT/P12 CLSAV/P12 CTLS/P12 CAVGT/P12 AAVGV1/P12 CADVP1/P12 CADVP2/P12
      CONLP/P12 CNRP/P12 CPMILE/P10.2 CARAR/P12 HTLARR/P12 TACH/P12 CMKT/P12 CC02/D12.2 CMILES/P12
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV2
END
-RUN
 
 
TABLE FILE TLEV2
PRINT CAVOL PCTVOL CNTKT CLSAV CTLS CAVGT AAVGV1 CADVP1 CADVP2
      CONLP CNRP CPMILE CARAR HTLARR TACH CMKT CC02 CMILES
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV3 FORMAT FOCUS
END
-RUN
 
 
-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &PHOLD1=&HLDPATH || 'TLEV3.FOC';
-SET &PHOLD2=&HLDPATH || 'TTL3.FOC';
-RUN
 
USE CLEAR *
-RUN
 
EX SUUSES
-RUN
 
USE ADD
&PHOLD1 AS TLEV3
&PHOLD2 AS TLEV3
END
-RUN
 
DEFINE FILE TLEV3
RANK_VAL1/A9 = IF RANK_VALUE EQ 'TOTAL' THEN 'ZZ' ELSE RANK_VALUE;
END
-RUN
 
TABLE FILE TLEV3
SUM CAVOL PCTVOL CNTKT CLSAV CTLS CAVGT AAVGV1 CADVP1 CADVP2
    CONLP CNRP CPMILE CARAR HTLARR TACH CMKT CC02 CMILES
BY RANK_VAL1
BY LVL_PRINT
ON TABLE HOLD AS TLEV4
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
JOIN ROLL WITH RANK_VAL1 IN TLEV4 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN
 
DEFINE FILE TLEV4
NOWTOD/A8 WITH RANK_VAL1 = HHMMSS (NOWTOD);
-SET &POS = 20;
-SET &POS1 = &POS - 3;
-SET &POS2 = &POS1 + 7;
-SET &POS3 = &POS2 + 5 ;
-SET &POS4 = &POS3 + 5;
-* -SET &POS4 = &POS3 + 5;
-* -SET &POS5 = &POS4 + 5 ;
-SET &POS5 = &POS4 + 6 ;
-SET &POS6 = &POS5 + 6;
-SET &POS7 = &POS6 + 5 ;
-SET &POS8 = &POS7 + 6;
-SET &POS9 = &POS8 + 6;
-SET &POS10 = &POS9 + 5;
-SET &POS11 = &POS10 + 5;
-SET &POS12 = &POS11 + 4;
-SET &POS13 = &POS12 + 4;
-SET &POS14 = &POS13 + 7;
-SET &POS15 = &POS14 + 5;
-SET &POS16 = &POS15 + 6;
-SET &POS17 = &POS16 + 4;
-SET &POS18 = &POS17 + 5;
ROLL/A8='&&ROLLUP.EVAL';
RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
RN1/A60=TRIM('B', '&&RPT_TTL2.EVAL', 60, ' ', 1, 'A60');
TDATE1/A8MDYY='&MDYY';
TDATE2/A10=EDIT(TDATE1,'99/99/9999');
TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
RD/A10 = 'Run Date:';
RT/A10 = 'Run Time:';
DTDESC/A20='Transaction Dates';
CUR/A15='Current Period:';
PRI/A15='Prior Period:';
SPACE/A1 = 'A';
END
 
 
 
 
TABLE FILE TLEV4
HEADING
-IF '&&OUTFMT.EVAL' NE 'PDF' GOTO JEKK;
-*"</2"
"<86 &&DTDESC"
"<42 <RN <83 <CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"<42 <RN1 <83 <PRI  &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
"<&&CSLP &&ROLLNME <+0> &&SP <+0> &&ROLLCS <+0> &&SP3 <+0> &&ROLLCNCT <+0> &&SP4 <+0> &&ROLLPHNEML"
-GOTO PDcont;
-JEKK;
-*"</4"
"&&ROLLNME &&SP1 &&ROLLCS11 &&SP3 &&RCNCT1 &&SP4 &&ROLLPHNEML"
"&&DTDESC  Period1: &FMM/&FMD/&FMY - &TOM/&TOD/&TOY  Period2: &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
-*" "
"<35 &&RPT_TTL1"
"<35 &&RPT_TTL2"
" "
-PDcont;
SUM CAVOL/P12M AS 'Total,Volume' IN &POS1.EVAL
    PCTVOL/P4% AS '% of,Total,Volume' IN &POS2.EVAL
    CNTKT/P12C AS 'Net,Tickets' IN &POS3.EVAL
    CLSAV/P12M AS 'Total,Lost,Savings' IN &POS4.EVAL
    CTLS/P4% AS '% of,LS to,Air,Volume' IN &POS5.EVAL
    CAVGT/D12M AS 'Avg,Ticket,Price' IN &POS6.EVAL
    AAVGV1/P4% AS 'Avg,Ticket,Price,Var %' IN &POS7.EVAL
    CADVP1/P4% AS '% Of,Net Tickets,Purchased,0-6 Days' IN &POS8.EVAL
    CADVP2/P4% AS '% of,Net Tickets,Purchased,7-13 Days' IN &POS9.EVAL
    CONLP/P4% AS '% Dom,Online' IN &POS10.EVAL
    CNRP/P4% AS '% of,NR' IN &POS11.EVAL
    CPMILE AS 'CPM' IN &POS12.EVAL
    CMILES/P12C AS 'Total,Miles' IN &POS13.EVAL
    CMKT/P12C AS 'Total, Nbr of,Segments' IN &POS14.EVAL
    CC02/D12.2 AS 'Tons,of CO2,Emissions' IN &POS15.EVAL
    CARAR/P12M AS 'Avg,Car,Rate' IN &POS16.EVAL
    HTLARR/P12M AS 'Avg,Hotel,Cost Per,Night' IN &POS17.EVAL
    TACH/P12M AS 'Total Air,Car &,Hotel,Spend' IN &POS18.EVAL
BY RANK_VAL1 NOPRINT
BY LVL_PRINT AS 'HIERARCHY'
ON TABLE SET PAGE-NUM OFF
 
-* ON LVL_PRINT SKIP-LINE
&&OUTLINE1
&&OUTLINE2

-*
-* FTR_DateTimePosit sets the position of the Post-trip date/time stamp text within the footer image.
-* The default line number (&FTR_DateTimeLine) in the FOOTING for the date/time stamp text is: 4.
-INCLUDE FTR_DateTimePosit
-INCLUDE FOOTERSM
-*
 
ON TABLE PAGE-BREAK AND SUBFOOT
-* " "
-INCLUDE SBFOOT
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.050000, RIGHTMARGIN=0.050000,
    TOPMARGIN=0.00000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
TYPE=REPORT, FONT='TIMES NEW ROMAN', SIZE=7, STYLE=NORMAL, BACKCOLOR=NONE, $
 
-IF '&&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO NOB;

TYPE=DATA,
     GRID=OFF,
     SIZE=7,
     STYLE=NORMAL,
     BACKCOLOR=('WHITE' RGB(239 239 239)),
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR=RGB(5 76 149),
     BORDER-BOTTOM-COLOR=RGB(5 76 149),
     BORDER-LEFT-COLOR=RGB(5 76 149),
     BORDER-RIGHT-COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
     WHEN=N1 NE 'ZZ',
$
 
 
TYPE=HEADING,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$
 
TYPE=HEADING,
     SIZE=9,
     LINE=2,
     ITEM=1,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$
 
TYPE=HEADING,
     SIZE=9,
     LINE=3,
     ITEM=1,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$
TYPE=TITLE,
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
     GRID=ON,
     SIZE=7,
     COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
$
-GOTO PDFFMT
 
-NOB
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.300000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=4, COLSPAN=19, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=19, SIZE=9,JUSTIFY=CENTER, $
-* TYPE=HEADING, LINE=5, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     BACKCOLOR=(RGB(242 242 242) 'WHITE' ),$
-JOY
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$
 
TYPE=TITLE, JUSTIFY=LEFT, $
 
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=FOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=SUBHEAD,
     STYLE=BOLD,
$
 
TYPE=SUBTOTAL,
     STYLE=BOLD,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $
 
TYPE=GRANDTOTAL, STYLE=BOLD, $
 
TYPE=REPORT, WRAP=1, JUSTIFY=LEFT, $
TYPE=REPORT, WRAP=2, JUSTIFY=LEFT, COLUMN=N2, $
TYPE=DATA, BACKCOLOR=NONE, GRID=OFF, SIZE=8,STYLE=BOLD, JUSTIFY=LEFT, WHEN=N1 EQ 'ZZ',$
-* TYPE=FOOTING, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=BOLD, COLOR='WHITE', $
-* TYPE=HEADING, SIZE=10, FONT=TIMES NEW ROMAN, STYLE=BOLD, $
-GOTO XSTY
 
-PDFFMT
TYPE=DATA,BACKCOLOR=NONE, GRID=OFF, JUSTIFY=LEFT, STYLE=BOLD, SIZE=8, WHEN=N1 EQ 'ZZ',$
-* SIZE=8, STYLE=BOLD, FONT='TIMES NEW ROMAN', JUSTIFY=CENTER, WHEN=N1 EQ 'ZZ',$
TYPE=SUBTOTAL, STYLE=BOLD, $
TYPE=FOOTING, SIZE=7, FONT='ARIAL', COLOR=RGB(70 136 177), $
TYPE=TABFOOTING, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=BOLD, COLOR=RGB(70 136 177), $
 
TYPE=REPORT, IMAGE=NEW_HDR.jpg, POSITION=(0 0.2), SIZE=(2.8 .37),PRESERVERATIO=ON, $ 
TYPE=REPORT, IMAGE=NEW_FTR.jpg, POSITION=(0 8.0), SIZE=(10.5 .58), PRESERVERATIO=ON, $     
-*
TYPE=FOOTING, LINE=&FTR_DateTimeLine, STYLE=BOLD,POSITION=(&FTR_DatePosit),$ 
-*
-XSTY
ENDSTYLE
END
-RUN
 
SET SPACE = 2
 
-GOTO XXIT;
 
-NOREC
-IF &&FROMAVRP NE 'Y' GOTO :AVSF2;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF2

-INCLUDE NORECRPT
-RUN
-EXIT
 
 
 
-XXIT;
 
