-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File AVIW25SM.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-*1/31/02      TANDT-JK             ADDED QUOTES AROUND TMRK IN THE
-*                                  FIRST HOLD FILE 
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*12/12/03   JK                 CARRIED THROUGH ORG_DST
-*4/10/14    JK                 ADDED LEVEL1
-*4/11/14    JK                 ADDED LOGIC TO COMPUTE % NET TICKETS
-* 07/25/16   RSB   S-20630 - New Employee ID database field
-*                            Added EMP_ID 
-**********************************************************************

-IF &&ROLLUP EQ 'CERN-R' GOTO CERNSET;

-SET &&RANK_LIMIT=100;
-GOTO NOCERNSET;

-CERNSET;
-SET &&RANK_LIMIT=9999;
-GOTO NOCERNSET;

-NOCERNSET;

SET ASNAMES=ON
-RUN

TABLE FILE &&RPT_HOLD
SUM FARE_PAID 
    MRK_TKT_CNT
    NET_TKT_CNT
    NEW_SEG_MILES
BY PASSNGR_NAME
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE &&RPT_HOLD
PRINT AIRLINE
      FARE_PAID
      EMP_ID
      LEVEL1
      LEVEL3
      MRK_TKT_CNT
      NET_TKT_CNT
      NEW_SEG_MILES
BY PASSNGR_NAME 
ON TABLE HOLD AS HOLD5
END
-RUN

TABLE FILE HOLD1
SUM   NEW_SEG_MILES
      MRK_TKT_CNT
      NET_TKT_CNT
      FARE_PAID
RANKED BY &&RANK_ORDER &&RANK_METH NOPRINT
BY PASSNGR_NAME
ON TABLE HOLD AS HOLD2
END
-RUN

DEFINE FILE HOLD2
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = (LAST LIST + 1);
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_LIMIT/I5 = &&RANK_LIMIT;
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE 
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE 
                  CRANK;
  
END
-RUN

MATCH FILE HOLD5
PRINT AIRLINE
      FARE_PAID
      EMP_ID
      LEVEL1
      LEVEL3
      MRK_TKT_CNT
      NET_TKT_CNT
      NEW_SEG_MILES
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE HOLD2
SUM NEW_SEG_MILES
    RANK_VALUE
    FARE_PAID
    MRK_TKT_CNT
    NET_TKT_CNT
BY PASSNGR_NAME
AFTER MATCH HOLD AS HOLD3 OLD-AND-NEW
END
-RUN

DEFINE FILE HOLD3
FLAG/A1 = 'A';
END
-RUN
TABLE FILE HOLD3
SUM NET_TKT_CNT MRK_TKT_CNT FARE_PAID NEW_SEG_MILES EMP_ID
BY FLAG
BY RANK_VALUE
BY PASSNGR_NAME
BY LEVEL1
BY AIRLINE
ON TABLE HOLD AS HOLD4A
END
-RUN

TABLE FILE HOLD4A
SUM NET_TKT_CNT/P8CS AS 'TNETT'
BY PASSNGR_NAME
ON TABLE HOLD AS HOLDT
END
-RUN

MATCH FILE HOLD4A
PRINT NET_TKT_CNT MRK_TKT_CNT FARE_PAID NEW_SEG_MILES RANK_VALUE LEVEL1 AIRLINE EMP_ID
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE HOLDT
SUM TNETT
BY PASSNGR_NAME
AFTER MATCH HOLD AS HOLD4 OLD-OR-NEW
END
-RUN


-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-**********************************************************************

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN

DEFINE FILE HOLD4
NOWTOD/A8 WITH AIRLINE = HHMMSS (NOWTOD);
PCTN/D8C% = IF TNETT EQ 0 THEN 0 ELSE NET_TKT_CNT/TNETT*100;
END
TABLE FILE HOLD4
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10

-INCLUDE FOOTERSM
ON TABLE COLUMN-TOTAL
-*ON TABLE SET STYLE &&SMSTY
-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**********************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
-INCLUDE &&PASTYS

-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-**********************************************************************
-IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;
-* -INCLUDE &&DSTY

-SKIP_DRILL

ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;

EX REPORT_3

-SKIP_R3
