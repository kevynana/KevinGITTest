                                                                                
-* File CHK_H50.FEX                                                             
-*                                                                              
-*****   This subroutine checks the stability of the ROLLUP Hierarchy file -    
-*****     if okay, Y is returned - if the Hierarchy needs rebuilding or        
-*****     re-tagging is necessary, N is returned.                              
-*****     Called from RPT_SET.                                                 
-*****   AUTHOR:  Dave Kuzanek                                                  
-*****   PRODUCTION DATE:                                                       
-***************************************************************************    
-* *************                                                                
-*  DATE                       NAME                         DESCRIPTION         
-* 08/29/00                  IBISTL-SB          CONVERTED THE ROLL_KEY FROM     
-* A VARIABLE                                                                   
-*                                              LENGTH TO FIXED LENGTH OF 50    
-*  CHARACTERS                                                                  
-*                                                                              
-* 09/14/00                  IBISTL-RJ          DELETED CODE FOR SHOW FORM1     
-***************************************************************************    
-* *************                                                                
MAINTAIN FILES ROLLUP AND CONTROL AND HIER50 FROM Hier_OK INTO Hier_OK          
                                                                                
COMPUTE Hier_OK/A1 = ;                                                          
COMPUTE Cnt1/I5 = ;                                                             
COMPUTE Eff_Hold/YYMD = ;                                                       
                                                                                
-***************************************************************************    
-* *************                                                                
-*  DATE                       NAME                         DESCRIPTION         
-* 08/29/00                  IBISTL-SB          CONVERTED THE ROLL_KEY FROM     
-* A VARIABLE                                                                   
-*                                              LENGTH TO FIXED LENGTH OF 50    
-*  CHARACTERS                                                                  
-*                                                                              
-***************************************************************************    
-* *************                                                                
INFER HIER50.ROLL_LEV INTO H_Temp                                               
INFER ROLLUP.ROLLUP_CODE INTO RStack                                            
INFER CONTROL.CONTROL_ID INTO Cntl_Stack                                        
                                                                                
REPOSITION CONTROL.CONTROL_ID                                                   
FOR ALL NEXT CONTROL.CONTROL_ID INTO Cntl_Stack                                 
                                                                                
REPOSITION ROLLUP.ROLLUP_CODE                                                   
NEXT ROLLUP.ROLLUP_CODE INTO RStack WHERE                                       
  ROLLUP.ROLLUP_CODE EQ Cntl_Stack(1).ROLLUP_CODE                               
                                                                                
REPOSITION HIER50.ROLL_LEV                                                      
STACK CLEAR H_Temp                                                              
FOR ALL NEXT HIER50.ROLL_LEV INTO H_Temp                                        
PERFORM Check_Status                                                            
GOTO END                                                                        
                                                                                
CASE Check_Status                                                               
  COMPUTE Hier_OK = 'Y';                                                        
  COMPUTE Cnt1 = 1;                                                             
  REPEAT WHILE Cnt1 LE H_Temp.FocCount                                          
    IF H_Temp(Cnt1).ROLL_KEY EQ                                                 
      '00000000000000000000000000000000000000000000000000' THEN                 
      COMPUTE Hier_OK = 'N';                                                    
    IF H_Temp(Cnt1).OLD_KEY NE H_Temp(Cnt1).ROLL_KEY                            
      THEN COMPUTE Hier_OK = 'N';                                               
    IF H_Temp(Cnt1).EFF_DATE GT Eff_Hold THEN                                   
      COMPUTE Eff_Hold = H_Temp(Cnt1).EFF_DATE;                                 
    COMPUTE Cnt1 = Cnt1 + 1;                                                    
  ENDREPEAT                                                                     
  IF Eff_Hold GT RStack(1).INST_EFF THEN COMPUTE Hier_OK = 'N';                 
endcase                                                                         
                                                                                
                                                                                
END                                                                             
