-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**


-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-* File DRMX1HR.FEX
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*----           ----                 ---------------------
-*8/31/00        SAC                   Initial Creation
-*11/25/03   STEVE              Added -INCLUDE RPTPARMS
-*7/26/04    JOY                Added/Changed Norecords logic
-*04/29/10   DEB      CHANGED RATE_GRP1  FOR ROUNDING
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

-*****************************************************************  
-*************************************************************************
-DEFAULT &&CPFLAG ='XX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';

-SET &&DTSTY = 'DT';

-SET &&FIELD = &ROW_LABEL;
-SET &&CPFLAG = &PARM1; 

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 11/03/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
-*
EX RESET
-RUN
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT

-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX SETMXTP SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   

-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-*  CY (CURRENT YEAR), PY (PRIOR YEAR)
-****

-FIELDS
-IF &&FIELD EQ '0-149' THEN GOTO HTLRATE
-  ELSE IF &&FIELD EQ '150-199' THEN GOTO HTLRATE1
-  ELSE IF &&FIELD EQ '200-249' THEN GOTO HTLRATE2
-  ELSE IF &&FIELD EQ '250+' THEN GOTO HTLRATE3
-  ELSE GOTO XXIT;

-HTLRATE
-SET &&RPT_TTL1 = '                    HOTEL SUMMARY REPORT';
-SET &&RTECHK = 'WHERE RATE_GRP1 EQ ''0-149''';
-SET &&SUBHEAD = '$0-$149';
-GOTO OUTSIDE

-HTLRATE1
-SET &&RPT_TTL1 = '                    HOTEL SUMMARY REPORT';
-SET &&RTECHK = 'WHERE RATE_GRP1 EQ ''150-199''';
-SET &&SUBHEAD = '$150-$199';
-GOTO OUTSIDE

-HTLRATE2
-SET &&RPT_TTL1 = '                    HOTEL SUMMARY REPORT';
-SET &&RTECHK = 'WHERE RATE_GRP1 EQ ''200-249''';
-SET &&SUBHEAD = '$200-$249';
-GOTO OUTSIDE

-HTLRATE3
-SET &&RPT_TTL1 = '                    HOTEL SUMMARY REPORT';
-SET &&RTECHK = 'WHERE RATE_GRP1 EQ ''250+''';
-SET &&SUBHEAD = '$250+';
-GOTO OUTSIDE

-OUTSIDE

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMM2
-RUN

-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN

EX HTLUSE
-RUN

-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
-*RATE_GRP1/A10 = IF ROOM_RATE GE 0 AND ROOM_RATE LT 150 OR
-*                   ROOM_RATE LE 0 AND ROOM_RATE GT (-150)
-*                   THEN '0-149' ELSE 
-*                IF  ROOM_RATE GE 150 AND ROOM_RATE LE 199 OR
-*                   ROOM_RATE LE (-150) AND ROOM_RATE GE (-199)
-*                   THEN '150-199' ELSE 
-*                IF  ROOM_RATE GE 200 AND ROOM_RATE LE 249 OR
-*                   ROOM_RATE LE (-200) AND ROOM_RATE GE (-249)
-*                   THEN '200-249' ELSE '250+';
RATE_GRP1/A10 = IF ((ROOM_RATE GE 0 AND ROOM_RATE LT 149.50) OR
                      (ROOM_RATE LE 0 AND ROOM_RATE GT (-149.50)))
                   THEN '0-149' ELSE
                   IF ((ROOM_RATE GE 149.50 AND ROOM_RATE LT 199.50) OR
                      (ROOM_RATE LE (-149.50) AND ROOM_RATE GT (-199.50)))
                   THEN '150-199' ELSE
                   IF ((ROOM_RATE GE 199.50 AND ROOM_RATE LT 249.50) OR
                      (ROOM_RATE LE (-199.50) AND ROOM_RATE GT (-249.50)))
                    THEN '200-249' ELSE '250+';



CY/A1 = IF (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) 
            THEN 'Y' ELSE 'N';
PY/A1 = IF (INVOICE_DATE GE &&FXDATE.EVAL) AND (INVOICE_DATE LE &&TXDATE.EVAL) 
            THEN 'Y' ELSE 'N';
FLAG/A10=IF CY EQ 'Y' THEN 'CURRENT' ELSE 'PRIOR';
CITY_ST/A35 = PROP_CITY||(', '| PROP_STATE);
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
NBR_DAYS/D12 = NUM_DAYS;
NBR_ROOMS/D12 = NUM_ROOMS;
NEW_ROOM_RATE/D12.2C = ROOM_RATE;
NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;

NNROOMS/D12 = IF (NBR_DAYS LT 0) AND
                 (NBR_ROOMS LT 0) THEN
                 (NBR_DAYS * NBR_ROOMS) * (-1) ELSE
                 (NBR_DAYS * NBR_ROOMS);

OUT_DATE/MDYY = CHKOUT_DATE;
END
-RUN

TABLEF FILE &&EXTRACT
PRINT RATE_GRP1
  CITY_ST  
  CHAIN_NAME
  HTL_KEY     
  IN_DATE 	
  INV_DT      
  INVOICE_DATE 	
  NBR_DAYS    
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT       
  NNROOMS	
  OUT_DATE    
  PASSNGR_NAME 	
  PROP_NAME 
  REFUSE_KEY
      
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
WHERE REFUSE_CODE NE 'X' OR 'S' OR 'N' OR 'M'
&&WHERE1
&&WHERE2
&&WHERE3

-INCLUDE RPTPARMS
WHERE (INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL)
ON TABLE HOLD AS RPTHOLD
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

-IF &&CPFLAG EQ 'CY' THEN GOTO CURRNK ELSE GOTO PRRNK;
-RUN

-* CREATE CURRENT TOP LIST

-CURRNK

DEFINE FILE RPTHOLD 
CY/A1 = IF (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) THEN
           'Y' ELSE 'N';
FLAG/A10 = IF CY EQ 'Y' THEN 'CURRENT' ELSE 'PRIOR';
END

TABLE FILE RPTHOLD
SUM NNROOMS NEW_TOTAL_AMT
BY PASSNGR_NAME
WHERE FLAG EQ 'CURRENT'
ON TABLE HOLD AS CURRTE1
END
-RUN

TABLE FILE CURRTE1
SUM NNROOMS NEW_TOTAL_AMT
RANKED BY HIGHEST NNROOMS NOPRINT 
BY PASSNGR_NAME
ON TABLE HOLD AS CURRTE2 
END
-RUN

DEFINE FILE CURRTE2
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  LIST/D9 = (LAST LIST + 1);
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF LIST GT &&RANK_LIMIT THEN 'OTHER' ELSE CRANK;
  RANK_LABEL/A33 = IF RANK_VALUE NE 'OTHER' THEN PASSNGR_NAME
                      ELSE 'ALL OTHER'; 
END

TABLE FILE CURRTE2
SUM NNROOMS NEW_TOTAL_AMT
BY RANK_VALUE
BY RANK_LABEL
ON TABLE HOLD AS CURRTE3 
END
-RUN

TABLE FILE CURRTE3
SUM NNROOMS NEW_TOTAL_AMT
BY RANK_VALUE
BY RANK_LABEL AS 'PASSNGR_NAME'
WHERE RANK_LABEL NE 'ALL OTHER'
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS CURRTE4 
END
-RUN

TABLE FILE RPTHOLD
SUM   NBR_ROOMS
      NNROOMS
      NEW_TOTAL_AMT
BY PASSNGR_NAME
BY RATE_GRP1
BY CHAIN_NAME
BY CITY_ST
BY FLAG
BY IN_DATE
BY INVOICE_DATE
BY OUT_DATE
BY PROP_NAME
WHERE FLAG EQ 'CURRENT'
ON TABLE HOLD AS HTLHOLD1 
END
-RUN

MATCH FILE HTLHOLD1
PRINT RATE_GRP1
      CHAIN_NAME
      CITY_ST
      FLAG
      IN_DATE
      INVOICE_DATE
      NBR_ROOMS
      NNROOMS
      NEW_TOTAL_AMT
      OUT_DATE
      PROP_NAME
BY PASSNGR_NAME
ON TABLE HOLD
RUN 
FILE CURRTE4
SUM NNROOMS NEW_TOTAL_AMT
BY PASSNGR_NAME
AFTER MATCH HOLD AS CURHTLM1 NEW
END
-RUN

-TYPE After Extract
DEFINE FILE CURHTLM1
  NOWTOD/A8 WITH NNROOMS = HHMMSS (NOWTOD);
END

TABLE FILE CURHTLM1
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
NNROOMS AS 'NUMBER,NIGHTS'
NBR_ROOMS AS 'NUMBER,ROOMS'
NEW_TOTAL_AMT AS 'TOTAL,AMOUNT'
COMPUTE AVGDR/D12.2CS = IF NEW_TOTAL_AMT LT 0 AND NNROOMS LT 0
 THEN ((NEW_TOTAL_AMT/NNROOMS) * (-1)) 
 ELSE NEW_TOTAL_AMT/NNROOMS; AS 'AVERAGE,DAILY,RATE'

-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY PROP_NAME AS 'HOTEL'
BY CITY_ST AS 'CITY/STATE'
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
&&RTECHK
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME RECOMPUTE AS 'TOTAL FOR'
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
"THIS REPORT INCLUDES ONLY TOP NUMBER OF SPECIFIED TRAVELERS"
"&&FOOTER"
"eTTek Review &DATE AT <NOWTOD <90 &&FOOTR"

-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-GOTO ENDIT;

-PRRNK

DEFINE FILE RPTHOLD 
CY/A1 = IF (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) THEN
           'Y' ELSE 'N';
FLAG/A10 = IF CY EQ 'Y' THEN 'CURRENT' ELSE 'PRIOR';
END

TABLE FILE RPTHOLD
SUM NNROOMS NEW_TOTAL_AMT
BY PASSNGR_NAME
WHERE FLAG EQ 'PRIOR'
ON TABLE HOLD AS PRRTE1
END
-RUN

TABLE FILE PRRTE1
SUM NNROOMS NEW_TOTAL_AMT
RANKED BY HIGHEST NNROOMS NOPRINT 
BY PASSNGR_NAME
ON TABLE HOLD AS PRRTE2 
END
-RUN

DEFINE FILE PRRTE2
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  LIST/D9 = (LAST LIST + 1);
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF LIST GT &&RANK_LIMIT THEN 'OTHER' ELSE CRANK;
  RANK_LABEL/A33 = IF RANK_VALUE NE 'OTHER' THEN PASSNGR_NAME
                      ELSE 'ALL OTHER'; 
END

TABLE FILE PRRTE2
SUM NNROOMS NEW_TOTAL_AMT
BY RANK_VALUE
BY RANK_LABEL
ON TABLE HOLD AS PRRTE3 
END
-RUN

TABLE FILE PRRTE3
SUM NNROOMS NEW_TOTAL_AMT
BY RANK_VALUE
BY RANK_LABEL AS 'PASSNGR_NAME'
WHERE RANK_LABEL NE 'ALL OTHER'
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS PRRTE4 
END
-RUN

TABLE FILE RPTHOLD
SUM   NBR_ROOMS
      NNROOMS
      NEW_TOTAL_AMT
BY PASSNGR_NAME
BY RATE_GRP1
BY CHAIN_NAME
BY CITY_ST
BY FLAG
BY IN_DATE
BY INVOICE_DATE
BY OUT_DATE
BY PROP_NAME
WHERE FLAG EQ 'PRIOR'
ON TABLE HOLD AS HTLHOLD1 
END
-RUN

MATCH FILE HTLHOLD1
PRINT RATE_GRP1
      CHAIN_NAME
      CITY_ST
      FLAG
      IN_DATE
      INVOICE_DATE
      NBR_ROOMS
      NNROOMS
      NEW_TOTAL_AMT
      OUT_DATE
      PROP_NAME
BY PASSNGR_NAME
ON TABLE HOLD
RUN 
FILE PRRTE4
SUM NNROOMS NEW_TOTAL_AMT
BY PASSNGR_NAME
AFTER MATCH HOLD AS PRHTLM1 NEW
END
-RUN

-TYPE After Extract
DEFINE FILE PRHTLM1
  NOWTOD/A8 WITH NNROOMS = HHMMSS (NOWTOD);
END

TABLE FILE PRHTLM1
-INCLUDE HEADMX
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
NNROOMS AS 'NUMBER,NIGHTS'
NBR_ROOMS AS 'NUMBER,ROOMS'
NEW_TOTAL_AMT AS 'TOTAL,AMOUNT'
COMPUTE AVGDR/D12.2CS = IF NEW_TOTAL_AMT LT 0 AND NNROOMS LT 0
 THEN ((NEW_TOTAL_AMT/NNROOMS) * (-1)) 
 ELSE NEW_TOTAL_AMT/NNROOMS; AS 'AVERAGE,DAILY,RATE'

-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY PROP_NAME AS 'HOTEL'
BY CITY_ST AS 'CITY/STATE'
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
&&RTECHK
-INCLUDE RPTPARMS
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME RECOMPUTE AS 'TOTAL FOR'
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
"&&FOOTER"
"eTTek Review &DATE AT <NOWTOD <90 &&FOOTR"

-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-GOTO ENDIT;


-XXIT
-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO ENDIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO ENDIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END


-ENDIT

