-* File multidrill5.fex
-*5/23/14    DEB                ADDED EX5 TO RADV_PURCH DEFINE


EX USESTEVE
END
-RUN

DEFINE FILE SR_50
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
LOST_SAVINGS/D12.2 = FARE_PAID - SEG_LOWEST;
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
NEW_ADV_PURCH/D9    = ADV_PURCH;
REC_CNT/D8CS = IF NET_TKT_CNT EQ 1 THEN 1 ELSE 0;
TTLDAYS/D8 = NEW_ADV_PURCH*REC_CNT;
SAVINGS/D12.2 = SEG_STANDARD - FARE_PAID;
TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
END

TABLE FILE SR_50
SUM
     NET_TKT_CNT AS 'TKT,CNT'
     FARE_PAID AS 'FARE,PAID'
     SAVINGS AS 'SAVINGS'
     LOST_SAVINGS AS 'LOST,SAVINGS'
     NEW_ADV_PURCH
     TTLDAYS
     REC_CNT
BY   TKT_SORT 
BY   AROLL_LEV4 
BY   AROLL_LEV5 AS 'DEPARTMENT'
BY   AROLL_DSC5 AS ' '
BY   EMBARK
BY   TKT_TYPE
BY   SEG_NBR
WHERE VOID_DATE EQ 0;
WHERE TRN_DATE GE '20051001';
WHERE TRN_DATE LE '20051031';
ON TABLE HOLD
END
-RUN

DEFINE FILE HOLD
RADV_PURCH/I8CS = IF (EMBARK EQ 'FP2' OR 'EX1' OR 'RF1' OR 'EX5') THEN 0 ELSE
                  IF (TKT_TYPE EQ 'P-RF' OR 'P-EX') THEN 0 ELSE 
                  IF (SEG_NBR EQ '00') THEN 0 ELSE         
                 IF (TKT_SORT NE LAST TKT_SORT) AND 
                    (FARE_PAID GT 0) THEN NEW_ADV_PURCH ELSE
                 IF (TKT_SORT NE LAST TKT_SORT) AND
                    (FARE_PAID LT 0) THEN NEW_ADV_PURCH * (-1) ELSE 0;
END

TABLE FILE HOLD
SUM
     NET_TKT_CNT AS 'TKT,CNT'
     FARE_PAID AS 'FARE,PAID'
     SAVINGS AS 'SAVINGS'
     LOST_SAVINGS AS 'LOST,SAVINGS'
     AND COMPUTE AVGT/P8.2 = FARE_PAID/NET_TKT_CNT; AS 'AVERAGE,TICKET,AMOUNT'
     AND COMPUTE AVGA/P8 = TTLDAYS/REC_CNT; AS 'AVERAGE,ADVANCE,PURCHASE'
BY   AROLL_LEV4 NOPRINT
BY   AROLL_LEV5 AS 'DEPARTMENT'
BY   AROLL_DSC5 AS ' '
ON TABLE SUBHEAD
" "
" "
" "
"TRANSACTION PERIOD:"
"10/01/2005 - 10/31/2005"
"JOINT Commission" 
"Report by Hierarchy - multi-level drill down"
" "
" "
HEADING
"<AROLL_LEV4"
WHERE ( AROLL_LEV4 EQ '&Lvl4' );
ON TABLE SET PAGE-NUM OFF
-* ON TABLE COLUMN-TOTAL
ON TABLE RECOMPUTE NET_TKT_CNT FARE_PAID SAVINGS LOST_SAVINGS AVGT AVGA
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     UNITS=IN,
     PAGESIZE='Letter',
     LEFTMARGIN=0.250000,
     RIGHTMARGIN=0.250000,
     TOPMARGIN=0.250000,
     BOTTOMMARGIN=0.250000,
     SQUEEZE=ON,
     ORIENTATION=LANDSCAPE,
$
TYPE=REPORT,
     GRID=OFF,
     FONT='TIMES NEW ROMAN',
     SIZE=10,
     COLOR='BLACK',
     BACKCOLOR='NONE',
     STYLE=NORMAL,
$
TYPE=DATA,
     COLUMN=N2,
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='multidrill6' Lvl5=N2),
$
TYPE=TABHEADING,
     LINE=6,
     SIZE=14,
     JUSTIFY=CENTER,
$
TYPE=TABHEADING,
     LINE=6,
     OBJECT=TEXT,
     ITEM=1,
     COLOR='BLACK',
     STYLE=BOLD,
$
TYPE=TABHEADING,
     LINE=7,
     SIZE=14,
     JUSTIFY=CENTER,
$
TYPE=TABHEADING,
     LINE=7,
     OBJECT=TEXT,
     ITEM=1,
     COLOR='BLACK',
     STYLE=BOLD,
$
TYPE=HEADING,
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drilldetail4' Lvl4=AROLL_LEV4),
$ 
TYPE=REPORT,
     IMAGE=JOINT.gif,
     POSITION=(.5 .3), SIZE=(2.0 .4),
$
TYPE=REPORT,
     IMAGE=logo15.gif,
     POSITION=(8.1 .5), SIZE=(1.3 .44),
$
ENDSTYLE
END
