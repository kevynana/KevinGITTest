-* Added Client Selection and Include/Exclude for Client and Category
-* Steve (03/03/2005)
-* CHANGED PROGRAM TO USE JOIN DUE TO RECORD LIMITATION ON IN FILE COMMAND
-* DEB 4/14/05
-* JEM 8/14/15 Story ID - S-10869 Changed to correct all reports under a user not showing up on report
-*  10/19/2016  REJ  S-22499 Updated the logo to Post Trip Reporting.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
-INCLUDE SETECHO
SET ASNAMES=ON
-*SET TEMPERASE = OFF
-*FILEDEF XUSER DISK D:\FOCTEMP\XUSER.FTM
SET PAGE-NUM=OFF
SET EMPTYREPORT=ON
TABLE FILE CONTROL
PRINT USER_ID
BY ROLLUP_CODE
BY SECURITY_LEVEL
ON TABLE SAVE AS XUSER
END
-RUN
-READ XUSER &&RLUP.A8. &&SEC.I1.
-RUN
 
TABLE FILE ROLLUPX
PRINT LOGO_2
BY ROLLUP_CODE NOPRINT
WHERE ROLLUP_CODE EQ '&&RLUP.EVAL'
ON TABLE SAVE
END
-RUN
 
-READ SAVE &LOGO2.8.
-SET &LOGO2 = '&LOGO2.EVAL' || '.GIF';
 
 
JOIN SET_KEY IN RPT_SET TO ALL SET_KEY IN RPT_INST
 
DEFINE FILE RPT_SET
-* XHIER/A30 = IF LEV_SELECT EQ ' ' THEN 'UNKNOWN' ELSE LEV_SELECT ;
DISTRB/A10 = DECODE DISTRB_FLAG ('T' 'SERVER' 'F' 'FTP'
                                 'E' 'EMAIL');
OTYPE/A22 = IF SETTINGS.OUTPUT_FORMAT EQ 'EXCEL' THEN 'EXCEL' ELSE
            IF SETTINGS.OUTPUT_FORMAT EQ 'EXL2K' THEN 'EXCEL 2000'
            ELSE IF SETTINGS.OUTPUT_FORMAT EQ 'PDF'
            THEN 'PDF' ELSE 'UNKNOWN';
 
END
 
-IF &&SEC EQ 9 THEN GOTO SEC9;
 
 
 
TABLE FILE RPT_SET
PRINT INST_KEY DISTRB OTYPE SET_FREQ USER_NAME
-*SET_NAME GLOBAL_PARM RPT_NAME CATG1 USER_SECURITY
SET_NAME GLOBAL_PARM RPT_NAME USER_SECURITY ROLLUP_CODE SET_KEY
BY INST_KEY NOPRINT
IF ROLLUP_CODE EQ '&&RLUP'
-* IF USER_SECURITY NE 9
IF USER_SECURITY LE &&SEC
ON TABLE HOLD AS RPTSET FORMAT FOCUS INDEX INST_KEY
END
-RUN
 
-GOTO REPORT
 
-SEC9
 
TABLE FILE RPT_SET
PRINT INST_KEY DISTRB OTYPE SET_FREQ USER_NAME
SET_NAME GLOBAL_PARM RPT_NAME ROLLUP_CODE SET_KEY
BY INST_KEY NOPRINT
IF ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS RPTSET FORMAT FOCUS INDEX INST_KEY
END
-RUN
 
-REPORT
 
-*JOIN INST_KEY IN RPTSET TO I_SUMM.INST_KEY IN I_SUMM AS J2
-* JOIN INST_KEY IN RPTSET TO INST_KEY IN RPT_FLDS
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
 
TABLE FILE RPTSET
PRINT DISTRB OTYPE SET_FREQ USER_NAME
SET_NAME GLOBAL_PARM RPT_NAME ROLLUP_CODE SET_KEY
BY INST_KEY
IF ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS XONE FORMAT FOCUS INDEX INST_KEY
END
-RUN
 
-* TABLE FILE XONE
-* PRINT INST_KEY
-* BY INST_KEY NOPRINT
-* ON TABLE SAVE
-* IF RECORDLIMIT EQ 2500
-* END
-* -RUN
-SET &BLANK = '                           ';
 
-IF &&RLUP EQ 'TOTAL-R' THEN GOTO TOTAL;
 
JOIN INST_KEY IN RPT_FLDS TO INST_KEY IN XONE
DEFINE FILE RPT_FLDS
INCCLI/A1 = IF HIEORG1.INCL1 = 1 THEN ' ' ELSE 'E';
INCCAT/A1 = IF HIEORG1.INCL2 = 1 THEN ' ' ELSE 'E';
END
 
TABLE FILE RPT_FLDS
PRINT HIEORG2.MEMBER AS 'MEMBER'
      HIEORG1.SEL_LEVEL AS 'SEL_LEVEL'
      HIEORG2.CATG AS 'CATG'
      INCCAT
      HIEORG2.CLIENT AS 'CLIENT'
      INCCLI
      HIEORG2.SEQ_NO AS 'SEQ_NO'
BY INST_KEY
-*WHERE INST_KEY IN FILE SAVE
WHERE RPT_FLDS.INST_KEY EQ XONE.INST_KEY
ON TABLE HOLD AS XTWO
END
-RUN
 
MATCH FILE XONE
PRINT DISTRB OTYPE SET_FREQ SET_NAME GLOBAL_PARM RPT_NAME
ROLLUP_CODE USER_NAME SET_KEY
BY INST_KEY
ON TABLE HOLD
RUN
FILE XTWO
PRINT MEMBER SEL_LEVEL CATG INCCAT CLIENT INCCLI SEQ_NO
BY INST_KEY
AFTER MATCH HOLD AS RPTSET1 OLD
END
-RUN
 
 
 
 
 
DEFINE FILE RPTSET1
USR_NAM/A30 = IF USER_NAME EQ ' ' THEN 'unassigned' ELSE USER_NAME;
SET_NM/A20 = EDIT(SET_NAME, '99999999999999999999');
RPT_NM/A25 = EDIT(RPT_NAME, '9999999999999999999999999');
INCCAT1/A1 = IF CATG EQ ' ' THEN ' ' ELSE IF INCCAT EQ ' ' THEN 'I' ELSE 'E';
INCCLI1/A1 = IF CLIENT EQ ' ' THEN ' ' ELSE IF INCCLI EQ ' ' THEN 'I' ELSE 'E';
END
 
 
 
TABLE FILE RPTSET1
HEADING
" "
" "
" "
"Post-trip Reporting"
"&DATE ........ &TOD        REPORT LISTING FOR : &&RLUP"
"REPORT NAME:  RPT_LSTA.FEX"
" "
" "
BY USR_NAM          AS 'USER NAME'
BY SET_FREQ         AS 'FREQUENCY'
BY SET_NM           AS 'SET NAME'
BY GLOBAL_PARM      AS 'REPORT ID'
PRINT RPT_NM        AS 'REPORT NAME'
      MEMBER        AS 'HIERARCHY'
      SEL_LEVEL     AS 'LEVEL'
      OTYPE         AS 'FORMAT'
      DISTRB        AS 'OUTPUT'
      CATG          AS 'CATEGORY'
      INCCAT1       AS 'INC/EXL'
      CLIENT        AS 'CLIENT'
      INCCLI1       AS 'INC/EXL'
 
-* IF USER_NAME NE ' '
-*WHERE HIEORG2.SEQ_NO EQ 1
WHERE SET_KEY NE 0
ON SET_NM SKIP-LINE
 
ON USR_NAM
    SUBFOOT
    "&BLANK.EVAL"
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.200000, RIGHTMARGIN=0.200000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=NORMAL, $
TYPE=DATA, COLUMN=N4, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N5, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N6, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N7, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N8, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N9, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N10, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, STYLE = BOLD ,$
TYPE=HEADING, LINE=4, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=1, STYLE=BOLD, BACKCOLOR=SILVER, GRID=ON, JUSTIFY=CENTER,  $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
-* TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
TYPE=REPORT, IMAGE=&LOGO2, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
END
-RUN
 
 
-GOTO DONE;
 
 
-TOTAL
 
 
TABLE FILE RPT_FLDS
PRINT ROLL2.SEQ_NO AS 'SEQ_NO'
BY INST_KEY
-* WHERE INST_KEY IN FILE SAVE
ON TABLE HOLD AS XTWO
END
-RUN
 
 
 
MATCH FILE XONE
PRINT DISTRB
      OTYPE
      SET_FREQ
      SET_NAME
      GLOBAL_PARM
      RPT_NAME
      ROLLUP_CODE
      USER_NAME
BY INST_KEY
ON TABLE HOLD
RUN
FILE XTWO
PRINT SEQ_NO
BY INST_KEY
AFTER MATCH HOLD AS TOTSET1 OLD
END
-RUN
 
 
 
DEFINE FILE TOTSET1
USR_NAM/A30 = IF USER_NAME EQ ' ' THEN 'unassigned' ELSE USER_NAME;
END
-RUN
 
TABLE FILE TOTSET1
HEADING
" "
" "
" "
"Post-trip Reporting"
"&DATE ........ &TOD        REPORT LISTING FOR : &&RLUP"
"REPORT NAME:  RPT_LSTA.FEX"
" "
" "
BY USR_NAM          AS 'USER NAME'
BY SET_FREQ         AS 'FREQUENCY'
BY SET_NAME         AS 'SET NAME'
BY GLOBAL_PARM      AS 'REPORT ID'
PRINT RPT_NAME      AS 'REPORT NAME'
      OTYPE         AS 'FORMAT'
      DISTRB        AS 'OUTPUT'
WHERE USER_NAME NE ' '
ON SET_NAME SKIP-LINE
ON USR_NAM
    SUBFOOT
    "&BLANK.EVAL"
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=6, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, STYLE = BOLD ,$
TYPE=HEADING, LINE=4, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=DATA, COLUMN=N4, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N5, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N6, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N7, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N8, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N9, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, COLUMN=N10, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=1, STYLE=BOLD, BACKCOLOR=SILVER, GRID=ON, JUSTIFY=CENTER,  $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
-* TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
TYPE=REPORT, IMAGE=&LOGO2, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
END
 
-GOTO DONE;
 
-NORECORDS
 
DEFINE FILE RPT_SET
EMPTY/A60 = 'NO REPORTS CURRENTLY SET UP FOR ANY USER                             ';
EMPTY/A60 WITH USER_NAME = 'NO REPORTS CURRENTLY SET UP FOR ANY USER';
 
END
 
 
TABLE FILE RPT_SET
PRINT EMPTY AS ''
BY USER_NAME NOPRINT
WHERE RECORDLIMIT EQ 1
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET STYLE *
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=12, BACKCOLOR=NONE, STYLE=NORMAL, $
-* TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(.2 .2 ), SIZE=(1.5 .4), $
TYPE=REPORT, IMAGE=&LOGO2, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
END
-RUN
 
 
-DONE
