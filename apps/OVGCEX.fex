-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492

-* Created for Global Currency Reporting

SET ASNAMES=ON

-SET &&OV1_GC = &&WEB_PATH || 'OV1_GC.FOC';
-RUN
FILEDEF OV1_GC DISK &&OV1_GC
-RUN


CREATE FILE OV1_GC
-RUN

DEFINE FILE SR_50
 FLAG/A1 = 'A';
 DOM/A30 = 'Domestic';
 INTL/A30 = 'International';
 TTL/A30 = 'Total';
 CY/A1 = IF (TRN_DATE GE &&FROMDT.EVAL) AND 
            (TRN_DATE LE &&TODT.EVAL) 
            THEN 'Y' ELSE 'N';

 PY/A1 = IF (TRN_DATE GE &&FXDATE.EVAL) AND 
            (TRN_DATE LE &&TXDATE.EVAL) 
            THEN 'Y' ELSE 'N';

 FARE_PAID/D15.2 = SEG_AMT + SEG_TAX;
 
 NET_TKT_CNT/P12      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

-*DOMESTIC PERIOD
-* CURRENT FARE AMOUNT DOMESTIC CALCULATION
      CDM1NET_AMT/D15.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                             (CY EQ 'Y') THEN FARE_PAID ELSE 0;

-* CURRENT NET TICKETS DOMESTIC CALCULATION
        CDM1TKT_ISS/P12 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                             (CY EQ 'Y') THEN NET_TKT_CNT ELSE 0;

-*INTERNATIONAL PERIOD
-* CURRENT FARE AMOUNT INTERNATIONAL CALCULATION
        CIM1NET_AMT/D15.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (CY EQ 'Y') THEN FARE_PAID ELSE 0;

-* CURRENT NET TICKTS INTERNATIONAL CALCULATION
        CIM1TKT_ISS/P12 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (CY EQ 'Y') THEN NET_TKT_CNT ELSE 0;

-*PRIOR PERIOD
-* PRIOR FARE AMOUNT DOMESTIC CALCULATION
      PDM1NET_AMT/D15.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                             (PY EQ 'Y') THEN FARE_PAID ELSE 0;
                      

-* PRIOR TICKETS ISSUED DOMESTIC CALCULATION
        PDM1TKT_ISS/P12 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                               (PY EQ 'Y') THEN NET_TKT_CNT ELSE 0;


-*INTERNATIONAL
-* PRIOR FARE AMOUNT INTERNATIONAL CALCULATION
        PIM1NET_AMT/D15.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (PY EQ 'Y') THEN FARE_PAID ELSE 0;                      

-* PRIOR TICKETS ISSUED INTERNATIONAL CALCULATION
        PIM1TKT_ISS/P12 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                             (PY EQ 'Y') THEN NET_TKT_CNT ELSE 0;
                             
-INCLUDE TRANTYPE
END
-RUN


TABLEF FILE SR_50
PRINT DOM
INTL
TTL
CDM1NET_AMT/P15 
CDM1TKT_ISS/P15 
CIM1NET_AMT/P15 
CIM1TKT_ISS/P15 
PDM1NET_AMT/P15
PDM1TKT_ISS/P15
PIM1NET_AMT/P15 
PIM1TKT_ISS/P15 
TKT_DATE
WHERE (TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL AND TRN_DATE LE &&TXDATE.EVAL);
WHERE VOID.VOID_DATE EQ 0;
&&WHERE1
&&WHERE2
&&WHERE3



-INCLUDE RPTPARMS

ON TABLE HOLD AS OVGCHLD
END
-RUN

TABLE FILE OVGCHLD
PRINT DOM
INTL
TTL
CDM1NET_AMT
CDM1TKT_ISS 
CIM1NET_AMT 
CIM1TKT_ISS 
PDM1NET_AMT
PDM1TKT_ISS
PIM1NET_AMT 
PIM1TKT_ISS 
BY TKT_DATE
ON TABLE HOLD AS OVGCHLD1 FORMAT FOCUS INDEX TKT_DATE
END
-RUN

-* Global Reporting JOIN
JOIN OVGCHLD1.TKT_DATE IN OVGCHLD1 TO GLOBAL.DATE IN GLOBAL AS J1

DEFINE FILE OVGCHLD1
 CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
  CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
  CDOMFP/D20.6 = CDM1NET_AMT * CURR1X;
  CINTFP/D20.6 = CIM1NET_AMT * CURR1X;
  PDOMFP/D20.6 = PDM1NET_AMT * CURR1X;
  PINTFP/D20.6 = PIM1NET_AMT * CURR1X;


-* DEFINES ON PREVIOUSLY DEFINES DEFINES

  CDOMFPX/D15.2 = CDOMFP;
  CINTFPX/D15.2 = CINTFP;
  PDOMFPX/D15.2 = PDOMFP;
  PINTFPX/D15.2 = PINTFP;

END
-RUN

TABLE FILE OVGCHLD1
SUM 
CDM1NET_AMT
CDM1TKT_ISS 
CIM1NET_AMT 
CIM1TKT_ISS 
PDM1NET_AMT
PDM1TKT_ISS
PIM1NET_AMT 
PIM1TKT_ISS 
CDOMFPX
CINTFPX
PDOMFPX
PINTFPX
BY DOM
BY INTL
BY TTL
BY NAMEC
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS OVGCHLD2
END
-RUN


DEFINE FILE OVGCHLD2
LABEL/A30 = 'Air Volume';
LABEL1/A30 = 'Average Ticket Price';
END
-RUN

TABLE FILE OVGCHLD2
SUM CDOMFPX
    CINTFPX
    PDOMFPX
    PINTFPX
    CDM1TKT_ISS
    PDM1TKT_ISS
    CIM1TKT_ISS
    PIM1TKT_ISS
COMPUTE CFARE/D15.2 = CDOMFPX + CINTFPX;
COMPUTE PFARE/D15.2 = PDOMFPX + PINTFPX;
COMPUTE CTKT/P15 = CDM1TKT_ISS + CIM1TKT_ISS;
COMPUTE PTKT/P15 = PDM1TKT_ISS + PIM1TKT_ISS;
COMPUTE CDAVG/D15.2 = IF ((CDM1TKT_ISS LT 1) AND (CDM1TKT_ISS GT (-1))) THEN 0
                    ELSE (CDOMFPX/CDM1TKT_ISS);
COMPUTE CIAVG/D15.2 = IF ((CIM1TKT_ISS LT 1) AND (CIM1TKT_ISS GT (-1))) THEN 0 
                    ELSE (CINTFPX/CIM1TKT_ISS); 
COMPUTE PDAVG/D15.2 = IF ((PDM1TKT_ISS LT 1) AND (PDM1TKT_ISS GT (-1))) THEN 0
                    ELSE (PDOMFPX/PDM1TKT_ISS);
COMPUTE PIAVG/D15.2 = IF ((PIM1TKT_ISS LT 1) AND (PIM1TKT_ISS GT (-1))) THEN 0 
                    ELSE (PINTFPX/PIM1TKT_ISS);
COMPUTE TCAVG/D15.2 = IF ((CTKT LT 1) AND (CTKT GT (-1))) THEN 0 ELSE
                       (CFARE/CTKT);
COMPUTE TPAVG/D15.2 = IF ((PTKT LT 1) AND (PTKT GT (-1))) THEN 0 ELSE
                       (PFARE/PTKT);
BY DOM
BY INTL
BY TTL
BY NAMEC
BY LABEL
BY LABEL1
ON TABLE HOLD AS OVGCHLD3
END
-RUN



-* Domestic Air Volume

DEFINE FILE OVGCHLD3
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 1;
END
-RUN

TABLE FILE OVGCHLD3
PRINT CATEGORY/A61 AND DOM/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CDOMFPX/P15C AS 'VOL_CURR' AND 
CDM1TKT_ISS/P9CS AS 'TKT_CURR' AND PDOMFPX/P15C AS 'VOL_PRIOR' 
AND PDM1TKT_ISS/P9CS AS 'TKT_PRIOR' AND COMPUTE ROW_ORDER/I2 = 1; 
ON TABLE HOLD AS OVGCHLDD
END
-RUN


TABLE FILE OVGCHLDD
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVGCHLDD1
END
-RUN
MODIFY FILE OV1_GC
FIXFORM FROM OVGCHLDD1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVGCHLDD1
END
-RUN

-* International Air Volume

DEFINE FILE OVGCHLD3
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 1;
END
-RUN

TABLE FILE OVGCHLD3
PRINT CATEGORY/A61 AND INTL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CINTFPX/P15C AS 'VOL_CURR' AND 
CIM1TKT_ISS/P9CS AS 'TKT_CURR' AND PINTFPX/P15C AS 'VOL_PRIOR' 
AND PIM1TKT_ISS/P9CS AS 'TKT_PRIOR' AND COMPUTE ROW_ORDER/I2 = 2; 
ON TABLE HOLD AS OVGCHLDI
END
-RUN


TABLE FILE OVGCHLDI
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVGCHLDI1
END
-RUN
MODIFY FILE OV1_GC
FIXFORM FROM OVGCHLDI1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVGCHLDI1
END
-RUN

-* Total Air Volume

DEFINE FILE OVGCHLD3
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 1;
END
-RUN

TABLE FILE OVGCHLD3
PRINT CATEGORY/A61 AND TTL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CFARE/P15C AS 'VOL_CURR' AND 
CTKT/P9CS AS 'TKT_CURR' AND PFARE/P15C AS 'VOL_PRIOR' 
AND PTKT/P9CS AS 'TKT_PRIOR' AND COMPUTE ROW_ORDER/I2 = 2; 
ON TABLE HOLD AS OVGCHLDT
END
-RUN


TABLE FILE OVGCHLDT
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVGCHLDT1
END
-RUN
MODIFY FILE OV1_GC
FIXFORM FROM OVGCHLDT1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVGCHLDT1
END
-RUN

-* Domestic Average Ticket

DEFINE FILE OVGCHLD3
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL1 ELSE LABEL1||'-'||NAMEC;
CAT_ORDER/I2 = 2;
END
-RUN

TABLE FILE OVGCHLD3
PRINT CATEGORY/A61 AND DOM/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND  CDAVG/P15C AS 'VOL_CURR'  CDM1TKT_ISS/P9CS AS 'TKT_CURR'
AND PDAVG/P15C AS 'VOL_PRIOR' AND PDM1TKT_ISS/P9CS AS 'TKT_PRIOR' AND
COMPUTE ROW_ORDER/I2 = 2; 
ON TABLE HOLD AS OVGCHLDDA
END
-RUN


TABLE FILE OVGCHLDDA
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVGCHLDDA1
END
-RUN
MODIFY FILE OV1_GC
FIXFORM FROM OVGCHLDDA1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVGCHLDDA1
END
-RUN

-* International Average Ticket

DEFINE FILE OVGCHLD3
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL1 ELSE LABEL1||'-'||NAMEC;
CAT_ORDER/I2 = 2;
END
-RUN

TABLE FILE OVGCHLD3
PRINT CATEGORY/A61 AND INTL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CIAVG/P15C AS 'VOL_CURR' CIM1TKT_ISS/P9CS AS 'TKT_CURR'
AND PIAVG/P15C  AS 'VOL_PRIOR' AND PIM1TKT_ISS/P9CS AS 'TKT_PRIOR' AND
COMPUTE ROW_ORDER/I2 = 2; 
ON TABLE HOLD AS OVGCHLDDA
END
-RUN


TABLE FILE OVGCHLDDA
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVGCHLDDA1
END
-RUN
MODIFY FILE OV1_GC
FIXFORM FROM OVGCHLDDA1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVGCHLDDA1
END
-RUN


-* Total Average Ticket

DEFINE FILE OVGCHLD3
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL1 ELSE LABEL1||'-'||NAMEC;
CAT_ORDER/I2 = 2;
END
-RUN

TABLE FILE OVGCHLD3
PRINT CATEGORY/A61 AND TTL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND TCAVG/P15C  AS 'VOL_CURR' AND 
CTKT/P9CS AS 'TKT_CURR' AND TPAVG/P15C  AS 'VOL_PRIOR' 
AND PTKT/P9CS AS 'TKT_PRIOR' AND COMPUTE ROW_ORDER/I2 = 2; 
ON TABLE HOLD AS OVGCHLDTT
END
-RUN


TABLE FILE OVGCHLDTT
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVGCHLDTT1
END
-RUN
MODIFY FILE OV1_GC
FIXFORM FROM OVGCHLDTT1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVGCHLDTT1
END
-RUN


