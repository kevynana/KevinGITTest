-* File DELDATA.FEX
-* THIS PROGRAM DELETES DATA FOR A CERTAIN TIME PERIOD FROM
-* AIR, CAR, HOTEL, SEGMENT, MARKET, DESTINATION, TKT_MAIN
-*

TABLE FILE S_BLV2
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR TRN_DATE
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE GE '20050501') AND (TRN_DATE LE '20050531')
ON TABLE HOLD AS SHOLD
END
-RUN

TABLE FILE S_BLV2
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE LT '20050501') OR (TRN_DATE GT '20050531')
ON TABLE HOLD AS SHOLD2
END
-RUN

MATCH FILE SHOLD
PRINT AIR_KEY TRN_DATE
BY AIR_KEY NOPRINT
RUN
FILE SHOLD2
PRINT AIR_KEY
BY AIR_KEY NOPRINT
AFTER MATCH HOLD AS SHOLD3 OLD-NOT-NEW
END
-RUN

TABLE FILE SHOLD3
SUM TRN_DATE
BY AIR_KEY
ON TABLE HOLD AS SHOLD4
END
-RUN

TABLE FILE SHOLD4
PRINT AIR_KEY 
BY AIR_KEY NOPRINT
ON TABLE HOLD AS SHOLD5
END
-RUN

-* AIR_MAIN
MODIFY FILE A_BLV2
FIXFORM FROM SHOLD5
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD5
END
-RUN

-* TKT_MAIN
MODIFY FILE T_BLV2
FIXFORM FROM SHOLD5
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD5
END
-RUN

-* SEGMENT
MODIFY FILE S_BLV2
FIXFORM FROM SHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD
END
-RUN

TABLE FILE M_BLV2
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
IF TRN_DATE GE '20050501'
IF TRN_DATE LE '20050531'
ON TABLE HOLD AS MHOLD
END
-RUN

-* MARKET
MODIFY FILE M_BLV2
FIXFORM FROM MHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON MHOLD
END
-RUN

TABLE FILE D_BLV2
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
IF TRN_DATE GE '20050501'
IF TRN_DATE LE '20050531'
ON TABLE HOLD AS DHOLD
END
-RUN

-* DESTINATION
MODIFY FILE D_BLV2
FIXFORM FROM DHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DHOLD
END
-RUN

-* CAR

TABLE FILE C_BLV2
PRINT CAR_KEY
BY CAR_KEY NOPRINT
IF INVOICE_DATE GE '20050501'
IF INVOICE_DATE LE '20050531'
ON TABLE HOLD AS CHOLD
END
-RUN

MODIFY FILE C_BLV2
FIXFORM FROM CHOLD
MATCH CAR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON CHOLD
END
-RUN

-* HOTEL

TABLE FILE H_BLV2
PRINT HTL_KEY
BY HTL_KEY NOPRINT
IF INVOICE_DATE GE '20050501'
IF INVOICE_DATE LE '20050531'
ON TABLE HOLD AS HHOLD
END
-RUN

MODIFY FILE H_BLV2
FIXFORM FROM HHOLD
MATCH HTL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HHOLD
END
-RUN

-TYPE PROGRAM RUN COMPLETED
-EXIT
