-INCLUDE SETECHO
 
SET BASEURL=https://www.ettekreview.com/image/
-RUN
 
DEFINE FILE PUSH_LINKS
HDR_IMAGE/A250='<IMG SRC=' || '&&SRVR.EVAL' || '/image/review.gif>';
TDATE/MDYY='&MDYY';
 
DASH_POS/I2 = POSIT(RPT_NAME, 65, '-', 1, 'I2') - 1;
RPT_TYPE/A12=SUBSTR(65, RPT_NAME, 1, DASH_POS, DASH_POS, RPT_TYPE);
RNAME/A65=SUBSTR(65, RPT_NAME, (DASH_POS + 2), 65, (65 - (DASH_POS + 2)), RNAME);
 
RPT_SUBHEAD/A50=IF RPT_TYPE EQ 'Weekly' THEN 'Weekly Reports' ELSE
                IF RPT_TYPE EQ 'Monthly' THEN 'Monthly Reports' ELSE
                IF RPT_TYPE EQ 'MYTD' THEN 'Monthly Year To Date Reports' ELSE
                IF RPT_TYPE EQ 'Quarterly' THEN 'Quarterly Reports' ELSE
                IF RPT_TYPE EQ 'QYTD' THEN 'Quarterly Year To Date Reports' ELSE
    IF RPT_TYPE EQ 'Annual' THEN 'Annual Reports' ELSE 'Unknown';
 
END
TABLE FILE PUSH_LINKS
PRINT RNAME AS ''
      RPT_SUBHEAD NOPRINT
BY RPT_TYPE NOPRINT
BY RNAME NOPRINT
BY RPT_LINK NOPRINT
ON RPT_TYPE SUBHEAD
" </1<RPT_SUBHEAD </1"
HEADING
"eTTek Review Push Reports"
" "
"Below are the links to your eTTek Review Push Reports that were produced on <TDATE>."
" "
"Click the links below to view the reports."
" "
FOOTING BOTTOM
"Copyright 2008 Travel and Transport, Inc"
WHERE SCHEDULE_ID EQ '&DSTSCHEDID';
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT HTML
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     UNITS=IN,
     SQUEEZE=ON,
     ORIENTATION=PORTRAIT,
$
TYPE=REPORT,
     GRID=OFF,
     FONT='ARIAL',
     SIZE=9,
$
TYPE=DATA,
     COLUMN=N4,
     TARGET='_blank',
     URL=(RPT_LINK),
$
TYPE=TABHEADING,
     SIZE=12,
     STYLE=BOLD,
$
TYPE=TABFOOTING,
     SIZE=12,
     STYLE=BOLD,
$
TYPE=HEADING,
     SIZE=10,
  COLOR=GRAY,
$
TYPE=HEADING,
     LINE=1,
     SIZE=12,
     STYLE=BOLD,
  JUSTIFY=CENTER,
  COLOR=BLACK,
$
TYPE=HEADING,
     LINE=3,
  COLOR=BLACK,
$
TYPE=FOOTING,
     SIZE=10,
  COLOR=GRAY,
  JUSTIFY=CENTER,
$
TYPE=SUBHEAD,
     SIZE=10,
     STYLE=BOLD,
$
TYPE=SUBFOOT,
     SIZE=10,
     STYLE=BOLD,
$
TYPE=SUBTOTAL,
     BACKCOLOR=RGB(210 210 210),
$
TYPE=ACROSSVALUE,
     SIZE=9,
$
TYPE=ACROSSTITLE,
     STYLE=BOLD,
$
TYPE=GRANDTOTAL,
     BACKCOLOR=RGB(210 210 210),
     STYLE=BOLD,
$
ENDSTYLE
END
-RUN
 
TABLE FILE PUSH_LINKS
BY SCHEDULE_ID
BY RPT_NUMBER
WHERE SCHEDULE_ID EQ '&DSTSCHEDID';
ON TABLE HOLD AS HSCHED
END
MODIFY FILE PUSH_LINKS
FIXFORM FROM HSCHED
MATCH SCHEDULE_ID RPT_NUMBER
 ON MATCH DELETE
 ON NOMATCH REJECT
DATA ON HSCHED
END
-RUN
