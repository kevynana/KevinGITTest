-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File RNKEXTRT.FEX
-********************************************************************
-********************************************************************
-*
-* This program extracts data for a drillable frequent flyer report
-* Added defines for ref_tkts and non_tkts JK 9/29/04
-* ADDED CODE FOR GLOBAL CURRENCY - DV 11/9/04
-* ADDED AA_TKT DL_TKT NW_TKT UA_TKT OTH_TKT DEFINE - DEB 11/17/04
-* ADDED TRANTYPE INCLUDE - JK 12/9/14 STORY ID S-06492

-********************************************************************
SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
  COMM_SEG/D12.2S   = SEG_COM;
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  KILO/D9      = CITYPAIR.SEG_MILES * 1.60934 ;  
  LEVEL_DESC/A60      = &&LDESC;
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_COMM/D12.2S     = (SEG_AMT - SEG_COM);
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
  NET_TKT_CNT/D8S    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_SEG_LOWEST/D9 = SEG_LOWEST;
  NEW_SEG_MILES/D9 = SEG_MILES;
  NEW_STAY/D9 = STAY;
  NEW_TRIP_LENGTH/D12 = TRIP_LENGTH;
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TOTAL_FARE/D12.2S = SEG_AMT + SEG_TAX;
  TRAN_MONTH/MT = TRN_DATE;
  VASAVINGS/D11.2CS = SEG_DISCOUNT - TOTAL_FARE;
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;
  
-* ****DEFINES ON PREVIOUS DEFINES****
  CPMILE/D12.2CS = IF ((SEG_MILES LT 1) AND (SEG_MILES GT (-1))) THEN 0 ELSE
                      (FARE_PAID/SEG_MILES);
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  AIRFEE_CNT/D8CS = IF AIRLINE_FEE GT 0 THEN 1 ELSE 0;
  NON_TKTS/P8CS = IF NONREF_FLAG EQ 'N' THEN NET_TKT_CNT ELSE 0;
  REF_TKTS/P8CS = IF NONREF_FLAG NE 'N' THEN NET_TKT_CNT ELSE 0;
  AA_TKT/P8CS = IF VAL_AIRLINE EQ 'AA' THEN NET_TKT_CNT ELSE 0;
  UA_TKT/P8CS = IF VAL_AIRLINE EQ 'UA' THEN NET_TKT_CNT ELSE 0;
  NW_TKT/P8CS = IF VAL_AIRLINE EQ 'NW' THEN NET_TKT_CNT ELSE 0;
  DL_TKT/P8CS = IF VAL_AIRLINE EQ 'DL' THEN NET_TKT_CNT ELSE 0;
  OTH_TKT/P8CS = IF VAL_AIRLINE NE 'AA' OR 'UA' OR 'NW' OR 'DL' THEN
                  NET_TKT_CNT ELSE 0;
-INCLUDE TRANTYPE                  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT   AIR_KEY
        AIRLINE
        AIRLINE_FEE
        AIR_MAIN.INTL_DOM	
        CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
        CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
        CLASS
        EMB_CTY.COUNTRY_CODE AS 'EMB_CNTRY_CODE'
        CPMILE	
        DISSAVINGS
        DPT_TIME
        DST_APC
        DST_APCX
        EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
        EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
        FARE_BAS		
        FARE_PAID
        KILO
        LEVEL_DESC	
        LEVEL1	
        LEVEL2	
        LEVEL3
        MRK_TKT_CNT
        NET_TKT_CNT         	
        NEW_SEG_MILES
        ORG_APC
        ORG_APCX
        OTH_TKT
        PASSNGR_NAME
        REFUSE_CODE
        REF_TKTS
        RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
        RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
        SAVINGS	
        SEG_NBR	
        TKT_AMOUNT
        TKT_DATE	
        TKT_NUM	
        TKT_SORT
        TKT_TYPE	
        TRN_DATE 
        VAL_AIRLINE
        XDPT_DT	
        XPSNGR_NM	
        XTRAN_DT
         

-INCLUDE &AIRDATES

WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS GCTOPTVL 

END
-RUN


TABLE FILE GCTOPTVL
PRINT AIR_KEY
        AIRLINE
        AIRLINE_FEE
        INTL_DOM	
        EMB_APT_CD	
        RTE_APT_CD	
        CLASS
        EMB_CNTRY_CODE
        CPMILE	
        DISSAVINGS
        DPT_TIME
        DST_APC
        DST_APCX
        EMB_APT_NM	
        EMB_CTY_NM
        FARE_BAS		
        FARE_PAID
        KILO
        LEVEL_DESC	
        LEVEL1	
        LEVEL2	
        LEVEL3
        MRK_TKT_CNT
        NET_TKT_CNT         	
        NEW_SEG_MILES
        ORG_APC
        ORG_APCX
        OTH_TKT
        PASSNGR_NAME
        REFUSE_CODE
        REF_TKTS
        RTE_APT_NM
        RTE_CTY_NM	
        SAVINGS	
        SEG_NBR	
        TKT_AMOUNT
        TKT_NUM	
        TKT_SORT
        TKT_TYPE	
        TRN_DATE 
        VAL_AIRLINE
        XDPT_DT	
        XPSNGR_NM	
        XTRAN_DT         
BY TKT_DATE 
ON TABLE HOLD AS TOPTRVL FORMAT FOCUS INDEX TKT_DATE
END
-RUN

-* Global Reporting JOIN
JOIN TOPTRVL.TKT_DATE IN TOPTRVL TO GLOBAL.DATE IN GLOBAL AS J1


DEFINE FILE TOPTRVL
NAMEC/A30 = CNAME;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
PAID_FAREX/D20.6 = FARE_PAID * CURR1X;

-* DEFINES ON PREVIOUSLY DEFINES DEFINES

PAID_FARE1/D20.6C = PAID_FAREX;
END
-RUN



TABLE FILE TOPTRVL
PRINT AIR_KEY
        AIRLINE
        AIRLINE_FEE
        INTL_DOM	
        EMB_APT_CD	
        RTE_APT_CD	
        CLASS
        EMB_CNTRY_CODE
        CPMILE	
        DISSAVINGS
        DPT_TIME
        DST_APC
        DST_APCX
        EMB_APT_NM	
        EMB_CTY_NM
        FARE_BAS		
        FARE_PAID
        KILO
        LEVEL_DESC	
        LEVEL1	
        LEVEL2	
        LEVEL3
        MRK_TKT_CNT
        NET_TKT_CNT         	
        NEW_SEG_MILES
        ORG_APC
        ORG_APCX
        OTH_TKT
        PASSNGR_NAME
        REFUSE_CODE
        REF_TKTS
        RTE_APT_NM
        RTE_CTY_NM	
        SAVINGS	
        SEG_NBR	
        TKT_AMOUNT
        TKT_NUM	
        TKT_SORT
        TKT_TYPE	
        TRN_DATE      
        XDPT_DT	
        XPSNGR_NM	
        XTRAN_DT 
        VAL_AIRLINE
        PAID_FAREX
        PAID_FARE1
        NAMEC
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS TOPDST
END
-RUN



-*-EXIT


        
