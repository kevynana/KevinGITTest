 
-**********************************************************************
-* Summary by Hierarchy - Air
-*11/03/16 - DLV - S-26742 - Created for EJSMHIER
-* 02/24/17 DLV S-22486 - updated position of header and footer NEW_HDR NEW_FTR

-**********************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON
SET SPACE = 1
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');
-SET &&SP = '|';
-SET &&SP1A = IF &&ROLLNME EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP1 = IF &&ROLLCS EQ ' ' THEN ' ' ELSE &&SP1A;
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&CSLPA = IF &&RCNCT.EVAL GE 10 OR &&CSL.EVAL GE 10 THEN 60 ELSE 68;
-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' THEN 78 ELSE &&CSLPA;
-SET &&ROLLCS11 = TRUNCATE(&&ROLLCS);
-SET &&RCNCT1 = TRUNCATE(&&ROLLCNCT);
-SET &&BLANK = ' ';
-RUN
 
-SET &WHATDATE = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'TRN_DATE' ELSE 'FST_DPT_DATE';
 

CREATE FILE QRHIEREJ 
-RUN

 
 
-*********************************************************************************
-* Gather/Add Air Info to QRHIEREJ File
-*********************************************************************************
 
DEFINE FILE SR_50
  CURRENT/A1 = IF &WHATDATE.EVAL GE &&FROMDT.EVAL AND &WHATDATE.EVAL LE &&TODT.EVAL THEN 'Y' ELSE 'N';
  PRIOR/A1 = IF &WHATDATE.EVAL GE &&FXDATE.EVAL AND &WHATDATE.EVAL LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
  CGADV/A8 = IF (ADV_PURCH LT 7) THEN '0-6' ELSE
             IF (ADV_PURCH GE 7) AND (ADV_PURCH LE 13) THEN '7-13' ELSE '14+';
          
  BRK_LVL/I2 = &&BRK_LVL;
  LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
  FARE_PAID/P12.2 = SEG_AMT + SEG_TAX;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;
 
  NET_TKT_CNT/P12    = IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (FARE_PAID GE 0) AND
                          (SEG_COUNT EQ 1) AND
                          (CONJ_REL EQ 1) THEN 1 ELSE
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (EX_FLAG EQ 'F') THEN (-1) ELSE
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
 
  SAVINGS/P12.2     = SEG_STANDARD - FARE_PAID;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
 
  NEW_SEG_MILES/D12 = SEG_MILES;
 -* Defines on Previous Defines
  DISSAVINGS/P12.2 = IF EMBARK EQ 'DT1' THEN 0 ELSE
                   FARE_PAID-SEG_LOWEST;
 
-* Defines added for CCI exchange fields
TTLDAYS/P8 = NEW_ADV_PURCH*NET_TKT_CNT ;
 
F_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 0 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN 1 ELSE 0;
CFEXCH/P12CS = IF CURRENT EQ 'Y' THEN F_EXCH_CNT ELSE 0;
CAIR_FEE/P12.2 = IF CURRENT EQ 'Y' THEN AIRLINE_FEE ELSE 0;
CTTLDAYS/P9 = IF CURRENT EQ 'Y' THEN TTLDAYS ELSE 0;
END
-RUN
 
 
TABLEF FILE SR_50
PRINT CGADV
  CURRENT
  PRIOR
  DISSAVINGS
  DOD_TYPE
  AIR_MAIN.INTL_DOM AS 'INTDOM'
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR
  TKT_NUM
  TRN_DATE
  CTTLDAYS
  REFUSE_CODE
  RF_FLAG
  EX_FLAG
WHERE (&WHATDATE.EVAL GE &&FROMDT.EVAL) AND (&WHATDATE.EVAL LE &&TODT.EVAL) OR
      (&WHATDATE.EVAL GE &&FXDATE.EVAL) AND (&WHATDATE.EVAL LE &&TXDATE.EVAL)
WHERE VOID_DATE EQ 0
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
TABLE FILE RPTHOLD
SUM DISSAVINGS FARE_PAID NET_TKT_CNT
BY CURRENT
BY PRIOR
BY LEVEL_DESC
BY TKT_NUM
BY REFUSE_CODE
ON TABLE HOLD AS HOLDONE 
END
-RUN
 
DEFINE FILE HOLDONE
FLAG/A1 = IF PRIOR EQ 'Y' THEN 'N' ELSE
          IF CURRENT EQ 'Y' AND DISSAVINGS EQ 0 THEN 'N' ELSE 'Y';
LS_TKT/P12CS = IF CURRENT EQ 'Y' AND FLAG EQ 'Y' THEN NET_TKT_CNT ELSE 0;
END
TABLE FILE HOLDONE
SUM DISSAVINGS NET_TKT_CNT FARE_PAID LS_TKT 
BY CURRENT
BY PRIOR
BY LEVEL_DESC
BY TKT_NUM
BY REFUSE_CODE
ON TABLE HOLD AS HOLDTWO
END
-RUN
 
MATCH FILE RPTHOLD
PRINT CGADV
  DISSAVINGS
  DOD_TYPE
  INTDOM
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR
  TKT_NUM
  TRN_DATE
  CTTLDAYS
  RF_FLAG
  EX_FLAG
BY CURRENT
BY PRIOR
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE HOLDTWO
PRINT LS_TKT
BY CURRENT
BY PRIOR
BY LEVEL_DESC
AFTER MATCH HOLD AS TRANONE OLD-OR-NEW
END
-RUN


 
-IF &LINES EQ 0 THEN GOTO NOREC;
 
-SET &&RPTSUF = 'HIERSUM';
-INCLUDE FDEFRPTS
-RUN
 
 
DEFINE FILE TRANONE
NEW_ADV_PURCH1/D9 = IF RF_FLAG EQ ' ' THEN NEW_ADV_PURCH ELSE
                    IF EX_FLAG EQ ' ' THEN NEW_ADV_PURCH ELSE (NEW_ADV_PURCH * (-1));
REC_CNT/D8CS = IF NET_TKT_CNT EQ 1 THEN 1 ELSE 0;
TTLDAYS/D8 = NEW_ADV_PURCH*NET_TKT_CNT;
 
 
CAVOL/P12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
CMILES/P12 = IF CURRENT EQ 'Y' THEN NEW_SEG_MILES ELSE 0;
PAVOL/P12.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CNTKT/P12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CNTKTD/P12 = IF CURRENT EQ 'Y' AND INTDOM EQ 'D' THEN NET_TKT_CNT ELSE 0;
PNTKT/P12 = IF PRIOR EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CLSAV/P12.2 = IF CURRENT EQ 'Y' THEN DISSAVINGS ELSE 0;
CADVT/P12 = IF (CURRENT EQ 'Y') AND
                (CGADV EQ '0-6') THEN NET_TKT_CNT ELSE 0;
CADVT1/P12 = IF (CURRENT EQ 'Y') AND
                 (CGADV EQ '7-13') THEN NET_TKT_CNT ELSE 0;
CADVT2/P12 = IF (CURRENT EQ 'Y') AND
                 (CGADV EQ '14+') THEN NET_TKT_CNT ELSE 0;
CONL/P12 = IF (CURRENT EQ 'Y') AND (ONLINE_TRADITIONAL EQ 'ONLINE') AND (INTDOM EQ 'D') THEN
                  NET_TKT_CNT ELSE 0;
CNR/P12 = IF (CURRENT EQ 'Y') AND (NONREF_FLAG EQ 'N') THEN
              NET_TKT_CNT ELSE 0;
CTTLDAYS/D8 = IF CURRENT EQ 'Y' THEN TTLDAYS ELSE 0;
CLS_TKT/P12 = IF CURRENT EQ 'Y' THEN LS_TKT ELSE 0;
END
-RUN
 
 
TABLE FILE TRANONE
SUM CADVT
    CADVT1
    CADVT2
    CAVOL
    CLSAV
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL
    CTTLDAYS
    CLS_TKT
BY LEVEL_DESC
ON TABLE HOLD AS TRANTWO
END
-RUN

  
 
TABLE FILE TRANTWO
PRINT CAVOL CMILES CNTKT CLSAV CADVT CADVT1 CADVT2 CONL CNTKTD LEVEL_DESC CTTLDAYS CLS_TKT
RANKED BY HIGHEST CAVOL
ON TABLE HOLD AS RNK1
END
-RUN
 
-INCLUDE OTHPARMS
-RUN
 
DEFINE FILE RNK1
-* RANK_LIMIT/I2 = 9999;
 RANK_LIMIT/I2 = &&RANK_LIMIT;
 RLIMIT/A2    = EDIT(RANK_LIMIT) ;
 CRANK/D5     = RANK;
 BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
 LIST/D6 = IF LEVEL_DESC NE LAST LEVEL_DESC THEN (LAST LIST + 1)
           ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999');
 RANK_VALUE/A9 = IF RANK_LIMIT EQ 0 THEN BRANK ELSE
                IF LIST GT RANK_LIMIT THEN 'ALL OTHER' ELSE FRANK;
 LVL_PRINT/A25 =IF RANK_VALUE LT RLIMIT THEN LEVEL_DESC ELSE 'ALL OTHER';
END
-RUN
 
TABLE FILE RNK1
SUM CAVOL  CNTKT  CLSAV  CADVT CADVT1 CADVT2 CONL CNTKTD  CTTLDAYS CLS_TKT
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS RNK2
END
-RUN
 

   
 
TABLE FILE RNK2
PRINT LVL_PRINT RANK_VALUE CAVOL CNTKT  CLSAV  CADVT CADVT1 CADVT2 CONL CNTKTD  CTTLDAYS CLS_TKT
ON TABLE SAVE
END
-RUN
 

 
MODIFY FILE QRHIEREJ
LOG DUPL MSG OFF
FIXFORM LVL_PRINT/25 RANK_VALUE/9 CAVOL/12 CNTKT/12 CLSAV/12 CADVT/12 CADVT1/12  CADVT2/12 CONL/12 CNTKTD/12  CTTLDAYS/8 CLS_TKT/12
MATCH RANK_VALUE LVL_PRINT
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
 
 
TABLE FILE RNK1
SUM LST.LIST LVL_PRINT
WHERE LVL_PRINT NE 'ALL OTHER'
ON TABLE HOLD AS LASTRANK
END
-RUN
 
-IF &RECORDS EQ 0 THEN GOTO NOADD;
 
 
DEFINE FILE LASTRANK
NRANK/D6 = LIST + 1;
NRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
FRANK/A6  = EDIT(NRANKX,'$999999');
CAVOL/P12.2 =  0;
CNTKT/P12 =  0;
CNTKTD/P12 =  0;
CLSAV/P12.2 =  0;
CADVT/P12 = 0;
CADVT1/P12 =  0;
CADVT2/P12 =  0;
CONL/P12 =  0;
CTTLDAYS/P8 = 0;
CLS_TKT/P12 = 0;
 RANK_VALUE/A9 = 'ALL OTHER' ;
 LVL_PRINT/A25=  'ALL OTHER';
END
TABLE FILE LASTRANK
PRINT NRANK LVL_PRINT RANK_VALUE CAVOL CNTKT  CLSAV  CADVT CADVT1 CADVT2 CONL CNTKTD  CTTLDAYS CLS_TKT
ON TABLE HOLD AS LASTRANK1
END
-RUN
 
TABLE FILE LASTRANK1
PRINT LVL_PRINT RANK_VALUE CAVOL CNTKT  CLSAV  CADVT CADVT1 CADVT2 CONL CNTKTD  CTTLDAYS CLS_TKT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIEREJ
LOG DUPL MSG OFF
FIXFORM LVL_PRINT/25 RANK_VALUE/9 CAVOL/12 CNTKT/12 CLSAV/12 CADVT/12 CADVT1/12  CADVT2/12 CONL/12 CNTKTD/12  CTTLDAYS/8 CLS_TKT/12
MATCH RANK_VALUE LVL_PRINT
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
 
-NOADD
DEFINE FILE RNK2
LVLP/A27 = ''''||LVL_PRINT||'''';
END
-RUN
TABLE FILE RNK2
PRINT LVLP
BY LVL_PRINT NOPRINT
ON TABLE SAVE AS LEVELF
END
-RUN
 

-*********************************************************************************
-* Gather/Add Car Info to QRHIER File Based upon
-*********************************************************************************
 
-IF &&FROMAVRP NE 'Y' GOTO :AVW1;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:AVW1
 
EX CARUSE
-RUN
 
 
-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN
 
-INCLUDE RESETCPM
-RUN
 
 
-SET &WHATDATEC = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'INVOICE_DATE' ELSE 'PICKUP_DATE';
 
 
 
DEFINE FILE CR_50
 LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
 RENTAL_DAYS/P12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
END
 
-RUN
TABLE FILE CR_50
SUM  RENTAL_AMT
BY LEVEL_DESC
 
-IF &&FROMAVRP EQ 'Y' GOTO :AVW2;
WHERE (&WHATDATEC.EVAL GE '&&FROMDT.EVAL') AND (&WHATDATEC.EVAL LE '&&TODT.EVAL')
-GOTO :AVW2_CONT;
-:AVW2
WHERE (&WHATDATEC.EVAL GE &&FROMDT.EVAL) AND (&WHATDATEC.EVAL LE &&TODT.EVAL)
-:AVW2_CONT
-INCLUDE RPTPARMS
ON TABLE HOLD AS CAR1
END
-RUN
 
 
 
 
 
TABLE FILE CAR1
SUM RENTAL_AMT/P12.2 AS 'CARVOL'
BY LEVEL_DESC
ON TABLE HOLD AS CAR2
END
-RUN
 
 
-******************************************************
-* Matching Hierarchy Levels
-******************************************************
 
TABLE FILE CAR2
SUM CARVOL
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARMREC
END
-RUN
 
 
 
 
TABLE FILE CARMREC
SUM CARVOL
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS CARMREC1
END
-RUN
 
TABLE FILE CARMREC1
SUM CARVOL  
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIEREJ
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12  
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL  
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
 
-**********************************************************************
-* Non Matching information ALL OTHER
-**********************************************************************
 
TABLE FILE CAR2
SUM CARVOL
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARNREC
END
-RUN
 
 
 
DEFINE FILE CARNREC
LVL_PRINT/A25 = 'ALL OTHER';
END
-RUN
TABLE FILE CARNREC
SUM CARVOL 
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIEREJ
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12  
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL 
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
  

 

-*********************************************************************************
-* Gather/Add HTL Info to QRHIEREJ File
-*********************************************************************************
FILEDEF RPTPARMS CLEAR
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVW3;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:AVW3
 
EX HTLUSE
-RUN
 
-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN
 
-INCLUDE RESETHPM
-RUN
 
-SET &WHATDATEH = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'INVOICE_DATE' ELSE 'CHKIN_DATE';
 
 
 
DEFINE FILE HR_50
 LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
 NBR_DAYS/P12=NUM_DAYS;
 NEW_TOTAL_AMT/P12.2 = TOTAL_AMT;
 NNROOMS/P12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
  CITY_ST/A35 = CITY.CITY_NAME||(' '| CITY.STATE_CODE);
HOME_RMS/P12 = IF CITY_ST EQ 'ONTARIO CA' OR 'ST LOUIS MO' OR 'PHOENIX AZ' THEN NNROOMS ELSE 0;
NONH_RMS/P12 = IF CITY_ST NE 'ONTARIO CA' OR 'ST LOUIS MO' OR 'PHOENIX AZ' THEN NNROOMS ELSE 0;

END
-RUN
 
TABLE FILE HR_50
PRINT NEW_TOTAL_AMT
    HOME_RMS
    NONH_RMS
    NNROOMS
    CITY_ST
BY LEVEL_DESC
-IF &&FROMAVRP EQ 'Y' GOTO :AVW4;
WHERE (&WHATDATEH.EVAL GE '&&FROMDT.EVAL') AND (&WHATDATEH.EVAL LE '&&TODT.EVAL')
-GOTO :AVW4_CONT;
-:AVW4
WHERE (&WHATDATEH.EVAL GE &&FROMDT.EVAL) AND (&WHATDATEH.EVAL LE &&TODT.EVAL)
-:AVW4_CONT

-INCLUDE RPTPARMS
ON TABLE HOLD AS HTL1
END
-RUN
 

 
-**************************************************************************
-* Matching Hotel Records
-**************************************************************************

TABLE FILE HTL1
SUM NEW_TOTAL_AMT AS 'HTLVOL' 
    NNROOMS AS 'HTLDAY'
    HOME_RMS AS 'HOMEPRMS'
    NONH_RMS AS 'NHOMERMS'
    
BY LEVEL_DESC
ON TABLE HOLD AS HTL2
END
-RUN



TABLE FILE HTL2
SUM HTLDAY HTLVOL HOMEPRMS NHOMERMS
BY LEVEL_DESC
ON TABLE HOLD AS HTL3
END
-RUN


TABLE FILE HTL3
SUM  HOMEPRMS NHOMERMS HTLVOL HTLDAY
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLMREC
END
-RUN
 
TABLE FILE HTLMREC
SUM HTLDAY HOMEPRMS NHOMERMS HTLVOL
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS HTLMREC1
END
-RUN
 
TABLE FILE HTLMREC1
SUM HOMEPRMS NHOMERMS HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIEREJ
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 HOMEPRMS/12 NHOMERMS/12 HTLVOL/12 HTLDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE HOMEPRMS NHOMERMS HTLVOL HTLDAY
ON NOMATCH REJECTHOMEPRMS
DATA ON SAVE
END
-RUN
 
-**********************************************************************
-* Non Matching ALL OTHER Records
-**********************************************************************
TABLE FILE HTL3
SUM HOMEPRMS NHOMERMS HTLVOL HTLDAY
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLNREC
END
-RUN
 
 
DEFINE FILE HTLNREC
LVL_PRINT/A25 = 'ALL OTHER';
END
-RUN
TABLE FILE HTLNREC
SUM  HOMEPRMS NHOMERMS HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE HOLD AS HTLNREC1
END
-RUN
 
TABLE FILE HTLNREC1
SUM HOMEPRMS NHOMERMS HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE QRHIEREJ
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 HOMEPRMS/12 NHOMERMS/12 HTLVOL/12 HTLDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE HOMEPRMS NHOMERMS HTLVOL HTLDAY
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN



 

 
 
-SUMMARY
 

DEFINE FILE QRHIEREJ
RANK_VALUEA/A9 = 'TOTAL';
LVL_PRINTA/A19 = 'TOTAL';
END
-RUN
 
TABLE FILE QRHIEREJ
SUM       
COMPUTE TOTSPD/P12.2M = CAVOL + CARVOL + HTLVOL;
CAVOL/P12.2M
CNTKT
COMPUTE CONLP/P12  = IF CNTKTD EQ 0 THEN 0 ELSE ((CONL/CNTKTD)*100);
COMPUTE ADVAP/P8 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE
            (CTTLDAYS/CNTKT);
COMPUTE CADVP2/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT1/CNTKT)*100);
COMPUTE CADVP1/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT/CNTKT)*100);

COMPUTE CADVP3/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT2/CNTKT)*100);
CLSAV/P12.2M
COMPUTE CLSTPCT/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CLS_TKT/CNTKT)*100);
COMPUTE AVGLS/P12.2 = IF CLS_TKT EQ 0 THEN 0 ELSE (CLSAV/CLS_TKT);
COMPUTE HOMEPCT/P12 = IF HTLDAY EQ 0 THEN 0 ELSE ((HOMEPRMS/HTLDAY)*100);
COMPUTE NHOMEPCT/P12 = IF HTLDAY EQ 0 THEN 0 ELSE ((NHOMERMS/HTLDAY)*100);
BY RANK_VALUEA AS 'RANK_VALUE'
BY LVL_PRINTA
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS TTL1
END
-RUN



 
 
TABLE FILE TTL1
SUM TOTSPD/P12M CAVOL/P12M CNTKT/P12 CONLP/P12 ADVAP/P8 CADVP2/P12 CADVP1/P12 CADVP3/P12 CLSAV/P12M 
    CLSTPCT/P12 AVGLS/P12M HOMEPCT/P12 NHOMEPCT/P12
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TTL2
END
-RUN
 
TABLE FILE TTL2
PRINT TOTSPD CAVOL CNTKT CONLP ADVAP CADVP2 CADVP1 CADVP3 CLSAV CLSTPCT AVGLS HOMEPCT NHOMEPCT
BY RANK_VALUE
BY LVL_PRINT
 
ON TABLE HOLD AS TTL3 FORMAT FOCUS
END
-RUN
 
 
DEFINE FILE QRHIEREJ
NWLVL_PRINT/A19 = EDIT(LVL_PRINT, '9999999999999999999');
END
TABLE FILE QRHIEREJ
SUM      
COMPUTE TOTSPD/P12.2M = CAVOL + CARVOL + HTLVOL;
CAVOL/P12.2M
CNTKT
COMPUTE CONLP/P12  = IF CNTKTD EQ 0 THEN 0 ELSE ((CONL/CNTKTD)*100);
COMPUTE ADVAP/P8 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE
            (CTTLDAYS/CNTKT);
COMPUTE CADVP2/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT1/CNTKT)*100);
COMPUTE CADVP1/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT/CNTKT)*100);

COMPUTE CADVP3/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CADVT2/CNTKT)*100);
CLSAV/P12.2M
COMPUTE CLSTPCT/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((CLS_TKT/CNTKT)*100);
COMPUTE AVGLS/P12.2 = IF CLS_TKT EQ 0 THEN 0 ELSE (CLSAV/CLS_TKT);
COMPUTE HOMEPCT/P12 = IF HTLDAY EQ 0 THEN 0 ELSE ((HOMEPRMS/HTLDAY)*100);
COMPUTE NHOMEPCT/P12 = IF HTLDAY EQ 0 THEN 0 ELSE ((NHOMERMS/HTLDAY)*100);
BY RANK_VALUE
BY NWLVL_PRINT AS 'LVL_PRINT'
ON TABLE HOLD AS TLEV1
END
-RUN




TABLE FILE TLEV1
SUM TOTSPD/P12M CAVOL/P12M CNTKT/P12 CONLP/P12 ADVAP/P8 CADVP2/P12 CADVP1/P12 CADVP3/P12 CLSAV/P12M  
    CLSTPCT/P12 AVGLS/P12M HOMEPCT/P12 NHOMEPCT/P12
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV2
END
-RUN
 
 
TABLE FILE TLEV2
PRINT TOTSPD CAVOL CNTKT CONLP ADVAP CADVP2 CADVP1 CADVP3 CLSAV CLSTPCT AVGLS HOMEPCT NHOMEPCT

BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV3 FORMAT FOCUS
END
-RUN
 
 
 
-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &PHOLD1=&HLDPATH || 'TLEV3.FOC';
-SET &PHOLD2=&HLDPATH || 'TTL3.FOC';
-RUN
 
USE CLEAR *
-RUN
 
EX SUUSES
-RUN
 
USE ADD
&PHOLD1 AS TLEV3
&PHOLD2 AS TLEV3
END
-RUN
 
 
DEFINE FILE TLEV3
RANK_VAL1/A9 = IF RANK_VALUE EQ 'TOTAL' THEN 'ZZ' ELSE RANK_VALUE;
END
-RUN
 
TABLE FILE TLEV3
SUM TOTSPD CAVOL CNTKT CONLP ADVAP CADVP2 CADVP1 CADVP3 CLSAV CLSTPCT AVGLS HOMEPCT NHOMEPCT
COMPUTE ZFLAG/A1 = IF (RANK_VAL1 EQ 'ALL OTHER') AND (TOTSPD EQ 0) THEN 'Y' ELSE 'N';
COMPUTE ZFLAG2/A1 = IF (LVL_PRINT EQ ' ') AND (TOTSPD EQ 0) THEN 'Y' ELSE 'N';
BY RANK_VAL1
BY LVL_PRINT
ON TABLE HOLD AS TLEV4
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
JOIN ROLL WITH RANK_VAL1 IN TLEV4 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN
 
DEFINE FILE TLEV4
NOWTOD/A8 WITH RANK_VAL1 = HHMMSS (NOWTOD);
ROLL/A8='&&ROLLUP.EVAL';
  RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
  RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
  RN1/A60=TRIM('B', '&&RPT_TTL2.EVAL', 60, ' ', 1, 'A60');
  TDATE1/A8MDYY='&MDYY';
  TDATE2/A10=EDIT(TDATE1,'99/99/9999');
  TIME1/A8=HHMMSS('A8');
  TIME2/A8=EDIT(TIME1,'99$:99$:99');
  RD/A10 = 'Run Date:';
  RT/A10 = 'Run Time:';
  DTDESC/A20='Transaction Dates';
  CUR/A15='Current Period:';
  PRI/A15='Prior Period:';
  SPACE/A1 = 'A';
  KEY/A1 = IF RANK_VAL1 EQ 'TOTAL' THEN 'B' ELSE 'A';
END
-RUN

 

TABLE FILE TLEV4
HEADING
-IF '&&OUTFMT.EVAL' EQ 'PDF' THEN GOTO HEAD2;
 
 
"&&ROLLNME &&SP1 &&ROLLCS11 &&SP3 &&RCNCT1 &&SP4 &&ROLLPHN"
"&&DTDESC  Period1: &FMM/&FMD/&FMY - &TOM/&TOD/&TOY  Period2: &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
"&&RPT_TTL1"
"&&RPT_TTL2"
 
 
-GOTO SkipHead
 
-HEAD2
"<86 &&DTDESC"
"<42 <RN <83 <CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"<42 <RN1 <83 <PRI  &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
"<76 &&ROLLNME <+0> &&SP <+0> &&ROLLCS <+0> &&SP3 <+0> &&ROLLCNCT <+0> &&SP4 <+0> &&ROLLPHN"
" "

-SkipHead

 
SUM TOTSPD/P12CM AS 'Total,Spend'
    CAVOL/P12M AS 'Air Spend'
    CNTKT/P12C AS 'Total,Airline,Tickets'
    CONLP/P4% AS 'Onl,Adopt.%'
    ADVAP/P8 AS 'Avg Adv,Pur Days'
    CADVP3/P4% AS 'Tickets,Purchased, 14+ in advance'
    CADVP1/P4% AS 'Tickets Purchased,< 6 days in advance'
    CADVP2/P4% AS 'Tickets Purchased, 7 - 13 Days in advance'
    CLSAV/P12CM AS 'Lost,Svgs'
    CLSTPCT/P6% AS '% of tickets with Lost Savings'
    AVGLS/P8.2M AS 'Avg Lost,Savings,per ticket'
    HOMEPCT/P4% AS 'Preferred,Hotel RN,Home Office Cities'
    NHOMEPCT/P4% AS 'Preferred, Hotel RN , Non Home Office Cities'
BY RANK_VAL1 NOPRINT
BY KEY NOPRINT
BY LVL_PRINT AS 'HIERARCHY'
WHERE ZFLAG EQ 'N'
WHERE ZFLAG2 EQ 'N'
 
 
ON TABLE SET PAGE-NUM OFF
&&OUTLINE1
&&OUTLINE2
FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
-* "</1"
-* "<15 &M1.EVAL <45 &M2.EVAL <75 &M3.EVAL"
"<60 &M1.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
-* " "
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-IF '&&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO NOB;
 
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.050000, RIGHTMARGIN=0.050000,
    TOPMARGIN=0.00000, BOTTOMMARGIN=0.010000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
 
 

 
 
 
TYPE=DATA,
     GRID=OFF,
     SIZE=7,
     STYLE=NORMAL,
     BACKCOLOR=('WHITE' RGB(239 239 239)),
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR=RGB(5 76 149),
     BORDER-BOTTOM-COLOR=RGB(5 76 149),
     BORDER-LEFT-COLOR=RGB(5 76 149),
     BORDER-RIGHT-COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
     WHEN=N1 NE 'ZZ',
$
-NOB
TYPE=REPORT, FONT='ARIAL', SIZE=7, STYLE=NORMAL, BACKCOLOR=NONE, JUSTIFY=LEFT, $
TYPE=REPORT, WRAP=1,$
TYPE=REPORT, COLUMN=N3, WRAP= 1.50,$
TYPE=REPORT, COLUMN=N6, WRAP= 1.25,$
TYPE=REPORT, COLUMN=N7, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N8, WRAP=  .75,$
TYPE=REPORT, COLUMN=N9, WRAP=  .75,$
TYPE=REPORT, COLUMN=N10, WRAP= .75,$
TYPE=REPORT, COLUMN=N11, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N12, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N13, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N14, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N15, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N16, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N17, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N18, WRAP= 1.0,$
TYPE=REPORT, COLUMN=N19, WRAP= 1.0,$
-IF &&OVERDRILL EQ 'Y' GOTO OVERDRILL;

TYPE=DATA, COLUMN=N5 , STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='A'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
-GOTO DEB;
TYPE=DATA, COLUMN=N6, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='N'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
TYPE=DATA, COLUMN=N7, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='L'     \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
TYPE=DATA, COLUMN=N9, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='T'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
TYPE=DATA, COLUMN=N11, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='6'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
TYPE=DATA, COLUMN=N12, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='7'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
 
TYPE=DATA, COLUMN=N13, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='P'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
TYPE=DATA, COLUMN=N14, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='O'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
 
TYPE=DATA, COLUMN=N15, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='R'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
TYPE=DATA, COLUMN=N17, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMCAR' ROW_LABEL=N1 LSEL=LVL_PRINT   \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
TYPE=DATA, COLUMN=N18, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMHTL' ROW_LABEL=N1 LSEL=LVL_PRINT   \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),
$
-DEB
-OVERDRILL;
-IF '&&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO NOB2;
 
TYPE=HEADING,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$
 
TYPE=HEADING,
     SIZE=9,
     LINE=2,
     ITEM=1,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$
 
TYPE=HEADING,
     SIZE=9,
     LINE=3,
     ITEM=1,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$
 
TYPE=TITLE,
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
     GRID=ON,
     SIZE=7,
     COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
$
-GOTO PDFFMT
 
-NOB2
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.300000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=9, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=9, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     BACKCOLOR=(RGB(242 242 242) 'WHITE' ),$
-JOY
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$
 
TYPE=TITLE, JUSTIFY=LEFT, $
 
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=FOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=SUBHEAD,
     STYLE=BOLD,
$
 
TYPE=SUBTOTAL,
     STYLE=BOLD,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $
 
TYPE=GRANDTOTAL, STYLE=BOLD, $
 
-GOTO XSTY
 
-PDFFMT
 
TYPE=DATA, BACKCOLOR=NONE, GRID=OFF, SIZE=7,STYLE=BOLD, FONT='TIMES NEW ROMAN', JUSTIFY=LEFT, WHEN=N1 EQ 'ZZ',$
TYPE=SUBTOTAL, STYLE=BOLD, $
TYPE=FOOTING, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=BOLD, COLOR=RGB(70 136 177), $
TYPE=TABFOOTING, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=BOLD, COLOR=RGB(70 136 177), $
 
TYPE=REPORT, IMAGE=NEW_HDR.jpg, POSITION=(0 0), SIZE=(2.8 .37),PRESERVERATIO=ON, $ 
TYPE=REPORT, IMAGE=NEW_FTR.jpg, POSITION=(0 8.0), SIZE=(10.5 .58), PRESERVERATIO=ON, $     
 
-* TYPE=REPORT,GRID=OFF, $
-XSTY
ENDSTYLE
 
END
-RUN
 
 
SET SPACE = 2
 
-GOTO XXIT;
 

-NOREC
-SET &&RPTSUF = 'MSG';
-INCLUDE FDEFRPTS
-RUN
JOIN SET_KEY IN BRPTINST TO SET_KEY IN BRPT_SET AS J1
DEFINE FILE BRPTINST
EMPTY/A35 = 'NO RECORDS FOUND FOR THIS SELECTION';
NOWTOD/A8 WITH INST_KEY = HHMMSS (NOWTOD);
END
TABLE FILE BRPTINST
-INCLUDE HEADAIR
PRINT EMPTY AS 'REPORT,MESSAGE'
BY ROLLUP_CODE AS 'ROLLUP,CODE'
BY RPT_NAME AS 'REPORT,NAME'
BY SELECT_LEV AS 'BREAK,LEVEL'
IF INST_KEY EQ &&IKEY
IF RECORDLIMIT EQ 1
-INCLUDE FOOTERDT
ON TABLE SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-EXIT
 
 
-XXIT
 
