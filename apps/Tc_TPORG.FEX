-***************************************************************************
-* REPORT NAME: TOP ORIGINATION SUMMARY
-* TOTAL COMPANY REPORTING
-* 08/11/16 - DLV - S-23156 - Changed to show top 50 instead of top 10 for TCTPORG TCTPORGD TCTPORGI
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO
 
SET ASNAMES=ON
 
 
CREATE FILE TCORG
-RUN
 
CREATE FILE TCORG1
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCORGEX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-IF &&SETF EQ 'TCTPORDS' OR 'TCTPORD' OR 'TCTPORDD' OR 'TCTPORDI' OR 'TCTPORI' OR 'TCTPOFD' THEN GOTO :ORGDST;
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
-SET &&RANK_METH   = NET_TKT_CNT;
 
-INCLUDE SetParmtime
-RUN
 
 
TABLE FILE TCORG
SUM FARE
    MILES
    NET_TKT_CNT
BY ORG_APCX
BY ORG_APC
ON TABLE HOLD AS HOLD1
END
-RUN
 
TABLE FILE HOLD1
PRINT FARE
      MILES
      NET_TKT_CNT
      ORG_APC
BY ORG_APCX
ON TABLE HOLD AS HOLD2 FORMAT FOCUS INDEX ORG_APCX
END
-RUN
 
JOIN HOLD2.ORG_APCX IN HOLD2 TO AIRPORT.APCODEX IN AIRPORT AS J2
 
TABLE FILE HOLD2
PRINT
    FARE
    MILES
    NET_TKT_CNT
    AIRPORT_NAME AS 'OAP_NAME'
RANKED BY HIGHEST &&RANK_METH
BY ORG_APC
ON TABLE HOLD AS RNKHLD2
END
-RUN
 
-SET &&RANK_LIMIT  = 50;
DEFINE FILE RNKHLD2
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = IF ORG_APC NE LAST ORG_APC THEN (LAST LIST + 1) ELSE LAST LIST;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                  CRANK;
  RANK_VAL/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                IF RANK GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                ARANK;
  RANK_LAB1/A25 = IF RANK_VAL NE 'ALL OTHER' THEN &&RANK_LABEL1
                  ELSE 'ALL OTHER';
  RANK_LABEL1/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL1
                      ELSE ' ';
  RANK_LABEL2/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL2
                      ELSE ' ';
  RANK_LABEL3/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL3
                      ELSE ' ';
OAP_NM/A25 = IF RANK_VALUE NE 'ALL OTHER' THEN OAP_NAME ELSE ' ';
 
END
 
 
 
 
TABLE FILE RNKHLD2
SUM OAP_NM AS 'OAP_NAME'
      ORG_APC
      NET_TKT_CNT
      FARE
      MILES
      NET_TKT_CNT
BY RANK_VALUE
BY RANK_LABEL1
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
JOIN CLEAR *
-RUN
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
-TYPE BEFORE DEFINES
DEFINE FILE AIRHOLD3
NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
 
 
TABLE FILE AIRHOLD3
-INCLUDE &&PAHDR
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-GOTO XXIT;
 
-********** Version Ranked by ORG_APC|DST_APC
 
-:ORGDST
-IF &&SETF EQ 'TCTPORDD' OR 'TCTPORDI' GOTO TKTS;
-SET &&RANK_METH   = TFARE;
-SET &&RANK_LIMIT = 100;
-GOTO DONESETS;
-TKTS
-SET &&RANK_METH   = TNTKT;
-SET &&RANK_LIMIT = 100;
-DONESETS
-INCLUDE SetParmtime
-RUN
 
 
TABLE FILE TCORG1
SUM FARE
    MILES
    NET_TKT_CNT
BY ORGDST
BY ORG_APCX
BY ORG_APC
BY DST_APCX
BY DST_APC
BY C_ITIN
ON TABLE HOLD AS HOLD1
END
-RUN
 
TABLE FILE HOLD1
PRINT FARE
      MILES
      NET_TKT_CNT
      ORGDST
      DST_APC
      DST_APCX
      C_ITIN
      ORG_APC
BY ORG_APCX
ON TABLE HOLD AS HOLD2 FORMAT FOCUS INDEX ORG_APCX
END
-RUN
 
TABLE FILE AIRPORT
PRINT AIRPORT_NAME
BY APCODEX
ON TABLE HOLD AS OAPHLD FORMAT FOCUS INDEX APCODEX
END
-RUN
 
JOIN HOLD2.ORG_APCX IN HOLD2 TO OAPHLD.APCODEX IN OAPHLD AS J2
-RUN
 
TABLE FILE HOLD2
SUM
    FARE
    MILES
    NET_TKT_CNT
    ORGDST
BY ORG_APC
BY AIRPORT_NAME AS 'OAP_NAME'
BY DST_APCX
BY DST_APC
BY C_ITIN
ON TABLE HOLD AS HOLD3A
END
-RUN
 
TABLE FILE HOLD3A
PRINT FARE
      MILES
     NET_TKT_CNT
     OAP_NAME
     ORG_APC
     ORGDST
     C_ITIN
     DST_APCX
BY DST_APCX
ON TABLE HOLD AS HOLD3 FORMAT FOCUS INDEX DST_APCX
END
-RUN
 
TABLE FILE AIRPORT
PRINT AIRPORT_NAME
BY APCODEX
ON TABLE HOLD AS DAPHLD FORMAT FOCUS INDEX APCODEX
END
-RUN
 
JOIN HOLD3.DST_APCX IN HOLD3 TO DAPHLD.APCODEX IN DAPHLD AS J3
-RUN
 
TABLE FILE HOLD3
SUM FARE
    MILES
    NET_TKT_CNT
BY ORGDST
BY OAP_NAME
BY AIRPORT_NAME AS 'DAP_NAME'
BY C_ITIN
ON TABLE HOLD AS HOLD4A
END
-RUN
 
 
TABLE FILE HOLD4A
SUM FARE
    MILES
    NET_TKT_CNT
BY ORGDST
BY OAP_NAME
BY DAP_NAME
BY C_ITIN
WHERE NET_TKT_CNT NE 0
ON TABLE HOLD AS HOLD4
END
-RUN
 
TABLE FILE HOLD4
SUM FARE AS 'TFARE'
    MILES AS 'TMILES'
    NET_TKT_CNT AS 'TNTKT'
BY ORGDST
ON TABLE HOLD AS TOTHLD
END
-RUN
 
MATCH FILE HOLD4
PRINT OAP_NAME
      DAP_NAME
      C_ITIN
      FARE
      MILES
      NET_TKT_CNT
BY ORGDST
ON TABLE HOLD
RUN
FILE TOTHLD
SUM TFARE
    TMILES
    TNTKT
BY ORGDST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN
 
-IF &&SETF NE 'TCTPORDS' GOTO NOSET;
-SET &&RANK_METH = 'NET_TKT_CNT';
-SET &&RANK_LIMIT  = 9999;
 
 
-NOSET;
 
 
TABLE FILE HOLD5
PRINT FARE
    MILES
    NET_TKT_CNT
    TFARE
    TMILES
    TNTKT
    ORGDST
    OAP_NAME
    DAP_NAME
    C_ITIN
RANKED BY HIGHEST &&RANK_METH
ON TABLE HOLD AS RNKHLD2
END
-RUN
 
 
DEFINE FILE RNKHLD2
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = IF ORGDST NE LAST ORGDST THEN (LAST LIST + 1) ELSE LAST LIST;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE CRANK;
  RANK_VAL/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                IF RANK GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                ARANK;
 
-* RANK_LAB1/A25 = IF RANK_VAL NE 'ALL OTHER' THEN &&RANK_LABEL1
-*                 ELSE 'ALL OTHER';
 
  ORGDST_PRNT/A10 = IF RANK_VALUE NE 'ALL OTHER' THEN ORGDST ELSE 'ALL OTHER';
 
  RANK_LABEL1/A25 = IF ORGDST_PRNT NE 'ALL OTHER' THEN &&RANK_LABEL1
                      ELSE ' ';
  RANK_LABEL2/A25 = IF ORGDST_PRNT NE 'ALL OTHER' THEN &&RANK_LABEL2
                      ELSE ' ';
  RANK_LABEL3/A67 = IF ORGDST_PRNT NE 'ALL OTHER' THEN &&RANK_LABEL3
                      ELSE ' ';
-*   ORGDST_NM/A51 = IF RANK_VALUE NE 'ALL OTHER' THEN ORGDST1 ELSE ' ';
 
END
-RUN
 
 
TABLE FILE RNKHLD2
SUM FARE
    TFARE
    MILES
    TMILES
    NET_TKT_CNT
    TNTKT
BY RANK_VALUE
BY ORGDST_PRNT
BY RANK_LABEL1
BY RANK_LABEL2
BY RANK_LABEL3
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
JOIN CLEAR *
-RUN
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
-TYPE BEFORE DEFINES
DEFINE FILE AIRHOLD3
NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
END
 
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE AIRHOLD3
-INCLUDE &&PAHDR
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
 
-XXIT
