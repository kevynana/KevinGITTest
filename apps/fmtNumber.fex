-******************************************************************************
-* This routine is used to format dollar values, and Max value is 12 digits(D12).
-*
-* The input value should not have $ sign, and comma. If a comma is included, it 
-* assumes it has the required format and return the original value. The decimal
-* point is an optional.
-* The Variable ALIGNED is used to indicate if the value returned needs to add 
-* extra spaces in order to align with other values.
-*
-* The ouput format should be $99,999.99, and the return value is always assigned
-* to a global variable &&RETVAL.
-* The command to execute this should be 
-*     EX FMTNUMBER VALUE=123456789.2, DOLLAR='Y',LEN=10,ALIGNED='N'
-*     EX FMTNUMBER VALUE=123456789.2, DOLLAR='Y',ALIGNED='N'
-* Date: 03/30/2006
-******************************************************************************
-*-INCLUDE SETECHO
-DEFAULT &VALUE='999.99';
-DEFAULT &DOLLAR = 'Y';
-DEFAULT &LEN = 8;
-DEFAULT &ALIGNED = 'Y';
-SET &DFLEN = &LEN;
-SET &LEN = ARGLEN(20, &VALUE, 'I2');
-IF &LEN GT 12 OR &LEN EQ 0 THEN GOTO NOFMT;

-*retain the original length
-SET &VALUE2 = &VALUE | 'X';
-SET &DPOS = POSIT(&VALUE2, 20, 'X', 1, 'I2');
-SET &OLEN = &DPOS - 1;

-SET &TEMP = TRIM('B', &VALUE, 12, ' ', 1, 'A12');
-SET &LEN = ARGLEN(12, &TEMP, 'I2');
-*comma
-SET &CPOS = POSIT(&TEMP, &LEN, ',', 1, 'I1');
-IF &CPOS GT 0 THEN GOTO NOFMT;
-*decimal
-SET &DPOS = POSIT(&TEMP, &LEN, '.', 1, 'I2');

-SET &DLEN = IF &DPOS GT 0 THEN (&LEN - &DPOS + 1) ELSE 0;
-SET &ILEN = &LEN - &DLEN;

-SET &IPART = IF &DPOS GT 0 THEN
-  SUBSTR(&LEN, &TEMP, 1, &ILEN, &ILEN, 'A&ILEN.EVAL') ELSE &TEMP;
-SET &DLEN = IF &DLEN EQ 0 THEN 1 ELSE &DLEN;
-SET &DPART = IF &DPOS GT 0 THEN
-  SUBSTR(&LEN, &TEMP, &DPOS, &LEN, &DLEN, 'A&DLEN.EVAL') ELSE '';

-SET &TEMP = 
-      IF &ILEN EQ 12 THEN EDIT(&IPART, '999,999,999,999')
- ELSE IF &ILEN EQ 11 THEN EDIT(&IPART, '99,999,999,999')
- ELSE IF &ILEN EQ 10 THEN EDIT(&IPART, '9,999,999,999')
- ELSE IF &ILEN EQ 9 THEN EDIT(&IPART, '999,999,999')
- ELSE IF &ILEN EQ 8 THEN EDIT(&IPART, '99,999,999')
- ELSE IF &ILEN EQ 7 THEN EDIT(&IPART, '9,999,999')
- ELSE IF &ILEN EQ 6 THEN EDIT(&IPART, '999,999')
- ELSE IF &ILEN EQ 5 THEN EDIT(&IPART, '99,999')
- ELSE IF &ILEN EQ 4 THEN EDIT(&IPART, '9,999')
- ELSE IF &ILEN EQ 3 THEN EDIT(&IPART, '999')
- ELSE IF &ILEN EQ 2 THEN EDIT(&IPART, '99')
- ELSE IF &ILEN EQ 1 THEN EDIT(&IPART, '9')
- ELSE '';
-SET &TEMP = &TEMP | &DPART;

-SET &VALUE = IF &DOLLAR EQ 'Y' THEN '$' | &TEMP ELSE &TEMP;

-NOFMT
-SET &VALUE2 = &VALUE | 'X';
-SET &DPOS = POSIT(&VALUE2, 20, 'X', 1, 'I2');
-SET &LEN = &DPOS - 1;
-SET &TEMP = RJUST(&LEN,'&VALUE.EVAL','A&LEN.EVAL');
-SET &DIFF = &DFLEN - &LEN;
-IF &ALIGNED EQ 'N' THEN GOTO ENDFMT2;
-IF &LEN GE &DFLEN THEN GOTO ENDFMT2;
-REPEAT ENDFMT2 &DIFF TIMES
-SET &TEMP = ' ' | '&TEMP.EVAL';
-ENDFMT2

-SET &VALUE = &TEMP;
-SET &&RETVAL = &VALUE;
