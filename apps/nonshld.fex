-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 2/23/05 - DEB - ADDED CODE FOR GLOBAL CURRENCY
-* File nonshld.FEX
-* This program extracts data for the following reports for the Non compliance 
-* Overview:
-* Advanced Purchase, Booking Type, Refundable/Non-Refundable
-* Lost Savings
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492

DEFINE FILE &&EXTRACT
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX ;
-* DEFINES ON PREVIOUS DEFINES
NET_TKT_CNT/D12      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
-INCLUDE TRANTYPE
END

TABLEF FILE &&EXTRACT
PRINT SEG_AMT AND SEG_TAX AND TRN_DATE AND TICKET_CODE AND SEG_NBR
AND SEG_COUNT AND CONJ_REL AND EX_FLAG AND RF_FLAG AND SEG_STANDARD
AND SEG_LOWEST AND AIRLINE AND EMBARK AND ROUTE AND CL_CAT 
AND BR_CL_IDX AND LEVEL1 AND LEVEL2 AND REFUSE_CODE AND FARE_PAID
AND ADV_PURCH AND DOD_TYPE AND NONREF_FLAG AND NET_TKT_CNT AND TKT_DATE

 WHERE VOID.VOID_DATE EQ 0
 WHERE (TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL) OR
       (TRN_DATE GE &&FXDATE.EVAL AND TRN_DATE LE &&TXDATE.EVAL)

&&WHERE1
&&WHERE2
&&WHERE3

-INCLUDE RPTPARMS
ON TABLE HOLD AS GNONH 
END
-RUN

TABLE FILE GNONH 
PRINT SEG_AMT
      SEG_TAX
      TRN_DATE
      TICKET_CODE
      SEG_NBR
      SEG_COUNT
      CONJ_REL
      EX_FLAG
      RF_FLAG
      SEG_STANDARD 
      SEG_LOWEST
      AIRLINE
      EMBARK
      ROUTE
      CL_CAT
      BR_CL_IDX
      LEVEL1
      LEVEL2
      REFUSE_CODE
      FARE_PAID
      ADV_PURCH
      DOD_TYPE
      NONREF_FLAG
      NET_TKT_CNT
BY TKT_DATE
ON TABLE HOLD AS GNONHLD2 FORMAT FOCUS INDEX TKT_DATE
END
-RUN

-* Global Reporting JOIN
JOIN GNONHLD2.TKT_DATE IN GNONHLD2 TO GLOBAL.DATE IN GLOBAL AS J1


DEFINE FILE GNONHLD2
CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;
DISSAVINGS/D12.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;


-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
PAID_FAREX/D20.6 = FARE_PAID * CURR1X;
GSEG_AMT/D20.6 = SEG_AMT * CURR1X;
GSEG_TAX/D20.6 = SEG_TAX * CURR1X;
DISSAVEX/D20.6 = DISSAVINGS * CURR1X;

END
-RUN 

TABLE FILE GNONHLD2
PRINT SEG_AMT
      SEG_TAX
      TRN_DATE
      TICKET_CODE
      SEG_NBR
      SEG_COUNT
      CONJ_REL
      EX_FLAG
      RF_FLAG
      SEG_STANDARD 
      SEG_LOWEST
      AIRLINE
      EMBARK
      ROUTE
      CL_CAT
      BR_CL_IDX
      LEVEL1
      LEVEL2
      REFUSE_CODE
      FARE_PAID
      ADV_PURCH
      DOD_TYPE
      NONREF_FLAG
      NET_TKT_CNT
      NAMEC
      PAID_FAREX/D16.2CS
      GSEG_AMT/D16.2CS
      GSEG_TAX/D16.2CS
      DISSAVINGS
      DISSAVEX/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS GNONHLD
END
-RUN   	 



-*-EXIT
