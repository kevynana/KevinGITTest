-*======================================================================
-*  DATE        PROGRAMMER  CHANGE
-*  ----        ----------  ---------------------------------
-*  08/27/2015  GSE         S-10446 Initial version
-*  03/22/2017  GSE         S-32051 Changed ROLLUP reference to
-*                          ROLLUP_SQL
-*  04/12/2017  GSE         S-30062 Added logic to delete FS* file
-*                          records.
-*======================================================================
-* File air_ticket_delete.fex
-TYPE air_ticket_delete
-SET &LOOPSTART = 0;
-SET &LOOPEND = 11;
-SET &NOW = HHMMSS('A8');
-RUN
-TYPE BEGINNING &NOW
-RUN
-*
-DEFAULT &ROLLUP = ' ';
-DEFAULT &TKT_NUM = ' ';
-DEFAULT &TASF = ' ';
-*
-IF '&ROLLUP.EVAL' EQ ' ' OR '&TKT_NUM.EVAL' EQ ' ' OR ('&TASF.EVAL' NE 'M' OR 'D' OR 'N') GOTO PARAMERROR;
-*
SET HOLDLIST = PRINTONLY
SET ASNAMES = ON
SET PCOMMA = ON
SET FIXFRMINPUT = NONCOND
SET MESSAGE = OFF
-*
EX REVIEW_50_SQL_LOADS/concat_all_dbs_bench ROLL=&ROLLUP.EVAL,FID=SEG,CLEAR=Y
-*
TABLE FILE SR_50
SUM
      LEVEL1          LEVEL2          LEVEL3          LEVEL4          LEVEL5          LEVEL6          LEVEL7
      INT_CODE        AROLL_LEV1      AROLL_LEV2      AROLL_LEV3      AROLL_LEV4      AROLL_LEV5      AROLL_LEV6
      AROLL_LEV7      AROLL_LEV8      AROLL_LEV9      AROLL_LEV10     AROLL_LEV11     AROLL_LEV12     AROLL_LEV13
      AROLL_LEV14     AROLL_LEV15
BY AIR_KEY
WHERE TKT_NUM EQ '&TKT_NUM.EVAL'
ON TABLE HOLD AS AIRDATA
END
-RUN
-IF &LINES EQ 0 GOTO NOPROC;
-*
TABLE FILE SR_50
SUM
      MIN.NUD_DATA    MIN.AUD_DATA
BY AIR_KEY
BY UDID
WHERE TKT_NUM EQ '&TKT_NUM.EVAL'
ON TABLE HOLD AS UDIDDATA
END
-RUN
-*
-SET &UDRECS = &LINES;
-*
 
-*
USE CLEAR *
-RUN
-*
TABLE FILE AIRDATA
BY AIR_KEY
ON TABLE HOLD AS AKEY
END
-RUN
-*
TABLE FILE ROLLUP_SQL
PRINT COMP QTR_ENABLE
BY ROLLUP_CODE
WHERE ROLLUP_CODE EQ '&ROLLUP.EVAL'
WHERE ROLLUP_CODE NE 'ABCD2-R' OR 'ABCD3-R' OR 'TOTAL-R'
-*WHERE UPD_ENABLE EQ 'X'
WHERE COMP NE ' '
ON TABLE HOLD AS ROLLLIST
END
-RUN
-*
-READROLL
 
-READFILE ROLLLIST
-RUN
-IF &IORETURN NE 0 GOTO XXIT;
-*
 
-*
-QTRD
-SET &NOW = HHMMSS('A8');
-RUN
-TYPE &NOW &ROLLUP_CODE
-RUN
-*
-SET &AUDIDFIL = 'UA' | '&COMP.EVAL';
-SET &HUDIDFIL = 'UH' | '&COMP.EVAL';
-SET &CUDIDFIL = 'UC' | '&COMP.EVAL';
-SET &LUDIDFIL = 'UL' | '&COMP.EVAL';
-SET &TUDIDFIL = 'UT' | '&COMP.EVAL';
-SET &PFEEFILE = 'PF' | '&COMP.EVAL';
-SET &PFSVFILE = 'FS' | '&COMP.EVAL';
-*
-PROCTASF
-*
-IF '&TASF.EVAL' NE 'D' GOTO ENDDELTASF;
-*
-DELTASF
-TYPE DELETE TASF PROCESSING
-*
TABLE FILE &PFEEFILE
BY SEQNBR
WHERE AIR_KEY EQ (AKEY)
ON TABLE HOLD AS PFEEDEL
END
-RUN
-*
-TYPE &PFEEFILE &LINES DELETE
-*
SET MESSAGE = ON
MODIFY FILE &PFEEFILE
FIXFORM FROM PFEEDEL
MATCH SEQNBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON PFEEDEL
END
SET MESSAGE = OFF
-RUN
-*
-TYPE &AUDIDFIL &UDRECS + 1 KEY DELETE
-*
SET MESSAGE = ON
MODIFY FILE &AUDIDFIL
FIXFORM FROM AKEY
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON AKEY
END
SET MESSAGE = OFF
-RUN
-*
-GOTO DONETASF
-*
-ENDDELTASF
-*
-IF '&TASF.EVAL' NE 'M' GOTO ENDMOVETASF;
-*
-MOVETASF
-*
-TYPE MOVE TASF PROCESSING
-*
JOIN AIR_KEY IN AIRDATA TO MULTIPLE AIR_KEY IN &PFEEFILE TAG J0 AS J0
-*
TABLE FILE AIRDATA
PRINT
      MISC_KEY NOPRINT
      AIRDATA.AIR_KEY AS MISC_KEY
      COMPUTE NEW_AIR_KEY/A45 = ' '; AS AIR_KEY
      ID_LEVEL1 NOPRINT
      AIRDATA.LEVEL1 AS ID_LEVEL1
      ID_LEVEL2 NOPRINT
      AIRDATA.LEVEL2 AS ID_LEVEL2
      ID_LEVEL3 NOPRINT
      AIRDATA.LEVEL3 AS ID_LEVEL3
      ID_LEVEL4 NOPRINT
      AIRDATA.LEVEL4 AS ID_LEVEL4
      ID_LEVEL5 NOPRINT
      AIRDATA.LEVEL5 AS ID_LEVEL5
      ID_LEVEL6 NOPRINT
      AIRDATA.LEVEL6 AS ID_LEVEL6
      ID_LEVEL7 NOPRINT
      AIRDATA.LEVEL7 AS ID_LEVEL7
      ID_DASH NOPRINT
      AIRDATA.INT_CODE AS ID_DASH
      AROLL_LEV1 NOPRINT
      AIRDATA.AROLL_LEV1
      AROLL_LEV2 NOPRINT
      AIRDATA.AROLL_LEV2
      AROLL_LEV3 NOPRINT
      AIRDATA.AROLL_LEV3
      AROLL_LEV4 NOPRINT
      AIRDATA.AROLL_LEV4
      AROLL_LEV5 NOPRINT
      AIRDATA.AROLL_LEV5
      AROLL_LEV6 NOPRINT
      AIRDATA.AROLL_LEV6
      AROLL_LEV7 NOPRINT
      AIRDATA.AROLL_LEV7
      AROLL_LEV8 NOPRINT
      AIRDATA.AROLL_LEV8
      AROLL_LEV9 NOPRINT
      AIRDATA.AROLL_LEV9
      AROLL_LEV10 NOPRINT
      AIRDATA.AROLL_LEV10
      AROLL_LEV11 NOPRINT
      AIRDATA.AROLL_LEV11
      AROLL_LEV12 NOPRINT
      AIRDATA.AROLL_LEV12
      AROLL_LEV13 NOPRINT
      AIRDATA.AROLL_LEV13
      AROLL_LEV14 NOPRINT
      AIRDATA.AROLL_LEV14
      AROLL_LEV15 NOPRINT
      AIRDATA.AROLL_LEV15
BY SEQNBR
ON TABLE HOLD AS FEEUPDT
END
-RUN
-*
-TYPE &PFEEFILE &LINES UPDATE
JOIN CLEAR J0
-*
SET MESSAGE = ON
MODIFY FILE &PFEEFILE
FIXFORM FROM FEEUPDT
MATCH SEQNBR
ON MATCH UPDATE AIR_KEY MISC_KEY
ON MATCH UPDATE ID_DASH ID_LEVEL1 ID_LEVEL2 ID_LEVEL3 ID_LEVEL4 ID_LEVEL5 ID_LEVEL6 ID_LEVEL7
ON MATCH UPDATE AROLL_LEV1 AROLL_LEV2 AROLL_LEV3 AROLL_LEV4 AROLL_LEV5 AROLL_LEV6 AROLL_LEV7
ON MATCH UPDATE AROLL_LEV8 AROLL_LEV9 AROLL_LEV10 AROLL_LEV11 AROLL_LEV12 AROLL_LEV13 AROLL_LEV14 AROLL_LEV15
ON NOMATCH REJECT
DATA ON FEEUPDT
END
SET MESSAGE = OFF
-RUN
-*
TABLE FILE UDIDDATA
PRINT
      NUD_DATA
      AUD_DATA
BY TOTAL COMPUTE MISC_KEY/A114 = AIR_KEY;
BY UDID
ON TABLE HOLD AS UDIDUPDT
END
-RUN
-*
-TYPE &TUDIDFIL &LINES + 1 KEY INSERT
-*
SET MESSAGE = ON
MODIFY FILE &TUDIDFIL
FIXFORM FROM UDIDUPDT
MATCH MISC_KEY UDID
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON UDIDUPDT
END
SET MESSAGE = OFF
-RUN
-*
-TYPE &AUDIDFIL &UDRECS + 1 KEY DELETE
-*
SET MESSAGE = ON
MODIFY FILE &AUDIDFIL
FIXFORM FROM AKEY
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON AKEY
END
SET MESSAGE = OFF
-RUN
-*
-GOTO DONETASF
-*
-ENDMOVETASF
-*
-IF '&TASF.EVAL' NE 'N' GOTO ENDNOTASF;
-*
-TYPE NO TASF PROCESSING
-*
-TYPE &AUDIDFIL &UDRECS + 1 KEY DELETE
-*
SET MESSAGE = ON
MODIFY FILE &AUDIDFIL
FIXFORM FROM AKEY
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON AKEY
END
SET MESSAGE = OFF
-RUN
-*
-GOTO DONETASF
-*
-ENDNOTASF
-*
-DONETASF
-*
TABLE FILE &PFSVFILE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
BY PFS_SEQ_NBR
WHERE AIR_KEY EQ (AKEY)
ON TABLE HOLD AS PFSDELS
END
-RUN
-*
-IF &RECORDS EQ 0 GOTO DONEPFS;
-*
-TYPE &PFSVFILE &LINES DELETE
-*
SET MESSAGE = ON
MODIFY FILE &PFSVFILE
FIXFORM FROM PFSDELS
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR PFS_SEQ_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON PFSDELS
END
SET MESSAGE = OFF
-RUN
-*
-DONEPFS
-*
-SET &QTRCTR = &LOOPSTART.EVAL;
-SET &ENDCTR = IF &QTR_ENABLE NE 'X' THEN 1 ELSE (&LOOPEND.EVAL - &LOOPSTART.EVAL + 1);
-*TYPE &ENDCTR Times
-TYPE -----
-*
-REPEAT QTRMOD &ENDCTR TIMES;
-SET &QTR = IF &QTR_ENABLE NE 'X' THEN '_' ELSE
-           IF &QTRCTR EQ 10 THEN 'Y' ELSE
-           IF &QTRCTR EQ 11 THEN 'Z' ELSE &QTRCTR;
-SET &SMODFILE = 'S' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &SFOCFILE = '&SMODFILE.EVAL' || '.foc';
-SET &MMODFILE = 'M' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &MFOCFILE = '&MMODFILE.EVAL' || '.foc';
-SET &DMODFILE = 'D' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &DFOCFILE = '&DMODFILE.EVAL' || '.foc';
-SET &TMODFILE = 'T' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &AMODFILE = 'A' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &AFOCFILE = 'A' | '&QTR.EVAL' | '&COMP.EVAL' || '.foc';
-SET &HMODFILE = 'H' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &CMODFILE = 'C' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &LMODFILE = 'L' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &QTRCTR = &QTRCTR + 1;
-*
TABLE FILE &AMODFILE
BY AIR_KEY
WHERE AIR_KEY EQ (AKEY)
ON TABLE HOLD AS KEYCNT
END
-RUN
-IF &RECORDS EQ 0 GOTO QTRMOD;
-*
-SET &KEYRECS = &LINES;
-*
-TYPE &TMODFILE &KEYRECS DELETE
-*
SET MESSAGE = ON
MODIFY FILE &TMODFILE
FIXFORM FROM AKEY
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON AKEY
END
SET MESSAGE = OFF
-RUN
-*
TABLE FILE &DMODFILE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
WHERE AIR_KEY EQ (AKEY)
ON TABLE HOLD AS DSTDELS
END
-RUN
-*
-TYPE &DMODFILE &LINES DELETE
-*
SET MESSAGE = ON
MODIFY FILE &DMODFILE
FIXFORM FROM DSTDELS
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DSTDELS
END
SET MESSAGE = OFF
-RUN
-*
TABLE FILE &MMODFILE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
WHERE AIR_KEY EQ (AKEY)
ON TABLE HOLD AS MKTDELS
END
-RUN
-*
-TYPE &MMODFILE &LINES DELETE
-*
SET MESSAGE = ON
MODIFY FILE &MMODFILE
FIXFORM FROM MKTDELS
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON MKTDELS
END
SET MESSAGE = OFF
-RUN
-*
TABLE FILE &SMODFILE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
WHERE AIR_KEY EQ (AKEY)
ON TABLE HOLD AS SEGDELS
END
-RUN
-*
-TYPE &SMODFILE &LINES DELETE
-*
SET MESSAGE = ON
MODIFY FILE &SMODFILE
FIXFORM FROM SEGDELS
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SEGDELS
END
SET MESSAGE = OFF
-RUN
-*
-TYPE &AMODFILE &KEYRECS DELETE
-*
SET MESSAGE = ON
MODIFY FILE &AMODFILE
FIXFORM FROM AKEY
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON AKEY
END
SET MESSAGE = OFF
-RUN
-*
-QTRMOD
-*
-*GOTO READROLL
-*
-GOTO XXIT
 
-PARAMERROR
-IF '&ROLLUP.EVAL' GT ' ' GOTO OKROLL;
-TYPE You must enter a value for ROLLUP
-OKROLL
-*
-IF '&TKT_NUM.EVAL' GT ' ' GOTO OKTKT;
-TYPE You must enter a value for TKT_NUM
-OKTKT
-*
-IF ('&TASF.EVAL' EQ 'M' OR 'D' OR 'N') GOTO OKTASF;
-TYPE TASF must be 'M' or 'D' OR 'N'
-OKTASF
-*
-GOTO XXIT
-*
-NOPROC
-TYPE No Air records are found for TKT_NUM &TKT_NUM
-XXIT
