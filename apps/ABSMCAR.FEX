-*********************************************************************************
-* Gather/Add Car Info to QRHIER File Based upon 
-*********************************************************************************

 
-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
 

DEFINE FILE &&EXTRACT
 LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
 RENTAL_DAYS/P12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
END

-RUN
TABLE FILE CR_50
SUM RENTAL_DAYS
    RENTAL_AMT
BY LEVEL_DESC
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL)
-IF &&SETF NE 'ABSMHID' GOTO NoDomonly;
WHERE PICK_CTY.INTL_DOM NE 'I' 
-NoDomonly;
-IF &&SETF NE 'ABSMHII' GOTO NoIntonly;
WHERE PICK_CTY.INTL_DOM EQ 'I' 
-NoIntonly;
-INCLUDE RPTPARMS
ON TABLE HOLD AS CAR1
END
-RUN

-QUIT


TABLE FILE CAR1
SUM RENTAL_AMT/P12.2 AS 'CARVOL'
    RENTAL_DAYS/P12 AS 'CARDAY'
BY LEVEL_DESC
ON TABLE HOLD AS CAR2
END
-RUN

-******************************************************
-* Matching Hierarchy Levels
-******************************************************

TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARMREC
END
-RUN

TABLE FILE CARMREC
SUM CARVOL
    CARDAY
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS CARMREC1
END
-RUN

TABLE FILE CARMREC1
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12 CARDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL CARDAY
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN

-**********************************************************************
-* Non Matching information ALL OTHER
-**********************************************************************

TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARNREC
END
-RUN

DEFINE FILE CARNREC
LVL_PRINT/A25 = 'ALL OTHER';
END
-RUN
TABLE FILE CARNREC
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12 CARDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL CARDAY
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN