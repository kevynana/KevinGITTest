-* Created for the Advanced Purchase excluding MCOs report
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
-INCLUDE SETECHO
 
CREATE FILE TCADV
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
 
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCADVEX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
 
-INCLUDE SetParmtime
-RUN
 
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
 
 
DEFINE FILE TCADV
FLAG/A1 = 'A';
END
 
TABLE FILE TCADV
SUM FARE_PAID
    NET_TKT_CNT
    REF_EXCH_CNT
    TKT_PURCH
    ADV_PURCH
BY FLAG
BY ADV_CAT
ON TABLE HOLD AS HOLDONE
END
-RUN
 
 
DEFINE FILE TCADV
FLAG/A1 = 'A';
END
 
TABLE FILE TCADV
SUM FARE_PAID AS 'TFARE'
    NET_TKT_CNT/P12 AS 'TNTKT'
    REF_EXCH_CNT/P12 AS 'TREF'
    TKT_PURCH/P12 AS 'TPUR'
    ADV_PURCH/P12 AS 'TADV'
BY FLAG
ON TABLE HOLD AS HOLDTWO
END
-RUN
 
MATCH FILE HOLDONE
SUM FARE_PAID
    NET_TKT_CNT
    REF_EXCH_CNT
    TKT_PURCH
    ADV_PURCH
BY FLAG
BY ADV_CAT
ON TABLE HOLD
RUN
FILE HOLDTWO
SUM TFARE
    TNTKT
    TREF
    TPUR
    TADV
BY FLAG
AFTER MATCH HOLD AS HOLD3 OLD-AND-NEW
END
-RUN
 
TABLE FILE HOLD3
SUM ADV_PURCH
    FARE_PAID
    TFARE
    NET_TKT_CNT
    TNTKT
    REF_EXCH_CNT
    TREF
    TKT_PURCH
    TPUR
    TADV
BY ADV_CAT
ON TABLE HOLD AS HOLD4
END
-RUN
 
 
-SET &&RPTSUF = 'ADVPURCH';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-INCLUDE FDEFRPTS
-RUN
 
 
DEFINE FILE HOLD4
NOWTOD/A8 WITH ADV_CAT = HHMMSS (NOWTOD);
FLAG/A1 = ' ';
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE HOLD4
-INCLUDE &&PAHDR
   SUM TPUR NOPRINT TADV NOPRINT
    TKT_PURCH AS 'TICKETS,PURCH'
    COMPUTE TPURP/P8.1CS = IF ((TPUR LT 1) AND (TPUR GT (-1))) THEN 0 ELSE (TKT_PURCH/TPUR)*100; AS '% OF TKTS,PURCH'
    REF_EXCH_CNT AS 'RFNDS/,EXCHGS'
    COMPUTE TREFP/P8.1CS = IF ((TREF LT 1) AND (TREF GT (-1))) THEN 0 ELSE (REF_EXCH_CNT/TREF)*100; AS '% OF RFNDS,/EXCHGS'
    FARE_PAID/D12.2CSM AS 'FARE,PAID'
    COMPUTE TFAREP/P8.1CS = IF ((TFARE LT 1) AND (TFARE GT (-1))) THEN 0 ELSE (FARE_PAID/TFARE)*100; AS '% OF,FARE PAID'
    NET_TKT_CNT AS 'NET TKTS,PURCH'
    COMPUTE TNETP/P8.1CS = IF ((TNTKT LT 1) AND (TNTKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT/TNTKT)*100; AS '% OF NET,TKT PURCH'
    COMPUTE AVGT/D12.2CSM = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE FARE_PAID/NET_TKT_CNT; AS 'AVG,TKT,PRICE'
BY FLAG NOPRINT
BY ADV_CAT AS 'ADVANCED,PURCHASE,CATEGORY'
ON ADV_CAT RECOMPUTE MULTILINES AS 'TOTAL'
ON FLAG RECOMPUTE TKT_PURCH REF_EXCH_CNT FARE_PAID NET_TKT_CNT AVGT AS 'TOTAL FOR'
ON FLAG COMPUTE AVGDAY/P8CS = IF ((TNTKT LT 1) AND (TNTKT GT (-1))) THEN 0 ELSE (TADV/TNTKT);
FOOTING BOTTOM
"NOTE:"
"eTTek Review &DATE AT <NOWTOD <60 &&FOOTR"
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN
 
 
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
