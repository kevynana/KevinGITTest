-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO
 
CREATE FILE TCDAY
-RUN
 
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
 
-TYPE TCAIRDB
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
 
 
-INCLUDE SETECHO
SET ASNAMES = ON
 
-*========================================================================
-*           Extract Data for each client selected
-*========================================================================
 
TABLEF FILE &&EXTRACT
PRINT ARR_DATE
      DPT_DATE
      EMB_APT.AIRPORT_NAME AS 'EAIRP'
      RTE_APT.AIRPORT_NAME AS 'RAIRP'
      SEG_COUNT
      SEG_NBR
      STOP_CON
      TKT_NUM
WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCH1
END
-RUN
 
-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
DEFINE FILE TCH1
ROLLCD/A8 = '&&ROLL';
DDAY/W = DPT_DATE;
ADAY/W = ARR_DATE;
DPT_DAY/A3   = DECODE DDAY (1 'MON' 2 'TUE' 3 'WED'
                            4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
ARR_DAY/A3 =   DECODE ADAY (1 'MON' 2 'TUE' 3 'WED'
                            4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
 
CASE1/A25 = IF STOP_CON EQ 'X' THEN EAIRP ELSE ' ';
DAY1/A3 = IF CASE1 NE ' ' THEN DPT_DAY ELSE ' ';
CASE1_CNT/P8CS = IF CASE1 NE ' ' THEN 1 ELSE 0;
CASE2/A25 = IF STOP_CON EQ 'O' THEN EAIRP ELSE ' ';
DAY2/A3 = IF CASE2 NE ' ' THEN DPT_DAY ELSE ' ';
CASE2_CNT/P8CS = IF CASE2 NE ' ' THEN 1 ELSE 0;
CASE3/A25 = IF STOP_CON EQ 'O' THEN RAIRP ELSE ' ';
DAY3/A3 = IF CASE3 NE ' ' THEN ARR_DAY ELSE ' ';
CASE3_CNT/P8CS = IF CASE3 NE ' ' THEN 1 ELSE 0;
END
-RUN
TABLE FILE TCH1
PRINT CASE1 DAY1 CASE1_CNT CASE2 DAY2 CASE2_CNT CASE3 DAY3 CASE3_CNT
BY ROLLCD
BY TKT_NUM
BY SEG_NBR
ON TABLE HOLD AS TWOH
END
-RUN
 
TABLE FILE TWOH
PRINT CASE1 AS 'CITY' DAY1 AS 'DAY' CASE1_CNT AS 'CITY_CNT'
BY ROLLCD
BY TKT_NUM
BY SEG_NBR
WHERE CASE1 NE ' '
ON TABLE HOLD AS H1 FORMAT FOCUS
END
-RUN
 
TABLE FILE TWOH
PRINT CASE2 AS 'CITY' DAY2 AS 'DAY' CASE2_CNT AS 'CITY_CNT'
BY ROLLCD
BY TKT_NUM
BY SEG_NBR
WHERE CASE2 NE ' '
ON TABLE HOLD AS H2 FORMAT FOCUS
END
-RUN
 
 
TABLE FILE TWOH
PRINT CASE3 AS 'CITY' DAY3 AS 'DAY' CASE3_CNT AS 'CITY_CNT'
BY ROLLCD
BY TKT_NUM
BY SEG_NBR
WHERE CASE3 NE ' '
ON TABLE HOLD AS H3 FORMAT FOCUS
END
-RUN
 
-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &H1=&HLDPATH || 'H1.FOC';
-SET &H2=&HLDPATH || 'H2.FOC';
-SET &H3=&HLDPATH || 'H3.FOC';
-RUN
 
USE CLEAR *
-RUN
 
EX SUUSES
-RUN
 
USE ADD
&H1 AS H1
&H2 AS H1
&H3 AS H1
END
-RUN
 
TABLE FILE H1
SUM CITY_CNT
BY ROLLCD
BY CITY
BY DAY
ON TABLE HOLD
END
-RUN
 
MODIFY FILE TCDAY
FIXFORM FROM HOLD
MATCH ROLLCD CITY DAY
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN
 
 
-NEXTONE
 
 
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
TABLE FILE TCDAY
SUM CITY_CNT AS 'TCITYC'
BY CITY
ON TABLE HOLD AS CITY1
END
-RUN
 
TABLE FILE TCDAY
SUM CITY_CNT
BY CITY
BY DAY
ON TABLE HOLD AS HOLDONE
END
-RUN
 
MATCH FILE HOLDONE
PRINT CITY_CNT DAY
BY CITY
ON TABLE HOLD
RUN
FILE CITY1
SUM TCITYC
BY CITY
AFTER MATCH HOLD AS HOLDTWO OLD-OR-NEW
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
-TYPE BEFORE DEFINES
 
DEFINE FILE HOLDTWO
DAY_SEQ/A1 = IF DAY EQ 'MON' THEN '1' ELSE
             IF DAY EQ 'TUE' THEN '2' ELSE
             IF DAY EQ 'WED' THEN '3' ELSE
             IF DAY EQ 'THU' THEN '4' ELSE
             IF DAY EQ 'FRI' THEN '5' ELSE
             IF DAY EQ 'SAT' THEN '6' ELSE '7';
NOWTOD/A8 WITH CITY_CNT = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE HOLDTWO
-INCLUDE &&PAHDR
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
FOOTING BOTTOM
"</1"
"CITY COUNT = NUMBER OF PEOPLE COMING INTO OR LEAVING THESE AIRPORTS"
"A CONNECTION IS COUNTED ONE TIME AND A STOPOVER IS COUNTED TWICE"
"(THE ARRIVAL AND DEPARTURE FROM THE CITY)"
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
