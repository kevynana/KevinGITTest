-***************************************************************************
-* REPORT NAME: QUARTERLY REVIEW (AIR EXTRACT)
-* TOTAL COMPANY REPORTING
-* Steve - temporary change to include Domestic only - 06/18/2004
-*2/22/05        JK                 ADDED TNT_COMPANY WHERE STATEMENT
-*04/20/10   DEB                CHANGED ORIG_SALE DEFINE

-***************************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON
-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================
DEFINE FILE SR_50

FARE_PAID/D12 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D8CS  =   IF (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) THEN 1 ELSE 
                        IF (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1)
				   THEN (-1) ELSE 
                        IF (DOC_TYPE NE '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') AND
				   (SEG_COUNT EQ -1)
				   THEN (-1) ELSE 0;

TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                             (SEG_NBR EQ '01') AND
                             (FARE_PAID GE 0) AND
                             (SEG_COUNT EQ 1) AND 
                             (CONJ_REL EQ 1) THEN 1 ELSE 0;


END  
-RUN 
  
  
-* Defines on Previously Defined Defines
  

TABLE FILE SR_50
PRINT AIR_MAIN.INTL_DOM AS 'INTL_DOM'
  AIR_KEY
  DOC_TYPE	
  DOD_TYPE
  DPT_DATE	
  EMBARK	
  EX_FLAG	
  EXCHG_SALE
  FARE_PAID
  NET_TKT_CNT
  RF_FLAG
  SEG_AMT/D12	
  SEG_COM/D12	
  SEG_COUNT
  SEG_NBR 	
  SEG_TAX/D12
  TKT_DATE
  TKT_NUM
  TKT_PURCH	
  TRN_DATE
  VOID_DATE 	
  VOID_STATUS 
BY TRN_DATE
WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
-* &&WHERE7
-* &&WHERE8
-* &&WHERE9
-* &&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TC_ABNM2
END
-RUN
 
 
DEFINE FILE TC_ABNM2

CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';                             

 
  
EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') THEN 1 ELSE 0;
FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;	
PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;

-*ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;	


ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND 
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;	

REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;
	
TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;	
	
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01')
  	     THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) 
	     THEN 1 ELSE 0;

-* Defines on previous DEFINEs

EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
  NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
  IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;	

TOT_ECNT/D9 = EXCHGS + EXMCOS;	
TOT_TRANS/D9 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;

-* DEFINES ON PREVIOUS DEFINES TO DETERMINE CURRENT
CTOT_TRANS/P9CS = IF CURRENT EQ 'Y' THEN TOT_TRANS ELSE 0;
CORIG/P9CS = IF CURRENT EQ 'Y' THEN ORIG_SALE ELSE 0;
CEXCHGS/P9CS = IF CURRENT EQ 'Y' THEN EXCHGS ELSE 0;
CVOIDS/P9CS = IF CURRENT EQ 'Y' THEN VOIDS ELSE 0;
CREFUNDS/P9CS = IF CURRENT EQ 'Y' THEN REFUNDS ELSE 0;
CTOT_ECNT/P9CS = IF CURRENT EQ 'Y' THEN TOT_ECNT ELSE 0;

TRAN_MY/MTYY = TRN_DATE;

END

TABLE FILE TC_ABNM2
SUM CTOT_TRANS
    CVOIDS
    CREFUNDS
    CTOT_ECNT
    CORIG
    CEXCHGS
BY TRAN_MY
ON TABLE HOLD AS TC_ABNM3
END
-RUN






-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*                <---------------- STEP5 ---------------->
-*========================================================================
-*   PROCESS EXTRACT DATA FROM STEP4 - HOLD TO FOCUS DATABASE TCAREV       
-*========================================================================
DEFINE FILE TC_ABNM3
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TC_ABNM3
SUM CTOT_TRANS 
    CVOIDS 
    CREFUNDS   
    CTOT_ECNT
    CORIG
    CEXCHGS
BY ROLLCD
ON TABLE HOLD AS HOLD2
END
-RUN



DEFINE FILE HOLD2
  NMB/I5 = NMB + 1;  
END
TABLE FILE HOLD2
SUM CTOT_TRANS
    CVOIDS
    CREFUNDS
    CTOT_ECNT
    CORIG
    CEXCHGS
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCM2
END
-RUN



TABLE FILE TCM2
SUM CTOT_TRANS
    CVOIDS
    CREFUNDS
    CTOT_ECNT
    CORIG
    CEXCHGS
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCM3
END
-RUN







MODIFY FILE TCABNM2
FIXFORM FROM TCM3
MATCH ROLLCD NMB
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON TCM3
END
-RUN




-NEXTONE
