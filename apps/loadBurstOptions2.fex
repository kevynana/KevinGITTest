-**************************************************************************************************
-* loadBurstOptions.fex 
-*    Update RPT_FLDS.HIEORG2.MEMBER field with REPORT_BREAK_HIERS.ROLL_LEV 
-*    Update RPT_FLDS.HIEORG1.SEL_LEVEL field with REPORT_BREAK_OWNERS.BREAK_LEVEL
-*    Update BRPTINST.SELECT_LEV with REPORT_BREAK_OWNERS.BREAK_LEVEL if neccessary
-*    (BRPTINST.SELECT_LEV GE REPORT_BREAK_OWNERS.BREAK_LEVEL)
-*
-**************************************************************************************************

-INCLUDE SETECHO
SET ASNAME=ON
-DEFAULT &&IKEY = 183591;
-DEFAULT &&BRKLEVEL = '03';
-DEFAULT &&MEMBER = 'ABCDB';

-**************************************************
-* UPDATE RPT_INST
-**************************************************
-SET &LEVEL = EDIT(&&BRKLEVEL);

DEFINE FILE RPT_INST
  LEVEL/I2 WITH SELECT_LEV = &LEVEL;
END
TABLE FILE RPT_INST
PRINT
  RPT_INST.INST_KEY
  SETTINGS.ID
  LEVEL AS 'SELECT_LEV'
WHERE INST_KEY EQ &&IKEY
WHERE SELECT_LEV LT LEVEL
ON TABLE HOLD AS HOLDI1
END
-RUN
-IF &LINES EQ 0 THEN GOTO UPDFLDS;
 
MODIFY FILE RPT_INST
FIXFORM FROM HOLDI1
MATCH INST_KEY
 ON MATCH CONTINUE
 ON NOMATCH REJECT
MATCH ID
 ON MATCH UPDATE SELECT_LEV
 ON NOMATCH REJECT
DATA ON HOLDI1
END
-RUN


TABLE FILE RPT_INST
PRINT
  RPT_INST.INST_KEY
  SETTINGS.ID
  SETTINGS.SELECT_LEV
WHERE INST_KEY EQ &&IKEY
END
-RUN

-UPDFLDS 

-**************************************************
-* UPDATE RPT_FLDS
-**************************************************
-*UPDATE SEL_LEVEL
DEFINE FILE RPT_FLDS
  INCL/I1 WITH HIEORG1.ID = 1;
  LEVEL/A2 WITH HIEORG1.ID = '&&BRKLEVEL.EVAL';  
END

TABLE FILE RPT_FLDS
PRINT INCL AS 'ID' HIEORG1.INCL1 HIEORG1.INCL2 INCL AS 'INCL3' LEVEL AS 'SEL_LEVEL'
BY RPT_INST.INST_KEY
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE AS HOLDRPT9
END
-RUN
 
MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 HIEORG1.ID/1 HIEORG1.INCL1/1 HIEORG1.INCL2/1
FIXFORM HIEORG1.INCL3/1 HIEORG1.SEL_LEVEL/A2
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH REJECT
MATCH * KEYS SEG HIEORG1
  ON NOMATCH REJECT
  ON MATCH UPDATE SEL_LEVEL
DATA ON HOLDRPT9
END
-RUN

-*CLEAR MEMBERS
DEFINE FILE RPT_FLDS
  MEM/A15 WITH MEMBER = '';
END
TABLE FILE RPT_FLDS
PRINT HIEORG2.SEQ_NO MEM AS 'MEMBER'
BY INST_KEY
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE AS HOLDMEMB
END
-RUN
-IF &LINES EQ 0 THEN GOTO DelEmptyOnes;

MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.MEMBER/A15
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH REJECT
MATCH * KEYS SEG HIEORG2
  ON NOMATCH REJECT
  ON MATCH UPDATE MEMBER
DATA ON HOLDMEMB
END
-RUN

-DelEmptyOnes
-*DELETE EMPTY RECORDS
TABLE FILE RPT_FLDS
PRINT SEG MEMBER
BY RPT_INST.INST_KEY
WHERE INST_KEY EQ &&IKEY
WHERE CLIENT EQ '' AND CATG EQ '' AND MEMBER EQ ''
ON TABLE SAVE AS HOLDRPT0
END
-RUN
-IF &LINES EQ 0 THEN GOTO GETHIERS;

MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.CLIENT/A8
FIXFORM HIEORG2.CATG/A15 
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH REJECT
MATCH * KEYS SEG HIEORG2
  ON NOMATCH REJECT
  ON MATCH DELETE
DATA ON HOLDRPT0
END
-RUN

-GETHIERS
-* get member from REPORT_BREAK_HIERS
JOIN CLEAR *
JOIN 
 INNER BREAK_KEY IN REPORT_BREAK_KEYS TO 
 MULTIPLE BREAK_KEY IN REPORT_BREAK_HIERS AS J1
END
DEFINE FILE REPORT_BREAK_KEYS
  CNT/I2 WITH ROLL_LEV = 1;
END
TABLE FILE REPORT_BREAK_KEYS
PRINT ROLL_LEV AS 'MEMBER'
BY INST_KEY
BY BREAK_KEY NOPRINT
BY CNT AS 'SEQ_NO'
WHERE INST_KEY EQ &&IKEY
WHERE ROLL_LEV EQ '&&MEMBER.EVAL'
ON TABLE SAVE AS HLDHIERS 
END
-RUN


MODIFY FILE RPT_FLDS
FIXFORM X4 INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.MEMBER/A15
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH REJECT
MATCH * KEYS SEG HIEORG2
  ON NOMATCH INCLUDE
  ON MATCH UPDATE MEMBER
DATA ON HLDHIERS
END
-RUN