-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 5/7/04 - DEB - CHANGED AVG AMOUNT TO BACK OUT NEGATIVE AMTS SO WOULD
-*                BALANCE TO THE TRAVEL SUMMARY
-*5/10/04   DEB   CHANGED FIELD DEFINE OF TOTAL_AMT FIELD TO 12.2
-*                DUE TO BALANCING ISSUE TO TRAVEL SUMMARY
-***************************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
 FLAG/A1 = 'A';
 NBR_DAYS/P12 = IF (NUM_DAYS LT 0) AND (NUM_ROOMS LT 0) THEN
                   ((NUM_DAYS * NUM_ROOMS) * (-1)) ELSE
                   (NUM_DAYS * NUM_ROOMS);
 NBR_ROOMS/P12 = NUM_ROOMS;
 NEW_TOTAL_AMT/P12.2 = TOTAL_AMT;
 INT_DOM/A1 =IF (CITY.INTL_DOM EQ 'I') THEN 'I' ELSE 'D';
 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT FLAG
  CURR_DATE
  INVOICE_DATE	
  INT_DOM
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
    
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL)
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

-INCLUDE RPTPARMS
ON TABLE HOLD AS QTRHTL1
END
-RUN

TABLE FILE QTRHTL1
PRINT FLAG
  INVOICE_DATE	
  INT_DOM
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
BY CURR_DATE
ON TABLE HOLD AS QHTL2 FORMAT FOCUS INDEX CURR_DATE
END
-RUN

-* Global Reporting JOIN
JOIN QHTL2.CURR_DATE IN QHTL2 TO GLOBAL.DATE IN GLOBAL AS J1
END


DEFINE FILE QHTL2
CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;
 

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
AMT_TOTALX/D20.6 = NEW_TOTAL_AMT * CURR1X;


END

TABLE FILE QHTL2
PRINT FLAG
  INVOICE_DATE	
  INT_DOM
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
  CURR_DATE
  NAMEC
  AMT_TOTALX/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HTLHLD
END
-RUN




DEFINE FILE HTLHLD
CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE &&FXDATE.EVAL AND
               INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
END

TABLE FILE HTLHLD
PRINT CURRENT	
  INT_DOM
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
  PRIOR
  NAMEC
  AMT_TOTALX
BY FLAG
ON TABLE HOLD AS HTLHLD1
END
-RUN

DEFINE FILE HTLHLD1
-* DEFINES FOR HEADINGS
HTLDAY/A50 = 'Number Of Hotel Nights';
HTLROOM/A50 = 'Number Of Hotel Rooms Booked';
HTLAVG/A50 = 'Average Hotel Rate Per Night';
DHTLAVG/A50 = 'Average Domestic Hotel Rate Per Night';
IHTLAVG/A50 = 'Average International Hotel Rate Per Night';
-* CURRENT
CHNGT/P12=IF CURRENT EQ 'Y' THEN NBR_DAYS ELSE 0;
CHDNGT/P12=IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'D') THEN NBR_DAYS ELSE 0;
CHINGT/P12=IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'I') THEN NBR_DAYS ELSE 0;
CHROOM/P12=IF CURRENT EQ 'Y' THEN NBR_ROOMS ELSE 0;
CHAMT/P12.2=IF CURRENT EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
CHDAMT/P12.2=IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'D') THEN 
               NEW_TOTAL_AMT ELSE 0;
CHIAMT/P12.2=IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'I') THEN 
               NEW_TOTAL_AMT ELSE 0;
GCHAMT/P16.2=IF CURRENT EQ 'Y' THEN AMT_TOTALX ELSE 0;
GCHDAMT/P16.2=IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'D') THEN 
               AMT_TOTALX ELSE 0;
GCHIAMT/P16.2=IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'I') THEN 
               AMT_TOTALX ELSE 0;


-* PRIOR
PHNGT/P12=IF PRIOR EQ 'Y' THEN NBR_DAYS ELSE 0;
PHDNGT/P12=IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'D') THEN NBR_DAYS ELSE 0;
PHINGT/P12=IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'I') THEN NBR_DAYS ELSE 0;
PHROOM/P12=IF PRIOR EQ 'Y' THEN NBR_ROOMS ELSE 0;
PHAMT/P12.2=IF PRIOR EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
GPHAMT/P16.2=IF PRIOR EQ 'Y' THEN AMT_TOTALX ELSE 0;
PHDAMT/P12.2=IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'D') THEN 
               NEW_TOTAL_AMT ELSE 0;
PHIAMT/P12.2=IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'I') THEN
               NEW_TOTAL_AMT ELSE 0;
GPHDAMT/P16.2=IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'D') THEN 
               AMT_TOTALX ELSE 0;
GPHIAMT/P16.2=IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'I') THEN
               AMT_TOTALX ELSE 0;

END

TABLE FILE HTLHLD1
SUM HTLDAY
HTLROOM
HTLAVG
DHTLAVG
IHTLAVG
CHNGT
CHROOM
CHAMT
CHDNGT
CHINGT
CHDAMT
CHIAMT
PHNGT
PHROOM
PHAMT
GPHAMT
GCHAMT
GPHDAMT
GPHIAMT
GCHAMT
GCHDAMT
GCHIAMT
NAMEC
COMPUTE CHAVG/P12=IF ((CHAMT LT 0) AND (CHNGT LT 0)) THEN
                     ((CHAMT/CHNGT) * (-1)) ELSE 
                  IF ((CHNGT LT 1) AND (CHNGT GT (-1))) THEN 0 ELSE
                     (CHAMT/CHNGT);
COMPUTE GCHAVG/P16=IF ((GCHAMT LT 0) AND (CHNGT LT 0)) THEN
                     ((GCHAMT/CHNGT) * (-1)) ELSE 
                  IF ((CHNGT LT 1) AND (CHNGT GT (-1))) THEN 0 ELSE
                     (GCHAMT/CHNGT);

COMPUTE CHDAVG/P12=IF ((CHDAMT LT 0) AND (CHDNGT LT 0)) THEN
                      ((CHDAMT/CHDNGT) * (-1)) ELSE
                   IF ((CHDNGT LT 1) AND (CHDNGT GT (-1))) THEN 0 ELSE
                      (CHDAMT/CHDNGT);
COMPUTE CHIAVG/P12=IF ((CHINGT LT 1) AND (CHINGT GT (-1))) THEN 0 ELSE
                      (CHIAMT/CHINGT);
COMPUTE GCHDAVG/P16=IF ((GCHDAMT LT 0) AND (CHDNGT LT 0)) THEN
                      ((GCHDAMT/CHDNGT) * (-1)) ELSE
                   IF ((CHDNGT LT 1) AND (CHDNGT GT (-1))) THEN 0 ELSE
                      (GCHDAMT/CHDNGT);
COMPUTE GCHIAVG/P16=IF ((CHINGT LT 1) AND (CHINGT GT (-1))) THEN 0 ELSE
                      (GCHIAMT/CHINGT);



COMPUTE PHAVG/P12=IF ((PHNGT LT 1) AND (PHNGT GT (-1))) THEN 0 ELSE
                     (PHAMT/PHNGT);
COMPUTE PHDAVG/P12=IF ((PHDNGT LT 1) AND (PHDNGT GT (-1))) THEN 0 ELSE
                      (PHDAMT/PHDNGT);
COMPUTE PHIAVG/P12=IF ((PHINGT LT 1) AND (PHINGT GT (-1))) THEN 0 ELSE
                      (PHIAMT/PHINGT);
COMPUTE GPHAVG/P16=IF ((PHNGT LT 1) AND (PHNGT GT (-1))) THEN 0 ELSE
                     (GPHAMT/PHNGT);
COMPUTE GPHDAVG/P16=IF ((PHDNGT LT 1) AND (PHDNGT GT (-1))) THEN 0 ELSE
                      (GPHDAMT/PHDNGT);
COMPUTE GPHIAVG/P16=IF ((PHINGT LT 1) AND (PHINGT GT (-1))) THEN 0 ELSE
                      (GPHIAMT/PHINGT);
-* INCREASE DECREASE COMPUTES
COMPUTE IDHNGT/P12=(((CHNGT-PHNGT)/PHNGT)*100);
COMPUTE IDHRM/P12=(((CHROOM-PHROOM)/PHROOM)*100);
COMPUTE IDHAVG/P12=(((CHAVG-PHAVG)/PHAVG)*100);
COMPUTE IDHDAVG/P12=(((CHDAVG-PHDAVG)/PHDAVG)*100);
COMPUTE IDHIAVG/P12=(((CHIAVG-PHIAVG)/PHIAVG)*100);
COMPUTE GIDHAVG/P16=(((GCHAVG-GPHAVG)/GPHAVG)*100);
COMPUTE GIDHDAVG/P16=(((GCHDAVG-GPHDAVG)/GPHDAVG)*100);
COMPUTE GIDHIAVG/P16=(((GCHIAVG-GPHIAVG)/GPHIAVG)*100);



BY FLAG
ON TABLE HOLD AS HTLTTL 
END
-RUN


-* Number of Hotel Nights

DEFINE FILE HTLTTL
LABEL/A35 = 'Hotel';
CATEGORY/A81 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 19;
END
-RUN

TABLE FILE HTLTTL
PRINT CATEGORY/A50 AND HTLDAY/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CHNGT/D15 AS 'CURRENT' AND
PHNGT/D16 AS 'PRIOR' IDHNGT AS 'IDPERCENT' 
COMPUTE ROW_ORDER/I2 = 1;  
ON TABLE HOLD AS HTLNGT
END
-RUN

TABLE FILE HTLNGT
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS HTLNGT1
END

MODIFY FILE GQTRREV1
FIXFORM FROM HTLNGT1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HTLNGT1
END
-RUN


-* Number of Hotel Rooms Booked

DEFINE FILE HTLTTL
LABEL/A35 = 'Hotel';
CATEGORY/A81 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 19;
END
-RUN

TABLE FILE HTLTTL
PRINT CATEGORY/A81 AND HTLROOM/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CHROOM/D16 AS 'CURRENT' AND
PHROOM/D16 AS 'PRIOR' IDHRM AS 'IDPERCENT' 
COMPUTE ROW_ORDER/I2 = 2;  
ON TABLE HOLD AS HTLROOM
END
-RUN

TABLE FILE HTLROOM
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS HTLROOM1
END

MODIFY FILE GQTRREV1
FIXFORM FROM HTLROOM1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HTLROOM1
END
-RUN

-* Average Hotel Rate Per Night

DEFINE FILE HTLTTL
LABEL/A35 = 'Hotel';
CATEGORY/A81 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 19;
END
-RUN

TABLE FILE HTLTTL
PRINT CATEGORY/A81 AND HTLAVG/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND GCHAVG/D16 AS 'CURRENT' AND
GPHAVG/D16 AS 'PRIOR' GIDHAVG AS 'IDPERCENT' 
COMPUTE ROW_ORDER/I2 = 3;  
ON TABLE HOLD AS HTLAVG
END
-RUN

TABLE FILE HTLAVG
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS HTLAVG1
END

MODIFY FILE GQTRREV1
FIXFORM FROM HTLAVG1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HTLAVG1
END
-RUN

-* Average Domestic Hotel Rate Per Night

DEFINE FILE HTLTTL
LABEL/A35 = 'Hotel';
CATEGORY/A81 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 19;
END
-RUN

TABLE FILE HTLTTL
PRINT CATEGORY/A81 AND DHTLAVG/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND GCHDAVG/D16 AS 'CURRENT' AND
GPHDAVG/D16 AS 'PRIOR' GIDHDAVG AS 'IDPERCENT' 
COMPUTE ROW_ORDER/I2 = 4;  
ON TABLE HOLD AS DHTLAVG
END
-RUN

TABLE FILE DHTLAVG
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS DHTLAVG1
END

MODIFY FILE GQTRREV1
FIXFORM FROM DHTLAVG1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON DHTLAVG1
END
-RUN

-* Average International Hotel Rate Per Night

DEFINE FILE HTLTTL
LABEL/A35 = 'Hotel';
CATEGORY/A81 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 19;
END
-RUN

TABLE FILE HTLTTL
PRINT CATEGORY/A81 AND IHTLAVG/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND GCHIAVG/D16 AS 'CURRENT' AND
GPHIAVG/D16 AS 'PRIOR' GIDHIAVG AS 'IDPERCENT' 
COMPUTE ROW_ORDER/I2 = 4;  
ON TABLE HOLD AS IHTLAVG
END
-RUN

TABLE FILE IHTLAVG
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS IHTLAVG1
END

MODIFY FILE GQTRREV1
FIXFORM FROM IHTLAVG1
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON IHTLAVG1
END
-RUN




