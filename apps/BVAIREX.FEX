-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File BVAIREX.FEX
-********************************************************************
-* MODIFICAIONS:
-* 1/27/15 - DEB - S-06899 CREATED FOR BVACHFIL
-*  09/20/2016  REJ  S-24432 ER5 Testing.  Changed the where on the dates
-*                               to use &AIRDATES
-*11/17/2016 - RSB - S-26926 Change reports to read employee ID from EMP_ID
-*                            Changed LEVEL1 to EMP_ID 
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-********************************************************************
 
-********************************************************************
 
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
 
DEFINE FILE &&EXTRACT
  BOOK_DT/MDYY         = RESV_DATE;
  OT_BOOK/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';
  CARD/A2=EDIT(CREDIT_CARD, '99$$$$$$$$$$$$$$$$');
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC/A60 = &&LDESC;
  MNON_REF/A15 = DECODE NONREF_FLAG  ('N' 'NON-REFUNDABLE'
                                     'R' 'REFUNDABLE'
                                     ' ' 'REFUNDABLE');
  SO_FLAG/A10 = DECODE STOP_CON ('O' 'STOPOVER'
                                 'X' 'CONNECTION'
                                 ' ' ' ');
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  VOID/A1 = IF VOID_DATE NE 0 THEN 'Y' ELSE 'N';
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        =
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
 
-* ****DEFINES ON PREVIOUS DEFINES****
CARD_FLAG/A1 = IF CARD EQ 'CA' THEN 'Y' ELSE 'N';
-INCLUDE TRANTYPE
END
TABLEF FILE &&EXTRACT
PRINT
  VAL_AIRLINE AS 'AIRLINE'
  ARR_DATE
  BOOK_DT
  CARD_FLAG
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'
  CITYPAIR.INTL_DOM
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'
  C_ITIN
  CLASS
  DPT_TIME
  FARE_PAID
  FLIGHT
  LEVEL_DESC
  EMP_ID
  LEVEL2
  LEVEL3
  MNON_REF
  OT_BOOK
  PASSNGR_NAME
  PNR_LOCATOR
  REFUSE_CODE
  SEG_COUNT
  SEG_LOWEST
  SEG_NBR
  SO_FLAG
  TKT_NUM
  TKT_SORT
  TKT_TYPE
  TRN_DATE
  XDPT_DT
  XPSNGR_NM
  XTRAN_DT	AS 'TRAN_DATE'
 
 
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
 
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
 
 
ON TABLE HOLD AS BVAIRHLD
END
 
 
 
TABLE FILE BVAIRHLD
SUM AIRLINE
    C_ITIN
    FARE_PAID
    PASSNGR_NAME
BY LEVEL_DESC
BY EMP_ID
BY XPSNGR_NM
BY PNR_LOCATOR
BY TRAN_DATE
BY TKT_NUM
BY TKT_TYPE
ON TABLE HOLD AS BVAIR
END
-RUN
 
 
 
