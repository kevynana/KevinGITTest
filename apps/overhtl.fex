-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
SET WARNING=OFF
DEFINE FILE OVHHOLD
NNROOMS/D10 = IF  (NUM_DAYS LT 0) AND
                  (NUM_ROOMS LT 0) THEN
                  (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                  (NUM_DAYS * NUM_ROOMS);
BITMAP/A12 WITH NNROOMS = 'HOTEL.GIF';

TH_AMT/D12.2 = TOTAL_AMT;


CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =   IF INVOICE_DATE GE &&FXDATE.EVAL AND
                INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';

CDAY/D10 = IF CURRENT EQ 'Y' THEN NNROOMS ELSE 0;
PDAY/D10 = IF PRIOR EQ 'Y' THEN NNROOMS ELSE 0;
CAMT/D12.2 = IF CURRENT EQ 'Y' THEN TH_AMT ELSE 0;
PAMT/D12.2 = IF PRIOR EQ 'Y' THEN TH_AMT ELSE 0;
END
-RUN

TABLE FILE OVHHOLD
SUM CDAY 
    CAMT
    BITMAP
BY CHAIN_NAME
WHERE CURRENT EQ 'Y'
ON TABLE HOLD AS HHOLD1 
END
-RUN

TABLE FILE OVHHOLD
SUM PDAY 
    PAMT
    BITMAP
BY CHAIN_NAME
WHERE PRIOR EQ 'Y'
ON TABLE HOLD AS HHOLD2  
END
-RUN

MATCH FILE HHOLD1
PRINT CDAY
      CAMT
      BITMAP
BY CHAIN_NAME
ON TABLE HOLD
RUN
FILE HHOLD2
PRINT PDAY
      PAMT
      BITMAP
BY CHAIN_NAME
AFTER MATCH HOLD AS HHOLD3 OLD-OR-NEW
END

TABLE FILE HHOLD3
SUM CDAY
    CAMT
    PDAY
    PAMT
    BITMAP
BY CHAIN_NAME
ON TABLE HOLD AS HHOLD4 
END
-RUN

TABLE FILE HHOLD4
PRINT *
RANKED BY HIGHEST CDAY NOPRINT
ON TABLE HOLD AS HHOLD5 
END
-RUN


-SET &RECFOUND = &RECORDS;



DEFINE FILE HHOLD5
  LIST/D9 = IF CHAIN_NAME NE LAST CHAIN_NAME THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 3 THEN 'OTHER' ELSE ARANK1;
  CHAIN/A25 = EDIT(CHAIN_NAME, '9999999999999999999999999$$$$$');
  RANK_LABEL1/A25 = IF RANK_VALUE NE 'OTHER' THEN CHAIN ELSE 'All Other';
  RANK_LABEL/A25 = LCWORD(25, RANK_LABEL1, RANK_LABEL);
END
-RUN

TABLE FILE HHOLD5
SUM CDAY
    CAMT
    PDAY
    PAMT
    BITMAP
BY RANK_VALUE NOPRINT
BY RANK_LABEL
ON TABLE HOLD AS HHOLD6 
END
-RUN


-IF &RECFOUND EQ 0 GOTO SKIP_GRAPH;
-RUN

GRAPH FILE HHOLD6
SUM CDAY AS ' '
ACROSS RANK_LABEL 
ON GRAPH SET LOOKGRAPH PIE
ON GRAPH SET GRAPHEDIT OFF
ON GRAPH SET GRAPHSTYLE *
setConnectLineMarkers(true);
setO1LabelDisplay(true);
setO1MajorGridDisplay(false);
setO1MinorGridDisplay(false);
setY1LabelDisplay(true);
setY1AxisSide(0);
setY1MajorGridDisplay(false);
setY1MinorGridDisplay(false);
setPieFeelerTextDisplay(1);
setPieLabelDisplay(0);
setAutofit(getPieSliceLabel(),true);
setFontStyle(getPieSliceLabel(),2);
setTextFormatPreset (getPieSliceLabel () ,1);
setLegendDisplay(true);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),true);
setFontStyle(getLegendText(),2);
setLegendMarkerPosition(2);
setFontSizeAbsolute(getTitle(),true); 
setAutofit(getTitle(),false);
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),11);
setFontSize(getTitle(),15);
setTitleString("Hotel Booking Summary");
setTextJustHoriz(getTitle(), 1);
setSubtitleString("Based Upon Number of Nights");
setTextJustHoriz(getSubtitle(), 1);
setFontSizeAbsolute(getSubtitle(),true); 
setAutofit(getSubtitle(),false);
setFontStyle(getSubtitle(),11);
setFontSize(getSubtitle(),15);
ENDSTYLE
ON GRAPH SET BARNUMB OFF
ON GRAPH SET 3D OFF
ON GRAPH SET GRID ON
ON GRAPH SAVE AS HOTEL FORMAT GIF
END
-RUN

-SKIP_GRAPH

-SET &&H_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';

-INCLUDE DTRANGE
-RUN


DEFINE FILE HHOLD6
BITMAP/A12 WITH CDAY = 'HOTEL.GIF';
CATEGORY/A25 = 'Hotel';
CAT_ORDER/I2 = 6;
END
-RUN

TABLE FILE HHOLD6
PRINT CATEGORY/A25 AND RANK_LABEL/A25 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CDAY/D15 AS 'VOL_CURR' AND 
CAMT/D15 AS 'TKT_CURR' AND PDAY/D15 AS 'VOL_PRIOR' 
AND PAMT/D15 AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS HHOLD7 
END
-RUN

TABLE FILE HHOLD7
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS HHOLD8 
END
MODIFY FILE OV1_MAIN
FIXFORM FROM HHOLD8
MATCH ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HHOLD8
END
-RUN




