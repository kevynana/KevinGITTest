-* 12/28/04 - DEB - ADDED ADDT'L DOD_TYPE TO BOOKING TYPE DEFINES
-*2/22/05        JK                 ADDED TNT_COMPANY WHERE STATEMENT
-*10/11/05   DEB  ADDED DOD_TYPE H TO BOOKING TYPE DEFINES
-*1/30/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*4/14/14 UPDATED THE FORMAT ON ABOOK_CNT AND ABOOK_CNT JK

-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT

FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
-*LEVEL_DESC/A60 = &&LDESC;
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
-** DEFINE ON PREVIOUS DEFINES
  ABOOK_CNT/P8CS = IF ONLINE_TRADITIONAL EQ 'TRADITIONAL' THEN NET_TKT_CNT ELSE 0;
  ABOOK_FARE/D12.2CS = IF ONLINE_TRADITIONAL EQ 'TRADITIONAL' THEN FARE_PAID ELSE 0;
  IBOOK_CNT/P8CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN NET_TKT_CNT ELSE 0;
  IBOOK_FARE/D12.2CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN FARE_PAID ELSE 0;
  IEBOOK_CNT/D8CS = IF DOD_DESC CONTAINS 'EXCHANGE' THEN NET_TKT_CNT ELSE 0;
  IEBOOK_FARE/D12.2CS = IF DOD_DESC CONTAINS 'EXCHANGE' THEN FARE_PAID ELSE 0;
  SIBOOK_CNT/D8CS = IF DOD_DESC CONTAINS 'WEB' THEN NET_TKT_CNT ELSE 0;
  SIBOOK_FARE/D12.2CS = IF DOD_DESC CONTAINS 'WEB' THEN FARE_PAID ELSE 0;

END

TABLEF FILE &&EXTRACT
PRINT 
  FARE_PAID
  NET_TKT_CNT
  ABOOK_CNT
  ABOOK_FARE
  IBOOK_CNT
  IBOOK_FARE
  IEBOOK_CNT
  IEBOOK_FARE
  SIBOOK_CNT
  SIBOOK_FARE
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS TC_BKG 
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
DEFINE FILE TC_BKG
ROLLCD/A8 = '&&ROLL';
END

TABLE FILE TC_BKG
SUM FARE_PAID
    NET_TKT_CNT
    ABOOK_CNT
    ABOOK_FARE
    IBOOK_CNT
    IBOOK_FARE
    IEBOOK_CNT
    IEBOOK_FARE
    SIBOOK_CNT
    SIBOOK_FARE
BY ROLLCD
ON TABLE HOLD AS TCB1 
END
-RUN

DEFINE FILE TCB1
 NMB/I5 = NMB + 1;
END

TABLE FILE TCB1
PRINT FARE_PAID
      NET_TKT_CNT
      ABOOK_CNT
      ABOOK_FARE
      IBOOK_CNT
      IBOOK_FARE
      IEBOOK_CNT
      IEBOOK_FARE
      SIBOOK_CNT
      SIBOOK_FARE
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCB2
END
-RUN

MODIFY FILE TCBKG
FIXFORM FROM TCB2
MATCH ROLLCD NMB
ON MATCH REJECT
ON NOMATCH INCLUDE 
DATA ON TCB2
END
-RUN



-NEXTONE
