-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/11/16 S-20630 DLV  Commented out EMP_ID define
-*4/26/2017-JEM-S-33318-updated logic for no records report

-INCLUDE SETECHO
-*SET &ECHO=ALL;
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';

DEFINE FILE &&EXTRACT
-*EMP_ID/A9 = EDIT(LEVEL2,'999999999');
RENTAL_DAYS/D8 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
LEVEL_DESC/A60 = &&LDESC;
NBR_DAYS/D12 = NUM_DAYS;
XPSNGR_NM/A15        = 
 EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
-* ****DEFINES ON PREVIOUS DEFINES****
NOMIDBK/D12S = IF  (CAR_CAT NE 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY' 
                  OR 'COMPACT') THEN RENTAL_DAYS ELSE 0; 
MIDBOOK/D12S = IF  (CAR_CAT EQ 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY' 
                  OR 'COMPACT') THEN RENTAL_DAYS ELSE 0;    
MID_AMT/D9.2 = IF (CAR_CAT EQ 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY' 
                     OR 'COMPACT') THEN RENTAL_AMT ELSE 0;
NONMID_AMT/D9.2 = IF (CAR_CAT NE 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY' 
                       OR 'COMPACT') THEN RENTAL_AMT ELSE 0;                    
END
-RUN

TABLEF FILE &&EXTRACT
PRINT AROLL_LEV4 
  RENTAL_DAYS
  EMP_ID
  LEVEL_DESC
  MID_AMT
  NONMID_AMT
  MIDBOOK
  NOMIDBK
  PASSNGR_NAME
  XPSNGR_NM
-INCLUDE &CARDATES
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS CARHOLD
END
-RUN



TABLE FILE CARHOLD
SUM AROLL_LEV4
  RENTAL_DAYS
  MID_AMT
  NONMID_AMT
  MIDBOOK
  NOMIDBK
  LEVEL_DESC
  PASSNGR_NAME
COMPUTE NMIDADR/D9.2C=IF ((NOMIDBK LT 1) AND
       (NOMIDBK GT (-1))) THEN 0 ELSE NONMID_AMT/NOMIDBK;  
BY EMP_ID
ON TABLE HOLD AS CARHOLD2
END
-RUN


TABLE FILE CARHOLD2
SUM MID_AMT AS 'TMIDAMT'
    MIDBOOK AS 'TMIDBK'
BY AROLL_LEV4
ON TABLE HOLD AS CARHOLD3
END
-RUN

TABLE FILE CARHOLD3
SUM TMIDAMT
    TMIDBK
COMPUTE TMIDADR/D9.2C=IF ((TMIDBK LT 1) AND
       (TMIDBK GT (-1))) THEN 0 ELSE TMIDAMT/TMIDBK;         
BY AROLL_LEV4
ON TABLE HOLD AS CARHOLD4
END
-RUN



MATCH FILE CARHOLD2
PRINT RENTAL_DAYS
  MID_AMT
  NONMID_AMT
  MIDBOOK
  NOMIDBK
  PASSNGR_NAME
  LEVEL_DESC
  EMP_ID
  NMIDADR
BY AROLL_LEV4
ON TABLE HOLD
RUN
FILE CARHOLD4
SUM TMIDAMT
    TMIDBK
    TMIDADR
BY AROLL_LEV4
AFTER MATCH HOLD AS CARHOLD4 OLD-AND-NEW
END
-RUN




TABLE FILE CARHOLD4
PRINT AROLL_LEV4
    MID_AMT
    MIDBOOK
    NMIDADR
    NOMIDBK
    NONMID_AMT
    RENTAL_DAYS
    TMIDAMT
    TMIDBK    
    TMIDADR
    LEVEL_DESC
    PASSNGR_NAME
BY EMP_ID
ON TABLE HOLD AS UPPSC
END
-RUN

-SET &&CARREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';
