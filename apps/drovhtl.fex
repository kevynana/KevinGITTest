-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO

-* File DRHTLSM.FEX
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*----           ----                 ---------------------
-*8/31/00        SAC                   Initial Creation
-*****************************************************************  
-*************************************************************************
-*DATE         NAME             DESCRIPTION
-*09/27/00    IBISTL-SB         ADDED SET STATEMENTS TO GET THE VALUE
-*                              INTO THIS DRILLED FEX
-*09/27/00     IBISTL-SB        ADDED THE LOGIC FOR LOCATING THE SET FILE
-*                              IN THE WEBPATH AND THEN EXECUTING THE SET
-*                              FILE
-*09/27/00    IBISTL-SB         REMOVED THE ';' FROM THE SET ASNAMES 
-*                              STATEMENT
-*10/05/00    IBISTL-SB         CHANGED THE PARAMETER OF &&CPFLAG TO 'C1'
-*                              'P1', ''C2', 'P2'
-* 3/07/01   TANDT-SS           CHANGE ALL NUMERIC FORMATS FROM P (PACKED) TO
-*                              D (FLOATING-POINT DOUBLE PRECISION) TO PROVIDE
-*                              CONSISTENCY IN ROUNDING AND PERCENTAGES.
-* 4/30/01    TANDT-SS          ADDED CODE TO CALCULATIONS TO KEEP NEGATIVE
-*                              NUMBER IN PLACE FOR BACK-OUT OF CANCELLATIONS
-*                              AND DUPLICATES
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*7/26/04    JOY                Added/Changed Norecords logic
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox
-*************************************************************************
-DEFAULT &&CPFLAG = 'XX';
-DEFAULT &&HTLN = 'XX';
-DEFAULT &&VALUERANK = 'XXXXXX';


-SET &&CPFLAG = &FLAGPARM;
-SET &&HTLN = &ROW_LABEL;
-SET &&VALUERANK = &VALUERANK;


-**********************************************************************
-* CHECK FOR NON-DRILLABLE FIELDS
-IF &&HTLN EQ 'All Other' THEN SKIPTHIS ELSE SKIPTHAT;
-**********************************************************************
-SKIPTHAT

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 11/03/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
-*
EX RESET
-RUN
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;


-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT

-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX OVSETDB SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   

-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-*  CY (CURRENT YEAR), PY (PRIOR YEAR)
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****

-IF &&CPFLAG EQ 'CY' THEN GOTO CURYEAR
-  ELSE IF &&CPFLAG EQ 'PY' THEN GOTO PRYEAR
-  ELSE GOTO CURYEAR; 

-CURYEAR
-SET &&DCHK='WHERE INV_DT GE '&&FROMDT.EVAL' AND INV_DT LE '&&TODT.EVAL'';
-SET &&FROMDT = &&FROMDT;
-SET &&TODT = &&TODT;
-GOTO ENDDATE

-PRYEAR
-SET &&DCHK='WHERE INV_DT GE '&&FXDATE.EVAL' AND INV_DT LE '&&TXDATE.EVAL'';
-SET &&FROMDT = &&FXDATE;
-SET &&TODT = &&TXDATE;
-GOTO ENDDATE

-ENDDATE
-SET &&RPT_TTL1 = '                HOTEL SUMMARY DETAIL REPORT';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMM2
-RUN

-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN


-*EX AIRUSE
EX HTLUSE
-RUN

-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
CITY_ST/A35 = PROP_CITY||(', '| PROP_STATE);
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
NBR_DAYS/D12 = NUM_DAYS;
NBR_ROOMS/D12 = NUM_ROOMS;
NEW_ROOM_RATE/D12.2C = ROOM_RATE;
NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;

NNROOMS/D12 = IF (NUM_DAYS LT 0) AND
                 (NUM_ROOMS LT 0) THEN
                 (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                 (NUM_DAYS * NUM_ROOMS);

OUT_DATE/MDYY = CHKOUT_DATE;
CHAIN/A30 = LCWORD(30, CHAIN_NAME, CHAIN);
  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT CITY_ST  
  CHAIN_NAME
  HTL_KEY     
  IN_DATE 	
  INV_DT      
  INVOICE_DATE 	
  NBR_DAYS    
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT       
  NNROOMS	
  OUT_DATE    
  PASSNGR_NAME 	
  PROP_NAME   
  REFUSE_KEY
      
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

WHERE CHAIN EQ '&&HTLN'
&&DCHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

-TYPE After Extract
DEFINE FILE RPTHOLD
  NOWTOD/A8 WITH NBR_DAYS = HHMMSS (NOWTOD);
END
-RUN
-TYPE Before Report

TABLE FILE RPTHOLD
-INCLUDE HEADAIR
"</2"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM 
NNROOMS AS 'NUMBER,NIGHTS'
NBR_ROOMS AS 'NUMBER,ROOMS'
NEW_TOTAL_AMT AS 'TOTAL,AMOUNT'
COMPUTE AVGDR/D12.2CS = IF NEW_TOTAL_AMT LT 0 AND NNROOMS LT 0
 THEN ((NEW_TOTAL_AMT/NNROOMS) * (-1)) 
 ELSE NEW_TOTAL_AMT/NNROOMS; AS 'AVERAGE,DAILY,RATE'

-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY PROP_NAME AS 'HOTEL'
BY CITY_ST AS 'CITY/STATE'
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME RECOMPUTE AS 'TOTAL FOR'
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
"&&FOOTER"
"eTTek Review &DATE AT <NOWTOD <90 &&FOOTR"

-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2


END
-RUN
-*
-TYPE After Report
-GOTO XXIT;

-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END


-XXIT

