-SET &DIRFTP = &&FPATH;
-SET &GOTOFTP= &&WEB_PATH || 'ftpfile.bat';
-SET &HOLDDIR= &&WEB_PATH || 'holddir.ftm';
-SET &FILEDIR= &&WEB_PATH || 'filedir.ftm';
-* GET FTP INFO
-*
TABLE FILE USRROLL
PRINT FTP_ADDRESS
      FTP_USER_NAME
      FTP_PWD
WHERE USER_NAME EQ '&&UNAME'
WHERE CLIENT_ID_ROLLUP EQ '&&ROLLUP'
ON TABLE HOLD AS HOLDFTP FORMAT ALPHA
END
-RUN
-READ HOLDFTP &FADDR.50 &FNAME.50 &FPWD.50
DOS DIR /b &DIRFTP > &HOLDDIR
-RUN
-*
FILEDEF TREE0 DISK &HOLDDIR.EVAL
FILEDEF FILEDIR DISK &FILEDIR.EVAL
-RUN
-*
DEFINE FILE TREE0
 PUTFTP/A104 = 'put ' | TREE;
 COUNTER/I9 WITH TREE = 1;
END
-*
TABLE FILE TREE0
ON TABLE SET PAGE-NUM OFF
PRINT TOT.COUNTER
      PUTFTP AS ''
ON TABLE HOLD AS FILEDIR FORMAT ALPHA
END
-RUN
-READ FILEDIR &I.9 &DIR99.104
-RUN
-*
FILEDEF TEMPFTP DISK &GOTOFTP.EVAL
-RUN
-WRITE TEMPFTP open &FADDR
-WRITE TEMPFTP &FNAME
-WRITE TEMPFTP &FPWD
-WRITE TEMPFTP binary
-REPEAT ENDLOOP FOR &I FROM 1 TO &I
-READ FILEDIR &J.9 &DIR99.104
-*SET &TFTPDIR = &DIRFTP || &DIR99;
-WRITE TEMPFTP &DIR99
-ENDLOOP
-WRITE TEMPFTP quit
-RUN
-*
CD &DIRFTP
-RUN
-DOS ftp -v -s:&GOTOFTP
-RUN
-*DELETE THE TEMPFILES USED TO BUILD FTP LIST
DOS DEL /Q &HOLDDIR
DOS DEL /Q &FILEDIR
-*DELETE THE REPORTS THAT WERE FTP
DOS DEL /Q &DIRFTP
-*DELETE THE TEMP DIRECTORY USED FOR FTP
DOS RMDIR &&FPATH
-RUN
-*-EXIT
