-*  01/10/2017  REJ  S-29063 Updated for Post-trip reporting
-*  01/11/2017  REJ  S-24224 Added logging to the viewing of the Report links.
-*  01/26/2017  REJ  S-29714 Changed urls to be full urls.
-*  06/21/2017  DHG  S-20639 Changed zip software from PKZIP25 to 7Zip

-INCLUDE SETECHO
-DEFAULT &RFILE='';
-DEFAULT &RNBR='';
 
-DEFAULT &DSTSCHEDID='';
 
SET LINES=99999
SET BASEURL=&&SRVR.EVAL/
SET FOCEXURL=/ibi_apps/WFServlet?
-RUN
 
FILEDEF GREPORT DISK GREPORT.FTM (APPEND
FILEDEF BREPORT DISK BREPORT.FTM (APPEND
-RUN
 
-WRITE GREPORT 00000000000
-WRITE BREPORT 00000000000
 
 
TABLE FILE PUSH_LINKS
PRINT *
WHERE SCHEDULE_ID EQ '&DSTSCHEDID';
ON TABLE SAVE AS HPLINKS FORMAT ALPHA
END
-RUN
 
-SET &CTR=&LINES;
 
-REPEAT CLOOP &CTR TIMES;
 
-READ HPLINKS &SID.12, &RNBR.11, &RNAME.100, &RLINK.255, &RFILE.255, &RKEY.11
 
-DOS STATE &RFILE
-TYPE RETCODE = &RETCODE
-IF &RETCODE EQ 0 GOTO GREPORT;
-WRITE BREPORT &RNBR
-TYPE ***********************************************
-TYPE Push Report Failed - Instance Key = &RKEY
-TYPE ***********************************************
-GOTO CLOOP;
-GREPORT
-WRITE GREPORT &RNBR
-CLOOP
-SET &T1=GETTOK(&RFILE, 255, 2, '\', 255, 'A255');
-SET &T2=GETTOK(&RFILE, 255, 3, '\', 255, 'A255');
-SET &T3=GETTOK(&RFILE, 255, 4, '\', 255, 'A255');
-SET &T4=GETTOK(&RFILE, 255, 5, '\', 255, 'A255');
-SET &TIME=EDIT(HHMMSS(A10),'99$99$99');
 
-SET &ZF = 'eTTekReview_Reports_' || &YYMD || '_' || &TIME || '_' || &DSTSCHEDID || '.zip';
-SET &ZFILE= '&&WEBSRV.EVAL' || '\' || &T2 || '\' || &T3 || '\' || &T4 || '\' || &ZF;
-SET &WZFILEX='&&SRVR.EVAL' || '/' || &T2 || '/' || &T3 || '/' || &T4 || '/' || &ZF;
-SET &WZFILE=TRUNCATE(&WZFILEX);
 
DEFINE FILE PUSH_LINKS
VAL/I1=1;
END
TABLE FILE PUSH_LINKS
PRINT * VAL
IF RPT_NUMBER EQ (GREPORT)
WHERE SCHEDULE_ID EQ '&DSTSCHEDID';
ON TABLE HOLD AS HGLINKS FORMAT FOCUS
END
 
DEFINE FILE PUSH_LINKS
VAL/I1=0;
END
TABLE FILE PUSH_LINKS
PRINT * VAL
IF RPT_NUMBER EQ (BREPORT)
WHERE SCHEDULE_ID EQ '&DSTSCHEDID';
ON TABLE HOLD AS HBLINKS FORMAT FOCUS
END
-RUN
 
TABLE FILE HGLINKS
PRINT RPT_FILE
ON TABLE HOLD AS HFLIST
END
-RUN
 
-SET &FILES=&LINES;
-REPEAT ZLOOP &FILES TIMES;
-READ HFLIST NOCLOSE &IFILE.A255
-DOS 7za a -mx1 -tzip &ZFILE &IFILE
-*DOS PKZIP25 -add &ZFILE &IFILE
-ZLOOP
 
USE CLEAR *
USE
HGLINKS.FOC AS HGLINKS
HBLINKS.FOC AS HGLINKS
END
-RUN
 
DEFINE FILE HGLINKS
VAL_TOT/I5=VAL;
END
TABLE FILE HGLINKS
SUM VAL_TOT
PRINT RPT_NAME
      VAL
      RPT_FILE
      RPT_INST_KEY
BY RPT_LINK
WHERE TOTAL VAL_TOT GT 0
ON TABLE HOLD AS HALINKS
END
-RUN
 
 
DEFINE FILE HALINKS
HDR_IMAGE/A250='<IMG SRC=' || '&&SRVR.EVAL' || '/image/review.gif>';
TDATE/MDYY='&MDYY';
MSG/A40=IF VAL EQ 0 THEN '*** Report Failed ***' ELSE '';
VAL_TOT/I5=VAL;
 
DASH_POS/I2 = POSIT(RPT_NAME, 100, '-', 1, 'I2') - 1;
RPT_TYPE/A16=SUBSTR(100, RPT_NAME, 1, DASH_POS, DASH_POS, RPT_TYPE);
RNAME/A100=SUBSTR(100, RPT_NAME, (DASH_POS + 2), 100, (100 - (DASH_POS + 2)), RNAME);
 
RPT_SUBHEAD/A50=IF RPT_TYPE EQ 'Weekly' THEN 'Weekly Reports' ELSE
                IF RPT_TYPE EQ 'Monthly' THEN 'Monthly Reports' ELSE
                IF RPT_TYPE EQ 'MYTD' THEN 'Monthly Year To Date Reports' ELSE
                IF RPT_TYPE EQ 'Monthly Last 6' THEN 'Monthly Reports - Last 6 Months' ELSE
                IF RPT_TYPE EQ 'Monthly Last 12' THEN 'Monthly Reports - Last 12 Months' ELSE
                IF RPT_TYPE EQ 'YTD' THEN 'Year To Date Reports' ELSE
                IF RPT_TYPE EQ 'Quarterly' THEN 'Quarterly Reports' ELSE
                IF RPT_TYPE EQ 'QYTD' THEN 'Quarterly Year To Date Reports' ELSE
                IF RPT_TYPE EQ 'Annual' THEN 'Annual Reports' ELSE
                IF RPT_TYPE EQ 'Biannual' THEN 'Biannual Reports' ELSE ' ';
-*                IF RPT_TYPE EQ 'Biannual' THEN 'Biannual Reports' ELSE 'Unknown';
-*COPYRIGHT/A1 = CTRAN(1, ' ', 32, 169, 'A1');
AMPER/A1 = HEXBYT(38, 'A1');
SCOLON/A1 = HEXBYT(59, 'A1');
COPYRIGHT/A6 WITH RPT_NAME = AMPER | 'copy' | SCOLON;
CDATE0/A8MDYY='&MDYY';
CDATE1/A10=EDIT(CDATE0,'99/99/9999');
CTIME/A10=HHMMSS('A10');
FOOT1/A255='Post-trip Reporting ' | CDATE1 | ' at ' | CTIME;
FOOT2/A255=COPYRIGHT | ' ' | EDIT(CDATE0,'$$$$9999') | ' Travel and Transport, Inc.';
FOOT3/A255='Personal and Confidential';
SORT/A1='';
ZIP_URL/A255='&WZFILE.EVAL';
END
TABLE FILE HALINKS
PRINT RNAME AS ''
      RPT_SUBHEAD NOPRINT
      MSG AS ''
   VAL NOPRINT
   ZIP_URL NOPRINT
      RPT_INST_KEY NOPRINT
BY SORT NOPRINT
BY RPT_TYPE NOPRINT
BY RNAME NOPRINT
BY RPT_LINK NOPRINT
ON RPT_TYPE SUBHEAD
" </1<RPT_SUBHEAD </1"
HEADING
"Post-trip Push Reports"
" "
"Below are the links to your Post-trip Push Reports that were produced on <TDATE>."
" "
"Click the links below to view the reports."
 
" "
ON SORT SUBFOOT
" "
"Zip File of these Reports"
FOOTING BOTTOM
"<FOOT1"
"<FOOT2"
"<FOOT3"
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT HTML
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     UNITS=IN,
     SQUEEZE=ON,
     ORIENTATION=PORTRAIT,
$
TYPE=REPORT,
     GRID=OFF,
     FONT='ARIAL',
     SIZE=9,
$
TYPE=DATA,
     COLUMN=N5,
     TARGET=_BLANK,
	 URL=&&SRVR.EVAL/ibi_apps/WFServlet? ( \
        IBIF_ex='VIEW_REPORT_LINK' \
	    RURL=RPT_LINK \
		RNAME=RPT_NAME \
		IKEY=RPT_INST_KEY \
		xform='no'),
     WHEN=VAL EQ 1,
$
TYPE=TABHEADING,
     SIZE=12,
     STYLE=BOLD,
$
TYPE=DATA,
     COLUMN=MSG,
  COLOR=RED,
$
TYPE=TABFOOTING,
     SIZE=12,
     STYLE=BOLD,
$
TYPE=HEADING,
     SIZE=10,
  COLOR=GRAY,
$
TYPE=HEADING,
     LINE=1,
     SIZE=12,
     STYLE=BOLD,
  JUSTIFY=CENTER,
  COLOR=BLACK,
$
TYPE=HEADING,
     LINE=3,
  COLOR=BLACK,
$
TYPE=FOOTING,
     SIZE=10,
  COLOR=GRAY,
  JUSTIFY=CENTER,
$
TYPE=SUBHEAD,
     SIZE=10,
     STYLE=BOLD,
$
TYPE=SUBFOOT,
     SIZE=10,
$
TYPE=SUBFOOT,
     LINE=2,
     TARGET=_BLANK,
	 URL=&&SRVR.EVAL/ibi_apps/WFServlet? ( \
       IBIF_ex='VIEW_REPORT_LINK' \
	   RURL=ZIP_URL \
	   RNAME='Zip File' \
	   IKEY='0' \
	   xform='no'),
$
TYPE=SUBTOTAL,
     BACKCOLOR=RGB(210 210 210),
$
TYPE=ACROSSVALUE,
     SIZE=9,
$
TYPE=ACROSSTITLE,
     STYLE=BOLD,
$
TYPE=GRANDTOTAL,
     BACKCOLOR=RGB(210 210 210),
     STYLE=BOLD,
$
ENDSTYLE
END
-RUN
 
TABLE FILE PUSH_LINKS
BY SCHEDULE_ID
BY RPT_NUMBER
WHERE SCHEDULE_ID EQ '&DSTSCHEDID';
ON TABLE HOLD AS HSCHED
END
MODIFY FILE PUSH_LINKS
FIXFORM FROM HSCHED
MATCH SCHEDULE_ID RPT_NUMBER
 ON MATCH DELETE
 ON NOMATCH REJECT
DATA ON HSCHED
END
-RUN
