-*5/10/16-JEM-S-16344 Created for new report that shows Vision travel transactions only by agent


-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
FARE/D12.2CS = SEG_AMT + SEG_TAX;
TRNMY/MDYY =DATEMOV(TRN_DATE, 'BOM');
END
-RUN
TABLE FILE &&EXTRACT
PRINT AROLL_LEV2
   AGENT_NUM
   DOC_TYPE
   DOD_TYPE
   EMBARK
   EX_FLAG
   EXCHG_SALE
   FARE
   DOD_DESC
   RF_FLAG
   RESV_BRANCH
   SEG_AMT/D12.2
   SEG_COM/D12.2
   SEG_COUNT
   SEG_NBR
   SEG_TAX/D12.2
   TKT_SORT
   TRN_DATE
   TKT_NUM
   VOID_DATE
   VOID_STATUS
   TKT_SORT
   TRNMY
   TNT_COMPANY
BY AGENT_NUM
-INCLUDE &AIRDATES
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TC_TRANA
END
-RUN


TABLE FILE TC_TRANA
PRINT 
   DOC_TYPE
   DOD_TYPE
   EMBARK
   EX_FLAG
   EXCHG_SALE
   FARE
   DOD_DESC
   RF_FLAG
   RESV_BRANCH
   SEG_AMT
   SEG_COM
   SEG_COUNT
   SEG_NBR
   SEG_TAX
   TKT_SORT
   TRN_DATE
   TKT_NUM
   VOID_DATE
   VOID_STATUS
   TKT_SORT
   TRNMY
BY AGENT_NUM
ON TABLE HOLD AS TC_TRANB FORMAT FOCUS INDEX AGENT_NUM
END

JOIN AGENT_NUM IN TC_TRANB TO AGENT IN AGENT AS JA1
-RUN

TABLE FILE TC_TRANB
PRINT AGENT_NUM
     AGENT_NAME
   DOC_TYPE
   DOD_TYPE
   EMBARK
   EX_FLAG
   EXCHG_SALE
   FARE
   DOD_DESC
   RF_FLAG
   RESV_BRANCH
   SEG_AMT
   SEG_COM
   SEG_COUNT
   SEG_NBR
   SEG_TAX
   TKT_SORT
   TRN_DATE
   TKT_NUM
   VOID_DATE
   VOID_STATUS
   TRNMY
BY TKT_SORT NOPRINT
ON TABLE HOLD AS TC_TRANA1
END
-RUN

TABLE FILE TC_TRANA1
PRINT DOC_TYPE
   DOD_TYPE
   EMBARK
   EX_FLAG
   EXCHG_SALE
   FARE
   RF_FLAG
   SEG_AMT
   SEG_COM
   SEG_COUNT
   SEG_NBR
   SEG_TAX
   TRN_DATE
   TKT_NUM
   VOID_DATE
   VOID_STATUS
   TRNMY
   AGENT_NUM
   AGENT_NAME
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS TC_TRAN
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;



DEFINE FILE TC_TRAN
EMCO_TAX/D12.2 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_TAX 
  ELSE 0;	
EXCH_AMT/D12.2 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF 
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR 
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND 
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_AMT ELSE 0;	
EXCH_MCO/D12.2 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_AMT 
  ELSE 0;	
EXCH_TAX/D12.2 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF 
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR 
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND 
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_TAX ELSE 0;	
EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE 
	NE ' ') THEN 1 ELSE 0;  
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;	
FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
	
NEW_SALES/D12.2 = IF (SEG_AMT GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE 
  EQ ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5') THEN SEG_AMT ELSE 0;	
NEW_SEG_AMT/D12.2 = IF VOID_STATUS EQ '0' THEN SEG_AMT ELSE 0;	
NEW_SEG_COM/D12.2 = IF VOID_STATUS EQ '0' THEN SEG_COM ELSE 0;	
NEW_SEG_TAX/D12.2 = IF VOID_STATUS EQ '0' THEN SEG_TAX ELSE 0;	
NEW_TAX/D12.2 = IF ((SEG_TAX GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE EQ 
  ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5')) THEN SEG_TAX ELSE 0;	
NEW_TOTAL/D12.2 = NEW_SALES + NEW_TAX;	
-*ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;	


ORIG_SALE/P12 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND 
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;	

OTH_SALES/D9 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN ORIG_SALE 
  ELSE 0;	
OTHR_AMT/D12.2 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_SALES 
  ELSE 0;	
OTHR_TAX/D12.2 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_TAX ELSE 
  0;	
OTHR_TOTAL/D12.2 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_TOTAL 
  ELSE 0;	
PAID_FARE/D12.2 = IF VOID_DATE EQ 0 THEN FARE_PAID ELSE 0;	
PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;	
PTA_AMT/D12.2 = IF DOC_TYPE EQ '4' THEN NEW_SALES ELSE 0;	
PTA_CNT/D9 = IF DOC_TYPE EQ '4' THEN ORIG_SALE ELSE 0;	
PTA_TAX/D12.2 = IF DOC_TYPE EQ '4' THEN NEW_TAX ELSE 0;	
PTA_TOTAL/D12.2 = IF DOC_TYPE EQ '4' THEN NEW_TOTAL ELSE 0;	
REFUND_AMT/D12.2 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_AMT ELSE 
  0;	
REFUND_TAX/D12.2 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_TAX ELSE 
  0;	
REFUND_TOTAL/D12.2 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN FARE_PAID ELSE 
  0;	
REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;

TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;	
TKTLESS/D9 = IF DOC_TYPE EQ '9' THEN ORIG_SALE ELSE 0;	
TKTLS_AMT/D12 = IF DOC_TYPE EQ '9' THEN NEW_SALES ELSE 0;	
TKTLS_TAX/D12 = IF DOC_TYPE EQ '9' THEN NEW_TAX ELSE 0;	
TKTLS_TOTAL/D12 = IF DOC_TYPE EQ '9' THEN NEW_TOTAL ELSE 0;	
TOT_ETAX/D12.2 = EXCH_TAX + EMCO_TAX;	
TOT_EXCH/D12.2 = EXCH_AMT + EXCH_MCO;	
TOUR_AMT/D12.2 = IF DOC_TYPE EQ '2' THEN NEW_SALES ELSE 0;	
TOUR_ORDERS/D9 = IF DOC_TYPE EQ '2' THEN ORIG_SALE ELSE 0;	
TOUR_TAX/D12 = IF DOC_TYPE EQ '2' THEN NEW_TAX ELSE 0;	
TOUR_TOTAL/D12 = IF DOC_TYPE EQ '2' THEN NEW_TOTAL ELSE 0;	
TRAN_DT/MDYY = TRN_DATE;	
VOID_AMT/D12.2 = IF VOID_DATE NE 0 THEN SEG_AMT ELSE 0;	
VOID_TAX/D12.2 = IF VOID_DATE NE 0 THEN SEG_TAX ELSE 0;	
VOID_TOTAL/D12.2 = IF VOID_DATE NE 0 THEN FARE_PAID ELSE 0;	
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01')
 THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) THEN 1 ELSE 0;	

-* Defines on previous DEFINEs
EMCO_TOTAL/D12.2 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN FARE_PAID 
  ELSE 0;	
EXCHGS/P12 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
 NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE IF (DOC_TYPE EQ '1') AND 
(EXCHG_SALE EQ 'E') THEN 0 ELSE IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE 
  ' ') THEN TKT_CNT ELSE 0;
  
 
EXCH_TOTAL/D12.2 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE 
  IF (((EXCHG_SALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EMBARK NE 'DT1' OR 
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ') AND (EX_FLAG EQ 'F' OR 'P') AND 
  (SEG_COUNT LE 0)) OR (EMBARK EQ 'EX1' OR 'EX5')) THEN FARE_PAID ELSE 0;	
FRFND_AMT/D12 = IF RF_FLAG EQ 'F' THEN REFUND_AMT ELSE 0;	
FRFND_TAX/D12 = IF RF_FLAG EQ 'F' THEN REFUND_TAX ELSE 0;	
FRFND_TOTAL/D12 = IF RF_FLAG EQ 'F' THEN REFUND_TOTAL ELSE 0;	
HNDWRT_AMT/D12 = IF DOC_TYPE EQ '3' THEN NEW_SALES ELSE 0;	
HNDWRT_TAX/D12 = IF DOC_TYPE EQ '3' THEN NEW_TAX ELSE 0;	
HNDWRT_TOTAL/D12 = IF DOC_TYPE EQ '3' THEN NEW_TOTAL ELSE 0;	
HNDWRTS/D9 = IF DOC_TYPE EQ '3' THEN ORIG_SALE ELSE 0;	
MCO_AMT/D12.2 = IF DOC_TYPE EQ '1' THEN NEW_SALES ELSE 0;	
MCO_CNT/D9 = IF DOC_TYPE EQ '1' THEN ORIG_SALE ELSE 0;		
MCO_TAX/D12.2 = IF DOC_TYPE EQ '1' THEN NEW_TAX ELSE 0;	
MCO_TOTAL/D12.2 = IF DOC_TYPE EQ '1' THEN NEW_TOTAL ELSE 0;
MCOS/D9CS           = IF (DOC_TYPE EQ '1') AND
                           (EX_FLAG EQ ' ')  AND
                           (EXCHG_SALE NE ' ') THEN 1 ELSE 0;	
NET_AMT/D12.2 = NEW_SALES + REFUND_AMT + TOT_EXCH - VOID_AMT;	
NET_TAX/D12.2 = NEW_TAX + REFUND_TAX + TOT_ETAX - VOID_TAX;	
PRFND_AMT/D12 = IF RF_FLAG EQ 'P' THEN REFUND_AMT ELSE 0;	
PRFND_TAX/D12 = IF RF_FLAG EQ 'P' THEN REFUND_TAX ELSE 0;	
PRFND_TOTAL/D12 = IF RF_FLAG EQ 'P' THEN REFUND_TOTAL ELSE 0;	
TOTAL_EXCHG/D12.2 = EXCH_TOTAL + EMCO_TOTAL;	
TOT_ECNT/D9 = EXCHGS + EXMCOS;	
TOT_TRANS/D9 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;	
CHARGE_TRN/D9 = ORIG_SALE + VOIDS + EXCHGS + REFUNDS;
KW_TRN/D9 = ORIG_SALE + EXCHGS;
TRN_TYPE/A4 = IF (VOIDS EQ 1) THEN 'VOID' ELSE IF (ORIG_SALE EQ 1) THEN 
  'SALE' ELSE IF (TOT_ECNT EQ 1) THEN 'EXCH' ELSE IF (REFUNDS EQ 1) THEN 
  'RFND' ELSE ' ';
ROLLCD/A8 = '&&ROLL'; 
  
END
-RUN


-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================

TABLE FILE TC_TRAN
SUM ORIG_SALE
    EXCHGS
BY TRNMY
BY ROLLCD
BY AGENT_NUM
BY AGENT_NAME
ON TABLE HOLD AS TCT1
END
-RUN


MODIFY FILE TCATRNV2K
FIXFORM FROM TCT1
DATA ON TCT1
END
-RUN



-NEXTONE
