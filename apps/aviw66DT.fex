-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-*                     FOCEXEC: AIRRPT
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*6/2/98         JK                 INCLUDE AIRLEVM INSTEAD OF AIRLEVT
-*7/31/00     IBISTL-KR             ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*2/05/01      TANDT-SS             ADD BASE_FARE, DEPT_MONTH
-*5/31/01      TANDT-SS             ADD COMMISSION (COMM)
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-**************************************************************************
SET ASNAMES=ON
-RUN

DEFINE FILE &&RPT_HOLD ADD
COMM/D12.2 = IF VOID_DATE EQ 0 THEN SEG_COM ELSE 0;
-* LEVEL_DESC/A60  = &&LDESC;
PAID_FARE/D12.2 = IF VOID_DATE EQ 0 THEN FARE_PAID ELSE 0;
REFUND/D9CS    = IF (VOID_DATE EQ 0) AND
                    (SEG_COUNT LE 0) AND
                    (TKT_SORT NE LAST TKT_SORT) AND
                    (EMBARK NE 'DT1' OR 'RF1' OR 'FP3') AND
                    (RF_FLAG NE ' ') THEN 1 ELSE 0;
EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE 
	NE ' ') THEN 1 ELSE 0;  


END
-RUN

TABLE FILE &&RPT_HOLD
SUM COMM AS 'TCOMM'
    EXCHGS AS 'TEXCH'
    PAID_FARE AS 'TFARE'
    MCOS AS 'TMCO'
    EXMCOS AS 'TEXMCO'
    NET_TKT_CNT AS 'TNET'
    REFUND AS 'TREF'
    VOIDS AS 'TVOID'
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN


TABLE FILE &&RPT_HOLD
PRINT AGENT_NUM
      AIR_KEY	
      AIRLINE
      BR_CL_IDX	
      CLIENT_NUM
      COMM	
      DPT_TIME	
      EMB_APT_CD	
      EXCHGS
      EXMCOS
      FARE_PAID	
      FLIGHT	
      LEVEL1	
      LEVEL2	
      LEVEL3
      MCOS
      NET_TKT_CNT
      PAID_FARE
      RESV_BRANCH
      RTE_APT_CD	
      SEG_COUNT	
      TKT_NUM	
      TKT_TYPE	
      XDPT_DT	
      XTRAN_DT	
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT AGENT_NUM
      AIR_KEY	
      AIRLINE
      BR_CL_IDX	
      CLIENT_NUM
      COMM	
      DPT_TIME	
      EMB_APT_CD	
      EXCHGS
      EXMCOS
      FARE_PAID	
      FLIGHT	
      LEVEL1	
      LEVEL2	
      LEVEL3
      MCOS
      NET_TKT_CNT
      PAID_FARE
      RESV_BRANCH	
      RTE_APT_CD	
      SEG_COUNT	
      TKT_NUM	
      TKT_TYPE	
      XDPT_DT	
      XTRAN_DT	
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TCOMM
      TEXCH
      TEXMCO
      TFARE
      TMCO
      TNET
      TREF
      TVOID
BY LEVEL_DESC
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

TABLE FILE AIRHOLD2
PRINT AGENT_NUM
      AIR_KEY	
      AIRLINE
      BR_CL_IDX	
      CLIENT_NUM
      COMM	
      DPT_TIME	
      EMB_APT_CD	
      EXCHGS
      EXMCOS
      FARE_PAID	
      FLIGHT
      LEVEL_DESC	
      LEVEL1	
      LEVEL2	
      LEVEL3
      MCOS
      NET_TKT_CNT
      PAID_FARE
      RESV_BRANCH	
      RTE_APT_CD	
      SEG_COUNT	
      TCOMM
      TEXCH
      TEXMCO
      TFARE
      TMCO
      TNET
      TREF
      TVOID
      TKT_NUM	
      TKT_TYPE
      TRN_DATE	
      XDPT_DT
      XPSNGR_NM	
      XTRAN_DT	
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN

-JOIN
JOIN AIRHOLD3.LEVEL2 IN AIRHOLD3 TO ALL KIMBORG.ORG_UNIT IN KIMBORG AS J1
-RUN
 
DEFINE FILE AIRHOLD3
PASNGR_NAME/A15 = IF TKT_NUM NE LAST TKT_NUM THEN XPSNGR_NM ELSE ' ';	
SEC_PRES/A46=ORG_UNIT||'-'||ORG_DESC;
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';	
XT_DTE/A8 = EDIT(TRN_DATE);	
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');	
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;	
REFUNDS/D9CS = IF TKT_SORT NE LAST TKT_SORT THEN TREF ELSE 0;
-******************************************************************

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||LEVEL_DESC||XPSNGR_NM;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
XCOMM/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TCOMM ELSE 0;
NET_TKT_CNT1/D8CS = IF (SEG_NBR EQ '01') THEN TNET ELSE 0;                       
EXCH_CNT/D9CS = IF (SEG_NBR EQ '01') THEN TEXCH ELSE 0;
-* MCO_CNT/D9CS = IF (FARE_PAID GE 0) THEN TMCO ELSE 0;
MCO_CNT/D9CS = IF SRT_DT NE LAST SRT_DT THEN TMCO ELSE 0;
EMCO_CNT/D9CS = IF SRT_DT NE LAST SRT_DT THEN TEXMCO ELSE 0;
VOID_CNT/D9CS = IF (SEG_NBR EQ '01') THEN TVOID ELSE 0;
-* *** DEFINES ON PREVIOUSLY DEFINED DEFINES**********
TOTAL_TRAN/D9CS = NET_TKT_CNT1 + EXCH_CNT + MCO_CNT + REFUNDS + VOID_CNT;

-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*7/31/00     IBISTL-KR             ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-**************************************************************************
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END

-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_DRIL;

-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'AIRHOLD3';
-INCLUDE DRILWHRS
-************************************************************
-SKIP_DRIL

TABLE FILE AIRHOLD3
-INCLUDE HEADER
"&&SUBHEAD"
"</2"
-*
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*
&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-*
-**********************************************************************
-* Where statements for Drilled Report
-**********************************************************************
-*
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
-*
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10
-*
-**********************************************************************
-* Report specific footer(s)
-**********************************************************************
-*
-INCLUDE FOOTERTR
-*
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT 

ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
