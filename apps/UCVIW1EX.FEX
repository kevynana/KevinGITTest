-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-*3/10/14 - ADDED LEVEL4 LEVEL5 RESV_STATUS- DV
-*4/18/14 - DEB - ADDED PICK_DAY CC_NUM
-*4/23/14 - DEB - ADDED TRIP_PURPOSE
-*6/10/14 - ADDED A HARD CODE TO SELECT UPON DROP DATE FOR GRCGL - JK
-*2/19/15 - DEB - S-07532 ADDED LEVEL6 FOR  AMNUCDTL 
-* 4/9/15 - S-08224 -ADDED DROP_DAY
-*12/22/15 - DLV - S-13284   Updated to allow reports to sort/subtotal by id-levels
-*7/11/16 S-20630 DLV  Added EMP_ID to extract
-* 10/25/17     JEM         S-39364 Carrying thru new field TRAVELER_EMAIL

-INCLUDE SETECHO 
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-SET &&IDLEVELBREAK = ''; 
-RUN  
 
-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT  
BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
  CAR_DSC/A20 = EDIT(CAR_DESC, '99999999999999999999$$$$$$$$$$');
  LEVEL_DESC1/A60 = &&LDESC; 
  NBR_DAYS/D12 = NUM_DAYS;
  NBR_CARS/D12 = NUM_CARS;
  DROP_OFF_DT/MDYY = DROP_DATE;	
  INV_DATE/MDYY = INVOICE_DATE;  
  PICK_UP_DT/MDYY = PICKUP_DATE;
  PUP_DT/I8MDYY = PICKUP_DATE;
  ALPHA_PIK_DT/A8 = EDIT(PUP_DT);
  RENTAL_DAYS/D8 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
  NEW_RENTAL_AMT/D14.2CS = RENTAL_AMT;                   
  XPICK_DOW/W   = PICKUP_DATE;
  XDROP_DOW/W   = DROP_DATE;

-* ****DEFINES ON PREVIOUS DEFINES****
  FOP/A2 = EDIT(CC_NUM,'99$$$$$$$$$$$$$$$$');
  FOP2/A7 = IF FOP EQ 'AX' THEN EDIT(CC_NUM, '99$$$$$$$$$$99999')
          ELSE EDIT(CC_NUM, '99$$$$$$$$$$$$9999');    
  PICK_DAY/A3   = DECODE XPICK_DOW (1 'MON' 2 'TUE' 3 'WED'
                        4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  DROP_DAY/A3   = DECODE XDROP_DOW (1 'MON' 2 'TUE' 3 'WED'
                        4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
-INCLUDE IDLEVDEFINE 

END


TABLEF FILE &&EXTRACT
PRINT 
  ALPHA_PIK_DT
  ALPHA_RATE
  AROLL_LEV5
  AROLL_LEV6
  BR_CL_IDX
  BOOKINGS
  CAR_DSC
  CAR_KEY	
  CAR_TYPE
  CCR_CONFIRM
  CC_NUM
  DAILY_RATE 
  DOD_TYPE
  DROP_CTY.CITY_NAME/A14 AS 'DROP_CTY_NAME' 
  DROP_DATE
  DROP_DAY
  DROP_OFF_DT
  EMP_ID
  FOP2
  INV_DATE
  INVOICE_DATE	
  INVOICE_NUM 	
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  LEVEL5
  LEVEL6
  NBR_DAYS
  NBR_CARS
  NEW_RENTAL_AMT
  PASSNGR_NAME
  PICK_CTY.CITY_NAME/A14 AS 'PICK_CTY_NAME'
  PICK_DAY
  PICKUP_CITY
  PICK_UP_DT
  PICKUP_DATE
  PNR_LOCATOR 	
  REFUSAL_DESC	
  REFUSE_CODE	
  RENTAL_AMT/D12.2
  RENTAL_DAYS	
  RESV_STATUS
  TRAVELER_EMAIL
  TRIP_PURPOSE
  VENDOR_CODE
  VENDOR_NAME  
-IF &&SETF EQ 'GRCGL' GOTO GROW;
-INCLUDE &CARDATES 
-GOTO WHR;
-GROW
WHERE DROP_DATE GE &&FROMDT.EVAL AND DROP_DATE LE &&TODT.EVAL
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
-WHR;
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
-INCLUDE CARRATE
-*ON TABLE HOLD AS CARHLD
ON TABLE HOLD AS &&RPT_HOLD
END
-RUN




