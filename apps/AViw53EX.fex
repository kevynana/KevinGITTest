
-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File AIREXTRT.FEX
-********************************************************************
-* MODIFICAIONS: 
-* 
-********************************************************************
-*
-* This program extracts data for Top Traveler No hotel booking report
-*
-*04/20/10   DEB                CHANGED ORIG_SALE DEFINE
-*1/23/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*7/21/16 S-20630 DLV - Added EMP_ID to extract
-*03/17/17 S-31790 DLV - Changed to use new hotel attachment logic
-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-INCLUDE SETECHO
-RUN
-* -SET &RPARM = &&WEB_PATH || 'RPTPARMS.fex';

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
DEFINE FILE &&EXTRACT
  
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  GROSS_FARE/D12.2CS  = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
  IBOOK/A42 = DOD_DESC;

  LEVEL_DESC/A60      = &&LDESC;
  MIR_TYPE/A24 = DECODE AUX_NON_MIR ('X' 'X-DUAL MIR'
                                    'N' 'N-NON-TICKETING MIR'
                                    'J' 'J-NON-FARING MIR'
                                    'A' 'A-NON-ACCOUNTING MIR'
                                    'B' 'B-ACCOUNTING MIR'
                                    'H' 'H-REGULAR TICKET MIR'
                                    'V' 'V-VOID'
                                    'E' 'E-ELECTRONIC TKT NETWORK'
                                    'L' 'L-ELECTRONIC TKT MIR'
                                    ' ' ' -MANUALLY ENTERED TRANS');
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NOHR_KEY/A2          = EDIT(NHR_KEY, '$$$$$$$$99');
-*  NOHTL_KEY/A10        = IF NOHR_KEY NE ' ' THEN NOHR_KEY ELSE 'NONE GIVEN'; 
  NOHTL_KEY/A40        = IF NOHR_KEY NE ' ' THEN NHR_DESC ELSE 'NONE GIVEN'; 
 
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  XARR_DOW/W           = ARR_DATE;
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;

TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;
BKD_ONLINE/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';

XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
XTRAN_DT/MDYY        = TRN_DATE; 
XFST_DPT/MDYY = FST_DPT_DATE;
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');

-* DEFINES ON PREVIOUS DEFINES
-*ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;

ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND 
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;	


-* PAX/A5 = EDIT(XPSNGR_NM, '99999$$$$$$$$$$');
EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
  NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
  IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;
EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') THEN 1 ELSE 0; 
PAX/A5 = EDIT(XPSNGR_NM, '99999$$$$$$$$$$');

-* ****DEFINES ON PREVIOUS DEFINES****

  ABOOK_CNT/D8CS = IF DOD_TYPE NE 'H' OR 'I' OR 'J' THEN NET_TKT_CNT ELSE 0;
  ABOOK_FARE/D12.2CS = IF DOD_TYPE NE 'H' OR 'I' OR 'J' THEN FARE_PAID ELSE 0;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' 
     THEN FARE_PAID ELSE 0;
  ARR_DAY/A3           = DECODE XARR_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  DPT_DAY/A3           = DECODE XDPT_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  IBOOK_CNT/D8CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN NET_TKT_CNT ELSE 0;
  IBOOK_FARE/D12.2CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN FARE_PAID ELSE 0;
  TRIPS/D8CS = IF STAY GT 0 THEN 1 ELSE
               IF STAY LT 0 THEN (-1) ELSE 0;
               
-INCLUDE TRANTYPE               


END
-RUN

TABLEF FILE &&EXTRACT
PRINT AIR_KEY
  AIRLINE
  ABOOK_CNT
  ABOOK_FARE
  AGENT_NUM 
  AIR_MAIN.INTL_DOM 
  ARR_DAY   
  ARR_TIME
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
  DOD_PAX_CAT   
  DPT_DAY   
  DPT_TIME  
  DST_APC
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM' 
  EMP_ID
  EXCHGS
  EXMCOS
  FARE_PAID 
  FLIGHT
  HOTEL_FLAG
  IBOOK
  IBOOK_CNT
  IBOOK_FARE
  INVOICE_NUM
  LEVEL_DESC    
  LEVEL1    
  LEVEL2    
  LEVEL3    
  NET_TKT_CNT
  NOHR_KEY
  NOHTL_KEY
  ORIG_SALE
  PASSNGR_NAME
  REFUSE_CODE
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'  
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'  
  SEG_NBR   
  SEG_STANDARD  
  STAY  
  TKT_NUM   
  TKT_SORT  
  TKT_TYPE  
  TRN_DATE  
  XARR_DT   
  XDPT_DT 
  XFST_DPT
  XPSNGR_NM
  XTKT_SRT
  XTRAN_DT
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
-*WHERE ORIG_SALE NE 0 OR EXMCOS NE 0 OR EXCHGS NE 0

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS &&RPT_HOLD
END
-RUN

