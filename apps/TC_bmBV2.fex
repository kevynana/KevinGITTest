-* 12/28/04 - DEB - ADDED DOD_TYPE 'J' TO P1ONL P2ONL P1DONL P2DONL DEFINE
-*2/18/05 - jk - Changed BLV annual volume to $9 mil from $8.5 per Cathy
-*2/21/05 - dv - changed annual volume of all accts per Cathy
-*2/22/05        JK                 ADDED TNT_COMPANY WHERE STATEMENT
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
 
-INCLUDE SETECHO
 
CREATE FILE TCBMBV
 
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCBMB2EX
-RUN
 
 
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
DEFINE FILE TCBMBV
P1/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
 
 
END
 
TABLE FILE TCBMBV
PRINT
  CGADV
  DOD_TYPE
  FARE_PAID
  INTL_DOM
  ITDOM
  NET_TKT_CNT
  P1
  TRN_DATE
  VOID_DATE
  VOID_STATUS
BY ROLLCD
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS TRANONE
END
-RUN
 
DEFINE FILE TRANONE
P1DAVOL/D12.2CS = IF (P1 EQ 'Y') AND (INTL_DOM EQ 'D') THEN FARE_PAID ELSE 0;
P1IAVOL/D12.2CS = IF (P1 EQ 'Y') AND (INTL_DOM EQ 'I') THEN FARE_PAID ELSE 0;
P1DNTKT/D8 = IF (P1 EQ 'Y') AND (INTL_DOM EQ 'D') THEN NET_TKT_CNT ELSE 0;
P1INTKT/D8 = IF (P1 EQ 'Y') AND (INTL_DOM EQ 'I') THEN NET_TKT_CNT ELSE 0;
P1ONL/D8 = IF (P1 EQ 'Y') AND (DOD_TYPE EQ 'I' OR 'J') THEN NET_TKT_CNT ELSE 0;
P1NTKT/D8 = IF (P1 EQ 'Y') THEN NET_TKT_CNT ELSE 0;
P1ADVT/P8 = IF (P1 EQ 'Y') AND (CGADV EQ '0-6') AND (INTL_DOM EQ 'D')
              THEN NET_TKT_CNT ELSE 0;
P1DONL/D8 = IF (P1 EQ 'Y') AND (DOD_TYPE EQ 'I' OR 'J') AND (INTL_DOM EQ 'D')
                THEN NET_TKT_CNT ELSE 0;
 
 
 
END
-RUN
 
TABLE FILE TRANONE
SUM P1DAVOL
    P1IAVOL
    P1DNTKT
    P1INTKT
    P1ONL
    P1NTKT
    P1ADVT
COMPUTE P1DONLP/P12.2 = IF ((P1DNTKT LT 1) AND (P1DNTKT GT (-1))) THEN 0
                         ELSE ((P1DONL/P1DNTKT)*100);
        P1DAVGT/D12.2  = IF ((P1DNTKT LT 1) AND (P1DNTKT GT (-1))) THEN 0 ELSE
                          (P1DAVOL/P1DNTKT);
        P1IAVGT/D12.2  = IF ((P1INTKT LT 1) AND (P1INTKT GT (-1))) THEN 0 ELSE
                          (P1IAVOL/P1INTKT);
        P1ADVP1/P12.2 = IF ((P1DNTKT LT 1) AND (P1DNTKT GT (-1))) THEN 0
                          ELSE ((P1ADVT/P1DNTKT)*100);
BY ROLLCD
ON TABLE HOLD AS TRANTWO
END
-RUN
 
 
 
-SET &&RPTSUF = 'BENCHMARK';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-SET &&SP = '|';
-SET &&SP1A = IF &&ROLLNME EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP1 = IF &&ROLLCS EQ ' ' THEN ' ' ELSE &&SP1A;
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&CSLPA = IF &&RCNCT.EVAL GE 10 OR &&CSL.EVAL GE 10 THEN 60 ELSE 68;
-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' THEN 78 ELSE &&CSLPA;
-SET &&ROLLCS11 = TRUNCATE(&&ROLLCS);
-SET &&RCNCT1 = TRUNCATE(&&ROLLCNCT);
-SET &&BLANK = ' ';
 
 
-INCLUDE FDEFRPTS
-RUN
 
 
DEFINE FILE TRANTWO
NOWTOD/A8 WITH ROLLCD = HHMMSS (NOWTOD);
BLANK/A1 = ' ';
ITYPE/A18 = IF ROLLCD EQ 'FUJI-R' OR 'KIMB-R' THEN 'CONSUMER PRODUCTS' ELSE
            IF ROLLCD EQ 'BLV-R' OR 'CH2M-R' THEN 'ENGINEERING' ELSE
            IF ROLLCD EQ 'METX-R' THEN 'INSURANCE' ELSE
            IF ROLLCD EQ 'CONG-R' THEN 'FOOD SERVICE' ELSE
            IF ROLLCD EQ 'UPTTL-R' THEN 'DISTRIBUTION' ELSE
            IF ROLLCD EQ 'KIEWIT-R' THEN 'CONSTRUCTION' ELSE
            IF ROLLCD EQ 'PRINC-R' THEN 'FINANCIAL' ELSE ' ';
GEOR/A10 =  IF ROLLCD EQ 'BLV-R' OR 'CONG-R' OR 'UPTTL-R' OR 'KIEWIT-R'
               OR 'PRINC-R' THEN 'MIDWEST' ELSE
            IF ROLLCD EQ 'KIMB-R' THEN 'SOUTH' ELSE
            IF ROLLCD EQ 'METX-R' THEN 'NORTHEAST' ELSE
            IF ROLLCD EQ 'CH2M-R' THEN 'WEST' ELSE
            IF ROLLCD EQ 'FUJI-R' THEN 'NATIONAL' ELSE ' ';
AVOL/A7 =   IF ROLLCD EQ 'BLV-R' THEN '$9.6 M' ELSE
            IF ROLLCD EQ 'KIMB-R' THEN '$21.4 M' ELSE
            IF ROLLCD EQ 'METX-R' THEN '$26.7 M' ELSE
            IF ROLLCD EQ 'CONG-R' THEN '$27.5 M' ELSE
            IF ROLLCD EQ 'UPTTL-R' THEN '$13 M' ELSE
            IF ROLLCD EQ 'KIEWIT-R' THEN '$10.5 M' ELSE
            IF ROLLCD EQ 'CH2M-R' THEN '$14.5 M' ELSE
            IF ROLLCD EQ 'PRINC-R' THEN '$8.5 M' ELSE
            IF ROLLCD EQ 'FUJI-R' THEN '$6 M' ELSE ' ';
CMPNY/A16 = IF ROLLCD EQ 'BLV-R' THEN 'BLACK AND VEATCH' ELSE
            IF ROLLCD EQ 'KIMB-R' THEN 'COMPANY A' ELSE
            IF ROLLCD EQ 'METX-R' THEN 'COMPANY B' ELSE
            IF ROLLCD EQ 'CONG-R' THEN 'COMPANY C' ELSE
            IF ROLLCD EQ 'UPTTL-R' THEN 'COMPANY D' ELSE
            IF ROLLCD EQ 'KIEWIT-R' THEN 'COMPANY E' ELSE
            IF ROLLCD EQ 'CH2M-R' THEN 'COMPANY F' ELSE
            IF ROLLCD EQ 'PRINC-R' THEN 'COMPANY H' ELSE
            IF ROLLCD EQ 'FUJI-R' THEN 'COMPANY I' ELSE ' ';
 
END
-RUN
 
TABLE FILE TRANTWO
SUM P1DAVGT
    P1IAVGT
    P1DONLP
    P1ADVP1
BY CMPNY
BY ITYPE
BY GEOR
BY AVOL
WHERE CMPNY NE ' '
ON TABLE HOLD AS TRAN3
END
-RUN
 
-IF &RECORDS EQ 0 GOTO XXITA;
-RUN
 
 
TABLE FILE TRANTWO
SUM P1DAVGT AS 'DOMESTIC, ATP YTD'
    P1IAVGT AS 'INTL ATP YTD'
    P1DONLP AS 'ONLINE,ADOPTION % YTD'
    P1ADVP1 AS 'DOM. ADVANCE,PURCHASE, 0-6 DAYS,YTD'
BY CMPNY AS 'COMPANY'
BY ITYPE AS 'INDUSTRY TYPE'
BY GEOR AS 'GEOGRAPHIC REGION'
BY AVOL AS 'ANNUAL VOLUME'
 
-GOTO HEAD
 
-HEAD
HEADING
HEADING
"&&ROLLNME &&SP1 &&ROLLCS11 &&SP3 &&RCNCT1 &&SP4 &&ROLLPHN"
"&&DTDESC &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"&&RPT_TTL1"
"&&RPT_TTL2"
" "
FOOTING BOTTOM
"eTTek Review &DATE AT <NOWTOD <60 &&FOOTR"
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
 
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.300000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=20, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=20, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=6, COLSPAN=4, JUSTIFY=LEFT, $
 
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     BACKCOLOR=(RGB(242 242 242) 'WHITE' ),$
 
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$
 
TYPE=TITLE, JUSTIFY=LEFT, $
 
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=FOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=SUBHEAD,
     STYLE=BOLD,
$
 
TYPE=SUBTOTAL,
     STYLE=BOLD,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $
 
TYPE=GRANDTOTAL, STYLE=BOLD, $
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN
 
-XXITA;
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
