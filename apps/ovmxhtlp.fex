-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Due to hotel market format change, changed city_st define to have an extra
-* space JK 3/23/04
-*7/13/05    DEB                ADDED REFUSE_CD 'R' AND 'T' TO APP_FLAG DEFINE
-*7/18/05    STEVE              Took out REFUSE_CD 'T' TO APP_FLAG DEFINE - per Robin Lewis
-*11/2/10    DEB    CHANGED APP_FLAG TO USE CLIENT_PREF INSTEAD OF REASON CODE
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/9/14 Updated Pref_non define to use exclude_flags - jk



TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.

SET ASNAMES = ON


DEFINE FILE OVMXHHLD
CITY_ST/A40 = PROP_STATE||(' '| ' '|PROP_CITY);
NNROOMS/D10 = IF  (NUM_DAYS LT 0) AND
                  (NUM_ROOMS LT 0) THEN
                  (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                  (NUM_DAYS * NUM_ROOMS);

TH_AMT/D12.2 = TOTAL_AMT;

CY/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
           INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 =   IF INVOICE_DATE GE &&FXDATE.EVAL AND
             INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
-*APP_FLAG/A30 = IF REFUSE_CODE EQ 'A' OR 'R' THEN 'PREFERRED'
-*               ELSE 'NON-PREFERRED';

-* APP_FLAG/A30= IF CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'PREFERRED' ELSE 'NON-PREFERRED';
 PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
 PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
 APP_FLAG/A30 = IF PNP_FLAG EQ 'I'  THEN 'PREFERRED' ELSE 'NON-PREFERRED';
 

CNGTS/D12 = IF (CY EQ 'Y') THEN NNROOMS ELSE 0;
PNGTS/D12 = IF (PY EQ 'Y') THEN NNROOMS ELSE 0;
END
-RUN

TABLE FILE OVMXHHLD
PRINT APP_FLAG
      CNGTS
      PNGTS
BY CITY_ST
ON TABLE HOLD AS MKTHLD
END
-RUN


DEFINE FILE METXHTL
HTL_MKT/A130 = IF H_MARKET NE ' ' THEN H_MARKET ELSE ST_CITY;
END

TABLE FILE METXHTL
PRINT HTL_MKT
BY ST_CITY AS 'CITY_ST'
WHERE ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS MKTFILE
END
-RUN


MATCH FILE MKTHLD
PRINT APP_FLAG
      CNGTS
      PNGTS
BY CITY_ST
ON TABLE HOLD
RUN
FILE MKTFILE
SUM HTL_MKT
BY CITY_ST
AFTER MATCH HOLD AS TTLMKT OLD-AND-NEW
END
-RUN

TABLE FILE TTLMKT
SUM CNGTS 
    PCT.CNGTS AS 'CPCT'
    PNGTS
    PCT.PNGTS AS 'PPCT'
BY APP_FLAG
ON TABLE HOLD AS APPHLD 
END
-RUN

-SET &RECFOUND = &RECORDS;

-GOTO AROUND

GRAPH FILE HHOLD6
SUM CDAY AS ' '
ACROSS RANK_LABEL 
ON GRAPH SET LOOKGRAPH PIE
ON GRAPH SET GRAPHEDIT OFF
-* setO1AxisSide(0);
ON GRAPH SET GRAPHSTYLE *
setConnectLineMarkers(true);
setO1LabelDisplay(true);
setO1MajorGridDisplay(false);
setO1MinorGridDisplay(false);
setY1LabelDisplay(true);
setY1AxisSide(0);
setY1MajorGridDisplay(false);
setY1MinorGridDisplay(false);
setPieFeelerTextDisplay(1);
setPieLabelDisplay(0);
setTextFormatPreset (getPieSliceLabel () ,1);
setLegendDisplay(true);
setLegendMarkerPosition(2);
setTitleString("HOTEL BOOKING SUMMARY");
setTextJustHoriz(getTitle(), 1);
setSubtitleString("Based On Number of Hotel Nights");
setTextJustHoriz(getSubtitle(), 1);
ENDSTYLE
ON GRAPH SET BARNUMB OFF
ON GRAPH SET 3D OFF
ON GRAPH SET GRID ON
ON GRAPH SAVE AS HOTEL FORMAT GIF
END

-AROUND


DEFINE FILE APPHLD
CATEGORY/A30 = 'PREFERRED HOTELS';
CAT_ORDER/I2 = 6;
END
-RUN

TABLE FILE APPHLD
PRINT CATEGORY/A30 AND APP_FLAG/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CNGTS/D15 AS 'VOL_CURR' AND 
CPCT/D15 AS 'TKT_CURR' AND PNGTS/D15 AS 'VOL_PRIOR' 
AND PPCT/D15 AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS APPHLD1
END
-RUN

TABLE FILE APPHLD1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS APPHLD2
END
MODIFY FILE OV1_MAIN
FIXFORM FROM APPHLD2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON APPHLD2
END
-RUN








