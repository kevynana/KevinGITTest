-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File BLVAIREX.FEX
-********************************************************************
-* MODIFICAIONS:
-* Created by JK 1/16/03
-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-*12/21/04  DEB CHANGED OT_BOOK DEFINE TO INCLUDE ADDTL DOD_TYPE
-*10/11/05  DEB CHANGED OT_BOOK DEFINE TO INCLUDE H DOD_TYPE
-* Changed AUTO_REPORT to NONREF_FLAG - TRX issue - SAC - 06/22/07
-*1/27/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE

-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
  BOOK_DT/MDYY         = RESV_DATE; 
  OT_BOOK/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';
  CARD/A2=EDIT(CREDIT_CARD, '99$$$$$$$$$$$$$$$$');
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;  
  LEVEL_DESC/A60 = &&LDESC;  
  MNON_REF/A15 = DECODE NONREF_FLAG  ('N' 'NON-REFUNDABLE'
                                     'R' 'REFUNDABLE'
                                     ' ' 'REFUNDABLE');
  SO_FLAG/A10 = DECODE STOP_CON ('O' 'STOPOVER'
                                 'X' 'CONNECTION'
                                 ' ' ' ');
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';  
  VOID/A1 = IF VOID_DATE NE 0 THEN 'Y' ELSE 'N';
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');  
  XTRAN_DT/MDYY        = TRN_DATE;

-* ****DEFINES ON PREVIOUS DEFINES**** 
CARD_FLAG/A1 = IF CARD EQ 'CA' THEN 'Y' ELSE 'N';
  
END
TABLEF FILE &&EXTRACT
PRINT 
  AIRLINE
  ARR_DATE	
  BOOK_DT
  CARD_FLAG	
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
  CITYPAIR.INTL_DOM
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
  CLASS	
  DPT_TIME	
  FARE_PAID	
  FLIGHT	
  LEVEL_DESC	
  LEVEL1	
  LEVEL2	
  LEVEL3
  MNON_REF
  OT_BOOK	
  PASSNGR_NAME	
  REFUSE_CODE	
  SEG_COUNT	
  SEG_LOWEST	
  SEG_NBR	
  SO_FLAG
  TKT_NUM	
  TKT_SORT	
  TRN_DATE
  VOID	
  VOID_DATE	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT	


WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
-* WHERE VOID_DATE EQ 0

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


ON TABLE HOLD AS &&RPT_HOLD
END






