-******************************************************************************
-* This program should be included in all reports in Dashboard Reporting.
-*
-* The following variables should be used in the programs
-*    &WHERE1 - Hierarchy selections
-*    &WHERE2 - date range selections.
-*    &WHERE3 - booking type selections
-*    &WHERE4 - DOM/INTL selection (AIR reports only)
-*    &WHERE5 - output format selection
-*    &SHIFT_PCT - Shift percentage
-*
-* Exceptional:
-*    &WHERE22 - date range from_my extended to include previous year
-*    &WHERE4CH - DOM/INTL selection (Car and Hotel Reports)
-*    &BREAKBY - to be used in Summary By Hierarchy 
-******************************************************************************
-INCLUDE SETECHO
-DEFAULT &&PushTeck = 'N'; 
-DEFAULT &&OUTLINE1 = '';
SET ASNAME = ON
-IF '&&PushTeck.EVAL' EQ 'Y' THEN GOTO SKIPPARM1;
-INCLUDE DBRSET1
-RUN
 
-DEFAULTS &LEVEL = ' ', &DEPT = ' ', &BREAKBY='' ;
-DEFAULTS &FROMDT = ' ', &TODT = ' ';
-DEFAULTS &DODTYPE = ' ', &DOMINTL = ' ', &FMT=' ', &PCNTSHIFT=' ';
 
-TYPE DEPT LENGTH = &DEPT.LENGTH;
-SET &LEVELX = IF '&LEVEL.EVAL' EQ 'FOC_NONE' OR '&LEVEL.EVAL' EQ ' ' THEN ' '
-  ELSE IF &LEVEL LT '10' THEN EDIT(&LEVEL,'$9') ELSE &LEVEL;
-SET &AROLL_LEV = 'AROLL_LEV' | &LEVELX;

-*Added for BreakBy
-SET &BREAKX = IF '&BREAKBY.EVAL' EQ 'FOC_NONE' OR '&LEVEL.EVAL' EQ ' ' THEN ' '
-  ELSE IF &BREAKBY LT '10' THEN EDIT(&BREAKBY,'$9') ELSE &BREAKBY;
-SET &AROLL_LEVX = IF '&BREAKBY.EVAL' EQ '01' THEN ' ' ELSE 'AROLL_LEV' | '&BREAKX.EVAL';
-SET &XBREAKBY = IF &BREAKBY EQ '01' THEN ' ' ELSE 'BY &AROLL_LEVX.EVAL';

-SET &ROLLX='''' || '&&ROLLUP.EVAL' || '''';
-SET &WHERE1 = IF '&LEVELX.EVAL' EQ ' ' OR '&LEVELX.EVAL' EQ 'FOC_NONE' THEN ' '
- ELSE IF '&LEVELX.EVAL' EQ '1' AND &DEPT.QUOTEDSTRING EQ &ROLLX THEN ' '
- ELSE IF '&LEVELX.EVAL' EQ '1' THEN 'WHERE TNT_COMPANY EQ ' | &DEPT
- ELSE 'WHERE AROLL_LEV' | '&LEVELX.EVAL' | ' EQ ' | &DEPT;
 
-SET &WHERE1X = IF '&LEVEL.EVAL' EQ '01' THEN ' ' ELSE 'WHERE TNT_COMPANY EQ ''1'''; 
-SET &FROMDT = IF EDIT(&FROMDT,'$$9') EQ '/' THEN &FROMDT ELSE EDIT(&FROMDT, '99/9999');
-SET &TODT = IF EDIT(&TODT,'$$9') EQ '/' THEN &TODT ELSE EDIT(&TODT, '99/9999');
 
-IF '&FROMDT.EVAL' NE ' ' AND '&TODT.EVAL' NE ' ' THEN GOTO SkipReadMY;
-SET &DTFILE = 'ED' || '&&COMP.EVAL';
TABLE FILE &DTFILE
PRINT FROM_MY
BY HIGHEST FROM_MY
ON TABLE SAVE
END
-RUN
-READ SAVE &FROMDT.6. &TODT.6.
-TYPE &FROMDT &TODT
-SET &FROMDT = EDIT(&FROMDT, '99') | '/' | EDIT(&FROMDT, '$$9999');
-SET &TODT = EDIT(&TODT, '99') | '/' | EDIT(&TODT, '$$9999');
 
-SkipReadMY
-SET &WHERE2 = IF '&FROMDT.EVAL' EQ 'FOC_NONE' AND '&TODT.EVAL' EQ 'FOC_NONE' THEN ' '
-  ELSE IF '&FROMDT.EVAL' EQ ' ' AND '&TODT.EVAL' EQ ' ' THEN ' '
-  ELSE 'WHERE INVOICE_MY IS-FROM ' | '''&FROMDT.EVAL''' | ' TO ' | '''&TODT.EVAL''';
 
-SET &WHERE3 = IF &DODTYPE EQ 'FOC_NONE' OR &DODTYPE EQ 'A' THEN ' '
- ELSE IF &DODTYPE EQ 'O' THEN 'WHERE DOD_TYPE EQ ' | '''I''' | ' OR DOD_TYPE EQ ' | '''J''' | ' OR DOD_TYPE EQ ' | '''H'''
- ELSE 'WHERE DOD_TYPE NE ' | '''I''' | ' AND DOD_TYPE NE ' | '''J''' | ' AND DOD_TYPE NE ' | '''H''' ;
 
-SET &WHERE4 = IF &DOMINTL EQ 'FOC_NONE' OR &DOMINTL EQ 'A' THEN ' '
- ELSE 'WHERE INTL_DOM_TKT EQ ' | '''' | &DOMINTL | '''';
 
-SET &WHERE4CH = IF &DOMINTL EQ 'FOC_NONE' OR &DOMINTL EQ 'A' OR &DOMINTL EQ ' ' THEN ' '
- ELSE 'WHERE INTL_DOM EQ ' | '''' | &DOMINTL | '''';
 
-SET &WHERE5 = IF &FMT EQ 'FOC_NONE' THEN ' '
- ELSE IF &FMT EQ 'H' THEN ' '
- ELSE 'PDF';
 
-SET &SHIFT_PCT = IF &PCNTSHIFT EQ ' ' OR &PCNTSHIFT EQ 'FOC_NONE' THEN ' '
- ELSE &PCNTSHIFT;
 
-*setup date range including previous year data for Performance graph in Executive Overview
-SET &TODT = IF EDIT(&TODT, '$$9') EQ '/' THEN &TODT
- ELSE EDIT(&TODT,'99/9999');
-SET &FROMYR = IF EDIT(&FROMDT,'$$9') EQ '/' THEN EDIT(&FROMDT, '$$$9999') ELSE EDIT(&FROMDT, '$$9999');
-SET &&ROLLFY = IF &&ROLLFY EQ ' ' THEN '01' ELSE &&ROLLFY;
-*-SET &FROMYR = &FROMYR;

-SET &FMTH=EDIT(&FROMDT,'99');
-SET &FROMYR = IF &&ROLLFY LT &FMTH THEN &FROMYR ELSE &FROMYR - 1;
-SET &FROMMY = &&ROLLFY | '/' | &FROMYR;

-SET &FROMMY = IF &FROMDT LT &TODT THEN &FROMDT ELSE &FROMMY;

-SET &WHERE22 = 'WHERE INVOICE_MY IS-FROM ' | '''&FROMMY.EVAL''' | ' TO ' | '''&TODT.EVAL''';
-SET &WHERETNT = IF '&LEVELX.EVAL' EQ '1' THEN ' ' ELSE 'WHERE TNT_COMPANY EQ ''1'''; 
 
-SET &&WEB_PATH=
- TEMPPATH(50,'A50');
SET EDAPATH=+&&WEB_PATH;
 
-SET &FILEA='EA'|'STAND';
-SET &FILEC='EC'|'STAND';
-SET &FILEH='EH'|'STAND';
-SET &HIERF='EO'|'STAND';

-*-TYPE &WHERE1
-*-TYPE &WHERE2
-*-TYPE &WHERE3
-*-TYPE &WHERE4
-*-TYPE &WHERE5
-*-TYPE &XBREAKBY
-*-TYPE &PCNTSHIFT
-SKIPPARM1 

-* -IF '&&PushTeck.EVAL' EQ 'Y' THEN GOTO SecChkDone;
-* -INCLUDE DBRChkSecurity
-*-RUN
-* -SecChkDone