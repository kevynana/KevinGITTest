-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-*****************************************************************
-*5/21/01      TANDT-SS               ADDED DEFINE TO NORECORDS SO MESSAGE
-*                                    "NO RECORDS FOUNDS FOR THIS SELECTION" 
-*                                    WILL APPEAR IN EXCEL 97
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-*****************************************************************

EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS
-RUN

-INCLUDE SetParmtime
-RUN

-TYPE &&OUTPUTDEST
-IF &&OUTLINE2 EQ 'OFFLINE' THEN GOTO XOFFLINE ELSE GOTO XONLINE;

-XONLINE
-TYPE Before ONLINE
ONLINE
-TYPE After ONLINE
-RUN
-GOTO CONT

-XOFFLINE
-SET &&OUTLINE2 = '';
-TYPE Before OFFLINE
OFFLINE
-TYPE After OFFLINE
-RUN
-GOTO CONT

-CONT
-SET &&RPT_HOLD = 'RPTHOLD';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

EX AIRUSE
-RUN

EX &&EXPARM
-RUN

-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW=&&EXPARM;
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-IF &RECORDS GT 0 THEN GOTO DOIT;

-NORECORDS
-SET &&RPTSUF = 'MSG';
-INCLUDE FDEFRPTS
-RUN
JOIN SET_KEY IN BRPTINST TO SET_KEY IN BRPT_SET AS J1
DEFINE FILE BRPTINST
EMPTY/A35 = 'NO RECORDS FOUND FOR THIS SELECTION';
NOWTOD/A8 WITH INST_KEY = HHMMSS (NOWTOD);
END
TABLE FILE BRPTINST
-INCLUDE HEADAIR
PRINT EMPTY AS 'REPORT,MESSAGE' 
BY ROLLUP_CODE AS 'ROLLUP,CODE'
BY RPT_NAME AS 'REPORT,NAME'
BY SELECT_LEV AS 'BREAK,LEVEL'
IF INST_KEY EQ 
 '&&IKEY.EVAL'
IF RECORDLIMIT EQ 1
-INCLUDE FOOTERDT
ON TABLE SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-GOTO XXIT

-DOIT

SET ASNAMES = ON
-RUN

DEFINE FILE &&RPT_HOLD ADD
RNDTRP/A13=IF SEG_NBR EQ '01' THEN TKT_NUM|EMB_APT_CD ELSE 
           IF SEG_NBR GT '01' THEN TKT_NUM|RTE_APT_CD ELSE ' ';
END
-RUN

TABLE FILE &&RPT_HOLD
SUM  FARE_PAID 
     MRK_TKT_CNT
     NET_TKT_CNT
BY ND_CP_NM
BY ND_CTY_NMO
BY ND_CTY_NMD
BY TKT_NUM
BY RNDTRP
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS ROUND1
END
-RUN


TABLE FILE ROUND1
SUM  FARE_PAID 
     MRK_TKT_CNT
     NET_TKT_CNT
BY ND_CP_NM
BY ND_CTY_NMO
BY ND_CTY_NMD
BY TKT_NUM
BY RNDTRP 
ON TABLE HOLD AS ROUND2
END
-RUN

DEFINE FILE ROUND2
TKT_TYPE/A10= IF MRK_TKT_CNT GE 2 THEN 'ROUND TRIP' ELSE
              IF (MRK_TKT_CNT EQ 1) AND (TKT_NUM NE LAST TKT_NUM)
              THEN 'ONE WAY' ELSE ' ';
XCNT/P9 = 1;
END
-RUN

TABLE FILE ROUND2
SUM XCNT
BY ND_CP_NM
BY ND_CTY_NMO
BY ND_CTY_NMD
BY TKT_NUM
BY TKT_TYPE
WHERE TKT_TYPE NE ' '
ON TABLE HOLD AS ROUND3
END
-RUN

MATCH FILE &&RPT_HOLD
SUM NET_TKT_CNT 
    FARE_PAID
BY ND_CP_NM
BY ND_CTY_NMO
BY ND_CTY_NMD
BY TKT_NUM
ON TABLE HOLD
RUN
FILE ROUND3
PRINT TKT_TYPE
BY ND_CP_NM
BY ND_CTY_NMO
BY ND_CTY_NMD
BY TKT_NUM
AFTER MATCH HOLD AS ROUND4 OLD-OR-NEW
END
-RUN


-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN


DEFINE FILE ROUND4
NOWTOD/A8 WITH ND_CP_NM = HHMMSS (NOWTOD) ;
END
-RUN 
       
TABLE FILE ROUND4
-INCLUDE HEADER
"&&SUBHEAD"
"</2"
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10

WHERE TKT_TYPE EQ 'ROUND TRIP'

-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW='RNDTRIP';
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-XXIT

-* REJ EX RESTART 
-*-EXIT

