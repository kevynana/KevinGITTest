-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/26/04    JOY                Added/Changed Norecords logic
-*2/25/05   DEB           ADDED CODE FOR GLOBAL CURRENCY
-*9/18/06    DEB                ADDED LEVEL2 FOR CERN


-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';
-DEFAULT &LVSEL1 = 'XX' 
-DEFAULT &LVSEL2 = 'XX' 

-DEFAULT &MB1 = 'XX';
-DEFAULT &MB2 = 'XX';
-DEFAULT &MB3 = 'XX';
-DEFAULT &MB4 = 'XX';
-DEFAULT &MB5 = 'XX';
 
-SET &&DTSTY = 'DT';
-SET &&FIELD = &ROW_LABEL;
-SET &&LSEL = &LSEL;
-SET &&MB1 = &MB1;
-SET &&MB2 = &MB2;
-SET &&MB3 = &MB3;
-SET &&MB4 = &MB4;
-SET &&MB5 = &MB5;
-SET &&LVSEL1 = &LVSEL1 ;
-SET &&LVSEL2 = &LVSEL2 ;


-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN

EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;

-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

-SET &RPARM = &&WEB_PATH || 'RPTPARMS.fex';

EX SETDBLS SETFILE=&&SETF

-RUN
-INCLUDE GetForDrillDown
-RUN

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****

TABLE FILE BRPTINST
BY BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE AS LVL
END
-RUN

-SET &NHLVL = &RECORDS;

-DEFAULT &LCHK1 = ' ';
-DEFAULT &LCHK2 = ' ';
-DEFAULT &LCHK3 = ' ';
-DEFAULT &LCHK4 = ' ';
-DEFAULT &LCHK5 = ' ';
-DEFAULT &FLAG1 = 'N';
-DEFAULT &FLAG2 = 'N';
-DEFAULT &FLAG3 = 'N'; 
-DEFAULT &FLAG4 = 'N';
-DEFAULT &FLAG5 = 'N'; 
 
-READ LVL &BL.18.
-SET &LEN = ARGLEN(60, '&BL.EVAL', 'I2');
-SET &NBRLVL = &LEN / 3;
-*-SET &V_PASS= IF &&MULTIBREAK NE 'Y' THEN 0 ELSE &NBRLVL;
-REPEAT AllHLevels FOR &VN FROM 1 TO &NBRLVL  
 
-SET &LEV = GETTOK(&BL, 18, &VN, ';', 2, 'A2');
-SET &LVLDSC = EDIT(&LEV, '99');
-SET &LVL.&VN = '&LVLDSC.EVAL';
-SET &N.&VN  = 'N' || '&VN.EVAL'; 
-SET &PARM.&VN = IF &LVLDSC GT '10' THEN 'AROLL_DSC' | &LVLDSC ELSE 'AROLL_DSC' | EDIT(&LVLDSC, '$9');
-SET &DESC.&VN = IF &LVLDSC GT '10' THEN 'LEVELDESC' | &LVLDSC ELSE 'LEVELDESC' | EDIT(&LVLDSC, '$9');
-SET &FLAG.&VN = IF &PARM.&VN EQ 'AROLL_DSC1' OR 'AROLL_DSC2' THEN 'Y' ELSE 'N';

-SET &&CNT  = '&NBRLVL.EVAL';
-AllHLevels
-CLOSE LVL

-IF '&FLAG1.EVAL' EQ 'Y' AND '&FLAG2.EVAL' EQ 'Y' THEN GOTO SETCHK2;

-SET &LCHK1 = IF '&FLAG1.EVAL' NE 'Y' THEN
-'WHERE ' | &DESC1 | ' EQ ' |  '''&&MB1.EVAL''' ELSE
-IF '&LVLDSC.EVAL' EQ 01 THEN 
-'WHERE ' | &DESC1 | ' CONTAINS ' | '''&&LVSEL1.EVAL''' ELSE
-'WHERE ' | &DESC1 | ' CONTAINS ' | '''&&LVSEL2.EVAL''';

-IF &&CNT LT 2 THEN GOTO NoMore;
-SETCHK2 
-SET &LCHK2 = IF &FLAG2 NE 'Y' THEN
-'WHERE ' | &DESC2 |  ' EQ ' |  '''&&MB2.EVAL''' ELSE
-IF '&LVLDSC.EVAL' EQ 02 THEN 
-'WHERE ' | &DESC2 | ' CONTAINS ' | '''&&LVSEL1.EVAL''' ELSE
-'WHERE ' | &DESC2 | ' CONTAINS ' | '''&&LVSEL2.EVAL''';

-IF &&CNT LT 3 THEN GOTO NoMore;

-SET &LCHK3 = IF &FLAG3 NE 'Y' THEN
-'WHERE ' | &DESC3 |  ' EQ ' |  '''&&MB3.EVAL''' ELSE
-'WHERE ' | &DESC3 | ' CONTAINS ' | '''&&MB3.EVAL''' ;

-IF &&CNT LE 3 THEN GOTO NoMore;

-SET &LCHK4 = IF &FLAG4 NE 'Y' THEN
-'WHERE ' | &DESC4 |  ' EQ ' |  '''&&MB4.EVAL''' ELSE
-'WHERE ' | &DESC4 | ' CONTAINS ' | '''&&MB4.EVAL''' ;

-IF &&CNT LE 4 THEN GOTO NoMore;

-SET &LCHK5 = IF &FLAG5 NE 'Y' THEN
-'WHERE ' | &DESC5 |  ' EQ ' |  '''&&MB5.EVAL''' ELSE
-'WHERE ' | &DESC5 | ' CONTAINS ' | '''&&MB5.EVAL''' ;

-NoMore
-TYPE &LCHK1
-TYPE &LCHK2
-TYPE &LCHK3
-TYPE &LCHK4
-TYPE &LCHK5

-SET &&CHGCK = IF &&FIELD NE 'O' THEN 
-'WHERE KEY EQ ''&&FIELD.EVAL'' OR KEY2 EQ ''&&FIELD.EVAL''  OR KEY3 EQ ''&&FIELD.EVAL'' ' ELSE
-'WHERE KEY EQ ''&&FIELD.EVAL'' AND KEY2 EQ ''&&FIELD.EVAL''  AND KEY3 EQ ''&&FIELD.EVAL'' ' ;
 




FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
 -RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN


EX HTLUSE
-RUN

-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN


-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';
DEFINE FILE &&EXTRACT
CHG_COD/A47 = IF LEVEL3 NE ' ' AND LEVEL2 NE ' ' THEN
                LEVEL1  || '-' || LEVEL2 || '-' || LEVEL3 ELSE
                IF LEVEL3 EQ ' ' AND LEVEL2 NE ' ' THEN
                LEVEL1  || '-' || LEVEL2 ELSE
                IF LEVEL3 EQ ' ' AND LEVEL2 EQ ' ' THEN LEVEL1;
  CC1/A1 = EDIT(LEVEL1, '9');
  CC2/A1 = EDIT(LEVEL2, '9');
  CC3/A1 = EDIT(LEVEL3, '9');
  CHG_DESC/A42 = IF CHG_TYPE EQ 'BCHG' THEN 'B Charge Codes' ELSE
               IF CHG_TYPE EQ 'GCHG' THEN 'G Charge Codes' ELSE
               IF CHG_TYPE EQ 'MCHG' THEN 'M Charge Codes' ELSE
               IF CHG_TYPE EQ 'PCHG' THEN 'P Charge Codes' ELSE
               IF CHG_TYPE EQ 'DEF PARTNERS' THEN 'Default Charge Codes - Partners' ELSE
               'Default Charge Codes - All other travelers';
  CHG_TYPE/A15 = IF CC1 EQ 'B' THEN 'BCHG' ELSE
                 IF CC1 EQ 'G' THEN 'GCHG' ELSE
                 IF CC1 EQ 'M' THEN 'MCHG' ELSE
                 IF CC1 EQ 'P' THEN 'PCHG' ELSE
                 IF CC1 EQ '1' THEN 'DEF PARTNERS' 
                 ELSE 'DEF ALL OTHER';
  CHG_TYPE2/A15 = IF CC2 EQ 'B' THEN 'BCHG' ELSE
                 IF CC2 EQ 'G' THEN 'GCHG' ELSE
                 IF CC2 EQ 'M' THEN 'MCHG' ELSE
                 IF CC2 EQ 'P' THEN 'PCHG' ELSE
                 IF CC2 EQ '1' THEN 'DEF PARTNERS' 
                 ELSE 'DEF ALL OTHER';
  CHG_TYPE3/A15 = IF CC3 EQ 'B' THEN 'BCHG' ELSE
                 IF CC3 EQ 'G' THEN 'GCHG' ELSE
                 IF CC3 EQ 'M' THEN 'MCHG' ELSE
                 IF CC3 EQ 'P' THEN 'PCHG' ELSE
                 IF CC3 EQ '1' THEN 'DEF PARTNERS' 
               ELSE 'DEF ALL OTHER';               
  KEY/A1 = IF CHG_TYPE EQ 'BCHG' THEN 'B' ELSE
           IF CHG_TYPE EQ 'GCHG' THEN 'G' ELSE
           IF CHG_TYPE EQ 'MCHG' THEN 'M' ELSE
           IF CHG_TYPE EQ 'PCHG' THEN 'P' ELSE
           IF CHG_TYPE EQ 'DEF PARTNERS' THEN 'D' ELSE 
           IF CHG_TYPE EQ 'DEF ALL OTHER' THEN 'O' ELSE ' ';
  KEY2/A1 = IF CHG_TYPE2 EQ 'BCHG' THEN 'B' ELSE
            IF CHG_TYPE2 EQ 'GCHG' THEN 'G' ELSE
            IF CHG_TYPE2 EQ 'MCHG' THEN 'M' ELSE
            IF CHG_TYPE2 EQ 'PCHG' THEN 'P' ELSE
            IF CHG_TYPE2 EQ 'DEF PARTNERS' THEN 'D' ELSE
            IF CHG_TYPE2 EQ 'DEF ALL OTHER' THEN 'O' ELSE ' ';
  KEY3/A1 = IF CHG_TYPE3 EQ 'BCHG' THEN 'B' ELSE
            IF CHG_TYPE3 EQ 'GCHG' THEN 'G' ELSE
            IF CHG_TYPE3 EQ 'MCHG' THEN 'M' ELSE
            IF CHG_TYPE3 EQ 'PCHG' THEN 'P' ELSE
            IF CHG_TYPE3 EQ 'DEF PARTNERS' THEN 'D' ELSE
            IF CHG_TYPE EQ 'DEF ALL OTHER' THEN 'O' ELSE ' ';
CITY_ST/A35 = PROP_CITY||(', '| PROP_STATE);
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
LEVEL_DESC/A60  = &&LDESC; 
NBR_ROOMS/D12 = NUM_ROOMS;
NBR_DAYS/D12 = NUM_DAYS;
NEW_ROOM_RATE/D12.2C = ROOM_RATE;
NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;
NNROOMS/D12 = IF NBR_DAYS LT 0 AND NUM_ROOMS LT 0 THEN
              ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
OUT_DATE/MDYY = CHKOUT_DATE;
REFUSE_CD/A2 = EDIT(REFUSE_KEY,'$$$$$$$$99');   
 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  CITY_ST  
  CHG_COD
  HTL_KEY     
  IN_DATE   
  INV_DT      
  INVOICE_DATE  
  LEVEL_DESC  
  NBR_DAYS    
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT       
  NNROOMS   
  OUT_DATE    
  PASSNGR_NAME  
  PROP_NAME   
  REFUSE_KEY
  REFUSE_CD
-INCLUDE BYHIERARCHY
-INCLUDE &HTLDATES    
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&CHGCK
-INCLUDE &RPARM
ON TABLE HOLD AS RPTHOLD
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

-TYPE After Extract
DEFINE FILE RPTHOLD
LEVEL_DESC/A60 = &&LDESC;
NW_LSEL/A12 = EDIT(LEVEL_DESC, '999999999999');
LVSEL/A60 = IF LEVEL_DESC EQ '(BOOZ-R) BOOZ & CO' THEN NW_LSEL ELSE 
            IF LEVEL_DESC EQ '(BOOZB) BOOZ & CO BUSINESS' THEN NW_LSEL ELSE
            IF LEVEL_DESC EQ '(BOOZP) BOOZ & CO PERSONAL' THEN NW_LSEL ELSE
            IF LEVEL_DESC EQ '(BOOZL) BOOZ LEISURE' THEN NW_LSEL  ELSE
            IF LEVEL_DESC EQ '(BOOZF) BOOZ & CO FINANCIAL' THEN NW_LSEL ELSE LEVEL_DESC;   
-INCLUDE DefineSubtotalFields 
LVSEL1/A81 ='(01) TOTAL COMPANY' || ' - '|LVSEL ;
LVSEL2/A93 = '(02) BUSINESS/PERSONAL/LEISURE' || ' - '|LVSEL ;
NOWTOD/A8 WITH OUT_DATE = HHMMSS (NOWTOD);
END
-RUN
-TYPE Before Report

TABLE FILE RPTHOLD
-INCLUDE HEADAIR
"&&SUBHEAD"
"&&LSEL"
"</2"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM 
NNROOMS AS 'NUMBER,NIGHTS'
NBR_ROOMS AS 'NUMBER,ROOMS'
NEW_TOTAL_AMT AS 'TOTAL,AMOUNT'
COMPUTE AVGDR/D12.2CS = IF NEW_TOTAL_AMT LT 0 AND NNROOMS LT 0
 THEN ((NEW_TOTAL_AMT/NNROOMS) * (-1)) 
 ELSE NEW_TOTAL_AMT/NNROOMS; AS 'AVERAGE,DAILY,RATE'
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
-INCLUDE LevelNOPRINT

BY PASSNGR_NAME AS 'PASSENGER NAME'
BY CHG_COD AS 'CHARGE,CODE'
BY PROP_NAME AS 'HOTEL'
BY CITY_ST AS 'CITY/STATE'
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
BY REFUSE_CD AS 'REASON'
&LCHK1
&LCHK2
&LCHK3
&LCHK4
&LCHK5
 
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
-INCLUDE TOTALBYHIERARCHY

ON PASSNGR_NAME RECOMPUTE AS 'TOTAL FOR'
-*


-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
-INCLUDE FOOTERDT

ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END


-XXIT



