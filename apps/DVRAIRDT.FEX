-**********************************************************************
-* Data Vault Air Detail
-* 5/10/10 - changed Level1 to be handled like all other Levels - Deb
-*7/12/16 -DLV -  S-20630   Added EMP_ID to report  
-*03/09/17 - DLV - S-22486 - Updated IMAGE define to use server report is being ran from

-**********************************************************************

-SET &RPTTITLE = IF '&&ROLLUP.EVAL' NE 'OTHER-R' THEN  'Travel and Transport Air Detail Report for &&ROLLNAME.EVAL Account'
- ELSE 'Travel and Transport Air Detail Report for Client &&CLIENT.EVAL Account';

-IF &&AIRDETAILRECS EQ 0 THEN GOTO NoAirDtRec2;

DEFINE FILE AIRHOLD3
-INCLUDE GENDEFINE
-*IMAGE/A80 = '<img src=HTTPS://www.ettekreview.com/asp/NEW_HDR.jpg>';
IMAGE/A80 = '<img src=' || '&&SRVR.EVAL' || '/asp/NEW_HDR.PNG>';
S_POS/I3=POSIT(XPSNGR_NM, 15, '/', 1, 'I3');
LNAME/A15=SUBSTR(15, XPSNGR_NM, 1, (S_POS - 1), (S_POS - 1), 'A15');
END

TABLE FILE AIRHOLD3
-*"&&SUBHEAD"
HEADING 
"<IMAGE>"
"</5"
"&RPTTITLE.EVAL"
"&&DTDESC.EVAL &&FROMDT - &&TODT " 

SUM BR_CL_IDX AS 'ACCT'
  LEVEL1 &&LV1
  XPSNGR_NM AS 'TRAVELER'
  EMP_ID &EMPID
  XTRAN_DT AS 'INVOICED'
  XDPT_DT AS 'TRAVEL' 
  INVOICE_NUM AS 'INVOICE'  
  PNR_LOCATOR AS 'PNR'
  DV_NUM AS 'BILLED TO'
  TKT_NUM AS 'TICKET'
  AIRLINE AS 'A/L'
  SEG_NBR AS 'LEG' 
  EMB_APT_CD AS 'ORIG' 
  RTE_APT_CD AS 'DEST'
  CLASS AS 'CLASS'
  FREQ_FLYER AS 'FF NUMBER'
  FF_MILES AS 'FF MILES'
  FARE_PAID AS 'FARE'
  LEVEL2 &&LV2 
  LEVEL3 &&LV3 
  LEVEL4 &&LV4 
  LEVEL5 &&LV5
  LEVEL6 &&LV6
  LEVEL7 &&LV7
  TRIP_PURPOSE &&TRIPPRP
  TRP_DESC &&TRIPRSN
  LNAME NOPRINT
BY BR_CL_IDX NOPRINT
BY XPSNGR_NM NOPRINT 
BY XTRAN_DT NOPRINT
BY TKT_SORT NOPRINT 
BY TRN_DATE NOPRINT
BY XDPT_DT NOPRINT
BY SEG_NBR NOPRINT
BY TKT_NUM NOPRINT
BY PNR_LOCATOR NOPRINT
ON TABLE COLUMN-TOTAL
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT 

ON TABLE SET STYLE *
-*-INCLUDE &&DTSTY
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=1.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=1.000000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=8, COLOR=BLACK, GRID=ON, STYLE=NORMAL,WRAP= OFF, $
TYPE=TITLE, FONT=TIMES NEW ROMAN, SIZE=9, BACKCOLOR=RGB(170 170 170), $
TYPE=FOOTING, SIZE=6, $
TYPE=TITLE, SIZE=9, STYLE=BOLD+UNDERLINE, $
TYPE=HEADING, SIZE=10, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=14, LINE=8, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=10, LINE=9, STYLE=BOLD, FONT=TIMES NEW ROMAN,$

-*TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(&&TNTTRPOS), SIZE=(&&TNTTRSIZ), $
-*TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLTRPOS), SIZE=(&&ROLTRSIZ), $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, COLOR=BLACK, BACKCOLOR=RGB(170 170 170),$
TYPE=REPORT, TITLETEXT='Air Detail',$
TYPE=DATA, STYLE=-UNDERLINE, COLOR=RGB(0 0 138),
     COLUMN=N12,
     URL=https://www.viewtrip.com/Home.aspx?(rloc=PNR_LOCATOR lastname=LNAME),
$
ENDSTYLE
-*
&&OUTLINE1
-*&&OUTLINE2
END
-RUN

-IF &FOCERRNUM EQ 205  OR &LINES EQ 0 THEN GOTO NoAirDtRec2;
-GOTO XXITAirDT2;
-NoAirDtRec2
-SET &TITLETEXT = 'Air Detail ';
-INCLUDE DVRNoRecord
-XXITAirDT2