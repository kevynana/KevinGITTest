-* 2/7/14 CREATED FOR NEW TOTAL TRANSACTION REPORT BY ROLLUP AND CATEGORY
-* 8/23/16-S-22623-JEM-Added Business total/annualized/prior number of Nights

-INCLUDE SETECHO
SET ASNAMES = ON
-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================

DEFINE FILE HR_50
EDATE/YYMD=  &&TODT.EVAL ;
EOM_DATE/YYMD=DATEMOV(EDATE,'EOM');

SDATE/YYMD = &&FROMDT.EVAL;
BOM_DATE/YYMD=DATEMOV(SDATE,'BOM');
BOY_DATE1/YYMD=DATEMOV(SDATE, 'BOY');
ANNUAL_BEGIN/YYMD = &&FROMDT.EVAL;
P_ANNUAL_BEGIN/YYMD=DATEMOV(DATEADD(BOY_DATE1, 'M', (-12)), 'BOM');

P_ANNUAL_END1/YYMD=DATEMOV(DATEADD(P_ANNUAL_BEGIN, 'M', (+0)), 'EOM');
P_ANNUAL_END2/YYMD=DATEMOV(DATEADD(P_ANNUAL_END1, 'M', (+11)), 'EOY');

YD/A1 = IF (INVOICE_DATE GE  &&FROMDT.EVAL)  AND (INVOICE_DATE LE  &&TODT.EVAL ) THEN 'Y' ELSE 'N';
P_YD/A1 = IF (INVOICE_DATE GE P_ANNUAL_BEGIN ) AND
             (INVOICE_DATE LE P_ANNUAL_END2) THEN 'Y' ELSE 'N';


NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
NBR_DAYS/D12 =  NUM_DAYS;
NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
NNROOMS/P12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                 ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
-*  YTD
 YHAMT/D14.2C = IF YD EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
 YHNGT/P12 = IF YD EQ 'Y' THEN NNROOMS ELSE 0;
-*  PRIOR  
 PYHAMT/D14.2C = IF P_YD EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
 PYHNGT/P12 = IF P_YD EQ 'Y' THEN NNROOMS ELSE 0;
 
ROLLCD/A8 = '&&CURROLL.EVAL';
END

TABLE FILE HR_50
SUM YHAMT YHNGT P_ANNUAL_BEGIN P_ANNUAL_END2
PYHAMT PYHNGT
COMPUTE HDAYS/I6=(EOM_DATE - ANNUAL_BEGIN) + 1;

BY ROLLCD
BY CATEGORY
WHERE CATEGORY NE 'GLOBAL' OR 'TRAVELFUND' OR 'FINANCIAL'
-* &AIRDATES CANNOT BE USED FOR THIS REPORT B/C IT WILL RUN FOR CURRENT MONTH ONLY INSTEAD OF YTD,QTR, ETC
-* -INCLUDE &AIRDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCHTL
END
-RUN


 

-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACT DATA FROM ABOVE
-*========================================================================
DEFINE FILE TCHTL
NEW_CAT/A15 = IF CATEGORY EQ 'CONF PLANNING' OR 'CONFERENCE' OR 'MAPS' OR 'OTHER' OR 'SITE INSPEC' THEN 'MEETING' ELSE
              IF CATEGORY EQ 'NITTOB' OR 'NONBILL' OR 'TOYOB' OR 'TTHUB' THEN 'BUSINESS' ELSE CATEGORY;
END
TABLE FILE TCHTL
SUM YHAMT/D12.2 PYHAMT/D12.2 YHNGT/P12 PYHNGT/P12
COMPUTE NDAYS/I6 = FST.HDAYS;
COMPUTE DIVBY/I6=IF NDAYS GT 365 THEN NDAYS ELSE 365; NOPRINT
COMPUTE HPROSP1/D12.2   = ((YHAMT/FST.HDAYS)*DIVBY) ;
COMPUTE HPRONGT1/P12 = ((YHNGT/FST.HDAYS)*DIVBY) ;

BY ROLLCD
BY NEW_CAT AS 'CATEGORY'
ON TABLE HOLD AS TCHTL2
END
-RUN




DEFINE FILE TCHTL2
CBUS_HAMT/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN YHAMT ELSE 0;
CBUS_HNGT/P12 = IF CATEGORY EQ 'BUSINESS' THEN YHNGT ELSE 0;
PBUS_HAMT/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN PYHAMT ELSE 0;
PBUS_HNGT/P12 = IF CATEGORY EQ 'BUSINESS' THEN PYHNGT ELSE 0;

BHPROSP1/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN HPROSP1 ELSE 0;
BHPRONGT1/P12 = IF CATEGORY EQ 'BUSINESS' THEN HPRONGT1 ELSE 0;

CMTG_HAMT/D12.2 = IF CATEGORY EQ 'MEETING' THEN YHAMT ELSE 0;
PMTG_HAMT/D12.2 = IF CATEGORY EQ 'MEETING' THEN PYHAMT ELSE 0;
MHPROSP1/D12.2 = IF CATEGORY EQ 'MEETING' THEN HPROSP1 ELSE 0;
END
TABLE FILE TCHTL2
SUM YHAMT YHNGT PYHAMT PYHNGT HPROSP1 HPRONGT1
    CBUS_HAMT PBUS_HAMT BHPROSP1 
    CBUS_HNGT PBUS_HNGT BHPRONGT1
    CMTG_HAMT PMTG_HAMT MHPROSP1
BY ROLLCD
BY CATEGORY
ON TABLE HOLD AS TCHTL3
END
-RUN




DEFINE FILE TCHTL3
  NMB/I5 = NMB + 1;  
END
TABLE FILE TCHTL3
SUM YHAMT
    YHNGT
    PYHAMT
    PYHNGT
    HPROSP1 
    HPRONGT1
    CBUS_HAMT
    CBUS_HNGT
    PBUS_HAMT 
    PBUS_HNGT
    BHPROSP1 
    BHPRONGT1
    CMTG_HAMT 
    PMTG_HAMT 
    MHPROSP1
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCHTL4
END
-RUN
 
MODIFY FILE TCHTLYD2
FIXFORM FROM TCHTL4
DATA ON TCHTL4
END
-RUN



-NEXTONE
 
