-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* 7/13/05 - DEB  ADDED REFUSE_CD 'R' OR 'T' IN NONPREF_RM DEFINE
-* 7/18/05 - Steve Took out REFUSE_CD 'T' in NONPREF_RM - per Robin Lewis
-* 9/19/14 Updated to use exclude flags in % - JK

-INCLUDE SETECHO


TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT
  CITY_ST/A40 = PROP_STATE||(' '| ' '|PROP_CITY);
  LEVEL_DESC/A60  = &&LDESC;
  FLAG/A1='A';
  NBR_DAYS/D12=NUM_DAYS;
  NBR_ROOMS/D12 = NUM_ROOMS;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
  REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
  PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
  PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
  
  PREF_NON/A15 = IF PNP_FLAG EQ 'I'  THEN 'PREFERRED' ELSE 'NON-PREFERRED';
  

-* Defines on Previous Defines
NNROOMS3/P12CS = IF PNP_IE EQ 'I' THEN NNROOMS ELSE 0;
NONPREF_RM/D12 = IF PREF_NON EQ 'NON-PREFERRED' THEN NNROOMS ELSE 0; 
NONPREF_RM1/D12 = IF PNP_FLAG EQ 'E' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 0;
-* NONPREF_RM/D12 IF PREF_NON EQ 'NON-PREFERRED' THEN NNROOMS ELSE 0;
RATE_RM1/D12 = IF ((ROOM_RATE GE 150 AND ROOM_RATE LE 199) OR
                  (ROOM_RATE LE (-150) AND ROOM_RATE GE (-199))) 
                  THEN NNROOMS ELSE 0;
RATE_RM2/D12 = IF ((ROOM_RATE GE 200 AND ROOM_RATE LE 249) OR
                  (ROOM_RATE LE (-200) AND ROOM_RATE GE (-249)))
                  THEN NNROOMS ELSE 0;
RATE_RM3/D12 = IF ((ROOM_RATE GE 250) OR (ROOM_RATE LE (-250))) THEN NNROOMS ELSE 0;
END
-RUN

TABLEF FILE &&EXTRACT
PRINT CITY_ST
      FLAG
      LEVEL_DESC
      NNROOMS 
      NNROOMS3
      NONPREF_RM
      NONPREF_RM1
      RATE_RM1
      RATE_RM2  
      RATE_RM3  
-INCLUDE &HTLDATES 
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS HHOLD
END
-RUN

TABLE FILE HHOLD
PRINT LEVEL_DESC
      FLAG
      NNROOMS 
      NNROOMS3
      NONPREF_RM
      NONPREF_RM1
BY CITY_ST
ON TABLE HOLD AS HHOLD1
END
-RUN


DEFINE FILE METXHTL
HTL_MKT/A130 = IF H_MARKET NE ' ' THEN H_MARKET ELSE ST_CITY;
END

TABLE FILE METXHTL
PRINT HTL_MKT
BY ST_CITY AS 'CITY_ST'
WHERE ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS MKTFILE
END
-RUN

MATCH FILE HHOLD1
PRINT LEVEL_DESC
      FLAG
      NNROOMS
      NNROOMS3
      NONPREF_RM
      NONPREF_RM1
BY CITY_ST
ON TABLE HOLD
RUN
FILE MKTFILE
SUM HTL_MKT
BY CITY_ST
AFTER MATCH HOLD AS HFILE OLD-AND-NEW
END
-RUN

TABLE FILE HFILE
SUM NNROOMS
    NNROOMS3
    NONPREF_RM
    NONPREF_RM1
    FLAG
BY LEVEL_DESC
WHERE CITY_ST NE ' '
ON TABLE HOLD AS HHOLD2
END
-RUN


TABLE FILE HHOLD
SUM  RATE_RM1
     RATE_RM2  
     RATE_RM3  
     NNROOMS AS 'NNROOMS1'
BY LEVEL_DESC
ON TABLE HOLD AS HHOLD3
END
-RUN

MATCH FILE HHOLD2
PRINT FLAG
      NNROOMS
      NNROOMS3
      NONPREF_RM
      NONPREF_RM1
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE HHOLD3
SUM RATE_RM1
    RATE_RM2
    RATE_RM3
    NNROOMS1
BY LEVEL_DESC
AFTER MATCH HOLD AS MXNONH OLD-OR-NEW
END
-RUN


TABLE FILE MXNONH
SUM  NNROOMS
     NNROOMS3
      NONPREF_RM
      NONPREF_RM1
      RATE_RM1
      RATE_RM2
      RATE_RM3
      NNROOMS1
BY LEVEL_DESC
ON TABLE HOLD AS MXNONHTL 
END
-RUN


-SET &&MXNOHTL = IF &LINES EQ 0 THEN 'Y' ELSE 'N';

