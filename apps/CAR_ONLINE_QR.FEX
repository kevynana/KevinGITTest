-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

SET ASNAMES=ON
-******************************************************************************
-* 10/25/17 - DLV - S-16607 - New extract created for KPI page
EX CONCAT_ALL_DBS ROLL=ABCD-R,FID=CAR,CLEAR=Y
-RUN

-INCLUDE SETECHO 
-SET &&FROMDT = '2017/07/01';
-SET &&TODT = '2017/09/30';
-SET &&CYFMDT = '&&FROMDT.EVAL';
-SET &&CYTODT = '&&TODT.EVAL';
-SET &&FXDATE = '2016/01/01';
-SET &&TXDATE = '2016/12/31';
-SET &&PYFMDT = '&&FXDATE.EVAL';

-SET &&PQFMDT= '2016/07/01';
-SET &&PQTODT= '2016/09/30';
-SET &&CQFMDT = '2017/07/01';
-SET &&CQTODT = '2017/09/30'; 
-SET &&PYTDDATE= '&&TXDATE.EVAL'; 
-SET &&ROLLUP = 'ABCD-R';
-SET &&ROLLFY = '01';
-SET &&FYFLAG = ' ';



-SET &&INTDOM = 'T';
-SET &&HXCONL001 = 'QRCONL001' || &&INTDOM;

-TYPE &&HXCONL001

 

DEFINE FILE CR_50
CLIENT/A8 = '&&ROLLUP.EVAL';
NEW_RENTL_AMT/P12.2C = RENTAL_AMT;
BOOKINGS/P8 = IF NEW_RENTL_AMT GE 0 THEN 1 ELSE (-1);
NBR_DAYS/P12 =  NUM_DAYS;

-INCLUDE QRFTRANC
END
TABLE FILE CR_50
SUM BOOKINGS
    NEW_RENTL_AMT
    NBR_DAYS
BY CLIENT AS 'ROLLUP_CODE'
BY ONLINE_TRADITIONAL
BY YEAR    
BY TRAN
BY CURRENT
BY PRIOR
BY PYTD
BY PQ
BY CQ
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') OR
      (INVOICE_DATE GE '&&FXDATE.EVAL') AND (INVOICE_DATE LE '&&TXDATE.EVAL')

-IF &&INTDOM EQ 'T' THEN GOTO SKIP;
WHERE CITY.INTL_DOM EQ '&&INTDOM.EVAL'

-SKIP
-*&&WHERE1 
-*&&WHERE2 
-*&&WHERE3
 
-*-INCLUDE RPTPARMS
WHERE AROLL_LEV2 EQ 'ABCDB'
ON TABLE HOLD AS QRCARONL  
END
-RUN  
 

 

DEFINE FILE QRCARONL
PERIOD/A1 = IF CQ EQ 'Y' THEN 'C' ELSE 
            IF PQ EQ 'Y' THEN 'P' ELSE 'X';
CCAMT/P12.2 = IF CQ EQ 'Y' THEN NEW_RENTL_AMT ELSE 0;
CCDAYS/P12 = IF CQ EQ 'Y' THEN NBR_DAYS ELSE 0;
CCBOOK/P8 = IF CQ EQ 'Y' THEN BOOKINGS ELSE 0;
PCAMT/P12.2 = IF PQ EQ 'Y' THEN NEW_RENTL_AMT ELSE 0;
PCBOOK/P8 = IF PQ EQ 'Y' THEN BOOKINGS ELSE 0;
PCDAYS/P12 = IF PQ EQ 'Y' THEN NBR_DAYS ELSE 0;

END
TABLE FILE QRCARONL
SUM CCAMT CCDAYS CCBOOK PCAMT PCDAYS PCBOOK  

BY ROLLUP_CODE
BY PERIOD
BY ONLINE_TRADITIONAL
WHERE PERIOD EQ 'C' OR 'P'
ON TABLE HOLD AS QRONLC
END
-RUN



TABLE FILE QRONLC
SUM CCAMT AS 'CCTOTAMT'
    CCDAYS AS 'CCTOTDAYS'
    CCBOOK AS 'CCTOTBOOK'
    PCAMT AS 'PCTOTAMT'
    PCBOOK AS 'PCTOTBOOK'
    PCDAYS AS 'PCTOTDAYS'
BY ROLLUP_CODE
ON TABLE HOLD AS QRCARONL
END
-RUN


MATCH FILE QRONLC
PRINT CCAMT CCDAYS CCBOOK PCAMT PCDAYS PCBOOK PERIOD ONLINE_TRADITIONAL
BY ROLLUP_CODE
ON TABLE HOLD
RUN 
FILE QRCARONL
SUM CCTOTAMT CCTOTDAYS CCTOTBOOK PCTOTAMT PCTOTDAYS PCTOTBOOK
BY ROLLUP_CODE
AFTER MATCH HOLD AS QRCONLINE
END
-RUN

TABLE FILE QRCONLINE
PRINT CCAMT CCDAYS CCBOOK PCAMT PCDAYS PCBOOK  CCTOTAMT CCTOTDAYS CCTOTBOOK PCTOTAMT PCTOTDAYS PCTOTBOOK
BY ROLLUP_CODE
BY PERIOD
BY ONLINE_TRADITIONAL
WHERE ONLINE_TRADITIONAL EQ 'ONLINE'
ON TABLE HOLD AS &&HXCONL001
END
-RUN



 



-REPORT;

-INCLUDE QR_KPI_CARONLINE_REPORT