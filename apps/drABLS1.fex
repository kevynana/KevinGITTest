-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/26/04    JOY                Added/Changed Norecords logic
-*2/25/05    DEB           ADDED CODE FOR GLOBAL CURRENCY
-* ADDED TRANTYPE INCLUDE - JK 12/18/14 STORY ID S-06492
-*  01/06/2016  REJ  S-13202 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-*01/12/16 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*1/21/16 - JEM - S-14814 Corrected drilldown to pass correct international/domestic flag in rptparms.fex
-*  09/12/2016  REJ  S-24432 Changes for ER5 testing.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO
-*SET TEMPERASE = OFF
 
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';
 
-SET &&DTSTY = 'DT';
-SET &&FIELD = &ROW_LABEL;
-SET &&IDLEVELBREAK = &PARM2;
 
 
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
EX RPTRESET
-RUN
 
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX SETDBLS SETFILE=&&SETF
-RUN
 
-* REJ added for er5 Test
-IF &&FROMAVRP NE 'Y' GOTO :DRD1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:DRD1
 
-INCLUDE GetForDrillDown
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKTS';
-SET &&TICOUNT = 'TKT_ISS AS TKT,ISS';
-SET &&FAREAMT = 'FARE_AMT AS FARE,PAID';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
 
 
 
TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN
 
 
-READ SAVE &LEVEL.2.
 
-IF &&IDLEVELBREAK EQ ' ' GOTO LEV1;
 
-SET &&LCHK = 'WHERE LEVEL_DESC EQ ''&&FIELD.EVAL''';
 
 
-GOTO ARNDLEV1;
 
 -LEV1;
-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &&LCHK='WHERE ' | &AROLL_DSC.EVAL | ' CONTAINS ' | '''&&FIELD.EVAL''';
 
-ARNDLEV1;
 
-SET &&RPT_TTL1='        LOST SAVINGS GREATER THAN $100 DETAIL';
 
 
 
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DCA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-RUN
-:DCA1
 
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX AIRUSE
-RUN
 
 
-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
 
-INCLUDE IDLEVROLLDEF
-RUN
 
 
DEFINE FILE &&EXTRACT
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
LEVEL_DESC1/A80 = &&LDESC;
 
  MXADV/A8  = IF (ADV_PURCH LT 7) THEN '00 - 06' ELSE
                 '7+ DAYS';
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
 NEW_ADV_PURCH/D9    = ADV_PURCH;
 TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
 TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
 
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  XPSNGR_NM/A15        =
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
-* DEFINES TO PREVIOUS DEFINES
DECLINE_AMT/D12.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         TKT_AMOUNT - LOWEST_AMT;
DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
-INCLUDE TRANTYPE
-INCLUDE IDLEVDEFINE
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT
  VAL_AIR.VAL_AIRLINE AS 'AIRLINE'
  DISSAVINGS
  FARE_PAID
  LEVEL2
  MXADV
  NET_TKT_CNT
  NEW_ADV_PURCH
  REFUSAL_DESC
  TRN_DATE
  TKT_TYPE
  XDPT_DT
  XTRAN_DT
  XPSNGR_NM
  TKT_NUM
  TKT_SORT
  SEG_COUNT
  SEG_LOWEST
  SEG_NBR
  LEVEL1
  TKT_DATE
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
-INCLUDE &AIRDATES
WHERE ((DECLINE_AMT GE 100) AND (DECLINE_AMT LE 200)) OR
   ((DECLINE_AMT LE (-100)) AND (DECLINE_AMT LE (-200)))
&&WHERE1
&&WHERE2
&&WHERE3
 
&&VOIDCHK
&&LCHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS GRPTHOLD
END
-RUN
 
TABLE FILE GRPTHOLD
PRINT
  AIRLINE
  DISSAVINGS
  FARE_PAID
  LEVEL2
  MXADV
  NET_TKT_CNT
  NEW_ADV_PURCH
  REFUSAL_DESC
  TRN_DATE
  TKT_TYPE
  XDPT_DT
  XTRAN_DT
  XPSNGR_NM
  TKT_NUM
  TKT_SORT
  SEG_COUNT
  SEG_LOWEST
  SEG_NBR
  LEVEL1
BY TKT_DATE
ON TABLE HOLD AS GRPTHLD2 FORMAT FOCUS INDEX TKT_DATE
END
-RUN
 
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-* Global Reporting JOIN
JOIN GRPTHLD2.TKT_DATE IN GRPTHLD2 TO GLOBAL.DATE IN GLOBAL AS J1
 
 
 
DEFINE FILE GRPTHLD2
NAMEC/A30 = CNAME;
 
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;
 
-* DEFINES ON PREVIOUS DEFINES
PAID_FAREX/D20.6 = FARE_PAID * CURR1X;
DISSAVEX/D20.6 = DISSAVINGS * CURR1X;
GSEG_LOW/D20.6 = SEG_LOWEST * CURR1X;
END
-RUN
 
 
TABLE FILE GRPTHLD2
PRINT
  AIRLINE
  DISSAVINGS
  FARE_PAID
  LEVEL2
  MXADV
  NET_TKT_CNT
  NEW_ADV_PURCH
  REFUSAL_DESC
  TRN_DATE
  TKT_TYPE
  XDPT_DT
  XTRAN_DT
  XPSNGR_NM
  TKT_NUM
  TKT_SORT
  SEG_COUNT
  SEG_LOWEST
  SEG_NBR
  LEVEL1
  TKT_DATE
  NAMEC
  PAID_FAREX/D16.2CS
  DISSAVEX/D16.2CS
  GSEG_LOW/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
 
 
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT1'
BY XPSNGR_NM
ON TABLE HOLD AS FIRSTHLD
END
-RUN
 
 
TABLE FILE RPTHOLD
SUM  FARE_PAID
     NET_TKT_CNT
     PAID_FAREX/D16.2CS
BY XPSNGR_NM
ON TABLE HOLD AS PAXHLD
END
-RUN
 
MATCH FILE PAXHLD
PRINT FARE_PAID
      NET_TKT_CNT
      PAID_FAREX
BY XPSNGR_NM
ON TABLE HOLD
RUN
FILE FIRSTHLD
PRINT TTKT1
BY XPSNGR_NM
AFTER MATCH HOLD AS PXHLD OLD-OR-NEW
END
-RUN
 
TABLE FILE PXHLD
SUM FARE_PAID
    NET_TKT_CNT
    TTKT1
    PAID_FAREX
BY XPSNGR_NM
ON TABLE HOLD AS PXHLD1
END
-RUN
 
 
TABLE FILE PXHLD1
SUM FARE_PAID
    NET_TKT_CNT
    TTKT1
    PAID_FAREX
RANKED BY HIGHEST TTKT1 NOPRINT
BY XPSNGR_NM
ON TABLE HOLD AS PXHLD2
END
-RUN
 
DEFINE FILE PXHLD2
 RANK_LIMIT/I5 = 999;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 LIST/D6 = IF XPSNGR_NM NE LAST XPSNGR_NM THEN (LAST LIST + 1)
    ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999');
 RANK_VALUE/A6 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE FRANK;
 
 
END
-RUN
 
TABLE FILE PXHLD2
SUM FARE_PAID
    NET_TKT_CNT
    TTKT1
    PAID_FAREX
BY RANK_VALUE
BY XPSNGR_NM
ON TABLE HOLD AS PXHLD3
END
-RUN
 
 
 
TABLE FILE RPTHOLD
PRINT XDPT_DT
      XTRAN_DT
BY XPSNGR_NM
BY LEVEL1
BY TKT_NUM
BY SEG_COUNT
BY TKT_TYPE
BY TKT_SORT
BY NEW_ADV_PURCH
BY AIRLINE
BY REFUSAL_DESC
BY LEVEL2
WHERE SEG_NBR EQ '01'
ON TABLE HOLD AS XHLD
END
-RUN
 
TABLE FILE XHLD
SUM XDPT_DT
    XTRAN_DT
BY XPSNGR_NM
BY LEVEL1
BY TKT_NUM
BY SEG_COUNT
BY TKT_TYPE
BY TKT_SORT
BY NEW_ADV_PURCH
BY AIRLINE
BY REFUSAL_DESC
BY LEVEL2
ON TABLE HOLD AS XHLD1
END
-RUN
 
TABLE FILE RPTHOLD
SUM FARE_PAID AS 'FARE'
    NET_TKT_CNT AS 'TKT'
    DISSAVINGS AS 'LOST'
    SEG_LOWEST AS 'LOW'
    PAID_FAREX/D16.2CS AS 'GFARE'
    DISSAVEX/D16.2CS AS 'GLOST'
    GSEG_LOW/D16.2CS AS 'GLOW'
    NAMEC
BY XPSNGR_NM
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS XAMT
END
-RUN
 
MATCH FILE XAMT
SUM FARE
    LOST
    TKT
    LOW
    GFARE
    GLOST
    GLOW
    NAMEC
BY XPSNGR_NM
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD
RUN
FILE XHLD1
PRINT XDPT_DT
      XTRAN_DT
      LEVEL1
      TKT_TYPE
      TKT_SORT
      AIRLINE
      REFUSAL_DESC
      NEW_ADV_PURCH
      LEVEL2
BY XPSNGR_NM
BY TKT_NUM
BY SEG_COUNT
AFTER MATCH HOLD AS XAMT1 OLD
END
-RUN
 
TABLE FILE XAMT1
SUM FARE
    LOST
    TKT
    LOW
    GFARE
    GLOST
    GLOW
    NAMEC
BY XPSNGR_NM
BY LEVEL1
BY TKT_NUM
BY SEG_COUNT
BY XDPT_DT
BY XTRAN_DT
BY TKT_SORT
BY TKT_TYPE
BY NEW_ADV_PURCH
BY AIRLINE
BY REFUSAL_DESC
BY LEVEL2
ON TABLE HOLD AS XAMT2
END
-RUN
 
 
MATCH FILE PXHLD3
SUM   RANK_VALUE
      TTKT1
BY XPSNGR_NM
ON TABLE HOLD
RUN
FILE XAMT2
PRINT FARE
    LOST
    LOW
    GFARE
    GLOW
    GLOST
    NAMEC
    SEG_COUNT
    TKT
    XDPT_DT
    XTRAN_DT
    TKT_NUM
    TKT_TYPE
    TKT_SORT
    LEVEL1
    AIRLINE
    REFUSAL_DESC
    NEW_ADV_PURCH
    LEVEL2
BY XPSNGR_NM
AFTER MATCH HOLD AS PXHLD5 OLD-AND-NEW
END
-RUN
 
JOIN ROLL WITH XPSNGR_NM IN PXHLD5 TO ROLLUP_CODE IN ROLLUP AS J3
-RUN
 
DEFINE FILE PXHLD5
NOWTOD/A8 WITH TKT = HHMMSS (NOWTOD);
ROLL/A8='&&ROLLUP.EVAL';
LVL/A60='&&LVL_DSC.EVAL';
RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
RD/A10 = 'Run Date:';
RT/A10 = 'Run Time:';
LEV/A25 = EDIT(LEVEL_DESC, '9999999999999999999999999');
RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
RN1/A60=TRIM('B', '&&FIELD.EVAL', 60, ' ', 1, 'A60');
TDATE1/A8MDYY='&MDYY';
TDATE2/A10=EDIT(TDATE1,'99/99/9999');
TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
END
-RUN
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS';
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
 
-IF &&CURRENCY EQ 'USD' THEN GOTO SKIPTHIS;
 
-SET &FARE_CUR = 'FARE,PAID, (' | &&CURRENCY || ')';
-SET &LOST_CUR = 'LOST,SAVINGS,PAID, (' | &&CURRENCY || ')';
-SET &LOW_CUR = 'LOW,FARE, (' | &&CURRENCY || ')';
-SET &&SUBHEAD = <NAMEC;
 
TABLE FILE PXHLD5
HEADING
-INCLUDE &&PAHDR
" "
-*
SUM
TKT AS 'COUNT'
GFARE AS '&FARE_CUR.EVAL'
GLOW AS '&LOW_CUR.EVAL'
GLOST AS '&LOST_CUR.EVAL'
 
-GOTO CONTINUE
 
-SKIPTHIS
 
TABLE FILE PXHLD5
HEADING
-INCLUDE &&PAHDR
" "
 
-IF '&&ROLLUP.EVAL' EQ 'CERN-R' THEN GOTO CERN;
 
-*
SUM
TKT AS 'COUNT'
FARE/D12.2CSM AS 'FARE,PAID'
LOW/D12.2CSM AS 'LOW,FARE'
LOST/D12.2CSM AS 'LST,SVGS'
 
-CONTINUE
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY RANK_VALUE NOPRINT
BY XPSNGR_NM AS 'PASSENGER,NAME'
BY TKT_NUM AS 'TKT,NUM'
BY TKT_SORT NOPRINT
BY TKT_TYPE AS 'TKT,TYPE'
BY XTRAN_DT AS 'TRN,DTE'
BY XDPT_DT AS 'DPT,DTE'
BY AIRLINE AS 'CR'
BY REFUSAL_DESC AS 'REASON'
 
 
-GOTO NextStep;
 
 
-CERN;
 
SUM
TKT AS 'COUNT'
FARE/D12.2CSM AS 'FARE,PAID'
LOW/D12.2CSM AS 'LOW,FARE'
LOST/D12.2CSM AS 'LST,SVGS'
 
 
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY RANK_VALUE NOPRINT
BY XPSNGR_NM AS 'PASSENGER,NAME'
BY LEVEL1 AS 'SUB-OC'
BY LEVEL2 AS 'PROJECT,NUMBER'
BY TKT_NUM AS 'TKT,NUM'
BY TKT_SORT NOPRINT
BY TKT_TYPE AS 'TKT,TYPE'
BY XTRAN_DT AS 'TRN,DTE'
BY XDPT_DT AS 'DPT,DTE'
BY AIRLINE AS 'CR'
BY REFUSAL_DESC AS 'REASON'
 
-NextStep
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON XPSNGR_NM RECOMPUTE AS 'TOTAL FOR'
FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
"</1"
"<15 &M1.EVAL <45 &M2.EVAL <75 &M3.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
 
-IF '&&ROLLUP.EVAL' EQ 'CERN-R' THEN GOTO CERNSTY;
 
ON TABLE SET STYLE *
 
-INCLUDE &&PASTYD
 
 
TYPE=REPORT, COLUMN=N5, WRAP=.5, $
TYPE=REPORT, COLUMN=N8, WRAP=.5, $
 
 
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;
 
-CERNSTY;
 
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.030000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.030000, BOTTOMMARGIN=0.010000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
 
TYPE=REPORT, FOCEXEC=NONE, FONT=ARIAL, SIZE=8, COLOR=BLACK,
    BACKCOLOR=NONE, GRID=OFF, STYLE=NORMAL, $
 
TYPE=TITLE,
     JUSTIFY=LEFT,
     STYLE=BOLD+UNDERLINE,
     FONT=ARIAL,
     COLOR=RGB(5 76 149),
$
 
TYPE=HEADING, SIZE=10, GRID=OFF, STYLE=BOLD, $
 
TYPE=HEADING, HEADALIGN=BODY,$
TYPE=HEADING, LINE=1, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=1, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=4, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=4, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=5, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=5, ITEM=2, COLSPAN=10, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=6, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=6, ITEM=2, COLSPAN=10, JUSTIFY=CENTER, $
 
 
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=8,   STYLE=BOLD, $
 
 
TYPE=FOOTING, FONT=ARIAL, SIZE=7, STYLE=BOLD,$
TYPE=REPORT, IMAGE=TSMHEAD.jpg, POSITION=(0 0), $
TYPE=REPORT, IMAGE=FOOTER.jpg, POSITION=(0 8.2), $
 
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;
 
-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT
-GOTO NEXT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-NEXT
