-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File CPMETX.FEX
-**********************************************************************
-* This report was created for Metlife to see their top 25 citypairs,
-* the day of the week and the passenger JK 7/2/02
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-**********************************************************************
-INCLUDE SETECHO
EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS

-INCLUDE SETPARMTIME
-RUN
-TYPE &&OUTPUTDEST
-IF &&OUTLINE2 EQ 'OFFLINE' THEN GOTO XOFFLINE ELSE GOTO XONLINE;

-XONLINE
-TYPE Before ONLINE
ONLINE
-TYPE After ONLINE
-RUN
-GOTO CONT

-XOFFLINE
-SET &&OUTLINE2 = '';
-TYPE Before OFFLINE
OFFLINE
-TYPE After OFFLINE
-RUN
-GOTO CONT

-CONT
-SET &&RPT_HOLD = 'RPTHOLD';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN


-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN


EX AIRUSE
-RUN

EX &&EXPARM
-RUN
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW=&&EXPARM;
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-IF &RECORDS GT 0 THEN GOTO DOIT;

-NORECORDS
-SET &&RPTSUF = 'MSG';
-INCLUDE FDEFRPTS
-RUN
JOIN SET_KEY IN BRPTINST TO SET_KEY IN BRPT_SET AS J1
DEFINE FILE BRPTINST
EMPTY/A35 = 'NO RECORDS FOUND FOR THIS SELECTION';
NOWTOD/A8 WITH INST_KEY = HHMMSS (NOWTOD);
END
TABLE FILE BRPTINST
-INCLUDE HEADAIR
PRINT EMPTY AS 'REPORT,MESSAGE' 
BY ROLLUP_CODE AS 'ROLLUP,CODE'
BY RPT_NAME AS 'REPORT,NAME'
BY SELECT_LEV AS 'BREAK,LEVEL'
IF INST_KEY EQ &&IKEY
IF RECORDLIMIT EQ 1
-INCLUDE FOOTERDT
ON TABLE SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-GOTO XXIT

-DOIT

DEFINE FILE &&RPT_HOLD
LABEL1/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMO ELSE ' ';

 LABEL2/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMD ELSE ' ';
END
-RUN

TABLE FILE &&RPT_HOLD
SUM NET_TKT_CNT 
    FARE_PAID
    MILES
BY LEVEL_DESC
BY ORGDST
BY LABEL1
BY LABEL2
BY DPT_DAY
BY XPSNGR_NM
ON TABLE HOLD AS HOLD1
END
-RUN


TABLE FILE HOLD1
SUM NET_TKT_CNT 
    FARE_PAID
    MILES      
BY LEVEL_DESC
BY ORGDST
BY LABEL1
BY LABEL2
BY DPT_DAY
BY XPSNGR_NM
RANKED BY HIGHEST &&RANK_METH   
ON TABLE HOLD AS HOLD2
END
-RUN

TABLE FILE HOLD1
SUM &&RANK_METH 
BY LEVEL_DESC
BY ORGDST
ON TABLE HOLD AS RHOLD1 
END
-RUN


TABLE FILE RHOLD1
SUM &&RANK_METH
BY LEVEL_DESC
BY ORGDST
RANKED BY HIGHEST &&RANK_METH AS 'SEG_RANK'
ON TABLE HOLD AS RHOLD2
END
-RUN

MATCH FILE HOLD1
PRINT DPT_DAY
      LABEL1
      LABEL2
      NET_TKT_CNT   
      XPSNGR_NM
BY LEVEL_DESC 
BY ORGDST 
ON TABLE HOLD
RUN
FILE RHOLD2
SUM SEG_RANK
BY LEVEL_DESC
BY ORGDST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN

TABLE FILE HOLD5
PRINT DPT_DAY
      LABEL1
      LABEL2    
      NET_TKT_CNT   
      ORGDST    
      XPSNGR_NM
BY LEVEL_DESC
RANKED BY HIGHEST SEG_RANK 
ON TABLE HOLD AS HOLD6
END
-RUN


DEFINE FILE HOLD6
  ORG_LIMIT/I2 = &&RANK_LIMIT ;
  OLIMIT/A2    = EDIT(ORG_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF ORGDST NE LAST ORGDST THEN (LAST LIST + 1) 
    ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999'); 
  ORG_VALUE/A6 = IF ORG_LIMIT EQ 0 THEN BRANK ELSE
               IF LIST GT ORG_LIMIT THEN 'OTHER' ELSE FRANK;
END
-RUN

TABLE FILE HOLD6
SUM NET_TKT_CNT 
    OLIMIT  
    ORG_LIMIT   
BY LEVEL_DESC
BY ORG_VALUE
BY ORGDST
BY LABEL1
BY LABEL2
BY DPT_DAY
BY XPSNGR_NM
ON TABLE HOLD AS HOLD7 
END
-RUN

DEFINE FILE HOLD7
  EMBARK_APT/A25 = IF ORG_VALUE LT OLIMIT THEN LABEL1 ELSE 'ALL OTHER'; 
  ORG_PRINT/A55 =IF ORG_VALUE LT OLIMIT THEN ORGDST ELSE 'ALL OTHER';
  ROUTE_APT/A25 = IF ORG_VALUE LT OLIMIT THEN LABEL2 ELSE 'ALL OTHER';
  DDPT_DAY/A10 = IF ORG_VALUE LT OLIMIT THEN DPT_DAY ELSE 'ALL OTHER';
-*   PAX_NAME/A15 = IF ORG_VALUE LT OLIMIT THEN XPSNGR_NM ELSE 'ALL OTHER';
END
-RUN

TABLE FILE HOLD7
SUM NET_TKT_CNT
    OLIMIT
BY LEVEL_DESC
BY ORG_VALUE     
BY ORG_PRINT
BY ORGDST
BY EMBARK_APT
BY ROUTE_APT  
BY DDPT_DAY
BY XPSNGR_NM
ON TABLE HOLD AS HOLD8 
END
-RUN


TABLE FILE &&RPT_HOLD
SUM NET_TKT_CNT 
BY LEVEL_DESC
BY ORGDST
BY DPT_DAY
BY XPSNGR_NM
ON TABLE HOLD AS HLD1 
END
-RUN

MATCH FILE HOLD8
PRINT EMBARK_APT
      NET_TKT_CNT
      OLIMIT
      ORGDST
      ORG_VALUE
      ORG_PRINT
      ROUTE_APT
BY LEVEL_DESC
BY ORGDST
BY DDPT_DAY
RUN
FILE HLD1
PRINT NET_TKT_CNT
      XPSNGR_NM
BY LEVEL_DESC
BY ORGDST
BY DPT_DAY
AFTER MATCH HOLD AS HOLD9 OLD
END
-RUN

DEFINE FILE HOLD9
PAX_NAME/A15 = IF ORG_VALUE LT OLIMIT THEN XPSNGR_NM ELSE 'ALL OTHER';
END

TABLE FILE HOLD9
SUM NET_TKT_CNT
BY LEVEL_DESC
BY ORG_VALUE     
BY ORG_PRINT  
BY EMBARK_APT
BY ROUTE_APT
BY DDPT_DAY
BY PAX_NAME
IF PAX_NAME NE 'ALL OTHER'
ON TABLE HOLD AS HOLD10 
END
-RUN


-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN

-TYPE BEFORE DEFINES

DEFINE FILE HOLD10
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
TABLE FILE HOLD10
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&SMSTY 
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW='CPDEMO';
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-XXIT
