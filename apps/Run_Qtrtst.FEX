-******************************************************************************** 
-* File: RUN_QTR.fex
-*   Run scheduled jobs for quarterly review.
-* Quarterly Review contains 27 reports in total so far.
-* The program retrieves the reports saved from brptflds database and run them. 
-*   Some reports have some special selections, such as airlines, peer group...
-********************************************************************************
-INCLUDE SETECHO
-DEFAULT &INSTKEY = 86753;
-SET &&RCRUN='Y';
-SET &&WRRUN = 'N';
-SET &&UDIDUSED = 'N';
-SET &ONEONLY = IF &INSTKEY NE 0 THEN 'Y' ELSE 'N';
-SET &&SKIPGRAPH='N';

-*SET TEMPERASE = OFF
-* Subroutine returns the TEMPDIR path
-SET &&WEB_PATH=TEMPPATH(50,'A50');
SET EDAPATH=+&&WEB_PATH;

EX SUUSES
-RUN

-MoreReports
-******************************************************************************
-* Retrieve the report to be run
-******************************************************************************
TABLE FILE RPT_QUE
PRINT SET_KEY ROLLUP_CODE USER_NAME 
BY INTIME 
BY INST_KEY

-IF &ONEONLY EQ 'Y' THEN GOTO RUNONE;
WHERE RPT_SEQ EQ 888 AND EX_FLAG NE 'M'
-GOTO CHECKPOINT1;

-RUNONE
WHERE INST_KEY EQ &INSTKEY AND EX_FLAG NE 'M'

-CHECKPOINT1
IF RECORDLIMIT EQ 1
ON TABLE SAVE AS SAVE1
END
-RUN
-IF &LINES EQ 0 THEN GOTO FinishQTR;

-READ SAVE1 &&INTIME.18. &&IKEY.7. &&SETKEY.7. &&ROLLUP.8. &&UNAME.A20. 
-CLOSE SAVE1
-******************************************************************************
-* common modules to be executed
-******************************************************************************
-INCLUDE LOADBQTR
-RUN

-INCLUDE LOADBINS
-RUN

EX CODEGEN
-RUN

-SET &&LDESC = IF &&BRK_LVL LT 10 THEN 'AROLL_DSC' | EDIT('&&BRK_LVL.EVAL','$9') 
-             ELSE 'AROLL_DSC' | EDIT('&&BRK_LVL.EVAL');
-******************************************************************************
-* Set up Quarterly Review AND define file in PDF format
-******************************************************************************
EX QRSETUP
-RUN

-*save base rollup as this report has peer group selected.
-SET &&BASEROLL = '&&ROLLUP.EVAL';
-SET &&BASECOMP = '&&COMPNY.EVAL';
-******************************************************************************
-SET &&AIRTRANS = 'Y';
EX QRCHECKER
-RUN

-IF '&&AIRTRANS.EVAL' EQ 'N' THEN GOTO MailToCSM;
-******************************************************************************

-INCLUDE QRDEFILE
-RUN
FILEDEF SAVREP DISK &&QTRFILE
-SET &&OUTLINE1 = 'ON TABLE SAVE AS SAVREP';
-*-SET &&OUTLINE1 = 'ON TABLE PCHOLD AS SAVREP';
-SET &&OUTLINE2 = 'FORMAT PDF OPEN';
-RUN

-*COVER PAGE
EX QRCoverPg
-RUN

-*get Peer Group
EX GETPGRP
-RUN
-******************************************************************************
-* retrieve the reports included in the Quarterly Review by order
-******************************************************************************
-CHKRPTID
TABLE FILE BRPTFLDS
PRINT RPTID
BY ORDERS NOPRINT
WHERE INST_KEY EQ &&IKEY AND SELECTED EQ 1
ON TABLE SAVE AS RPTIDS
END
-RUN

-SET &&CNT = &LINES;
-SET &CNT2 = 1;
-REPEAT TOEND &&CNT TIMES
-READ RPTIDS NOCLOSE &VAL.3.
-SET &RPTID.&CNT2 = &VAL;
-SET &CNT2 = &CNT2 + 1;
-TOEND
-CLOSE RPTIDS
-******************************************************************************
-* Run all the reports selected in Quarterly Review by order
-******************************************************************************

-SET &CNT2 = 1;
-REPEAT TOEXIT &&CNT TIMES
-SET &RPTID = &RPTID.&CNT2;

-SET &&OUTLINE2 = 
- IF &&CNT EQ 1 AND (&RPTID EQ 3 OR &RPTID EQ 17 OR &RPTID EQ 26) 
-  THEN 'FORMAT PDF CLOSE'
- ELSE IF &&CNT EQ 1 AND &&CP EQ 0 THEN 'FORMAT PDF'
- ELSE IF &&CNT EQ 1 AND &&CP EQ 1 THEN 'FORMAT PDF CLOSE'
- ELSE IF &CNT2 LT &&CNT THEN 'FORMAT PDF OPEN'
- ELSE 'FORMAT PDF CLOSE';
-*-TYPE &&OUTLINE2

-INCLUDE QRDECODE

-*-TYPE EXECUTING &PRGRMNAME ......
EX &PRGRMNAME
-RUN

-SET &CNT2 = &CNT2 + 1;
-TOEXIT
-******************************************************************************
-* mail file
-******************************************************************************
-*-IF  &FOCERRNUM GT 0 THEN GOTO MoreReports;
-IF '&&DEST.EVAL' NE 'E' THEN GOTO CLEARIT;

-MailToCSM
EX QRMAIL
-RUN

-CLEARIT
-******************************************************************************
-* Delete this report instance in the RPT_QUE and H* SU databases 
-******************************************************************************
-INCLUDE CLEARQUE
-RUN

-IF &ONEONLY EQ 'Y' THEN GOTO FinishQTR;
-GOTO MoreReports
-FinishQTR
