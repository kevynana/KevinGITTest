SET ASNAMES=ON
-* EXPIRE_REPORTS=1
-*FILEDEF XUSER DISK D:\FOCTEMP\XUSER.FTM
SET PAGE-NUM=OFF
SET EMPTYREPORT=ON


JOIN SET_KEY IN RPT_SET TO ALL SET_KEY IN RPT_INST AS J1

DEFINE FILE RPT_SET
-* XHIER/A30 = IF LEV_SELECT EQ ' ' THEN 'UNKNOWN' ELSE LEV_SELECT ;
DISTRB/A10 = DECODE DISTRB_FLAG ('T' 'SERVER' 'F' 'FTP'
                                 'E' 'EMAIL');
OTYPE/A22 = IF RPT_INST.OUTPUT_FORMAT EQ 'EXCEL' THEN 'EXCEL' ELSE
            IF RPT_INST.OUTPUT_FORMAT EQ 'EXL2K' THEN 'EXCEL 2000' 
            ELSE IF RPT_INST.OUTPUT_FORMAT EQ 'PDF' 
            THEN 'PDF' ELSE 'UNKNOWN';

END  

TABLE FILE RPT_SET
PRINT INST_KEY DISTRB OTYPE SET_FREQ USER_NAME ROLLUP_CODE
SET_NAME GLOBAL_PARM RPT_NAME SEND_TO 
BY INST_KEY NOPRINT
BY ROLLUP_CODE NOPRINT
IF USER_NAME EQ 'master'
IF SET_FREQ NE ' ' OR 'NONE'
ON TABLE HOLD AS RPTSET1 FORMAT FOCUS INDEX INST_KEY
END
-RUN

JOIN ROLLUP_CODE IN RPTSET1 TO ALL ROLLUP_CODE IN ROLLUP AS J2

TABLE FILE RPTSET1
PRINT INST_KEY DISTRB OTYPE SET_FREQ USER_NAME ROLLUP_CODE
SET_NAME GLOBAL_PARM RPT_NAME ROLL_FY SEND_TO LOAD_GRP
BY INST_KEY NOPRINT
BY ROLLUP_CODE NOPRINT
IF RUN_RPT EQ 'X'
IF USER_NAME EQ 'master
-* IF SET_FREQ NE ' ' OR 'NONE'
IF SET_FREQ CONTAINS 'MONTHLY' OR 'M YTD' OR 'QUARTERLY' OR 'Q YTD'
IF ROLLUP NE 'ABCD-R'
IF LOAD_GRP NE 'X'
-* IF LOAD_GRP EQ 'W' OR 'V'
 ON TABLE HOLD AS RPTSET FORMAT FOCUS INDEX INST_KEY
END
-RUN

TABLE FILE RPTSET
PRINT DISTRB OTYPE SET_FREQ USER_NAME ROLLUP_CODE
SET_NAME GLOBAL_PARM RPT_NAME ROLL_FY SEND_TO LOAD_GRP
BY INST_KEY
ON TABLE HOLD AS XONE
END
-RUN

TABLE FILE XONE
PRINT INST_KEY
BY INST_KEY NOPRINT
ON TABLE SAVE
END
-RUN

TABLE FILE RPT_FLDS
PRINT HIEORG2.MEMBER AS 'MEMBER'
      HIEORG2.CATG AS 'CATG'
      HIEORG2.SEQ_NO AS 'SEQ_NO'
BY INST_KEY
WHERE INST_KEY IN FILE SAVE
ON TABLE HOLD AS XTWO
END
-RUN

-* JOIN INST_KEY IN RPTSET TO INST_KEY IN RPT_FLDS

MATCH FILE XONE
PRINT DISTRB
      OTYPE
      SET_FREQ
      SET_NAME
      GLOBAL_PARM
      RPT_NAME
      ROLL_FY
      SEND_TO
      ROLLUP_CODE
      LOAD_GRP
BY INST_KEY
ON TABLE HOLD
RUN
FILE XTWO
PRINT MEMBER CATG SEQ_NO
BY INST_KEY
AFTER MATCH HOLD AS RPTSET2 OLD
END
-RUN


DEFINE FILE RPTSET2
SND_TO/A18 = EDIT(SEND_TO, '999999999999999999$$$$$$$$$$$$$$$$$$$$$$'); 
END
-RUN
                        
TABLE FILE RPTSET2
HEADING
" "
" "
" "
"TRAVEL & TRANPORT, INC. - eTTek Review"
"&DATE ........ &TOD        REPORT LISTING FOR "
"REPORT NAME:  RPT_LSTB.FEX"
" "
" "
BY LOAD_GRP         AS 'LOAD,GROUP'
BY ROLLUP_CODE      AS 'ROLLUP'
BY SND_TO           AS 'SEND TO'
BY SET_FREQ         AS 'FREQUENCY'
BY SET_NAME         AS 'SET NAME'
BY GLOBAL_PARM      AS 'REPORT ID'
PRINT RPT_NAME      AS 'REPORT,NAME'
      MEMBER        AS 'HIERARCHY'
      OTYPE         AS 'FORMAT'
      DISTRB        AS 'OUTPUT'
      CATG          AS 'CATEGORY'
      ROLL_FY       AS 'FISCAL,START,MONTH'
-*WHERE RPT_FLDS.HIEORG2.SEQ_NO EQ 1
ON ROLLUP_CODE PAGE-BREAK      
ON SET_NAME SKIP-LINE
-* ON TABLE SET ONLINE-FMT PDF
ON TABLE SET ONLINE-FMT EXL2K

ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.200000, RIGHTMARGIN=0.200000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=6, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, STYLE = BOLD ,$
TYPE=HEADING, LINE=4, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=14, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
-* TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(.5 .5 ), SIZE=(1.5 .4), $

END
-RUN
JOIN CLEAR J1
JOIN CLEAR J2

-RUN
