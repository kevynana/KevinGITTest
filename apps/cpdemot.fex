-* File CPDEMOT.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-*5/21/01      TANDT-SS               ADDED DEFINE TO NORECORDS SO MESSAGE
-*                                    "NO RECORDS FOUNDS FOR THIS SELECTION" 
-*                                    WILL APPEAR IN EXCEL 97
-*4/30/02      TANDT-JK             COMMENTED OUT THE USE OF CPCOVPG
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-**********************************************************************
EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS

-TYPE &&OUTPUTDEST
-IF &&OUTLINE2 EQ 'OFFLINE' THEN GOTO XOFFLINE ELSE GOTO XONLINE;

-XONLINE
-TYPE Before ONLINE
ONLINE
-TYPE After ONLINE
-RUN
-GOTO CONT

-XOFFLINE
-SET &&OUTLINE2 = '';
-TYPE Before OFFLINE
OFFLINE
-TYPE After OFFLINE
-RUN
-GOTO CONT

-CONT
-SET &&RPT_HOLD = 'RPTHOLD';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN

EX AIRUSE
-RUN

EX SELDESC2
-RUN

EX &&EXPARM
-RUN

-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW=&&EXPARM;
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-IF &RECORDS GT 0 THEN GOTO DOIT;

-NORECORDS
-SET &&RPTSUF = 'MSG';
-INCLUDE FDEFRPTS
-RUN
JOIN SET_KEY IN BRPTINST TO SET_KEY IN BRPT_SET AS J1
DEFINE FILE BRPTINST
EMPTY/A35 = 'NO RECORDS FOUND FOR THIS SELECTION';
NOWTOD/A8 WITH INST_KEY = HHMMSS (NOWTOD);
END
TABLE FILE BRPTINST
-INCLUDE HEADAIR
PRINT EMPTY AS 'REPORT,MESSAGE' 
BY ROLLUP_CODE AS 'ROLLUP,CODE'
BY RPT_NAME AS 'REPORT,NAME'
BY SELECT_LEV AS 'BREAK,LEVEL'
IF INST_KEY EQ &&IKEY
IF RECORDLIMIT EQ 1
-INCLUDE FOOTERDT
ON TABLE SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-GOTO XXIT

-DOIT

TABLE FILE &&RPT_HOLD
SUM COMM_SEG	
    CP_FARE_PD AS 'FARE'
    CTY_PR
    DEMB_APT 	
    DRTE_APT	
    EMB_APT
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	
    NEW_SEG_COUNT	
    RTE_APT	
COMPUTE AVG_TKT/D12.2 = IF ((NET_TKT_CNT LT 1) AND 
                           (NET_TKT_CNT GT (-1))) THEN 0 
                        ELSE (CP_FARE_PD/NET_TKT_CNT);
BY LEVEL_DESC
BY ORGDST
ON TABLE HOLD AS HOLD1 FORMAT ALPHA
END
-RUN

TABLE FILE HOLD1
PRINT AVG_TKT
      COMM_SEG	
      CTY_PR
      DEMB_APT	
      DRTE_APT	
      EMB_APT
      FARE
      MILES	
      NET_COMM	
      NET_FARE 	
      NET_TKT_CNT	
      NEW_SEG_COUNT	
      RTE_APT
BY LEVEL_DESC
BY ORGDST
RANKED BY HIGHEST &&RANK_METH   
ON TABLE HOLD AS HOLD2 FORMAT ALPHA
END
-RUN


TABLE FILE HOLD1
SUM &&RANK_METH 
BY LEVEL_DESC
BY ORGDST
ON TABLE HOLD AS RHOLD1 FORMAT ALPHA
END
-RUN

TABLE FILE RHOLD1
PRINT *
BY LEVEL_DESC
RANKED BY HIGHEST &&RANK_METH AS 'SEG_RANK'
ON TABLE HOLD AS RHOLD2 FORMAT ALPHA
END
-RUN

MATCH FILE HOLD2
PRINT AVG_TKT
      COMM_SEG
      CTY_PR
      DEMB_APT	
      DRTE_APT	
      EMB_APT
      FARE	
      MILES	
      NET_COMM	
      NET_FARE 	
      NET_TKT_CNT	
      NEW_SEG_COUNT	
      RTE_APT
BY LEVEL_DESC 
BY ORGDST 
ON TABLE HOLD
RUN
FILE RHOLD2
SUM SEG_RANK
BY LEVEL_DESC
BY ORGDST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN

TABLE FILE HOLD5
PRINT AVG_TKT
      COMM_SEG
      CTY_PR
      DEMB_APT	
      DRTE_APT	
      EMB_APT	
      FARE
      MILES	
      NET_COMM	
      NET_FARE 	
      NET_TKT_CNT	
      NEW_SEG_COUNT	
      ORGDST	
      RTE_APT	
BY LEVEL_DESC
RANKED BY HIGHEST SEG_RANK 
ON TABLE HOLD AS HOLD6 FORMAT ALPHA
END
-RUN

DEFINE FILE HOLD6
  ORG_LIMIT/I2 = &&ORLMT ;
  OLIMIT/A2    = EDIT(ORG_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF ORGDST NE LAST ORGDST THEN (LAST LIST + 1) 
    ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999'); 
  ORG_VALUE/A6 = IF ORG_LIMIT EQ 0 THEN BRANK ELSE
                 IF LIST GT ORG_LIMIT THEN 'OTHER' ELSE FRANK;
END
-RUN

TABLE FILE HOLD6
SUM AVG_TKT
    COMM_SEG
    CTY_PR
    DEMB_APT	
    DRTE_APT	
    EMB_APT
    FARE
    MILES	
    NET_COMM	
    NET_FARE  	
    NET_TKT_CNT	
    NEW_SEG_COUNT	
    OLIMIT	
    ORG_LIMIT	
    RTE_APT	
    SEG_RANK	
BY LEVEL_DESC
BY ORG_VALUE
BY ORGDST
ON TABLE HOLD AS HOLD7
END
-RUN

DEFINE FILE HOLD7
  DEMBARK_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN DEMB_APT ELSE 'ALL OTHER';
  DROUTE_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN DRTE_APT ELSE 'ALL OTHER';
  EMBARK_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN EMB_APT ELSE 'ALL OTHER'; 
  ORG_PRINT/A55 =IF ORG_VALUE LT (OLIMIT) THEN ORGDST ELSE 'ALL OTHER'; 
-*   RO_PRINT/A12   =IF ORG_VALUE LT (OLIMIT) THEN AIRLINE_PRINT ELSE 'OTHER';
  ROUTE_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN RTE_APT ELSE 'ALL OTHER';
END
-RUN

TABLE FILE HOLD7
SUM AVG_TKT
    COMM_SEG
    DEMBARK_APT    	
    DROUTE_APT	
    EMBARK_APT
    FARE
    MILES	
    NET_COMM	
    NET_FARE	
    NET_TKT_CNT	
    NEW_SEG_COUNT
    ORGDST	
    ROUTE_APT
    SEG_RANK	
BY LEVEL_DESC
BY ORG_VALUE     
BY ORG_PRINT  
ON TABLE HOLD AS HOLD8
END
-RUN


-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN


DEFINE FILE HOLD8
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
TABLE FILE HOLD8
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&SMSTY 
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW='CPDEMOT';
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-XXIT
