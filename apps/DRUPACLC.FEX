 
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/8/14 Updated Pref_non define to use exclude_flags
-*  10/17/2016  REJ  S-24432 ER5 Testing updates.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
 
-INCLUDE SETECHO
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&FIELD1 = 'XX';
 
-SET &&DTSTY = 'DT';
 
-SET &&FIELD = &ROW_LABEL;
-SET &&FIELD1 = &LSEL;
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
-IF &&FIELD EQ 'ALL OTHERS' THEN GOTO SECOND ELSE GOTO FIRST;
 
-FIRST;
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
EX RPTRESET
-RUN
EX RESET
-RUN
 
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX SETDBLS SETFILE=&&SETF
-RUN
 
-IF &&FROMAVRP EQ '' GOTO :OTER5;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:OTER5
 
-INCLUDE GetForDrillDown
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
 
-* -SET &&DSCCHK='WHERE  EQ ''&&FIELD.EVAL''';
 
-* -GOTO ENDLEV
 
- IF &LSEL EQ 'AROLL_DSC1' THEN GOTO ROLL1
- ELSE IF &LSEL EQ 'AROLL_DSC2' THEN GOTO ROLL2
- ELSE IF &LSEL  EQ 'AROLL_DSC3' THEN GOTO ROLL3
- ELSE IF &LSEL  EQ 'AROLL_DSC4' THEN GOTO ROLL4
- ELSE IF &LSEL  EQ 'AROLL_DSC5' THEN GOTO ROLL5
- ELSE IF &LSEL  EQ 'AROLL_DSC6' THEN GOTO ROLL6
- ELSE IF &LSEL  EQ 'AROLL_DSC7' THEN GOTO ROLL7
- ELSE IF &LSEL  EQ 'AROLL_DSC8' THEN GOTO ROLL8
- ELSE GOTO FIRST;
 
 
-ROLL1
-SET &&LCHK='WHERE AROLL_DSC1
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
-RUN
 
-ROLL2
-SET &&LCHK='WHERE AROLL_DSC2
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
-RUN
 
-ROLL3
-SET &&LCHK='WHERE AROLL_DSC3
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
-RUN
 
-ROLL4
-SET &&LCHK='WHERE AROLL_DSC4
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
 
-ROLL5
-SET &&LCHK='WHERE AROLL_DSC5
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
 
 
-ROLL6
-SET &&LCHK='WHERE AROLL_DSC6
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
 
-ROLL7
-SET &&LCHK='WHERE AROLL_DSC7
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
 
-ROLL8
-SET &&LCHK='WHERE AROLL_DSC8
- EQ ''&&FIELD.EVAL''';
-GOTO ENDLEV
 
-ENDLEV
 
-SET &&RPT_TTL1='                             CLC HOTEL DETAIL';
 
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DCA1;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-RUN
-:DCA1
 
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX HTLUSE
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';
 
DEFINE FILE &&EXTRACT
CITY_ST/A35 = PROP_STATE||(', '| PROP_CITY);
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
OUT_DATE/MDYY = CHKOUT_DATE;
TOTAL_RATE/D14.2C = TOTAL_AMT;
NBR_DAYS/D12=NUM_DAYS;
NBR_ROOMS/D12 = NUM_ROOMS;
NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                 ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
PROP_ZIP5/A5=EDIT(PROP_ZIP, '99999$$$$$');
LEVEL_DESC/A60  = &&LDESC;
REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
 
BOOKINGS/D8 = IF TOTAL_RATE GE 0 THEN 1 ELSE (-1);
  PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
  PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT BOOKINGS
  CITY_ST
  IN_DATE
  INV_DT
  LEVEL_DESC
  NNROOMS
  OUT_DATE
  PASSNGR_NAME
  PROP_NAME
  PROP_ADDRESS
  HOTEL_PHONE
  PROP_ZIP5
  REFUSE_CODE
  TOTAL_RATE
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
-INCLUDE RPTPARMS
-INCLUDE &HTLDATES
&&LCHK
-* WHERE CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L'
WHERE PNP_FLAG EQ 'I'
ON TABLE HOLD AS DRHHLD
END
-RUN
 
-IF &RECORDS EQ 0 GOTO NORECORDS;
-RUN
 
TABLE FILE DRHHLD
SUM BOOKINGS
    NNROOMS
    TOTAL_RATE
BY LEVEL_DESC
BY PASSNGR_NAME
BY INV_DT
BY IN_DATE
BY OUT_DATE
BY PROP_NAME
BY PROP_ADDRESS
BY PROP_ZIP5
BY CITY_ST
BY REFUSE_CODE
ON TABLE HOLD AS DRHHLD1
END
-RUN
 
DEFINE FILE DRHHLD1
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
TABLE FILE DRHHLD1
-INCLUDE HEADAIR
SUM
BOOKINGS AS 'BOOKINGS'
NNROOMS AS 'NUMBER,NIGHTS'
TOTAL_RATE AS 'TOTAL,AMOUNT'
COMPUTE AR/D12.2CSM = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE TOTAL_RATE/NNROOMS; AS 'AVERAGE,NIGHTLY,RATE'
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY LEVEL_DESC NOPRINT
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY INV_DT NOPRINT
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
BY PROP_NAME AS 'HOTEL'
BY PROP_ADDRESS AS 'ADDRESS'
BY PROP_ZIP5 AS 'ZIP,CDE'
BY CITY_ST AS 'CITY/STATE'
BY REFUSE_CODE AS 'RSN,CDE'
 
ON LEVEL_DESC RECOMPUTE AS 'TOTAL FOR'
-INCLUDE FOOTERDT
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-GOTO ENDIT;
 
-SECOND;
-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO ENDIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-ENDIT
-GOTO XXIT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT
 
