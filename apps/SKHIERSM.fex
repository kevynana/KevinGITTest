
-**********************************************************************
-* Summary by Hierarchy - Air
-* 7/8/13 - Added defines/carried thru fields for CCI exchange fields
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-*4/25/2017-JEM-S-33318-updated logic for no records report
-*09/18/2017 CA S-35421 Report view to be updated with standard footer (part 4)
-**********************************************************************
-INCLUDE SETECHO
-*
SET ASNAMES = ON
SET SPACE = 1
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');
-SET &&SP = '|';
-SET &&SP1A = IF &&ROLLNME EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP1 = IF &&ROLLCS EQ ' ' THEN ' ' ELSE &&SP1A;
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&CSLPA = IF &&RCNCT.EVAL GE 10 OR &&CSL.EVAL GE 10 THEN 60 ELSE 68;
-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' THEN 78 ELSE &&CSLPA;
-SET &&ROLLCS11 = TRUNCATE(&&ROLLCS);
-SET &&RCNCT1 = TRUNCATE(&&ROLLCNCT);
-SET &&BLANK = ' ';
-RUN

CREATE FILE SKHIER
-RUN

-*********************************************************************************
-* Gather/Add Air Info to SKHIER File
-*********************************************************************************

DEFINE FILE SR_50
  CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
  PRIOR/A1 = IF TRN_DATE GE &&FXDATE.EVAL AND TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
    
  BRK_LVL/I2 = &&BRK_LVL;
  LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
  FARE/P12.2 = TICKET_AMT + TAX_AMT;
  FARE_PAID/P12.2 = SEG_AMT + SEG_TAX;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;

  NET_TKT_CNT/P12    = IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (FARE_PAID GE 0) AND
                          (SEG_COUNT EQ 1) AND 
                          (CONJ_REL EQ 1) THEN 1 ELSE 
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND 
                          (EX_FLAG EQ 'F') THEN (-1) ELSE 
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

 -* Defines on Previous Defines
  DISSAVINGS/P12.2 = IF EMBARK EQ 'DT1' THEN 0 ELSE
                   FARE_PAID-SEG_LOWEST;

  CAIR_FEE/P12.2 = IF CURRENT EQ 'Y' THEN AIRLINE_FEE ELSE 0;
-INCLUDE TRANTYPE
END
-RUN


TABLEF FILE SR_50
PRINT 
  AIR_KEY
  CURRENT
  PRIOR
  DISSAVINGS
  DOD_TYPE
  AIR_MAIN.INTL_DOM AS 'INTDOM'
  FARE
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  SEG_NBR 	
  TKT_NUM
  TRN_DATE
  CAIR_FEE
WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)
WHERE VOID_DATE EQ 0
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN



TABLE FILE UASKECH
SUM AUD_DATA AS 'ADV'
BY AIR_KEY
WHERE UDID EQ 'ADV'
WHERE AUD_DATA NE ' '
WHERE AUD_DATA NE 'EXCH'
ON TABLE HOLD AS UDIDH FORMAT FOCUS INDEX AIR_KEY
END
-RUN

TABLE FILE RPTHOLD
PRINT 
  CAIR_FEE
  DISSAVINGS
  FARE
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  SEG_NBR 	
  TKT_NUM
  TRN_DATE
  CURRENT
  PRIOR
  LEVEL_DESC
BY AIR_KEY   
ON TABLE HOLD AS TRANONEA FORMAT FOCUS INDEX AIR_KEY
END
-RUN

JOIN AIR_KEY IN TRANONEA TO AIR_KEY IN UDIDH AS J1
-RUN

DEFINE FILE TRANONEA
ADV_AMT1/D12.2CS = EDIT(ADV);
ADV_AMTA/D12.2CS = IF SEG_NBR EQ '01' THEN ADV_AMT1 ELSE 0;
END
-RUN
TABLE FILE TRANONEA
SUM ADV_AMTA
    DISSAVINGS
    FARE_PAID
    NET_TKT_CNT
    CAIR_FEE  
BY CURRENT
BY LEVEL_DESC
BY TKT_NUM
ON TABLE HOLD AS TRANONEA1
END
-RUN


DEFINE FILE TRANONEA1
ADV_AMT/D12.2CS = IF FARE_PAID EQ 0 THEN 0 ELSE ADV_AMTA;
-* FP/D12.2CS = IF FARE_PAID LT 0 THEN FARE_PAID * (-1) ELSE FARE_PAID;
CADV_FP/D12.2CS = IF ADV_AMT GT 0 THEN FARE_PAID ELSE 0;
CADV_LSV/D12.2CSM =  CADV_FP - ADV_AMT;
END
-RUN

TABLE FILE TRANONEA1
PRINT
  ADV_AMT
  CADV_FP
  CADV_LSV
  CAIR_FEE
  DISSAVINGS
  FARE_PAID
  NET_TKT_CNT 
BY CURRENT
BY LEVEL_DESC
BY TKT_NUM
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS TRANONE
END
-RUN

-IF &LINES EQ 0 THEN GOTO NOREC;

-SET &&RPTSUF = 'HIERSUM';
-INCLUDE FDEFRPTS
-RUN


DEFINE FILE TRANONE
CAVOL/P12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
PAVOL/P12.2 = 0;
CNTKT/P12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PNTKT/P12 =  0;
CLSAV/P12.2 = IF CURRENT EQ 'Y' THEN DISSAVINGS ELSE 0;
END
-RUN

TABLE FILE TRANONE
SUM CAVOL
    CLSAV
    CNTKT
    PNTKT
    PAVOL   
    CAIR_FEE
    CADV_LSV AS 'CADV_AMT'
BY LEVEL_DESC
ON TABLE HOLD AS TRANTWO
END
-RUN

TABLE FILE TRANTWO
PRINT CAVOL CNTKT PNTKT CLSAV PAVOL LEVEL_DESC CAIR_FEE CADV_AMT
RANKED BY HIGHEST CAVOL
ON TABLE HOLD AS RNK1
END
-RUN

-INCLUDE OTHPARMS
-RUN
 
DEFINE FILE RNK1
RANK_LIMIT/I2 = 9999;
-*  RANK_LIMIT/I2 = &&RANK_LIMIT;
 RLIMIT/A2    = EDIT(RANK_LIMIT) ;
 CRANK/D5     = RANK;
 BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
 LIST/D6 = IF LEVEL_DESC NE LAST LEVEL_DESC THEN (LAST LIST + 1) 
           ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999'); 
 RANK_VALUE/A9 = IF RANK_LIMIT EQ 0 THEN BRANK ELSE
                IF LIST GT RANK_LIMIT THEN 'ALL OTHER' ELSE FRANK;
 LVL_PRINT/A25 =IF RANK_VALUE LT RLIMIT THEN LEVEL_DESC ELSE 'ALL OTHER';
END
-RUN

TABLE FILE RNK1
SUM CAVOL CNTKT PNTKT CLSAV PAVOL LEVEL_DESC CAIR_FEE CADV_AMT
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS RNK2
END
-RUN


TABLE FILE RNK2
PRINT LVL_PRINT RANK_VALUE CAVOL CNTKT PNTKT CLSAV PAVOL CAIR_FEE CADV_AMT
ON TABLE SAVE
END
-RUN

MODIFY FILE SKHIER
LOG DUPL MSG OFF
FIXFORM LVL_PRINT/25 RANK_VALUE/9 CAVOL/12 CNTKT/12 PNTKT/12 CLSAV/12 PAVOL/12 CAIR_FEE/12 CADV_AMT/12
MATCH RANK_VALUE LVL_PRINT
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


TABLE FILE RNK1
SUM LST.LIST LVL_PRINT 
WHERE LVL_PRINT NE 'ALL OTHER'
ON TABLE HOLD AS LASTRANK
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NOADD;

 
DEFINE FILE LASTRANK
NRANK/D6 = LIST + 1;
NRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
FRANK/A6  = EDIT(NRANKX,'$999999'); 
CAVOL/P12.2 =  0;
PAVOL/P12.2 =  0;
CNTKT/P12 =  0;
PNTKT/P12 =  0;
CLSAV/P12.2 =  0;
CAIR_FEE/P12.2 = 0;
CADV_AMT/P12.2 = 0;
 RANK_VALUE/A9 = 'ALL OTHER' ;
 LVL_PRINT/A25=  'ALL OTHER'; 
END
TABLE FILE LASTRANK
PRINT NRANK LVL_PRINT RANK_VALUE CAVOL CNTKT PNTKT CLSAV PAVOL CAIR_FEE CADV_AMT
ON TABLE HOLD AS LASTRANK1 
END
-RUN

 
TABLE FILE LASTRANK1
PRINT LVL_PRINT RANK_VALUE CAVOL CNTKT PNTKT CLSAV PAVOL CAIR_FEE CADV_AMT
ON TABLE SAVE
END
-RUN

MODIFY FILE SKHIER
LOG DUPL MSG OFF
FIXFORM LVL_PRINT/25 RANK_VALUE/9 CAVOL/12 CNTKT/12 PNTKT/12 CLSAV/12 PAVOL/12 CAIR_FEE/12 CADV_AMT/12
MATCH RANK_VALUE LVL_PRINT
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN 


-NOADD
DEFINE FILE RNK2
LVLP/A27 = ''''||LVL_PRINT||'''';
END
-RUN
TABLE FILE RNK2
PRINT LVLP
BY LVL_PRINT NOPRINT
ON TABLE SAVE AS LEVELF
END
-RUN



-*********************************************************************************
-* Gather/Add Car Info to SKHIER File Based upon 
-*********************************************************************************

EX CARUSE
-RUN


-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN

-INCLUDE RESETCPM
-RUN

DEFINE FILE CR_50
 LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
 RENTAL_DAYS/P12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
END

-RUN
TABLE FILE CR_50
SUM RENTAL_DAYS
    RENTAL_AMT
BY LEVEL_DESC
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL)
-INCLUDE RPTPARMS
ON TABLE HOLD AS CAR1
END
-RUN
 


TABLE FILE CAR1
SUM RENTAL_AMT/P12.2 AS 'CARVOL'
    RENTAL_DAYS/P12 AS 'CARDAY'
BY LEVEL_DESC
ON TABLE HOLD AS CAR2
END
-RUN


-******************************************************
-* Matching Hierarchy Levels
-******************************************************

TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARMREC
END
-RUN


TABLE FILE CARMREC
SUM CARVOL
    CARDAY
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS CARMREC1
END
-RUN

TABLE FILE CARMREC1
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE SKHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12 
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN

-**********************************************************************
-* Non Matching information ALL OTHER
-**********************************************************************

TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARNREC
END
-RUN

 

DEFINE FILE CARNREC
LVL_PRINT/A25 = 'ALL OTHER';
END
-RUN
TABLE FILE CARNREC
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE SKHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12 
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL 
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
 

-*********************************************************************************
-* Gather/Add HTL Info to SKHIER File
-*********************************************************************************
FILEDEF RPTPARMS CLEAR
-RUN 

EX HTLUSE
-RUN

-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN

-INCLUDE RESETHPM
-RUN


DEFINE FILE HR_50
 LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = EDIT(LEVEL_DESC1, '9999999999999999999999999');
 NBR_DAYS/P12=NUM_DAYS;
 NEW_TOTAL_AMT/P12.2 = TOTAL_AMT;
 NNROOMS/P12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
END
-RUN

TABLE FILE HR_50
SUM NEW_TOTAL_AMT
    NNROOMS
BY LEVEL_DESC
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) 
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTL1
END
-RUN
 

-**************************************************************************
-* Matching Hotel Records
-**************************************************************************
TABLE FILE HTL1
SUM NEW_TOTAL_AMT AS 'HTLVOL'
    NNROOMS AS 'HTLDAY'
BY LEVEL_DESC
ON TABLE HOLD AS HTL2
END
-RUN


TABLE FILE HTL2
SUM HTLVOL
   HTLDAY
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLMREC
END
-RUN

TABLE FILE HTLMREC
SUM HTLVOL HTLDAY
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS HTLMREC1
END
-RUN

TABLE FILE HTLMREC1
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE SKHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 HTLVOL/12 
MATCH LVL_PRINT
ON MATCH UPDATE HTLVOL
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN

-**********************************************************************
-* Non Matching ALL OTHER Records
-**********************************************************************
TABLE FILE HTL1
SUM NEW_TOTAL_AMT AS 'HTLVOL'
    NNROOMS AS 'HTLDAY'
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLNREC
END
-RUN


DEFINE FILE HTLNREC
LVL_PRINT/A25 = 'ALL OTHER';
END
-RUN
TABLE FILE HTLNREC
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE HOLD AS HTLNREC1
END
-RUN

TABLE FILE HTLNREC1
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE SKHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 HTLVOL/12
MATCH LVL_PRINT
ON MATCH UPDATE HTLVOL
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN


DEFINE FILE SKHIER
RANK_VALUEA/A9 = 'TOTAL';
LVL_PRINTA/A25 = 'TOTAL';
END
-RUN

TABLE FILE SKHIER
SUM CAVOL/P12.2M 
    CNTKT 
    CLSAV/P12.2M 
    CAIR_FEE/P12.2M
    CADV_AMT/P12.2M
    CARVOL/P12.2M
    HTLVOL/P12.2M
COMPUTE TACH/P12.2M = CAVOL + CARVOL + HTLVOL; 
BY RANK_VALUEA AS 'RANK_VALUE'
-* BY LVL_PRINTA AS 'LVL_PRINT'
BY LVL_PRINTA
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS TTL1
END
-RUN


TABLE FILE TTL1
SUM CAVOL/P12 CNTKT/P12 CLSAV/P12 CAIR_FEE/P12 CADV_AMT/P12 CARVOL/P12 HTLVOL/P12 TACH/P12
BY RANK_VALUE 
BY LVL_PRINT
ON TABLE HOLD AS TTL2
END
-RUN

TABLE FILE TTL2
PRINT CAVOL CNTKT CLSAV CAIR_FEE CADV_AMT CARVOL HTLVOL TACH
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TTL3 FORMAT FOCUS
END
-RUN

TABLE FILE SKHIER
SUM CAVOL/P12.2M 
    CNTKT 
    CLSAV/P12.2M 
    CAIR_FEE/P12.2M
    CADV_AMT/P12.2M
    CARVOL/P12.2M
    HTLVOL/P12.2M
COMPUTE TACH/P12.2M = CAVOL + CARVOL + HTLVOL; 
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV1
END
-RUN

TABLE FILE TLEV1
SUM CAVOL/P12 CNTKT/P12 CLSAV/P12 CAIR_FEE/P12 CADV_AMT/P12 CARVOL/P12 HTLVOL/P12 TACH/P12
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV2 
END
-RUN


TABLE FILE TLEV2
PRINT CAVOL CNTKT CLSAV CAIR_FEE CADV_AMT CARVOL HTLVOL TACH
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV3 FORMAT FOCUS
END
-RUN


-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &PHOLD1=&HLDPATH || 'TLEV3.FOC';
-SET &PHOLD2=&HLDPATH || 'TTL3.FOC';
-RUN

USE CLEAR *
-RUN

EX SUUSES
-RUN

USE ADD 
&PHOLD1 AS TLEV3
&PHOLD2 AS TLEV3
END
-RUN


DEFINE FILE TLEV3
RANK_VAL1/A9 = IF RANK_VALUE EQ 'TOTAL' THEN 'ZZ' ELSE RANK_VALUE;
END
-RUN

TABLE FILE TLEV3
SUM CAVOL CNTKT CLSAV CAIR_FEE CADV_AMT CARVOL HTLVOL TACH
COMPUTE ZFLAG/A1 = IF (RANK_VAL1 EQ 'ALL OTHER') AND (TACH EQ 0) THEN 'Y' ELSE 'N';    
BY RANK_VAL1
BY LVL_PRINT
ON TABLE HOLD AS TLEV4
END
-RUN

JOIN ROLL WITH RANK_VAL1 IN TLEV4 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN

DEFINE FILE TLEV4
NOWTOD/A8 WITH RANK_VAL1 = HHMMSS (NOWTOD);
ROLL/A8='&&ROLLUP.EVAL';
  RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
  RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
  RN1/A60=TRIM('B', '&&RPT_TTL2.EVAL', 60, ' ', 1, 'A60');
  TDATE1/A8MDYY='&MDYY';
  TDATE2/A10=EDIT(TDATE1,'99/99/9999');
  TIME1/A8=HHMMSS('A8');
  TIME2/A8=EDIT(TIME1,'99$:99$:99');
  RD/A10 = 'Run Date:';
  RT/A10 = 'Run Time:';
  DTDESC/A20='Transaction Dates';
  CUR/A15='Current Period:';
  PRI/A15='Prior Period:';
  SPACE/A1 = 'A';
  KEY/A1 = IF RANK_VAL1 EQ 'TOTAL' THEN 'B' ELSE 'A';
END
-RUN


TABLE FILE TLEV4
HEADING 
"&&ROLLNME &&SP1 &&ROLLCS11 &&SP3 &&RCNCT1 &&SP4 &&ROLLPHN"
"&&DTDESC  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"&&RPT_TTL1"
"&&RPT_TTL2"
" "
SUM CAVOL/P12CM AS 'Total,Air/Rail,Volume' 
    CLSAV/P12CM AS 'Lost Savings,Lower Fare Options'
    CADV_AMT/P12CM AS 'Lost Savings,Booked within 14 days adv'
    CAIR_FEE/P12CM AS 'Fees/Add Collect Amounts,for changes'
    CARVOL/P12CM AS 'Total,Car,Volume'
    HTLVOL/P12CM AS 'Total,Hotel,Volume'
    TACH/P12CM AS 'Total,Air/Rail,Car & Hotel,Volume'
BY RANK_VAL1 NOPRINT
BY KEY NOPRINT
BY LVL_PRINT AS 'HIERARCHY'
WHERE ZFLAG EQ 'N'
ON TABLE SET PAGE-NUM OFF
&&OUTLINE1
&&OUTLINE2
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *

UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000, 
    TOPMARGIN=0.300000, BOTTOMMARGIN=0.050000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
    
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=9, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=9, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=3, JUSTIFY=LEFT, $

TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8, 
     BACKCOLOR=(RGB(242 242 242) 'WHITE' ),$
     
TYPE=REPORT, COLUMN=P1, SQUEEZE=2.000, $     
TYPE=REPORT, COLUMN=P2, SQUEEZE=1.200, $     
TYPE=REPORT, COLUMN=P3, SQUEEZE=1.200, $     
TYPE=REPORT, COLUMN=P4, SQUEEZE=1.200, $     
TYPE=REPORT, COLUMN=P5, SQUEEZE=1.200, $     
TYPE=REPORT, COLUMN=P6, SQUEEZE=1.200, $     
TYPE=REPORT, COLUMN=P7, SQUEEZE=1.200, $     
TYPE=REPORT, COLUMN=P8, SQUEEZE=1.200, $     
     

TYPE=DATA, JUSTIFY=LEFT, $     
     
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$
     
TYPE=TITLE, JUSTIFY=LEFT, $     
    
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
     
TYPE=FOOTING, SIZE=7, FONT='ARIAL', COLOR=RGB(70 136 177), $

TYPE=SUBHEAD,
     STYLE=BOLD,
$

TYPE=SUBTOTAL, 
     STYLE=BOLD, 
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $
     
TYPE=GRANDTOTAL, STYLE=BOLD, $


-XSTYSK
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
 

SET SPACE = 2

-GOTO XXIT;



-NOREC
-IF &&FROMAVRP NE 'Y' GOTO :AVSF2;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF2

-INCLUDE NORECRPT
-RUN
-EXIT


-XXIT

