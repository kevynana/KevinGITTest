-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File AIREXTRT.FEX
-********************************************************************
-* MODIFICAIONS:
-* 5/22/1998 ADDED TRN_DATE CRITERIA TO WHERE STATEMENTS - LP
-* 7/09/1998 ADDED A DEFINE FOR TICKETLESS MIR TRANSACTIONS - LP
-* 4/29/2002 CHANGED MRK_TKT_CNT DEFINE TO CORRECTLY BACK OUT PARTIAL
-*           REFUNDS AND EXCHANGES - SC DB
-* 11/17/03  ADDED THE FOLLOWING DEFINES FOR THE IHI ROUNDTRIP ANALYSIS
-*           EMB_APT, RTE_APT, ND_CP_NM WILL USE RNDTRIP1 STREAM JK 
-*03/01/13  CHANGED CLASS_CAT DEFINE DUE TO CLASS CATEGORY CHANGES - DV
-* ADDED TRANTYPE INCLUDE - JK 12/10/14 STORY ID S-06492
-*12/08/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-* 11/09/2016 - RSB - S-26926 Change reports to read employee ID from EMP_ID
-*                             Added: EMP_ID
-*09/26/2017 - DLV - S-37268 - UPDATED defines ND_APT_CDO ND_APT_CDD
-*
-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.
-*
-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
  BASE_FARE/D12.2CS = SEG_AMT - SEG_COM;
  BOOK_DT/MDYY         = RESV_DATE;
  CARD_NUM/A12=EDIT(CREDIT_CARD, '$$999999999999$$$$');
  CARD1_NUM/A4=EDIT(CREDIT_CARD, '$$$$$$$$$$$$$$9999');
  CARD_NUM1/A16=EDIT(CREDIT_CARD,'$$9999999999999999');
  CCARD/A4 = EDIT(CREDIT_CARD, '$$9999$$$$$$$$$$$$');
  CCARD1/A4 =EDIT(CREDIT_CARD, '$$$$$$9999$$$$$$$$');
  CCARD2/A6 =EDIT(CREDIT_CARD, '$$999999$$$$$$$$$$');
  CCARD3/A6 =EDIT(CREDIT_CARD, '$$999999$$$$$$$$$$');
  CCARD5/A6 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$999999');
-*  CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'
-*                              'D' 'DISCOUNT'  'B' 'BUSINESS');
  CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE' 
                                ' ' 'DEFAULT'); 
  COM_AMT/D12.2CS = SEG_COM;
  CRD_CRD/A2=EDIT(CREDIT_CARD, '99$$$$$$$$$$$$$$$');
  DEPT_MONTH/MT        = DPT_DATE;
  DIR_CP_CD/A50        = CITYPAIR.EMBARK||'-'||CITYPAIR.ROUTE;
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  GROSS_FARE/D12.2CS  = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
  LEVEL_DESC1/A80      = &&LDESC;
  MIR_TYPE/A24 = DECODE AUX_NON_MIR ('X' 'X-DUAL MIR'
                                    'N' 'N-NON-TICKETING MIR'
                                    'J' 'J-NON-FARING MIR'
                                    'A' 'A-NON-ACCOUNTING MIR'
                                    'B' 'B-ACCOUNTING MIR'
                                    'H' 'H-REGULAR TICKET MIR'
                                    'V' 'V-VOID'
                                    'E' 'E-ELECTRONIC TKT NETWORK'
                                    'L' 'L-ELECTRONIC TKT MIR'
                                    ' ' ' -MANUALLY ENTERED TRANS');
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  MTG_NUM/A7 = EDIT(LEVEL3, '$$$$$$$$9999999');
  ND_CITY_NM/A50= NDO_CTY.CITY_NAME | NDD_CTY.CITY_NAME;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NET_TKT_CNT1/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_COUNT/D8CS = SEG_COUNT;
  NEW_SEG_DISC/D12.2CS = SEG_DISC;
  NEW_SEG_MILES/D9= SEG_MILES;
  NOREF_SEG_COUNT/D8CS = IF (RF_FLAG EQ ' ') AND
                            (EX_FLAG EQ ' ') THEN
                           NEW_SEG_COUNT ELSE 0;
  REF_E_CNT/D8CS      = IF (RF_FLAG NE ' ') OR
                           (EX_FLAG NE ' ') THEN
                            1 ELSE 0;
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 0 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  REFEXCH/D12.2CS     = IF (RF_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE
                        IF (EX_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  XARR_DOW/W           = ARR_DATE;
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        =
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
EMB_APT/A21=EDIT(NDO_APT.AIRPORT_NAME,'999999999999999999999$$$$');
RTE_APT/A21=EDIT(NDD_APT.AIRPORT_NAME,'999999999999999999999$$$$');
-*ND_APT_CDO/A25 = NDO_APT.ND_ORG;
-*ND_APT_CDD/A25 = NDD_APT.ND_DST;
ND_APT_CDO/A25 = ND_ORG;
ND_APT_CDD/A25 = ND_DST;
ND_CTY_NMO/A25 = NDO_CTY.CITY_NAME;
ND_CTY_NMD/A25 = NDD_CTY.CITY_NAME;


-* ****DEFINES ON PREVIOUS DEFINES****

  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3'
     THEN FARE_PAID ELSE 0;
  ARR_DAY/A3           = DECODE XARR_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  DPT_DAY/A3           = DECODE XDPT_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  GROSS_DSAVE/D12.2CS = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                           DISSAVINGS ELSE 0;
  ND_CP_NM/A50 = EMB_APT|'        '|RTE_APT;

-INCLUDE TRANTYPE
-INCLUDE IDLEVDEFINE 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT ADIR_STP
  AGENT_NUM
  AIR_KEY
  AIR_MAIN.INTL_DOM
  AIRLINE
  AIRLINE_FEE
  AIRLINE_NAME
  AROLL_DSC4
  AR_BRANCH
  ARR_DAY
  ARR_TIME
  AUX_NON_MIR
  BASE_FARE
  BOOK_DT
  BR_CL_IDX
  CARD_NUM
  CARD_NUM1
  CARD1_NUM
  CCARD
  CCARD1
  CCARD2
  CCARD3
  CCARD5
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'
  CL_CAT
  CLASS
  CLASS_CAT
  CLIENT_NAME
  CLIENT_NUM
  COM_AMT
  CRD_CRD
  CREDIT_CARD
  DEPT_MONTH
  DIR_CP_CD
  DISSAVINGS
  DOC_TYPE
  DPT_DAY
  DPT_TIME
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
  EMB_CTY.COUNTRY_CODE
  EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'
  EMBARK
  EMP_ID
  FARE_BAS
  FARE_PAID
  FLIGHT
  GROSS_DSAVE
  GROSS_FARE
  GROUP_CODE
  GSA_GTR
  INT_CODE
  INVOICE_NUM
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  MIR_TYPE
  MRK_TKT_CNT
  MTG_NUM
  ND_CP_NM
  ND_CITY_NM
  ND_APT_CDD
  ND_APT_CDO
  ND_CTY_NMO
  ND_CTY_NMD
  NET_TKT_CNT
  NET_TKT_CNT1
  NEW_ADV_PURCH
  NEW_SEG_COUNT
  NEW_SEG_DISC
  NEW_SEG_MILES
  NOREF_SEG_COUNT
  PASSNGR_NAME
  PNR_LOCATOR
  REF_E_CNT
  REF_EXCH_CNT
  REFEXCH
  REFUSAL_DESC
  REFUSE_CODE
  REFUSE_KEY
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'
  RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'
  ROUTE
  SAVINGS
  SEG_AMT
  SEG_COUNT
  SEG_DESIG
  SEG_DISCOUNT
  SEG_LOWEST
  SEG_MILES
  SEG_NBR
  SEG_STANDARD
  SEG_TAX
  STAY
  TICKET_BRANCH
  TKT_AMOUNT
  TKT_NUM
  TKT_PURCH
  TKT_SORT
  TKT_TYPE
  TOUR_CODE
  TRAN_MONTH
  TRIP_LENGTH
  TRN_DATE
  VAL_AIRLINE
  VOID_DATE
  XARR_DT
  XDPT_DT
  XPSNGR_NM
  XTRAN_DT


-INCLUDE &AIRDATES
-*WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD

END
-RUN

 


-*-EXIT



