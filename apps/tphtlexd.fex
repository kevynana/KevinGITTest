-* 1/30/13 - DEB changed due to expansion of hotel database to
-* include trip purpose
-* CHANGED CODE DUE TO DRILL DOWN NOT WORKING - DEB 11/18/13
-* Added HCOM_CIT flag for Stand-r compliant hotel cities - Joy 2/18/14
-* Removed reason code N from being non-compliant - Joy 2/21/14
-* Removed HCOM_CIT flag for Stand-r want to show all hotels 3/7/13
-* Updated the Compliance flag to validate CVG and PDX and the reason codes and all cities would always be compliant per Andy 3/20/14 - JK
-* Updated Compliance flag to validate specific phone number - JK 4/1/14
-*02/12/16 - DLV - S-13286  Updated to allow reports to sort/subtotal by id-levels
-*06/30/16 - RSB - S-20630 New Employee ID database field
-*                         Added EMP_ID
-*
SET ASNAMES=ON

-INCLUDE SETECHO

FILEDEF RPTPARMS CLEAR
-RUN

EX HTLUSE
-RUN

-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN

DEFINE FILE BRPTINST
  RT/A3 = 'HTL';
END
TABLE FILE BRPTINST
PRINT RT AS 'RPT_GROUP'
BY INST_KEY
ON TABLE SAVE AS AIRACH00
END
-RUN
MODIFY FILE BRPTINST
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 RPT_GROUP/3
MATCH INST_KEY
  ON MATCH UPDATE RPT_GROUP
DATA ON AIRACH00
END
-RUN

MODIFY FILE BRPTFLDS
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 HTL.ID/1 HTL.INCL1/1 
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HTL
  ON NOMATCH INCLUDE
  ON MATCH UPDATE HTL.INCL1
DATA ON AIRACH00
END
-RUN

EX CODEGEN
-RUN

-SET &&IDLEVELBREAK = '';
-RUN
 
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
HBOOK/P8CS = IF TOTAL_AMT GE 0 THEN 1 ELSE (-1);
LEVEL_DESC1/A60      = &&LDESC;
NBR_DAYS/D12 =  NUM_DAYS;
NNROOMS/P12CS = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
XPSNGR_NM/A15 = EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
HTL_DT/MDYY         = CHKIN_DATE;
HTL_AMT/D12.2CS = TOTAL_AMT;

TRN_DATE/YYMD = INVOICE_DATE;
HC_ITIN/A67 = CITY.CITY_NAME;
TRIP_KEY/A2   = EDIT(TRP_KEY, '$$$$$$$$99');
TRIPRSN/A10 = IF TRIP_KEY NE ' ' THEN TRIP_KEY ELSE 'ZZZZZZZZZZ';
TRIP_RSN/A10 = IF TRIPRSN EQ 'ZZZZZZZZZZ' THEN 'NONE GIVEN' ELSE TRIPRSN; 
XHINV_DT/MDYY = INVOICE_DATE;
FLAG/A1 = 'H';
REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
HRSNCDE/A48=REFUSE_CD|| '-' ||REFUSAL_DESC;
HCOM_CIT/A1 = IF PROP_CITY_CD EQ 'PDX' OR 'CVG' THEN 'Y' ELSE 'N';
HPHONE/A1 = IF HOTEL_PHONE EQ '5032414100' OR '5035055000' OR '5134219100' OR '7704325555' OR '5032261611' THEN 'Y' ELSE 'N';
HCOM/A3 = IF HCOM_CIT EQ 'N' THEN 'YES' ELSE
          IF (HPHONE EQ 'Y') THEN 'YES' ELSE 'NO';
-*HCOM/A3 = IF CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'YES' ELSE 'NO';         
-INCLUDE IDLEVDEFINE  

END                
TABLE FILE &&EXTRACT
PRINT AROLL_DSC3 AROLL_DSC4 HBOOK NNROOMS LEVEL_DESC HC_ITIN HTL_DT HTL_AMT PROP_NAME HTL_DT XPSNGR_NM TRN_DATE TKT_NUM 
      INVOICE_NUM PNR_LOCATOR LEVEL4 LEVEL1 TRIP_KEY TRIP_RSN XHINV_DT  TRP_REF.TRP_DESC INVOICE_NUM AS 'HINV_NUM' FLAG HCOM HRSNCDE CHAIN_NAME 
      HCOM_CIT PROP_CITY_CD EMP_ID
BY HTL_KEY 
-INCLUDE &HTLDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHLDH 
END
-RUN

-IF '&&SETF.EVAL' EQ 'SDACHC' GOTO STAND;

JOIN HTL_KEY IN RPTHLDH TO HTL_KEY IN HTLUDID
-RUN
-GOTO XXIT;

-STAND;


TABLE FILE RPTHLDH
PRINT HBOOK NNROOMS HTL_AMT HCOM HRSNCDE PROP_NAME AS 'CHAIN_NAME'
BY LEVEL_DESC
BY AROLL_DSC3
BY AROLL_DSC4
BY FLAG
BY XPSNGR_NM
BY PNR_LOCATOR
BY TKT_NUM
BY HC_ITIN
BY TRIP_KEY
BY TRIP_RSN
BY TRP_DESC
BY LEVEL4
BY LEVEL1
BY XHINV_DT
BY HTL_DT
BY HINV_NUM
-* WHERE HCOM_CIT EQ 'Y'
ON TABLE HOLD AS HTLHLD
END
-RUN

-XXIT;





