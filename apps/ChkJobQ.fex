-**********************************************************************
-* This program is created to check if the job was submitted 2 hours ago
-* and ex_flag was set to M.  If yes, then reset the ex_flag to Y and
-* rerun the job by using RUN_SET.
-* Date: 05/20/2004
-**********************************************************************
-INCLUDE SETECHO
DEFINE FILE RPT_QUE
-*Job scheduled date and time
  DATE/A8YYMD = EDIT(INTIME, '99999999');
  HOUR/A2  = EDIT(INTIME, '$$$$$$$$$$99');
  SCHDATE/YYMD = DATE;
  SCHOUR/I2 = EDIT(HOUR);
 
-*Current date and time
  THISDAY/YYMD = &YYMD;
  THITIME/A8 = HHMMSS(THITIME);
  CHOUR/I2 = EDIT(EDIT(THITIME,'99'));

  DDIFF/I2 = THISDAY - SCHDATE;
  HDIFF/I2 = CHOUR - SCHOUR;

END
TABLE FILE RPT_QUE
-*PRINT INTIME SCHDATE SCHOUR THISDAY THITIME CHOUR DDIFF HDIFF
PRINT INTIME
BY INST_KEY
WHERE (DDIFF GE 1 OR HDIFF GE 2) AND EX_FLAG EQ 'M'
WHERE ROLLUP_CODE NE 'TOTAL-R'
ON TABLE HOLD
END
-RUN
-SET &CNT = &LINES;

MODIFY FILE RPT_QUE
FIXFORM FROM HOLD
COMPUTE EX_FLAG/A1 = 'Y';
MATCH * KEYS SEG 1
  ON MATCH UPDATE EX_FLAG
DATA ON HOLD
END
-RUN

-IF &CNT EQ 0 THEN GOTO ENDIT;
EX RUN_SET
-RUN
-ENDIT
