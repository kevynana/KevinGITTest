-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
-* File INTLDOMG.FEX
-*****************************************************
-*DATE         NAME          DESCRIPTION
-* 1/22/01     TANDT-SS
-* This version selects out seg_design
-* Revised for ISU Goal CityPairs 10/31/00
-* 2/9/01      TANDT-SS      Add ticket_designator selection for additional
-*                           companies and make selection dynamic
-* 5/18/01     TANDT-SS      Add EURO information
-* 5/21/01      TANDT-SS     ADDED DEFINE TO NORECORDS SO MESSAGE
-*                           "NO RECORDS FOUNDS FOR THIS SELECTION"
-*                           WILL APPEAR IN EXCEL 97
-* 7/9/01       TANDT-SS     Add MACTEC-R information
-* 8/24/01      TANDT-SS     Add TOMK-R information
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*1/28/03      DEB           ADDED NORDSN-R INFORMATION
-*11/4/04      JK            CHANGED TO USE MKTSHR1 INTEAD OF &&RPT_HOLD
-*                           CHANGED DUE TO GLOBAL REPORTING
-*4/22/05      DEB           ADDED USE CLEAR COMMAND
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*10/4/16-JEM-S-25410-Corrected error
-*4/25/2017-JEM-S-33318-updated logic for no records report

-*****************************************************
SET ASNAMES=ON
EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS
 
-TYPE &&OUTPUTDEST
-IF &&OUTLINE2 EQ 'OFFLINE' THEN GOTO XOFFLINE ELSE GOTO XONLINE;
 
-XONLINE
-TYPE Before ONLINE
ONLINE
-TYPE After ONLINE
-RUN
-GOTO CONT
 
-XOFFLINE
-SET &&OUTLINE2 = '';
-TYPE Before OFFLINE
OFFLINE
-TYPE After OFFLINE
-RUN
-GOTO CONT
 
-CONT
-SET &&RPT_HOLD = 'RPTHOLD';
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-* RUN CATQTR
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
-INCLUDE SETPARMTIME
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :TA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:TA1
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
EX AIRUSE
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :ID1;
JOIN CLEAR *
JOIN PARM_SEQ IN BRPT_FLDS_PARM_REQUIRED TO ALL PARM_SEQ IN BRPT_FLDS_PARM_VALUES_REQUIRED AS J1
-RUN
TABLE FILE BRPT_FLDS_PARM_REQUIRED
PRINT DATA_VALUE
WHERE DB_FIELD EQ 'AIRLINE'
ON TABLE HOLD AS HLD001 FORMAT ALPHA
END
-RUN
 
-SET &CNT = 0;
-REPEAT TOEND2 &LINES TIMES
-READ HLD001 NOCLOSE  &AIR.50.
-SET &CNT = &CNT + 1;
-SET &AIR1=TRUNCATE(&AIR);
-SET &&AIR.&CNT = '&AIR1.EVAL';
-SET &&ACNT=&CNT;
-TOEND2
-CLOSE HLD001
 
-:S31_CONT
 
-:ID1
 
EX &&EXPARM
-RUN
 
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW=&&EXPARM;
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS
 
-IF &RECORDS GT 0 THEN GOTO DOIT;
 
-NORECORDS
-IF &&FROMAVRP NE 'Y' GOTO :AVSF2;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF2

-INCLUDE NORECRPT
-RUN


-GOTO XXIT
 
-DOIT
 
DEFINE FILE MKTSHR1 ADD
  AIRSELECT/A3 = IF ('&&AIR1' EQ AIR_LINE) THEN AIR_LINE ELSE '0';
  ROLL_COMP/A8 = EDIT(REFUSE_KEY, '99999999');
  LABEL1/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMO ELSE ' ';
 
 LABEL2/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMD ELSE ' ';
END
-RUN
 
-* For goaled pairs
TABLE FILE MKTSHR1
SUM COMM_SEG AS 'TCOMM'
    CP_FARE_PD AS 'FARE'
    MILES
    NET_COMM AS 'NCOMM'
    NET_TKT_CNT AS 'TICK_SEG'
    ROLL_COMP
    TOTAL_FARE AS 'TFARE'
BY AIRSELECT
BY AIRLINE_NAME
BY ORGDST
BY LABEL1
BY LABEL2
WHERE CP_AIRLINE EQ '&&AIR1'
-* dynamic selection for additional companies
-IF &&ROLLUP EQ 'ISU-R' THEN GOTO ISU;
-IF &&ROLLUP EQ 'CH2M-R' THEN GOTO CH2M;
-IF &&ROLLUP EQ 'CONVER-R' THEN GOTO CONVER;
-IF &&ROLLUP EQ 'EURO-R' THEN GOTO EURO;
-IF &&ROLLUP EQ 'MACTEC-R' THEN GOTO MACTEC;
-IF &&ROLLUP EQ 'NORDSN-R' THEN GOTO NORDSN;
-IF &&ROLLUP EQ 'TOMK-R' THEN GOTO TOMK;
-GOTO ENDSEL
-ISU
WHERE SEG_DESIG EQ 'C8506'
-SET &&TEST_SEG='C8506';
-GOTO ENDSEL
-CH2M
WHERE SEG_DESIG EQ 'C9772'
-SET &&TEST_SEG='C9772';
-GOTO ENDSEL
-CONVER
WHERE SEG_DESIG EQ 'C9073'
-SET &&TEST_SEG='C9073';
-GOTO ENDSEL
-EURO
WHERE SEG_DESIG EQ 'C9313'
-SET &&TEST_SEG='C9313';
-GOTO ENDSEL
-MACTEC
WHERE SEG_DESIG EQ 'C6799'
-SET &&TEST_SEG='C6799';
-GOTO ENDSEL
-NORDSN
WHERE SEG_DESIG EQ 'COCTU'
-SET &&TEST_SEG='COCTU';
-GOTO ENDSEL
-TOMK
WHERE SEG_DESIG EQ 'C6444'
-SET &&TEST_SEG='C6444';
-ENDSEL
ON TABLE HOLD AS CPHOLD1
END
-RUN
 
 
 
 
-* For non-goaled pairs
TABLE FILE MKTSHR1
SUM COMM_SEG AS 'TCOMM'
    CP_FARE_PD AS 'FARE'
    MILES
    NET_COMM AS 'NCOMM'
    NET_TKT_CNT AS 'TICK_SEG'
    ROLL_COMP
    TOTAL_FARE AS 'TFARE'
BY AIRSELECT
BY AIRLINE_NAME
BY ORGDST
BY LABEL1
BY LABEL2
WHERE CP_AIRLINE EQ '&&AIR1'
ON TABLE HOLD AS CPHOLD1N
END
-RUN
 
 
-* For goaled pairs
TABLE FILE CPHOLD1
PRINT AIRLINE_NAME
      AIRSELECT
      FARE
      MILES
      NCOMM
      ROLL_COMP
      TCOMM
      TFARE
      TICK_SEG
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD AS CPHOLD2
END
-RUN
 
 
 
 
-* For non-goaled pairs
TABLE FILE CPHOLD1N
PRINT AIRLINE_NAME
      AIRSELECT
      FARE
      MILES
      NCOMM
      ROLL_COMP
      TCOMM
      TFARE
      TICK_SEG
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD AS CPHOLD2N
END
-RUN
 
-* For goaled pairs
TABLE FILE MKTSHR1
SUM COMM_SEG AS 'ACOMM'
    CP_FARE_PD AS 'AFARE'
    NET_COMM AS 'ANET'
    NET_TKT_CNT AS 'ATICK_SEG'
    ROLL_COMP AS 'AROLL_COMP'
    TOTAL_FARE AS 'ATFARE'
BY ORGDST
BY LABEL1
BY LABEL2
WHERE CP_AIRLINE NE '&&AIR1'
-* dynamic selection for additional companies
WHERE SEG_DESIG NE '&&TEST_SEG'
ON TABLE HOLD AS HOLD1
END
-RUN
 
 
 
 
 
-* For non-goaled pairs
TABLE FILE MKTSHR1
SUM COMM_SEG AS 'ACOMM'
    CP_FARE_PD AS 'AFARE'
    NET_COMM AS 'ANET'
    NET_TKT_CNT AS 'ATICK_SEG'
    ROLL_COMP AS 'AROLL_COMP'
    TOTAL_FARE AS 'ATFARE'
BY ORGDST
BY LABEL1
BY LABEL2
WHERE CP_AIRLINE NE '&&AIR1'
ON TABLE HOLD AS HOLD1N
END
-RUN
 
-* For goaled pairs
TABLE FILE HOLD1
SUM AFARE
    ANET
    ACOMM
    ATFARE
    ATICK_SEG
    AROLL_COMP
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD AS HOLD2
END
-RUN
 
 
 
-* For non-goaled pairs
TABLE FILE HOLD1N
SUM AFARE
    ANET ACOMM
    ATFARE
    ATICK_SEG
    AROLL_COMP
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD AS HOLD2N
END
-RUN
 
 
-* For goaled pairs
MATCH FILE CPHOLD2
PRINT AIRLINE_NAME
      AIRSELECT
      FARE
      MILES
      NCOMM
      ROLL_COMP
      TCOMM
      TFARE
      TICK_SEG
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD
RUN
FILE HOLD2
PRINT AFARE
    ANET
    ACOMM
    ATFARE
    ATICK_SEG
    AROLL_COMP
BY ORGDST
BY LABEL1
BY LABEL2
AFTER MATCH HOLD AS HOLD3 OLD-OR-NEW
END
-RUN
 
 
 
 
-* For non-goaled pairs
MATCH FILE CPHOLD2N
PRINT AIRLINE_NAME
      AIRSELECT
      FARE
      MILES
      NCOMM
      ROLL_COMP
      TCOMM
      TFARE
      TICK_SEG
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD
RUN
FILE HOLD2N
PRINT AFARE
    ANET ACOMM
    ATFARE
    ATICK_SEG
    AROLL_COMP
BY ORGDST
BY LABEL1
BY LABEL2
AFTER MATCH HOLD AS HOLD3N OLD-OR-NEW
END
-RUN
 
 
 
-*This will give all goaled citypairs and then rank them
DEFINE FILE G_CITYP
ORIGDEST/A7=ORIG | '-' | DEST;
END
-RUN
 
TABLE FILE G_CITYP
PRINT *
BY ORIGDEST
BY ORIG
BY DEST
ON TABLE HOLD AS GCPHOLD
END
-RUN
 
 
 
 
-* For goaled pairs
-TYPE JOIN3 GOALED
JOIN
GCPHOLD.ORIGDEST IN GCPHOLD TO ALL HOLD3.ORGDST IN HOLD3 AS MYJOIN
END
-RUN
 
 
 
 
 
-* For goaled pairs
TABLE FILE GCPHOLD
PRINT *
BY ORGDST
BY LABEL1
BY LABEL2
-* dynamic selection for additional companies
WHERE (ROLLUP_CD EQ '&&ROLLUP')
ON TABLE HOLD AS HOLD3G
END
-RUN
 
 
 
 
-* For goaled pairs
DEFINE FILE HOLD3G
  BASE_FARE/D12.2CS = AFARE + FARE;
  FARE_PAID/D12.2CS = ATFARE + TFARE;
  NET_FARE/D12.2CS = ANET + NCOMM;
  NET_MKTS/D8CS = ATICK_SEG + TICK_SEG;
  TOTAL_COMM/D12.2 = ACOMM + TCOMM;
  EMBARK_APT/A25 = IF '&&CTYPR' EQ 'ALL' THEN LABEL1 ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE LABEL1;
  ROUTE_APT/A25 = IF '&&CTYPR' EQ 'ALL' THEN LABEL2 ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE LABEL2;
  ODTEST/A50=IF '&&CTYPR' EQ 'ALL' THEN ORGDST ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE ORGDST;
  SUBTOT/A1=IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN '1' ELSE ' ';
  GOAL/A10 = 'GOALED';
END
-RUN
 
-* For goaled pairs
TABLE FILE HOLD3G
PRINT
  ACOMM
  AFARE
  ANET
  ATFARE
  ATICK_SEG
  BASE_FARE
  FARE
  FARE_PAID
  GOAL
  MILES
  NCOMM
  NET_FARE
  NET_MKTS
  TCOMM
  TFARE
  TICK_SEG
  TOTAL_COMM
RANKED BY &&RANK_ORDER &&RANK_METH
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
WHERE EMBARK_APT NE ' '
ON TABLE HOLD AS HOLD4G
END
-RUN
 
 
 
 
-* For goaled pairs
DEFINE FILE HOLD4G
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = LAST LIST + 1;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A20 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN
                   'ALL OTHER NOT IN MKT' ELSE
                   IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                   IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER IN MKT'
                   ELSE CRANK;
  RANK_LABEL/A50 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   ODTEST ELSE ' ';
  EMB_LABEL/A25 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   EMBARK_APT ELSE ' ';
  RTE_LABEL/A25 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   ROUTE_APT ELSE ' ';
END
-RUN
 
-* For goaled pairs
TABLE FILE HOLD4G
PRINT
  ACOMM
  AFARE
  ANET
  ARANK
  ATFARE
  ATICK_SEG
  BASE_FARE
  CRANK
  DRANK
  EMB_LABEL
  FARE
  FARE_PAID
  GOAL
  MILES
  NCOMM
  NET_FARE
  NET_MKTS
  RANK_LABEL
  RANK_VALUE
  RTE_LABEL
  TCOMM
  TFARE
  TICK_SEG
  TOTAL_COMM
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
ON TABLE HOLD AS HOLD5G FORMAT FOCUS
END
-RUN
 
 
 
 
 
-* This will select all others not goaled and then rank them
JOIN CLEAR MYJOIN
-RUN
 
MATCH FILE HOLD3N
PRINT *
BY ORGDST
ON TABLE HOLD
RUN
FILE HOLD3G
PRINT *
BY ORGDST
AFTER MATCH HOLD AS HOLD3NG OLD-NOT-NEW
END
-RUN
 
 
 
 
 
DEFINE FILE HOLD3NG
  BASE_FARE/D12.2CS = AFARE + FARE;
  FARE_PAID/D12.2CS = ATFARE + TFARE;
  NET_FARE/D12.2CS = ANET + NCOMM;
  NET_MKTS/D8CS = ATICK_SEG + TICK_SEG;
  TOTAL_COMM/D12.2 = ACOMM + TCOMM;
  EMBARK_APT/A25 = IF '&&CTYPR' EQ 'ALL' THEN LABEL1 ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE LABEL1;
  ROUTE_APT/A25 = IF '&&CTYPR' EQ 'ALL' THEN LABEL2 ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE LABEL2;
  ODTEST/A50=IF '&&CTYPR' EQ 'ALL' THEN ORGDST ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE ORGDST;
  SUBTOT/A1=IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN '1' ELSE ' ';
  GOAL/A10 = 'NON-GOALED';
END
-RUN
 
TABLE FILE HOLD3NG
PRINT
  ACOMM
  AFARE
  ANET
  ATFARE
  ATICK_SEG
  BASE_FARE
  FARE
  FARE_PAID
  GOAL
  MILES
  NCOMM
  NET_FARE
  NET_MKTS
  TCOMM
  TFARE
  TICK_SEG
  TOTAL_COMM
RANKED BY &&RANK_ORDER &&RANK_METH
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
ON TABLE HOLD AS HOLD4NG
END
-RUN
 
 
 
 
 
DEFINE FILE HOLD4NG
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = LAST LIST + 1;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A20 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN
                   'ALL OTHER NOT IN MKT' ELSE
                   IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                   IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER IN MKT'
                   ELSE CRANK;
  RANK_LABEL/A50 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   ODTEST ELSE ' ';
  EMB_LABEL/A25 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   EMBARK_APT ELSE ' ';
  RTE_LABEL/A25 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   ROUTE_APT ELSE ' ';
END
-RUN
 
TABLE FILE HOLD4NG
PRINT
  ACOMM
  AFARE
  ANET
  ARANK
  ATFARE
  ATICK_SEG
  BASE_FARE
  CRANK
  DRANK
  EMB_LABEL
  FARE
  FARE_PAID
  GOAL
  MILES
  NCOMM
  NET_FARE
  NET_MKTS
  RANK_LABEL
  RANK_VALUE
  RTE_LABEL
  TCOMM
  TFARE
  TICK_SEG
  TOTAL_COMM
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
ON TABLE HOLD AS HOLD5NG FORMAT FOCUS
END
-RUN
 
-* Combine goal and non-goal files
-SET &HLD_PATH = TEMPPATH(50,'A50');
-SET &HG = &HLD_PATH || HOLD5G.FOC;
-SET &HNG = &HLD_PATH || HOLD5NG.FOC;
-RUN
 
USE CLEAR *
-RUN
 
 
 
 
USE ADD
&HG AS HOLD5G
&HNG AS HOLD5G
END
-RUN
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
 
 
DEFINE FILE HOLD5G
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH SUBTOT = HHMMSS (NOWTOD);
END
TABLE FILE HOLD5G
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
 
ON TABLE SET STYLE *
 
 
-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW='INTLDOMG';
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS
 
-XXIT
