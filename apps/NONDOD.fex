-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 12/28/04 - DEB - ADDED ADDT'L DOD_TYPE TO INT_FLAG1 DEFINE
-*  2/25/05 - DEB - ADDED CODE FOR GLOBAL CURRENCY
-* 10/11/05 - DEB - ADDED DOD_TYPE 'H' TO  INT_FLAG1 DEFINE
-*1/30/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE

SET ASNAMES=ON

DEFINE FILE GNONHLD
CY/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
           TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
INT_FLAG1/A15 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN ' ' ELSE 'TRADITIONAL';
INT_FLAG/A15 = LCWORD(15, INT_FLAG1, INT_FLAG);
BITMAP/A12 WITH NET_TKT_CNT = 'DODTYP.SVG';
MONTH/Mtr = TRN_DATE;
FLAG/A1 = 'A';
CT_TKT/D12 = IF CY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PT_TKT/D12 = IF PY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
END
-RUN

TABLE FILE GNONHLD
SUM NET_TKT_CNT
    NAMEC
BY FLAG  
BY MONTH
BY INT_FLAG
WHERE CY EQ 'Y'
WHERE INT_FLAG NE ' '
ON TABLE HOLD AS OVINT1 
END
-RUN

TABLE FILE GNONHLD
SUM CT_TKT PT_TKT
BY FLAG
BY MONTH
ON TABLE HOLD AS TTKTH
END
-RUN



TABLE FILE GNONHLD
SUM NET_TKT_CNT AS 'NET_TKT_CNT1'
    NAMEC
BY FLAG    
BY MONTH
BY INT_FLAG
WHERE PY EQ 'Y'
WHERE INT_FLAG NE ' '
ON TABLE HOLD AS POVINT1
END
-RUN 

MATCH FILE OVINT1
PRINT NET_TKT_CNT
      NAMEC
      FLAG
BY MONTH      
BY INT_FLAG
ON TABLE HOLD
RUN
FILE POVINT1
SUM NET_TKT_CNT1
    NAMEC
    FLAG
BY MONTH    
BY INT_FLAG
AFTER MATCH HOLD AS TOVINT1 OLD-OR-NEW
END
-RUN

TABLE FILE TOVINT1
PRINT NET_TKT_CNT NET_TKT_CNT1 NAMEC INT_FLAG
BY FLAG
BY MONTH
ON TABLE HOLD AS TOVINT2
END
-RUN

MATCH FILE TOVINT2
PRINT NET_TKT_CNT NET_TKT_CNT1 NAMEC INT_FLAG
BY FLAG
BY MONTH
ON TABLE HOLD
RUN
FILE TTKTH
SUM CT_TKT PT_TKT
BY FLAG
BY MONTH
AFTER MATCH HOLD AS TOVINT3 OLD-AND-NEW
END
-RUN

TABLE FILE TOVINT3
SUM NET_TKT_CNT NET_TKT_CNT1 CT_TKT PT_TKT
BY FLAG
BY MONTH
BY INT_FLAG
ON TABLE HOLD AS TOVINT3A
END
-RUN



-SET &RECFOUND = &RECORDS;

-IF &RECFOUND EQ 0 GOTO NOGRAPH;

GRAPH FILE TOVINT3A
SUM COMPUTE CPCT/D8 = IF ((CT_TKT LT 1) AND (CT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT/CT_TKT); AS 'Period 1'
    COMPUTE PPCT/D8 = IF ((PT_TKT LT 1) AND (PT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT1/PT_TKT); AS 'Period 2'
BY FLAG NOPRINT
BY MONTH AS ' '
ON GRAPH SET LOOKGRAPH VLINE
ON GRAPH SET GRAPHEDIT SERVER
ON GRAPH SET BARNUMB OFF
ON GRAPH SET GRID ON
ON GRAPH SET VAXIS 140
ON GRAPH SET HAXIS 390
ON GRAPH SET UNITS POINTS
ON GRAPH SET GRMERGE ON
ON GRAPH SAVE AS DODTYP FORMAT SVG
ON TABLE SET GRAPHSTYLE *
setFillColor(getFrame(), new Color(239,239,239));
setO1AxisSide(0);
setO1MajorGridDisplay(true);
setO1MajorGridStyle(0);
setO1MinorGridDisplay(false);
setAxisAssignment(0,0);
setUseSeriesShapes(true);

setO1LabelDisplay(true);
setO1LabelRotate(3);
setFontSizeAbsolute(getO1Label(),true);
setAutofit(getO1Label(),false); 
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),8);
setFontName(getO1Label(),"Arial");
setFillColor(getO1Label(),new Color(5,76,149));
setTextJustHoriz(getO1Label(),2);



setY1MustIncludeZero(true);
setY1LabelDisplay(true);
setTextFormatPreset(getY1Label(),2);
setTextJustHoriz(getY1Label(),0);
setFontName(getY1Label(),"Times New Roman");
setFontSizeAbsolute(getY1Label(),true);
setFillColor(getY1Label(),new Color(5,76,149));
setFontSize(getY1Label(),9);
setFontStyle(getY1Label(),1);
setY1MajorGridDisplay(false);

setTitleString(" ");
setTextJustHoriz(getSubtitle(), 1);
setSubtitleString("Traditional Bookings %");
setFontSizeAbsolute(getSubtitle(),true);
setAutofit(getSubtitle(),false);
setFontSize(getSubtitle(),10);
setFontStyle(getSubtitle(),0);
setFontName(getSubtitle(),"Times New Roman");
 

setTextJustHoriz(getLegendText(),1);
setFontSizeAbsolute(getLegendText(),true);
setFontSize(getLegendText(),9);
setFontStyle(getLegendText(),0);
setFontName(getLegendText(),"Times New Roman");
setTextRotation(getLegendText(),0);
setTextWrap(getLegendText(),false);
setFillColor(getLegendText(),new Color(5,76,149));
setLegendDisplay(true);
setLegendPosition(2);
 
setSeriesFillColor(0, new Color, 0 0 138));
setSeriesFillColor(1, new Color, 115 162 247));
ENDSTYLE
END
-RUN

-IF &&OUTFMT NE 'PDF' GOTO NOGRAPH ELSE GOTO GRAPH;
-GRAPH
-SET &&D_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';
-GOTO HLDF
-NOGRAPH
-SET &&D_GIF='N';

-HLDF


TABLE FILE TOVINT3
SUM NET_TKT_CNT
    NET_TKT_CNT1
    NAMEC
COMPUTE CPCT/D16 = IF ((CT_TKT LT 1) AND (CT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT/CT_TKT)*100;
COMPUTE PPCT/D16 = IF ((PT_TKT LT 1) AND (PT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT1/PT_TKT)*100;
BY INT_FLAG
ON TABLE HOLD AS TOVINT4
END
-RUN

DEFINE FILE TOVINT4
BITMAP/A12 WITH INT_FLAG = 'DODTYP.SVG';
LABEL/A30 = 'Booking Type';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 4;
END
-RUN

TABLE FILE TOVINT4
PRINT CATEGORY/A61 AND INT_FLAG/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND NET_TKT_CNT/D16 AS 'VOL_CURR' AND  
CPCT/D16 AS 'TKT_CURR' AND NET_TKT_CNT1 AS 'VOL_PRIOR' AND
PPCT/D16 AS 'TKT_PRIOR'
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1; 
WHERE INT_FLAG EQ 'Traditional'   
ON TABLE HOLD AS OVINT2A
END
-RUN

TABLE FILE OVINT2A
PRINT CAT_ORDER ROW_ORDER VOL_CURR AND TKT_CURR AND VOL_PRIOR AND TKT_PRIOR
BY CATEGORY
BY ROW_LABEL
ON TABLE HOLD AS OVINT3A
END
-RUN

MODIFY FILE OV_OFF
LOG DUPL MSG OFF
FIXFORM FROM OVINT3A
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVINT3A
END
-RUN











