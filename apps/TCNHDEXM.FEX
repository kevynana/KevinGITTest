-*10/16/17 - D-01665 - DLV - Created new extract to obtain hotel attachment rate for TCVASAVEM

-INCLUDE SETECHO
SET ASNAMES = ON

-TYPE '&&ROLL.EVAL'

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;

  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0; 
NEW_STAY/P12 = STAY;
TRNMY/MTY = TRN_DATE;  
  
ROLLCD/A8 = '&&ROLL';
-* Defines on Previous
END

TABLE FILE &&EXTRACT
PRINT EMBARK
HOTEL_FLAG
NET_TKT_CNT
NEW_STAY
PASSNGR_NAME
TRN_DATE
TRNMY
BY ROLLCD
BY TKT_NUM
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCANHD
END
-RUN

TABLE FILE TCANHD
SUM NEW_STAY        NOPRINT
BY ROLLCD   NOPRINT 
BY PASSNGR_NAME NOPRINT
BY TKT_NUM
BY TRN_DATE     NOPRINT
WHERE HOTEL_FLAG NE ' ';
WHERE EMBARK OMITS 'DT1';
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS TKT_HOLDT
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
DEFINE FILE TCANHD
 HTL_ATTCH  /P9 = IF (NET_TKT_CNT GT 0) AND (HOTEL_FLAG EQ 'Y') THEN 1 ELSE 0;
 HTL_NOATTCH/P9 = IF (NET_TKT_CNT GT 0) AND (HOTEL_FLAG EQ 'N') THEN 1 ELSE 0;
END
-*
TABLE FILE TCANHD
PRINT NET_TKT_CNT
      HOTEL_FLAG
      NEW_STAY
      HTL_ATTCH
      HTL_NOATTCH
      TRNMY
BY ROLLCD
BY PASSNGR_NAME
BY TKT_NUM
BY TRN_DATE
WHERE TKT_NUM IN FILE TKT_HOLDT;
ON TABLE HOLD AS TCANHD1M
END
-RUN

TABLE FILE TCANHD1M
SUM HTL_ATTCH
    HTL_NOATTCH
COMPUTE TOT_ATTCH/P9 = HTL_ATTCH + HTL_NOATTCH;
BY ROLLCD
BY TRNMY
BY PASSNGR_NAME
BY TKT_NUM
BY TRN_DATE
ON TABLE HOLD AS TCNHDM2
END
-RUN
 
TABLE FILE TCNHDM2
SUM HTL_ATTCH
    HTL_NOATTCH
    TOT_ATTCH
BY TRNMY
BY ROLLCD
ON TABLE HOLD AS TCNHDM3
END
-RUN



MODIFY FILE TC_NHDM
FIXFORM FROM TCNHDM3
MATCH TRNMY ROLLCD 
ON NOMATCH INCLUDE
ON MATCH UPDATE HTL_ATTCH HTL_NOATTCH TOT_ATTCH
DATA ON TCNHDM3
END
-RUN



-NEXTONE
