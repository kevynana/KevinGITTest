-* JK added code to select only tnt_company eq 1 or 2 2/21/05
-*4/5/17-JEM-S-32830 Updated ORGDST field to have a - 
-*09/26/2017 - DLV - S-37268 - UPDATED defines for ND_CP_CD  

-INCLUDE SETECHO
SET ASNAMES = ON
-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
DEMB_APT/A21=EDIT(EMB_APT.AIRPORT_NAME,'999999999999999999999$$$$');
DIR_CITY_CD/A50 = EMB_CTY.EMB_CITY || '-' || RTE_CTY.RTE_CITY;
DIR_CITY_NM/A50 = EMB_CTY.CITY_NAME | RTE_CTY.CITY_NAME; 
DIR_CP_CD/A50 = CITYPAIR.EMBARK || '-' || CITYPAIR.ROUTE;
DIR_CP_NM/A50 = EMB_APT.AIRPORT_NAME | RTE_APT.AIRPORT_NAME;
DPT/A2 = EDIT (DPT_TIME, '99$$');
DRTE_APT/A21=EDIT(RTE_APT.AIRPORT_NAME,'999999999999999999999$$$$');
EMB_APT/A21=EDIT(NDO_APT.AIRPORT_NAME,'999999999999999999999$$$$');
COMM_SEG/D12.2S   = SEG_COM;
FARE_PAID/D12.2S  = SEG_AMT;
LEVEL_DESC/A60 = 'AROLL_DSC2';
NET_COMM/D12.2S   = (SEG_AMT - SEG_COM);
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
ND_CITY_CD/A50 = NDO_CTY.NDO_CITY || '-' || NDD_CTY.NDD_CITY;
ND_CITY_NM/A50 = NDO_CTY.CITY_NAME | NDD_CTY.CITY_NAME;
-*ND_CP_CD/A50  = NDO_APT.ND_ORG || '-' || NDD_APT.ND_DST;
ND_CP_CD/A50  = ND_ORG || '-' || ND_DST;

RTE_APT/A21=EDIT(NDD_APT.AIRPORT_NAME,'999999999999999999999$$$$');
-* Defines on Previous Defines
ND_CP_NM/A55 = EMB_APT|'        '|RTE_APT;
-* ND_APT_CDO/A3 = NDO_APT.ND_ORG;
-* ND_APT_CDD/A3 = NDD_APT.ND_DST;
-* DEFINES ON PREVIOUS DEFINES
-* ORGDST/A7 = ND_APT_CDO|| '-' ||ND_APT_CDD;
CTY_CHK/A1 = IF ND_CP_CD EQ 'JFK-LAX' OR 'JFK-SFO' OR 'ORD-SFO' OR 'ATL-SFO' OR
                                  'LAX-SFO' OR 'PDX-SFO' OR 'SFO-SEA' OR 'JFK-LHR' OR 
                                  'CDG-JFK' OR 'LHR-SFO' OR 'SFO-TPE' OR 'HKG-SFO' THEN 'Y' ELSE 'N';
-* ORGDST/A55 = &&ORGDST;
ORGDST/A55=NDO_APT.AIRPORT_NAME||'-'||NDD_APT.AIRPORT_NAME;
END

TABLEF FILE &&EXTRACT
PRINT 
  CITYPAIR.AIRLINE AS 'AIR_LINE'
  CITYPAIR.INTL_DOM AS 'INTL_DOM'	
  CITYPAIR.NET_COMM AS 'NET_COMM'	
  CITYPAIR.SEG_MILES/D9C AS 'MILES'	
  COMM_SEG	
  FARE_PAID  AS 'CP_FARE_PD'
  LEVEL_DESC
  NET_FARE	
  NET_TKT_CNT
  ORGDST
  ROLLUP_CODE	
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
   
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS

ON TABLE HOLD AS TC_HOLD1 
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACT DATA FROM STEP4 - HOLD TO FOCUS DATABASE TCARNK        
-*========================================================================
TABLE FILE TC_HOLD1
SUM COMM_SEG	
    CP_FARE_PD AS 'FARE'
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	
BY ORGDST
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
PRINT COMM_SEG	
    FARE
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	
RANKED BY HIGHEST &&RANK_METH   
BY ORGDST
ON TABLE HOLD AS HOLD2
END
-RUN

DEFINE FILE HOLD2
  RANK_LIMIT/I5 = 10;
  DRANK/D5      = RANK ;
  LIST/D6 = IF ORGDST NE LAST ORGDST THEN (LAST LIST + 1) ELSE LAST LIST;
  ARANKX/A6     = FTOA(LIST, '(D5)', 'A6');
  ARANK/A5      = EDIT(ARANKX,'$99999');
  RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN ARANK ELSE
                  IF LIST GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN

TABLE FILE HOLD2
PRINT COMM_SEG	
    FARE
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	  
BY RANK_VALUE
BY ORGDST
ON TABLE HOLD AS HOLD3
END
-RUN


DEFINE FILE HOLD3
ORGDST_PRINT/A55 = IF RANK_VALUE LE '10' THEN ORGDST ELSE 'OTHER';
END
-RUN

TABLE FILE HOLD3
SUM COMM_SEG	
    FARE
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	          
BY RANK_VALUE
BY ORGDST
BY ORGDST_PRINT
ON TABLE HOLD AS HOLD4
END
-RUN


TABLE FILE HOLD4
SUM COMM_SEG	
    FARE
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	
BY RANK_VALUE
BY ORGDST_PRINT
ON TABLE HOLD AS HOLD5
END
-RUN

TABLE FILE HOLD4
SUM MILES AS 'C_MILES'
    NET_COMM AS 'C_COMM'
    FARE AS 'C_FARE' 
    NET_TKT_CNT AS 'C_SEGMT'
BY ORGDST_PRINT  
ON TABLE HOLD AS HOLD9
END
-RUN

MATCH FILE HOLD5
PRINT COMM_SEG	
    FARE
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	
    RANK_VALUE
BY ORGDST_PRINT 
ON TABLE HOLD
RUN
FILE HOLD9
SUM C_COMM
    C_FARE
    C_MILES
    C_SEGMT
BY ORGDST_PRINT
AFTER MATCH HOLD AS HOLD10 OLD-OR-NEW
END
-RUN

-TYPE BEFORE DEFINES

DEFINE FILE HOLD10 
  PER_FARE/D8.1=    IF ((C_FARE LT 1) AND (C_FARE GT (-1))) THEN 0
                    ELSE ((FARE/C_FARE)*100);
  SEGPER/D8.1 =     IF ((C_SEGMT LT 1) AND (C_SEGMT GT (-1))) THEN 0
                    ELSE ((NET_TKT_CNT/C_SEGMT)*100);
  PER_NFARE/D8.1=   IF ((C_COMM LT 1) AND (C_COMM GT (-1))) THEN 0
                    ELSE ((NET_COMM/C_COMM)*100);
  MILE_PCT/D8.1=    IF ((C_MILES LT 1) AND (C_MILES GT (-1))) THEN 0
                    ELSE ((MILES/C_MILES)*100);  
-* NOWTOD/A8 WITH ORGDST_PRINT = HHMMSS (NOWTOD);
  ROLLCD/A8 = '&&ROLL' ;
  AVG_FLT/P12.2 = IF (NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))
                 THEN 0 ELSE (FARE/NET_TKT_CNT);
  COST_PER_MILE/P12.2= IF (MILES LT 1) AND (MILES GT (-1))
                 THEN 0 ELSE (FARE/MILES);
END

TABLE FILE HOLD10
SUM NET_TKT_CNT
    SEGPER
    FARE
    NET_COMM
    PER_FARE
    MILES
    AVG_FLT
    COST_PER_MILE
BY ROLLCD
BY RANK_VALUE
BY ORGDST_PRINT
ON TABLE HOLD AS TCR1
END
-RUN

MODIFY FILE TCARNK
FIXFORM FROM TCR1
MATCH ROLLCD
  ON NOMATCH INCLUDE
MATCH RANK_VALUE
  ON NOMATCH INCLUDE
DATA ON TCR1
END
-RUN

-NEXTONE
