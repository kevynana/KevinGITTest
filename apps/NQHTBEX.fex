-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the new quarterly review - JK 8/2/05
-******************************************************************************

-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-SET &&HTL_LMT=3;
-SET &&CTY_LMT=25;
-RUN

DEFINE FILE &&EXTRACT 
  PROP_CITY1/A25 = LCWORD(25, PROP_CITY, PROP_CITY1);
  CITY_ST/A36 = PROP_CITY1||(' '| PROP_STATE);
  INV_DATE/MDYY = INVOICE_DATE;
  NEW_ROOM_RATE/D12.2C = ROOM_RATE;
  NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;
  NBR_DAYS/D12 =  NUM_DAYS; 
  NNROOMS/D12 = IF (NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)
                THEN ((NBR_DAYS*NUM_ROOMS)*(-1))
                ELSE NBR_DAYS*NUM_ROOMS;
  ROLL/A12 = IF ROLLUP_CODE EQ '&&BASEROLL.EVAL' THEN 'Base Company' 
             ELSE 'Peer Group';
  
-* ***DEFINES ON PREVIOUS DEFINED DEFINES***
 BOOKINGS/D12 = IF NEW_TOTAL_AMT GE 0 THEN 1 ELSE (-1);
 HTL_CNT/D12 = IF PROP_NAME NE LAST PROP_NAME THEN 1 ELSE 0;
-INCLUDE QRFTRANC
END


TABLEF FILE &&EXTRACT
PRINT BOOKINGS
      CHAIN_CODE
      CITY_ST
      CURRENT
      PYTD
      PRIOR
      HTL_CNT
      INVOICE_DATE
      NEW_TOTAL_AMT
      NNROOMS
      ROLL
      TRAN
      YEAR
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') OR
      (INVOICE_DATE GE '&&FXDATE.EVAL') AND (INVOICE_DATE LE '&&TXDATE.EVAL')
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTLB1
END
-RUN

-***************************************************************************************
-* Hotel City Ranking
-***************************************************************************************
TABLE FILE HTLB1
SUM BOOKINGS
    NEW_TOTAL_AMT
    NNROOMS
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') 
BY ROLL
BY CITY_ST
BY CHAIN_CODE
ON TABLE HOLD AS HOLD1
END
-RUN

-SET &&NOHTLSV = IF &LINES EQ 0 THEN 'Y' ELSE 'N';
-IF &LINES EQ 0 THEN GOTO NoCurHtl;

TABLE FILE HOLD1
SUM CNT.CHAIN_CODE AS 'HTL_CNT'
BY ROLL
BY CITY_ST
BY CHAIN_CODE
ON TABLE HOLD AS HTLCNT
END
-RUN

MATCH FILE HOLD1
PRINT BOOKINGS
      NEW_TOTAL_AMT
      NNROOMS
BY ROLL
BY CITY_ST
BY CHAIN_CODE
ON TABLE HOLD
RUN
FILE HTLCNT
SUM HTL_CNT
BY ROLL
BY CITY_ST
BY CHAIN_CODE
AFTER MATCH HOLD AS HTLCNT1 OLD-AND-NEW
END
-RUN

TABLE FILE HTLCNT1
PRINT BOOKINGS
    CHAIN_CODE
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
BY ROLL
BY CITY_ST
RANKED BY HIGHEST NNROOMS NOPRINT
ON TABLE HOLD AS HOLD2
END
-RUN

DEFINE FILE HOLD2
 RANK_LIMIT/I5 = &&HTL_LMT;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN

TABLE FILE HOLD2
PRINT BOOKINGS
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
BY ROLL
BY CITY_ST
BY RANK_VALUE
ON TABLE HOLD AS HOLD3
END
-RUN


TABLE FILE HOLD3
SUM BOOKINGS
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
BY ROLL
BY CITY_ST
BY RANK_VALUE
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE HOLD1
SUM BOOKINGS
    NEW_TOTAL_AMT
    NNROOMS
BY ROLL
BY CITY_ST
ON TABLE HOLD AS RHOLD1
END
-RUN

TABLE FILE RHOLD1
PRINT *
RANKED BY HIGHEST NNROOMS AS 'DAYS_RANK'
ON TABLE HOLD AS RHOLD2
END
-RUN

MATCH FILE HOLD4
PRINT *
BY CITY_ST
ON TABLE HOLD
RUN
FILE RHOLD2
SUM DAYS_RANK
BY CITY_ST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN

TABLE FILE HOLD5
PRINT *
RANKED BY HIGHEST DAYS_RANK
ON TABLE HOLD AS HOLD6
END
-RUN

DEFINE FILE HOLD6
  CTY_LIMIT/I2 = &&CTY_LMT;
  CLIMIT/A2    = EDIT(CTY_LIMIT) ;
  CRANK/D5     = RANK;    
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF CITY_ST NE LAST CITY_ST THEN (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6=EDIT(FRANKX,'$999999');
  CTY_VALUE/A9 = IF CTY_LIMIT EQ 0 THEN BRANK ELSE
                 IF LIST GT CTY_LIMIT THEN 'All Other' ELSE FRANK;  
  CTY_PRINT/A36 = IF CTY_VALUE NE 'All Other' THEN CITY_ST ELSE 'All Other';
  RANK_VAL/A1 =  IF CTY_VALUE EQ 'All Other' AND RANK_VALUE EQ 'OTHER' THEN ' ' ELSE EDIT(RANK_VALUE, '$$$$9');
END
-RUN

TABLE FILE HOLD6
SUM BOOKINGS
  HTL_CNT
  NEW_TOTAL_AMT
  NNROOMS
BY ROLL	
BY CTY_VALUE
BY CTY_PRINT
BY RANK_VAL
ON TABLE HOLD AS HOLD7
END
-RUN


DEFINE FILE HOLD7
HNNROOMS/D12 = IF RANK_VAL NE 'R' THEN NNROOMS ELSE 0;
END

TABLE FILE HOLD7
SUM BOOKINGS
    HNNROOMS
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
BY ROLL
BY CTY_VALUE
BY CTY_PRINT
ON TABLE HOLD AS HOLD8
END
-RUN    

TABLE FILE HOLD8
PRINT ROLL CTY_VALUE CTY_PRINT BOOKINGS HNNROOMS HTL_CNT NEW_TOTAL_AMT NNROOMS
ON TABLE SAVE
END
-RUN

MODIFY FILE QRHCTY
FIXFORM ROLL/12 CTY_VALUE/9 CTY_PRINT/36 BOOKINGS/12 HNNROOMS/12 HTL_CNT/5 NEW_TOTAL_AMT/12 NNROOMS/12
MATCH ROLL CTY_VALUE CTY_PRINT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


TABLE FILE HTLB1
SUM NNROOMS NEW_TOTAL_AMT CURRENT PYTD PRIOR
BY ROLL
BY YEAR
BY TRAN
ON TABLE HOLD AS BSE1
END
-RUN

TABLE FILE BSE1
PRINT ROLL YEAR TRAN NNROOMS NEW_TOTAL_AMT CURRENT PYTD PRIOR
ON TABLE SAVE
END
-RUN

MODIFY FILE QRHBCTY
FIXFORM ROLL/12 YEAR/4 TRAN/7 NNROOMS/12 NEW_TOTAL_AMT/12 CURRENT/1 PYTD/1 PRIOR/1
MATCH ROLL TRAN YEAR
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


-NoCurHtl

