-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/26/04    JOY                Added/Changed Norecords logic
-*5/23/14    DEB                ADDED EX5 TO BE EXCLUDED WHEN EX1 IS EXCLUDED
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.
-* 02/24/2017 - DLV - S-22486 - Uncommented out INCLUDE FOOTERDT
 
-INCLUDE SETECHO
-* File DRTRAN.FEX
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*----           ----                 ---------------------
-*8/31/00        SAC                   Initial Creation
-*10/16/00        IBISTL-KR            ACCEPT DRILL DOWN PARMS
-*                                     AND USE ORIGINAL SETFILE
-*09/10/01       SAC         Changed EXCHGS, EXCH_AMT, EXCH_MCO defines to
-*                           match summary
-*02/15/02       SAC         Added additional WHERE DOD_TYP selections below
-*12/28/04       DEB         ADDED DOD_TYPE TO INTERNET_FLAG DEFINE
-*10/11/05       DEB         ADDED DOD_TYPE 'H' TO INTERNET_FLAG DEFINE
-*1/24/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*  09/20/2016  REJ  S-24432 Changes for ER5 testing.
 
-*****************************************************************
-SET &&FIELD = &TRANTYPE;
-SET &&FROMDT = &FRMDT;
-SET &&TODT = &TODT;
-SET &&WRRUN = 'Y';
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 11/03/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
-*
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-**********************************************************************
-CONT
 
-**********************************************************************
-* USE ORIGINAL SETFILE
-**********************************************************************
-*EX TSSETDB SETFILE=DRTRNSF
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-RUN
-SET &&SETF='TEMPDRIL';
EX SETDBLS SETFILE=&&SETF
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DRD1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:DRD1
 
-INCLUDE GetForDrillDown
-RUN
 
-* EX WRBATCH
-* -RUN
-* EX LOADBINS
-* -RUN
-*EX CODEGEN
-*-RUN
-*-INCLUDE OTHPARMS
 
-* EX GETFPARM
-* -RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DDTA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-:DDTA1
-RUN
 
-SET &&RPTTYP = ' ';
-INCLUDE SELCATQT
-RUN
 
EX CATQTR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
EX AIRUSE
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-IF &&FIELD EQ '   Mcos' THEN GOTO MCOS
-  ELSE IF &&FIELD EQ '   Tour Orders' THEN GOTO TOUR
-  ELSE IF &&FIELD EQ '   Handwrites' THEN GOTO HAND
-  ELSE IF &&FIELD EQ '   Ptas' THEN GOTO PTAS
-  ELSE IF &&FIELD EQ '   Paper/Etkt' THEN GOTO OTHR
-  ELSE IF &&FIELD EQ '   Ticketless' THEN GOTO TKTL
-  ELSE IF &&FIELD EQ '   Exchg Sales' THEN GOTO EXCH
-  ELSE IF &&FIELD EQ '   Exchg Mcos' THEN GOTO EMCO
-  ELSE IF &&FIELD EQ '   Full Refunds' THEN GOTO FRFD
-  ELSE IF &&FIELD EQ '   Partial Rfnd' THEN GOTO PRFD
-  ELSE IF &&FIELD EQ 'VOIDS' THEN GOTO VOID
-  ELSE GOTO OUTSIDE;
 
-MCOS
-SET &&FIELD = 'WHERE MCO_CNT NE 0';
-SET &&FIELD1 = '';
-SET &&FIELD2 = '';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT1';
-SET &&RPT_TTL1 = '                   MCO TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-TOUR
-SET &&FIELD = 'WHERE DOC_TYPE EQ ''2''';
-SET &&FIELD1 = 'WHERE EXCHG_SALE EQ '' ''';
-SET &&FIELD2 = 'WHERE EMBARK NE ''EX1'' OR ''RF1'' OR ''EX5''';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT';
-SET &&RPT_TTL1 = '               TOUR ORDER TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-HAND
-SET &&FIELD = 'WHERE DOC_TYPE EQ ''3''';
-SET &&FIELD1 = 'WHERE EXCHG_SALE EQ '' ''';
-SET &&FIELD2 = 'WHERE EMBARK NE ''EX1'' OR ''RF1'' OR ''EX5''';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT';
-SET &&RPT_TTL1 = '                HANDWRITE TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-PTAS
-SET &&FIELD = 'WHERE DOC_TYPE EQ ''4''';
-SET &&FIELD1 = 'WHERE EXCHG_SALE EQ '' ''';
-SET &&FIELD2 = 'WHERE EMBARK NE ''EX1'' OR ''RF1'' OR ''EX5''';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT';
-SET &&RPT_TTL1 = '                  PTA TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-OTHR
-SET &&FIELD = 'WHERE DOC_TYPE EQ ''5'' OR ''6'' OR ''7'' OR ''8''';
-SET &&FIELD1 = 'WHERE EXCHG_SALE EQ '' ''';
-SET &&FIELD2 = 'WHERE EMBARK NE ''EX1'' OR ''RF1'' OR ''EX5''';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT';
-SET &&RPT_TTL1 = '           PAPER/ETICKET TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-TKTL
-SET &&FIELD = 'WHERE DOC_TYPE EQ ''9''';
-SET &&FIELD1 = 'WHERE EXCHG_SALE EQ '' ''';
-SET &&FIELD2 = 'WHERE EMBARK NE ''EX1'' OR ''RF1'' OR ''EX5''';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT';
-SET &&RPT_TTL1 = '              TICKETLESS TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-EXCH
-SET &&FIELD = 'WHERE EXCHGS NE 0';
-SET &&FIELD1 = '';
-SET &&FIELD2 = '';
-SET &&COUNT = 'NET_TKT_CNT1';
-SET &&FARE = 'FARE_AMT1';
-SET &&RPT_TTL1 = '             EXCHANGE SALE TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-EMCO
-SET &&FIELD = 'WHERE EXCH_MCO NE 0';
-SET &&FIELD1 = '';
-SET &&FIELD2 = '';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT1';
-SET &&RPT_TTL1 = '             EXCHANGE MCO TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-FRFD
-SET &&FIELD = 'WHERE RF_FLAG EQ ''F''';
-SET &&FIELD1 = '';
-SET &&FIELD2 = '';
-SET &&COUNT = 'FRF_CNT';
-SET &&FARE = 'FARE_AMT1';
-SET &&RPT_TTL1 = '              FULL REFUND TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-PRFD
-SET &&FIELD = 'WHERE RF_FLAG EQ ''P''';
-SET &&FIELD1 = '';
-SET &&FIELD2 = '';
-SET &&COUNT = 'PRF_CNT';
-SET &&FARE = 'FARE_AMT1';
-SET &&RPT_TTL1 = '             PARTIAL REFUND TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-VOID
-SET &&FIELD = 'WHERE VOID_DATE NE 0';
-SET &&FIELD1 = '';
-SET &&FIELD2 = '';
-SET &&COUNT = 'NET_TKT_CNT';
-SET &&FARE = 'FARE_AMT1';
-SET &&RPT_TTL1 = '                  VOID TRANSACTION DETAIL';
-GOTO OUTSIDE
 
-OUTSIDE
 
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
 
DEFINE FILE SR_50
  INTERNET_FLAG/A21 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'INTERNET BOOKINGS' ELSE
  'NON-INTERNET BOOKINGS';
  NEW_SALES/D12.2CS = IF (SEG_AMT GT 0) AND (SEG_COUNT GE 0) THEN
  SEG_AMT ELSE 0;
  NEW_TAX/D12.2CS = IF (SEG_TAX GT 0) AND (SEG_COUNT GE 0) THEN
  SEG_TAX ELSE 0;
  FARE_PAID/D12.2CS = NEW_SALES + NEW_TAX;
  FARE_PAY/D12.2CS = SEG_AMT + SEG_TAX;
 
  FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND
  (TKT_NUM NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
 
  NET_TKT_CNT/P8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND
  (SEG_COUNT EQ 1) THEN 1 ELSE IF DOC_TYPE EQ '1' THEN 1 ELSE 0;
 
  TKT_CNT/P8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND
  (SEG_COUNT EQ 1) THEN 1 ELSE 0;
 
  EXCH_AMT/D12 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE NE ' ') THEN 0 ELSE
  IF ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND
  (EMBARK NE 'DT1' OR 'RF1' OR 'FP3')) THEN FARE_PAY ELSE
  IF ((SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') AND (EMBARK NE 'RF1'))
  THEN FARE_PAY ELSE 0;
 
  EXCHGS/D9 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE NE ' ') THEN 0 ELSE
  IF ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND
  (EMBARK NE 'DT1' OR 'RF1' OR 'FP3')) THEN 1 ELSE
  IF ((SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ')) AND (EMBARK NE 'RF1')
  THEN 1 ELSE 0;
 
  EXCH_MCO/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') THEN 1 ELSE 0;
 
  MCO_CNT/D9 = IF (EX_FLAG EQ ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE
  NE ' ') THEN 1 ELSE 0;
 
  PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND
  (TKT_NUM NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
  TKT_TYPE/A4         = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  MONTH/MT = TRN_DATE;

END
-RUN
 
TABLEF FILE SR_50
PRINT
  AIRLINE
  DOD_TYPE
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  EXCHG_SALE
  FARE_PAID
  FARE_PAY
  FULL_RFNDS
  INTERNET_FLAG
  NET_TKT_CNT
  PART_RFNDS
  PASSNGR_NAME
  REFUSE_CODE
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  TKT_CNT
  TKT_NUM
  TKT_TYPE
  TRN_DATE
  VAL_AIRLINE
  DPT_TIME
  XDPT_DT
  XTRAN_DT
  MONTH
  TKT_SORT
  SEG_NBR
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
 
-INCLUDE &AIRDATES
 
&&FIELD
&&FIELD1
&&FIELD2
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
-TYPE After Extract
-IF &RECORDS EQ 0 GOTO NORECORDS;
-RUN
 
-TYPE Before Report
TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT'
FARE_PAID AS 'TFARE'
FARE_PAY AS 'TFP'
TKT_CNT AS 'TEX'
FULL_RFNDS AS 'TRFD'
PART_RFNDS AS 'TPRFD'
-************************IBISTL KR*******************
-*TOT_TRANS AS 'TTRNS'
-****************************************************
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN
 
TABLE FILE RPTHOLD
PRINT AIRLINE
      EMB_APT_NM
      FARE_PAID
      FARE_PAY
      INTERNET_FLAG
      REFUSE_CODE
      RTE_APT_NM
      TKT_NUM
      TKT_TYPE
      DPT_TIME
      XDPT_DT
      XTRAN_DT
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN
 
MATCH FILE XTWO
PRINT AIRLINE
      EMB_APT_NM
      FARE_PAID
      FARE_PAY
      INTERNET_FLAG
      REFUSE_CODE
      RTE_APT_NM
      TKT_NUM
      TKT_TYPE
      DPT_TIME
      XDPT_DT
      XTRAN_DT
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TFARE
      TTKT
      TFP
      TEX
      TPRFD
      TRFD
-**************************IBISTL KR******************
-*      TTRNS
-*****************************************************
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN
 
TABLE FILE AIRHOLD2
PRINT AIRLINE
      EMB_APT_NM
      FARE_PAID
      FARE_PAY
      INTERNET_FLAG
      PASSNGR_NAME
      REFUSE_CODE
      RTE_APT_NM
      TEX
      TFARE
      TFP
      TKT_NUM
      TKT_TYPE
      TPRFD
      TRFD
      TRN_DATE
      TTKT
-*************************IBISTL KR*****************
-*      TTRNS
-***************************************************
      XDPT_DT
      DPT_TIME
      XTRAN_DT
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
-SET &&DTSTY='DT';
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
DEFINE FILE AIRHOLD3
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN PASSNGR_NAME ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;
 
-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
NET_TKT_CNT/P9S = IF SRT_DT NE LAST SRT_DT THEN TTKT ELSE 0;
NET_TKT_CNT1/P9S = IF SRT_DT NE LAST SRT_DT THEN TEX ELSE 0;
FRF_CNT/P9S = IF SRT_DT NE LAST SRT_DT THEN TRFD ELSE 0;
PRF_CNT/P9S = IF SRT_DT NE LAST SRT_DT THEN TPRFD ELSE 0;
FARE_AMT/P12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
FARE_AMT1/P12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFP ELSE 0;
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END
-RUN
 
 
TABLE FILE AIRHOLD3
-INCLUDE HEADAIR
"&&SUBHEAD"
"</1"
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'REASON, CODE'
XDPT_DT AS 'DEPART, DATE' DPT_TIME AS 'TIME'
EMB_APT_NM AS 'ORG'  RTE_APT_NM AS 'DST'
AIRLINE AS 'CR'
-* FARE_AMT AS 'FARE,PAID'
&&FARE AS 'FARE,PAID'
&&COUNT AS 'TRAN,CNT'
-* NET_TKT_CNT AS 'TRAN,CNT'
 
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
 
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY TICKET_NMBR AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'
 
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
ON XTKT_SRT SKIP-LINE
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
-INCLUDE FOOTERDT
-*
 
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
 
-XXIT
