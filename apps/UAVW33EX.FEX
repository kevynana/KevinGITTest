-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File AIREXTRT.FEX
-********************************************************************
-* MODIFICAIONS:
-* 5/22/1998 ADDED TRN_DATE CRITERIA TO WHERE STATEMENTS - LP
-* 7/09/1998 ADDED A DEFINE FOR TICKETLESS MIR TRANSACTIONS - LP
-*11/16/2001 ADDED DEFINE FOR KIMBERLY CLARK LEVEL1      -DB
-* 4/29/2002 CHANGED MRK_TKT_CNT DEFINE TO CORRECTLY BACK OUT PARTIAL
-*           REFUNDS AND EXCHANGES - SC DB
-* 6/12/03   ADDED UPCOST DEFINE - DV
-* 12/30/04  ADDED AROLL_LEV4 - DEB
-* 2/21/08 - CREATED FROM AVIW33 FOR ConAgra Air compliance file - DV
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492

-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
  BASE_FARE/D12.2CS = SEG_AMT - SEG_COM;
  BOOK_DT/MDYY         = RESV_DATE;
  CG_CLT/A10 = IF BR_CL_IDX EQ 'RF892430' THEN 'APPLICANT' ELSE
              IF BR_CL_IDX EQ 'RF891788' THEN 'CONTRACTOR' ELSE 'EMPLOYEE';

-*   CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'   
-*                               'D' 'DISCOUNT'  'B' 'BUSINESS');

CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE' 
                                ' ' 'DEFAULT'); 

  COM_AMT/D12.2CS = SEG_COM;
  CRD_CRD/A4=EDIT(CREDIT_CARD, '$$9999$$$$$$$$$$$');
  DEPT_MONTH/MT        = DPT_DATE;
  DIR_CP_CD/A50        = CITYPAIR.EMBARK||'-'||CITYPAIR.ROUTE;
  
-****************************************************************************
-* 10/3/00  IBISTL-RJ    FIELD WAS BEING TRUNCATED IN FOCUS 6, NEED TO RUN A 
-*                       SUBROUTINE IN WEBFOCUS TO TRUNCATE.
  DIR_CP_NMX/A72 = '(' || CITYPAIR.EMBARK || '/' 
           || CITYPAIR.ROUTE || ') ' | 
           EMB_APT.AIRPORT_NAME ||'---' || RTE_APT.AIRPORT_NAME;
  DIR_CP_NM/A50 = SUBSTR(72,DIR_CP_NMX,1,50,50,'A50');
  DOD_TYP2/A1 = IF DOD_TYPE EQ ' ' OR 'K' THEN 'T' ELSE 
                IF DOD_TYPE EQ 'J' THEN 'I' ELSE DOD_TYPE;
  EMAIL_ADR/A50 = 
  IF EMAIL_ADDR EQ '                                                  ' THEN
                   'NONE GIVEN' ELSE EMAIL_ADDR;               
-*
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  GROSS_FARE/D12.2CS  = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
  HIGH_CLS/A1 = IF ADIR_ALL CONTAINS '*F' THEN 'F' ELSE
			           IF ADIR_ALL CONTAINS '*B' THEN 'B' ELSE
			           IF ADIR_ALL CONTAINS '*C' THEN 'C' ELSE
			           IF ADIR_ALL CONTAINS '*D' THEN 'D' ELSE ' '; 
  HIGH_CAT/A8 = DECODE HIGH_CLS ('F' 'FIRST'     'C' 'COACH'   
                              'D' 'DISCOUNT'  'B' 'BUSINESS'    ' ' 'OTHER');
  LEVEL_DESC/A60      = &&LDESC;
  MIR_TYPE/A24 = DECODE AUX_NON_MIR ('X' 'X-DUAL MIR'
                                    'N' 'N-NON-TICKETING MIR'
                                    'J' 'J-NON-FARING MIR'
                                    'A' 'A-NON-ACCOUNTING MIR'
                                    'B' 'B-ACCOUNTING MIR'
                                    'H' 'H-REGULAR TICKET MIR'
                                    'V' 'V-VOID'
                                    'E' 'E-ELECTRONIC TKT NETWORK'
                                    'L' 'L-ELECTRONIC TKT MIR'
                                    ' ' ' -MANUALLY ENTERED TRANS');
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  ND_CITY_NM/A50= NDO_CTY.CITY_NAME | NDD_CTY.CITY_NAME;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_COUNT/D8CS = SEG_COUNT;
  NEW_SEG_DISC/D12.2CS = SEG_DISC;
  NEW_SEG_MILES/D9= SEG_MILES;
  NOREF_SEG_COUNT/D8CS = IF (RF_FLAG EQ ' ') AND
                            (EX_FLAG EQ ' ') THEN
                           NEW_SEG_COUNT ELSE 0;
  REF_E_CNT/D8CS      = IF (RF_FLAG NE ' ') OR
                           (EX_FLAG NE ' ') THEN
                            1 ELSE 0;  
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  REFEXCH/D12.2CS     = IF (RF_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 
                        IF (EX_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;                      
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_CNT/D9CS        = IF (SEG_NBR EQ '01') AND 
                           (FARE_PAID GE 0) AND 
                           (SEG_COUNT EQ 1) THEN 1 ELSE 0;   
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE
				IF (VOID_DATE NE 0) THEN 'VOID' ELSE ' ';
  XARR_DOW/W           = ARR_DATE;
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XFST_DPT/MDYY = FST_DPT_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
  
-* ****DEFINES ON PREVIOUS DEFINES****

  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' 
     THEN FARE_PAID ELSE 0;
  ARR_DAY/A3           = DECODE XARR_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  DPT_DAY/A3           = DECODE XDPT_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  GROSS_DSAVE/D12.2CS = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                           DISSAVINGS ELSE 0; 
  EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND 
                 (SEG_COUNT LE 0) AND 
                 (EMBARK NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE 
              IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE 
              IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;
 DECLINE_AMT/D12.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         TKT_AMOUNT - LOWEST_AMT;
 USVISA/A5 = IF CRD_CRD EQ '4246' THEN 'TRUE' ELSE 'FALSE';  
-INCLUDE TRANTYPE 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  AGENT_NUM	
  AIR_KEY	
  AIR_MAIN.INTL_DOM	
  AIRLINE	
  AIRLINE_FEE	
  AIRLINE_NAME	
  AR_BRANCH
  ARR_DAY	
  ARR_TIME
  AROLL_LEV1
  AROLL_LEV2
  AROLL_LEV3
  AROLL_LEV4
  AROLL_LEV5
  AROLL_LEV6
  AROLL_LEV7
  AROLL_LEV8
  AROLL_LEV9
  AROLL_LEV10
  AROLL_LEV11
  AROLL_LEV12
  AROLL_LEV13
  AROLL_LEV14
  AROLL_DSC5
  AUX_NON_MIR	
  BASE_FARE	
  BOOK_DT	
  BR_CL_IDX
  C_ITIN
  CG_CLT
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
  CL_CAT	
  CLASS	
  CLASS_CAT	
  CLIENT_NAME
  CLIENT_NUM	
  COM_AMT	
  CRD_CRD	
  CREDIT_CARD
  DECLINE_AMT	
  DEPT_MONTH	
  DIR_CP_CD	
  DIR_CP_NM	
  DISSAVINGS	
  DOC_TYPE
  DOD_TYPE
  DOD_TYP2
  DPT_DAY	
  DPT_TIME	
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
  EMB_CTY.COUNTRY_CODE	
  EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'
  EMAIL_ADR
  EXCHGS
  EX_FLAG
  FARE_BAS	
  FARE_PAID	
  FLIGHT	
  GROSS_DSAVE	
  GROSS_FARE
  HIGH_CLS
  HIGH_CAT
  INT_CODE	
  INVOICE_NUM
  LEVEL_DESC	
  LEVEL1	
  LEVEL2	
  LEVEL3	
  MIR_TYPE	
  MRK_TKT_CNT	
  ND_CITY_NM	
  NET_TKT_CNT	
  NEW_ADV_PURCH	
  NEW_SEG_COUNT	
  NEW_SEG_DISC	
  NEW_SEG_MILES	
  NOREF_SEG_COUNT	
  PASSNGR_NAME	
  PNR_LOCATOR	
  REF_E_CNT	
  REF_EXCH_CNT	
  REFEXCH	
  REFUSAL_DESC	
  REFUSE_CODE	
  REFUSE_KEY 
  RF_FLAG
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
  RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'	
  SAVINGS	
  SEG_AMT	
  SEG_COUNT	
  SEG_DESIG	
  SEG_DISCOUNT	
  SEG_LOWEST	
  SEG_MILES	
  SEG_NBR	
  SEG_STANDARD	
  SEG_TAX	
  STAY	
  TKT_AMOUNT	
  TKT_NUM	
  TKT_PURCH	
  TKT_SORT	
  TKT_TYPE	
  TRIP_LENGTH	
  TRN_DATE
  USVISA
  VAL_AIRLINE
  VOID_DATE
  XARR_DT	
  XDPT_DT
  XFST_DPT
  XPSNGR_NM	
  XTRAN_DT	


-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD

END



-*-EXIT


