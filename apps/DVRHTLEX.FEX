-************************************************************************************
-* File DVRHTLEX.FEX
-* CREATED FOR DATA VAULT EXPENSE REPORT 

-* This program extracts data for the series of basic hotel location listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-* 9/11/14 - DEB - ADDED TASF_TKT_NUM 
-* 9/15/15 - DV - S-11541 - CORRECTED ISSUE TO CORRECT DV_NUM FOR SVC_FEE
-* 7/11/16 -DLV -  S-20630   Added EMP_ID ASSOC_CONF_NUM to extract
-*   Changed Case1 to AND instead of OR - to work correctly if fee only selection
-*   was made as well as statement prior to GOTO JoinHFee

-* 4 cases to be considered: 
-*  1) Htl, fee both selected(Y Y); 2) Neither Htl nor Fee is selected (N N).
-*  3) Htl selected, Fee not (Y N);     4) Htl not, Fee Yes            (N Y)
-************************************************************************************
-INCLUDE SETECHO 
 
-DEFAULT &GETHTLEXP = 'Y';
-DEFAULT &GETPROFEXP = 'Y';

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-*Case 1: Hotel No, Fee No
-*-IF ('&GETHTLEXP.EVAL' EQ 'N' AND '&HTLPROF.EVAL' EQ 'N') OR ('&GETHTLEXP.EVAL' EQ 'N' AND '&GETPROFEXP.EVAL' EQ 'N') 
-IF ('&GETHTLEXP.EVAL' EQ 'N' AND '&HTLPROF.EVAL' EQ 'N') AND ('&GETHTLEXP.EVAL' EQ 'N' AND '&GETPROFEXP.EVAL' EQ 'N') 
- THEN GOTO HtlXXIT;

-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT
  
  BLANK1/A11 = '           ';
  DATA_V1/A9 = '*********';
  DV_NUM2/A6 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$999999');
  DV_NUM/A15 = IF DV_NUM2 EQ ' ' THEN ' ' ELSE DATA_V || DV_NUM2;
  LEVEL_DESC/A60 = &&LDESC;
  SEG/A2= '  ';

  IN_DATE/MDYY = CHKIN_DATE;
  INV_DATE/MDYY = INVOICE_DATE;
  NBR_DAYS/D12 =  NUM_DAYS; 
  NNROOMS/D12 = IF (NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)
                THEN ((NBR_DAYS*NUM_ROOMS)*(-1))
                ELSE NBR_DAYS*NUM_ROOMS;
  NEW_TOTAL_AMT/D12.2CS = TOTAL_AMT;
  OUT_DATE/MDYY = CHKOUT_DATE;
  PRP_CITY/A25 = PROP_CITY;
  PRP_ST/A25 = PROP_STATE_CD;
  TRIP_PURPOSE/A2 = ' ';
  TRP_DESC/A40 = '                                        ';
  TYPE/A3 = 'SAL';
  CHN_CODE/A3 = CHAIN_CODE;
  STATUS/A1 = 'O';
  HTL_CNF/A30 = HHL_CONFIRM;
  TASF_TKT_NUM/A10 = '          '; 
  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT BR_CL_IDX AS 'ACCT'
  TYPE
  BLANK1 AS 'TSORT'
  DV_NUM
  PRP_CITY AS 'CITY1'
  HTL_KEY
  IN_DATE AS 'DATE'
  INV_DATE AS 'TRAN_DATE'
  INVOICE_DATE
  INVOICE_NUM
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3 
  LEVEL4
  LEVEL5
  LEVEL6
  LEVEL7
  EMP_ID
  NEW_TOTAL_AMT AS 'AMOUNT'
  NNROOMS AS 'TTL_COUNT'
  OUT_DATE AS 'DATE2'
  SEG
  PASSNGR_NAME
  PNR_LOCATOR
  CHN_CODE AS 'NAME'
  STATUS
  HTL_CNF AS 'TKT_NUM'
  TRIP_PURPOSE
  TRP_DESC
  CITY.GEO_ZONE AS 'GEO_ZONE'
  TASF_TKT_NUM
-INCLUDE &HTLDATES 

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS HTLHOLD

END
-RUN



-IF '&&ROLLUP.EVAL' NE 'SURGIC-R' THEN GOTO SkipHtlTrip;

EX GetHtlTripDesc
-RUN



-SkipHtlTrip

-*-IF '&HTLPROF.EVAL' EQ 'Y' AND '&GETPROFEXP.EVAL' EQ 'Y' THEN GOTO JoinHFee;
-IF '&HTLPROF.EVAL' EQ 'Y' OR '&GETPROFEXP.EVAL' EQ 'Y' THEN GOTO JoinHFee;

-*Case 2: Hotel Yes, Fee No.
-NoHJoin
DEFINE FILE HTLHOLD
GEO_ZONE/A3 = IF GEO_ZONE EQ ' ' THEN 'USA' ELSE GEO_ZONE;
FLAG/A1 = 'H';
FEE_AMT/D9.2 = 0;
ASSOC_CONF_NUM/A30= ' ';
END
-SET &HFILENAME = 'HTLHOLD';
-GOTO ToHoldHtl;
   
-JoinHFee  
-INCLUDE  DVRHTLUSE
-RUN


TABLE FILE PFHOLDH
PRINT TRN_TYPE TRN_DATE FEE_SRC FEE_AMT PAY_METHOD CREDIT_CARD FST_DPT_DATE
BY HTL_KEY
WHERE HTL_KEY NE ' '
ON TABLE HOLD AS HTLFEE
END
-RUN
 

DEFINE FILE HTLFEE
  HKEY/A71='''' | HTL_KEY | '''';
END

TABLE FILE HTLFEE
PRINT HKEY 
BY HTL_KEY NOPRINT
ON TABLE SAVE AS SAVEH
END
-RUN

DEFINE FILE HR_50

TKT_NUM/A30 = HHL_CONFIRM;
NAME/A3 = CHAIN_CODE;
CITY1/A25 = PROP_CITY;

DATA_V/A9 = '*********';
DV_NUM2/A6 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$999999');
DV_NUM/A15 = IF DV_NUM2 EQ ' ' THEN ' ' ELSE DATA_V || DV_NUM2;


IN_DATE/MDYY = CHKIN_DATE;
OUT_DATE/MDYY = CHKOUT_DATE;
LEVEL_DESC/A60 = &&LDESC;
GEO_ZONE/A3 = IF GEO_ZONE EQ ' ' THEN 'USA' ELSE GEO_ZONE;
TRIP_PURPOSE/A2 = ' ';
TRP_DESC/A40 = '                                        ';  
TASF_TKT_NUM/A10 = '          '; 
END

TABLE FILE HR_50
PRINT 
  PASSNGR_NAME
  PNR_LOCATOR
  INVOICE_DATE
  TKT_NUM

  NAME
  IN_DATE 
  OUT_DATE
  CITY1
-*  DV_NUM
  BR_CL_IDX 
  INVOICE_NUM
  
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  LEVEL5
  LEVEL6
  LEVEL7
  EMP_ID
  GEO_ZONE
  TRIP_PURPOSE
  TRP_DESC
  TASF_TKT_NUM
BY HTL_KEY
-INCLUDE RPTPARMS
WHERE HTL_KEY IN FILE SAVEH
ON TABLE HOLD AS 'HR50SUB' FORMAT FOCUS INDEX HTL_KEY
END
-RUN

JOIN HTL_KEY IN PFHOLDH TO HTL_KEY IN HR50SUB
DEFINE FILE PFHOLDH
TRAN_DATE/MDYY = INVOICE_DATE;

TSORT/A11 = '           ';
TYPE/A3 = 'SAL';
SEG/A2= '  ';
STATUS/A1 = 'O';
AMOUNT/D12.2 = FEE_AMT;
DATA_V/A9 = '*********';
DV_NUM2/A6 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$999999');
DV_NUM/A15 = IF DV_NUM2 EQ ' ' THEN ' ' ELSE DATA_V || DV_NUM2;
ASSOC_CONF_NUM/A30= ' ';

END

TABLE FILE PFHOLDH
PRINT 
  PASSNGR_NAME
  PNR_LOCATOR
  TRAN_DATE
  TKT_NUM
  TSORT 
  TYPE
  EMP_ID
  SEG    
  NAME
  IN_DATE AS 'DATE'
  OUT_DATE AS 'DATE2'
  CITY1
  DV_NUM
  AMOUNT
  CNT AS 'TTL_COUNT'
  BR_CL_IDX AS 'ACCT'  
  INVOICE_NUM
  STATUS
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  LEVEL5
  LEVEL6
  LEVEL7
  GEO_ZONE
  TRIP_PURPOSE
  TRP_DESC
  TASF_TKT_NUM
  ASSOC_CONF_NUM 
BY FLAG
WHERE PASSNGR_NAME NE ' '
ON TABLE HOLD AS HOLDHP FORMAT FOCUS
END
-RUN

-*Case 3: Hotel No, Fee Yes.
-IF '&GETHTLEXP.EVAL' EQ 'Y' THEN GOTO Hcase4;
-SET &HFILENAME = 'HOLDHP';
-GOTO ToHoldHtl;

-Hcase4
-*Case 4: Hotel Yes, Fee Yes. Merger Hotel and Fee together
JOIN CLEAR *
DEFINE FILE HTLHOLD
GEO_ZONE/A3 = IF GEO_ZONE EQ ' ' THEN 'USA' ELSE GEO_ZONE;
FLAG/A1 = 'H';
ASSOC_CONF_NUM/A30= ' ';
END

TABLE FILE HTLHOLD
PRINT 
PASSNGR_NAME
PNR_LOCATOR
TRAN_DATE
TKT_NUM
TSORT
TYPE
EMP_ID
SEG      
NAME
DATE
DATE2
CITY1
DV_NUM
AMOUNT
TTL_COUNT
ACCT
INVOICE_NUM
STATUS
LEVEL_DESC
LEVEL1
LEVEL2
LEVEL3
LEVEL4
LEVEL5
LEVEL6
LEVEL7
GEO_ZONE
TRIP_PURPOSE
TRP_DESC
TASF_TKT_NUM
ASSOC_CONF_NUM 
BY FLAG
ON TABLE HOLD
END
-RUN

MODIFY FILE HOLDHP
FIXFORM FROM HOLD
DATA ON HOLD
END
-RUN
-SET &HFILENAME = 'HOLDHP';
-RUN

-ToHoldHtl
TABLE FILE &HFILENAME
PRINT 
  PASSNGR_NAME
  PNR_LOCATOR
  TRAN_DATE
  TKT_NUM
  TSORT 
  TYPE
  EMP_ID
  SEG    
  NAME
  DATE
  DATE2
  CITY1
  DV_NUM
  AMOUNT
  TTL_COUNT
  ACCT
  INVOICE_NUM
  STATUS
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  LEVEL5
  LEVEL6
  LEVEL7
  GEO_ZONE
  TRIP_PURPOSE
  TRP_DESC
  TASF_TKT_NUM
  ASSOC_CONF_NUM 
BY FLAG
ON TABLE HOLD AS HOLDH FORMAT FOCUS
END
-RUN


-HtlXXIT

 
  
 

