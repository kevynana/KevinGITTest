-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-* File DRCLC.FEX
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*----           ----                 ---------------------
-*8/31/00        SAC                   Initial Creation
-*****************************************************************  

-*************************************************************************
-*DATE         NAME             DESCRIPTION
-*09/27/00    IBISTL-SB         ADDED SET STATEMENTS TO GET THE VALUE
-*                              INTO THIS DRILLED FEX
-*10/15/00    IBISTL-SB         ADDED THE LOGIC FOR LOCATING THE SET FILE
-*                              IN THE WEBPATH AND THEN EXECUTING THE SET
-*                              FILE
-*10/15/00    IBISTL-SB         REMOVED THE ';' FROM THE SET ASNAMES 
-*                              STATEMENT
-* 4/30/01    TANDT-SS          ADDED CODE TO CALCULATIONS TO KEEP NEGATIVE
-*                              NUMBER IN PLACE FOR BACK-OUT OF CANCELLATIONS
-*                              AND DUPLICATES
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*11/25/03   STEVE              Added -INCLUDE RPTPARMS
-*7/26/04    JOY                Added/Changed Norecords logic
-* 07/18/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox
-*************************************************************************

-DEFAULT &&CPFLAG = 'XX';
-DEFAULT &&CATEGORY = 'XX';

-SET &&CPFLAG = &CPFLAG;
-SET &&CATEGORY = &CATGRY;

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
-*
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT


-*EX TSSETDB SETFILE=DRHTLS
-* EX SETDBLS SETFILE=&&SETF
-*RUN

-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX TSSETDB SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-* CP (CURRENT MONTH), PP (PRIOR MONTH), CY (CURRENT YEAR), PY (PRIOR YEAR)
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****

-IF &&CPFLAG EQ 'C1' THEN GOTO CURMONTH
-  ELSE IF &&CPFLAG EQ 'P1' THEN GOTO PRRMONTH
-  ELSE IF &&CPFLAG EQ 'C2' THEN GOTO CURYEAR
-  ELSE IF &&CPFLAG EQ 'P2' THEN GOTO PRRYEAR
-  ELSE GOTO CURMONTH; 

-CURMONTH
-SET &&DCHK='WHERE INV_DT GE '&&CMFMDT.EVAL' AND INV_DT LE '&&CMTODT.EVAL'';
-SET &&FROMDT = &&CMFMDT;
-SET &&TODT = &&CMTODT;
-GOTO ENDDATE

-PRRMONTH
-SET &&DCHK='WHERE INV_DT GE '&&PMFMDT.EVAL' AND INV_DT LE '&&PMTODT.EVAL'';
-SET &&FROMDT = &&PMFMDT;
-SET &&TODT = &&PMTODT;
-GOTO ENDDATE

-CURYEAR
-SET &&DCHK='WHERE INV_DT GE '&&CYFMDT.EVAL' AND INV_DT LE '&&CYTODT.EVAL'';
-SET &&FROMDT = &&CYFMDT;
-SET &&TODT = &&CYTODT;
-GOTO ENDDATE

-PRRYEAR
-SET &&DCHK='WHERE INV_DT GE '&&PYFMDT.EVAL' AND INV_DT LE '&&PYTODT.EVAL'';
-SET &&FROMDT = &&PYFMDT;
-SET &&TODT = &&PYTODT;
-GOTO ENDDATE

-ENDDATE
-SET &&RPT_TTL1 = IF &&CATEGORY EQ 'OTHER'
- THEN '                 OTHER HOTEL BOOKED DETAIL '
- ELSE '                  CLC HOTEL BOOKED DETAIL  ';


FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN

-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN

-************************IBISTL*****************
-*EX TSAIRUSE
-******************************
EX HTLUSE
-RUN

-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
CITY_ST/A35 = PROP_CITY||(', '| PROP_STATE);
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
NBR_DAYS/P12 = NUM_DAYS;
NBR_ROOMS/P12 = NUM_ROOMS;
NEW_ROOM_RATE/P12.2C = ROOM_RATE;
NEW_TOTAL_AMT/P12.2C = TOTAL_AMT;
NNROOMS/P12 = IF NBR_DAYS LT 0 AND NUM_ROOMS LT 0 THEN
 ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
OUT_DATE/MDYY = CHKOUT_DATE;
REASON_CD/A5 = IF HTL_REF.REFUSE_KEY EQ 'UPTTL-R A' THEN
                'CLC' ELSE 'OTHER';
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  CITY_ST     
  HTL_KEY     
  IN_DATE 	
  INV_DT      
  INVOICE_DATE 	
  NBR_DAYS    
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT       
  NNROOMS	
  OUT_DATE    
  PASSNGR_NAME 	
  PROP_NAME   
  REFUSE_KEY
   
    
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

&&DCHK
-INCLUDE RPTPARMS
WHERE REASON_CD EQ '&&CATEGORY.EVAL'
ON TABLE HOLD AS RPTHOLD

END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

-TYPE After Extract
DEFINE FILE RPTHOLD
  NOWTOD/A8 WITH NBR_DAYS = HHMMSS (NOWTOD);
END
-RUN
-TYPE Before Report

TABLE FILE RPTHOLD
-INCLUDE HEADAIR
"</2"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM 
NNROOMS AS 'NUMBER,NIGHTS'
NBR_ROOMS AS 'NUMBER,ROOMS'
NEW_TOTAL_AMT AS 'TOTAL,AMOUNT'
COMPUTE AVGDR/D12.2CS = IF NEW_TOTAL_AMT LT 0 AND NNROOMS LT 0
 THEN ((NEW_TOTAL_AMT/NNROOMS) * (-1)) 
 ELSE NEW_TOTAL_AMT/NNROOMS; AS 'AVERAGE,DAILY,RATE'

-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY PROP_NAME AS 'HOTEL'
BY CITY_ST AS 'CITY/STATE'
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME RECOMPUTE AS 'TOTAL FOR'
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
"&&FOOTER"
"eTTek Review &DATE AT <NOWTOD <90 &&FOOTR"
-* "&&CRIGHT"

-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2


END
-RUN
-*
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END



-XXIT

