-****************************************************************************************
-*DVRVendors.fex - GET airline codes from AirHold3 for Air Detail (DVRAIRDT.FEX)
-*                 GET car vendor codes from Carhold3 (DVRCARHOLD3.FEX)
-*                 GET hotel chain code from HTLHLD   (HVIW22EX.FEX)
-*DATE:  11/26/2008
-*03/09/17 - DLV - S-22486 - Updated IMAGE define to use server report is being ran from
-****************************************************************************************
-INCLUDE SETECHO
SET ASNAME = ON

-SET &HOLDFILE = '&&WEB_PATH.EVAL' || 'DVENDORS.ftm';
-*-SET &HOLDFILE = 'APCODE.ftm';
-DOS DEL &HOLDFILE
FILEDEF &HOLDFILE CLEAR
FILEDEF DVENDORS DISK &HOLDFILE (APPEND
-RUN

TABLE FILE AIRHOLD3
PRINT AIRLINE/A3  AS 'CODE'
ON TABLE HOLD AS DVENDORS
END
TABLE FILE CARHOLD3
PRINT VENDOR_CODE/A3 AS 'CODE'
ON TABLE HOLD AS DVENDORS
END
-RUN
TABLE FILE HTLHLD
PRINT CHAIN_CODE/A3 AS 'CODE'
ON TABLE HOLD AS DVENDORS
END
-RUN

TABLE FILE DVENDORS
BY CODE
ON TABLE HOLD
END
-RUN

-IF &LINES EQ 0 GOTO NOVENDORS;

-SET &RPTTITLE = 'Vendor Codes';
DEFINE FILE COVERPG
  NOWTOD/A8 = HHMMSS (NOWTOD);
-*IMAGE/A80 = '<img src=HTTPS://www.ettekreview.com/asp/NEW_HDR.jpg>';
IMAGE/A80 = '<img src=' || '&&SRVR.EVAL' || '/asp/NEW_HDR.PNG>';
  VCODE/A3 = EDIT(CP_TYPE1,'999');
END
TABLE FILE COVERPG
HEADING
"<IMAGE>"
"</5"
"&RPTTITLE.EVAL"
"&&DTDESC.EVAL &&FROMDT - &&TODT " 

PRINT VCODE AS 'Code' CP_DESC AS 'Vendor'
BY VCODE NOPRINT

WHERE ROLLUP EQ '&&ROLLUP.EVAL'
WHERE VCODE IN FILE HOLD
WHERE CAT_DESC EQ 'AIRLINE CODE' OR 'HTL CHAIN CODE' OR 'CAR VENDOR CODE'

&&OUTLINE1
&&OUTLINE2

ON TABLE SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.200000, RIGHTMARGIN=0.200000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=8, COLOR=BLACK, GRID=ON, STYLE=NORMAL,WRAP= OFF, $
TYPE=TITLE, SIZE=9, STYLE=BOLD+UNDERLINE,  $
TYPE=HEADING, SIZE=10, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=14, LINE=8, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=10, LINE=9, STYLE=BOLD, FONT=TIMES NEW ROMAN,$

TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=14, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=REPORT, TITLETEXT=&DSC,$

ENDSTYLE

END
-RUN

-IF &FOCERRNUM EQ 205  OR &LINES EQ 0 THEN GOTO NOVENDORS;
-GOTO XXNOVENDORS;
-NOVENDORS
-SET &TITLETEXT = 'Vendors  ';
-INCLUDE DVRNoRecord
-XXNOVENDORS