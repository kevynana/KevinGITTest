-*======================================================================
-*  DATE        PROGRAMMER  CHANGE
-*  ----        ----------  ---------------------------------
-*  07/27/2015  GSE         S-08380 Initial version
-*======================================================================
-* File Populate_TKT_Purch_Fnl.fex
-*
SET HOLDLIST = PRINTONLY
SET ASNAMES = ON
SET PCOMMA = ON
SET FIXFRMINPUT = NONCOND
SET MESSAGE = OFF
-*
-DEFAULT &TMPDELS = 0;
-*
TABLE FILE TKT_PURCHASE_TEMP
PRINT
      STATUS
      ADD_DATE
BY ROLLUP_CODE
BY TKT_NUM
WHERE STATUS GT ' '
ON TABLE HOLD AS FNLINPUT
END
-RUN
-*
-SET &INSRECS = &LINES;
-*
FILEDEF REJECT DISK REJECT
-RUN
-*
SET MESSAGE = ON
MODIFY FILE TKT_PURCHASE_FNL
  LOG DUPL ON REJECT MSG OFF
FIXFORM FROM FNLINPUT
MATCH ROLLUP_CODE TKT_NUM
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON FNLINPUT
END
SET MESSAGE = OFF
-RUN
-*
-SET &FNLREJS = &DUPLS;
-SET &FNLINPT = &ACCEPTS;
-*
-IF &FNLINPT EQ 0 GOTO NODELETE;
-*
SET MESSAGE = ON
MODIFY FILE TKT_PURCHASE_TEMP
  LOG NOMATCH ON REJECT MSG OFF
FIXFORM FROM FNLINPUT
MATCH ROLLUP_CODE TKT_NUM
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON FNLINPUT
END
SET MESSAGE = OFF
-RUN
-*
-SET &TMPDELS = &DELTD;
-*
-NODELETE
-*
-SET &SUPPRESS1 = IF &FNLINPT GT 0 THEN FOC_NONE ELSE ' ';
-RUN
TABLE FILE ROLLUP
BY ROLLUP_CODE NOPRINT
WHERE RECORDLIMIT EQ 1
HEADING
" "
" TKT_Purchase_Fnl Processing Results "
" Records retrieved from TKT_Purchase_Temp - &INSRECS "
" "
" Records inserted into TKT_Purchase_Fnl - &FNLINPT "
" Records rejected due to duplicate Rollup/TKT_NUM - &FNLREJS "
" "
" Records deleted from TKT_Purchase_Temp - &TMPDELS "
" "
" Warning - No records were inserted into TKT_Purchase_Fnl &SUPPRESS1 "
" "
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE SET STYLE *
TYPE=HEADING,
     LINE=2,
	 JUSTIFY=CENTER,
	 STYLE=BOLD,
	 $
TYPE=HEADING,
     LINE=10,
	 STYLE=BOLD,
	 COLOR=RED,
	 SIZE=11,
	 $
TYPE=HEADING,
     LINE=11,
	 STYLE=BOLD,
	 COLOR=RED,
	 SIZE=11,
	 $
 
ENDSTYLE
END
-*
-XXIT
