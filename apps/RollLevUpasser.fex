-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File ROLLLEVU.FEX
-*
-*****   This routine updates the IHIER_SL file with the NEW ROLL_LEV
-*****   field.
-*****   AUTHOR:  Steve Cords
-*****   PRODUCTION DATE: 11/12/2001
-*
SET ASNAMES=ON
-SET &&COMPANY='ZUP';
-SET &&HPCOMP='HP'||&&COMPANY;
-* DELETE IHIER_SL RECORDS THAT DO NOT MATCH RPT_INST

-* -GOTO NEWSTEP

-GOTO STEVECHK

-STEP1
TABLE FILE RPT_INST
PRINT INST_KEY
BY INST_KEY NOPRINT
IF INST_KEY CONTAINS '&&COMPANY';
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE IHIER_SL
PRINT INST_KEY SEL_KEY
BY INST_KEY NOPRINT
IF INST_KEY CONTAINS '&&COMPANY';
ON TABLE HOLD AS HOLD2
END
-RUN

MATCH FILE HOLD1
PRINT INST_KEY AS 'RINST_KEY'
BY INST_KEY NOPRINT
RUN
FILE HOLD2
SUM INST_KEY SEL_KEY
BY INST_KEY NOPRINT
AFTER MATCH HOLD AS HOLD3 NEW-NOT-OLD
END
-RUN

TABLE FILE HOLD3
PRINT INST_KEY SEL_KEY
BY INST_KEY NOPRINT
ON TABLE HOLD
END
-RUN

MODIFY FILE IHIER_SL
FIXFORM FROM HOLD
MATCH INST_KEY SEL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN

-* GET ROLL_LEV BY MATCHING DESCRIPTIONS
-STEP2

TABLE FILE IHIER_SL
PRINT SEL_KEY INST_KEY SEL_DESC
BY SEL_DESC NOPRINT
IF INST_KEY CONTAINS '&&COMPANY';
ON TABLE HOLD AS HOLD2
END
-RUN

TABLE FILE &&HPCOMP
PRINT ROLL_KEY AND ROLL_LEV AND LEVEL_DESC
BY LEVEL_DESC NOPRINT
ON TABLE HOLD AS HOLD3
END
-RUN

MATCH FILE HOLD2
PRINT SEL_KEY INST_KEY SEL_DESC
BY SEL_DESC NOPRINT
RUN
FILE HOLD3
SUM ROLL_KEY ROLL_LEV LEVEL_DESC
BY LEVEL_DESC NOPRINT
AFTER MATCH HOLD AS HOLD4 OLD
END
-RUN

TABLE FILE HOLD4
PRINT INST_KEY AND SEL_KEY AND ROLL_LEV AS 'ROL_LEVEL'
BY INST_KEY NOPRINT
BY SEL_KEY NOPRINT
ON TABLE HOLD AS HOLD5
END
-RUN

MODIFY FILE IHIER_SL
FIXFORM FROM HOLD5
MATCH INST_KEY SEL_KEY
ON MATCH UPDATE ROL_LEVEL
ON NOMATCH REJECT
DATA ON HOLD5
END
-RUN

-* DETERMINE ALL OF THE BAD RECORDS WITHOUT A ROLL_LEV

TABLE FILE HOLD4
PRINT INST_KEY SEL_KEY ROLL_LEV
BY INST_KEY NOPRINT
BY SEL_KEY NOPRINT
IF ROLL_LEV EQ ' ';
ON TABLE HOLD AS HOLD6
END
-RUN

TABLE FILE RPT_INST
PRINT INST_KEY SET_ID RPT_NAME
BY INST_KEY NOPRINT
ON TABLE HOLD AS HOLD7
END
-RUN

MATCH FILE HOLD6
PRINT INST_KEY
BY INST_KEY NOPRINT
RUN
FILE HOLD7
SUM INST_KEY SET_ID RPT_NAME
BY INST_KEY NOPRINT
AFTER MATCH HOLD AS HOLD8 OLD
END
-RUN

TABLE FILE RPT_SET
PRINT SET_ID SET_NAME USER_NAME
BY SET_ID NOPRINT
ON TABLE HOLD AS HOLD9
END

MATCH FILE HOLD8
PRINT SET_ID RPT_NAME
BY SET_ID NOPRINT
RUN
FILE HOLD9
SUM SET_ID SET_NAME USER_NAME
BY SET_ID NOPRINT
AFTER MATCH HOLD AS HOLD10 OLD
END
-RUN

TABLE FILE HOLD10
PRINT USER_NAME SET_NAME RPT_NAME
BY USER_NAME NOPRINT
ON TABLE SAVE AS &&COMPANY FORMAT PDF
HEADING
"</2"
"<38 &&COMPANY "
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
ENDSTYLE
END
-RUN

-NEWSTEP
-* UPDATE SEL_KEY AND SEL_DESC AND SEL_LEVEL FROM IHIER_SL
-* BASED OFF OF ROL_LEVEL

TABLE FILE IHIER_SL
PRINT ROL_LEVEL INST_KEY SEL_KEY
BY ROL_LEVEL NOPRINT
IF ROL_LEVEL NE ' ';
IF INST_KEY CONTAINS '&&COMPANY';
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE &&HPCOMP
PRINT ROLL_LEV ROLL_KEY LEVEL_DESC
BY ROLL_LEV NOPRINT
ON TABLE HOLD AS HOLD2
END
-RUN

MATCH FILE HOLD1
PRINT ROL_LEVEL INST_KEY SEL_KEY
BY ROL_LEVEL NOPRINT
RUN
FILE HOLD2
SUM ROLL_LEV ROLL_KEY LEVEL_DESC
BY ROLL_LEV NOPRINT
AFTER MATCH HOLD AS HOLD3 OLD
END
-RUN

TABLE FILE HOLD3
PRINT INST_KEY SEL_KEY
ON TABLE HOLD AS HOLD33
END
-RUN

MODIFY FILE IHIER_SL
FIXFORM FROM HOLD33
MATCH INST_KEY SEL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HOLD33
END
-RUN

DEFINE FILE HOLD3
NEWROLL/A18 = EDIT(ROLL_KEY, '999999999999999999');
FIRST/A2 = EDIT(NEWROLL, '$$99$$$$$$$$$$$$$$');
SECOND/A2 = EDIT(NEWROLL, '$$$$99$$$$$$$$$$$$');
THIRD/A2 = EDIT(NEWROLL, '$$$$$$99$$$$$$$$$$');
FOURTH/A2 = EDIT(NEWROLL, '$$$$$$$$99$$$$$$$$');
FIFTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$99$$$$$$');
SIXTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$$$99$$$$');
SEVENTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$$$$$99$$');
EIGHTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$$$$$$$99');
END
-RUN

TABLE FILE HOLD3
PRINT INST_KEY ROLL_KEY AS 'SEL_KEY' LEVEL_DESC AS 'SEL_DESC'
AND COMPUTE SEL_LEVEL/I2 = IF FIRST EQ '00' THEN 01 ELSE
IF SECOND EQ '00' THEN 02 ELSE
IF THIRD EQ '00' THEN 03 ELSE
IF FOURTH EQ '00' THEN 04 ELSE
IF FIFTH EQ '00' THEN 05 ELSE
IF SIXTH EQ '00' THEN 06 ELSE
IF SEVENTH EQ '00' THEN 07 ELSE
IF EIGHTH EQ '00' THEN 08 ELSE 09;
ROL_LEVEL
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE HOLD4
PRINT INST_KEY SEL_KEY SEL_DESC SEL_LEVEL ROL_LEVEL
BY INST_KEY NOPRINT
BY SEL_KEY NOPRINT
ON TABLE HOLD AS HOLD5
END
-RUN

MODIFY FILE IHIER_SL
FIXFORM FROM HOLD5
MATCH INST_KEY SEL_KEY
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HOLD5
END
-RUN

-* TABLE FILE IHIER_SL
-* PRINT *
-* IF INST_KEY CONTAINS '&&COMPANY';
-* END
-* -RUN

-* UPDATE LEVEL_KEY AND LEV_SELECT AND SELECT_LEV AND IF_LN1 FROM RPT_INST
-* BASED OFF OF ROL_LEVEL (IHIER_SL)

-NEWSTEP2

TABLE FILE IHIER_SL
PRINT INST_KEY SEL_KEY AS 'LEVEL_KEY' SEL_DESC AS 'LEV_SELECT'
AND SEL_LEVEL AS 'SELECT_LEV'
BY INST_KEY NOPRINT
IF INST_KEY CONTAINS '&&COMPANY';
ON TABLE HOLD AS IHOLD1
END
-RUN

MODIFY FILE RPT_INST
FIXFORM FROM IHOLD1
MATCH INST_KEY
ON MATCH UPDATE LEVEL_KEY
ON MATCH UPDATE LEV_SELECT
ON MATCH UPDATE SELECT_LEV
ON NOMATCH REJECT
DATA ON IHOLD1
END
-RUN

-* TABLE FILE RPT_INST
-* PRINT INST_KEY LEVEL_KEY IF_LN1 LEV_SELECT SELECT_LEV
-* IF INST_KEY CONTAINS '&&COMPANY';
-* END
-* -RUN

TABLE FILE IHIER_SL
PRINT IHIER_SL.SEL_KEY AND IHIER_SL.SEL_DESC AND IHIER_SL.SEL_LEVEL AND
IHIER_SL.ROL_LEVEL BY IHIER_SL.INST_KEY
WHERE IHIER_SL.ROL_LEVEL EQ ' ';
IF INST_KEY CONTAINS '&&COMPANY';
END
-RUN
-EXIT

-STEVECHK

TABLE FILE IHIER_SL
PRINT INST_KEY SEL_KEY SEL_DESC SEL_LEVEL
BY INST_KEY
IF ROL_LEVEL EQ ' ';
ON TABLE HOLD AS HOLD1
END
-RUN

JOIN SET_ID IN RPT_INST TO SET_ID IN RPT_SET
TABLE FILE RPT_INST
PRINT ROLLUP_CODE RPT_NAME SET_NAME USER_NAME RPT_NAME LEVEL_KEY
BY INST_KEY
ON TABLE HOLD AS HOLD2
END
-RUN

JOIN INST_KEY IN HOLD1 TO INST_KEY IN HOLD2
TABLE FILE HOLD1
PRINT ROLLUP_CODE USER_NAME SET_NAME RPT_NAME
BY ROLLUP_CODE NOPRINT
BY USER_NAME NOPRINT
BY SET_NAME NOPRINT
BY RPT_NAME NOPRINT
-* IF USER_NAME EQ 'steve';
END
-RUN
-EXIT


