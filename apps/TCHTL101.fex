-*========================================================================
-* 08/16/2003
-*
-*   TOTAL COMPANY NON-DIRECTIONAL CITY PAIR - NON QUARTERED CLIENTS
-*   	
-* 4/27/04  SAC   ADDED TRP_REF AND NHR_REF
-* 12/28/04 DEB   ADDED ADDT'L DOD_TYPE TO IBOOK CGBOOK DEFINE
-*1/30/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14  DEB    ADDED EX5EX5 TO CTY_PR DEFINE


-*========================================================================
-*                <---------------- STEP1 ---------------->
-*========================================================================
-*      CLEAR ROLLUPX PROCESS STAMPS BEFORE TOTAL COMPANY REPORT RUN
-*========================================================================
-* -INCLUDE SETECHO
-SET HOLDATTR = ON;

-SET &&LP0 = 0;
-SET &&XP0 = 0;
-SET &&LP1 = 0;

SET ASNAMES = ON

USE
D:\TNT\TTRACKER\SYSTEM\DATA\ROLLUPX.FOC
END

FILEDEF WLT DISK D:\FOCTEMP\WLT.DAT
-RUN

EX TCNONPAQ
-RUN

EX SETDBLS SETFILE=&&SETF
-RUN
-*========================================================================
-*              SET PROCESS STAMP TO ' ' (FIELD = PR_STAMP)
-*========================================================================
DEFINE FILE ROLLUPX
XL01/A1 = ' ' ;
END
TABLE FILE ROLLUPX
BY ROLLUP_CODE NOPRINT
PRINT ROLLUP_CODE
      XL01 AS 'PR_STAMP' 
IF UPD_ENABLE EQ 'X'
ON TABLE HOLD 
END
MODIFY FILE ROLLUPX
FIXFORM FROM HOLD
MATCH ROLLUP_CODE
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-*========================================================================
-* SET PROCESS STAMP TO 'T' (FIELD = PR_STAMP) - ROLLUP CODES TO PROCESS
-*========================================================================
DEFINE FILE ROLLUPX
XL01/A1 = 'T' ;
END
TABLE FILE ROLLUPX
BY ROLLUP_CODE NOPRINT
PRINT ROLLUP_CODE
      XL01 AS 'PR_STAMP'
IF UPD_ENABLE EQ 'X'
IF QTR_ENABLE EQ ' '
ON TABLE HOLD 
END
MODIFY FILE ROLLUPX
FIXFORM FROM HOLD
MATCH ROLLUP_CODE
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-*========================================================================
-*       FIND CLIENTS TO PROCESS (SETS OF 25 - THIS IS THE MAXIMUM
-*========================================================================
-TCLOOP
USE CLEAR *

TABLE FILE ROLLUPX
BY ROLLUP_CODE NOPRINT
PRINT COMP
IF PR_STAMP EQ 'T'
IF QTR_ENABLE EQ ' '
IF ROLLUP NE 'TOTAL-R'
IF RECORDLIMIT EQ 25
ON TABLE SAVE
END
-RUN
-IF &RECORDS EQ 0 GOTO STOP;
-SET &&AIR_PATH = 'D:\TNT\TTRACKER\DATA\' ;
-SET &&COM_PATH = 'D:\TNT\TTRACKER\SYSTEM\DATA\' ;
-SET &&SU_PATH = 'D:\TNT\TTRACKER\SUDATA\' ;
-SET &CNT  = 1;
-RUN
-REPEAT GETROLL WHILE &CNT LE &RECORDS;
-READ SAVE &&XX.A6.
-SET &&COMP.&CNT = &&XX ;
-*========================================================================
-*                SET-UP DIALOUGE MANAGER FOR 'AL' DATABASES
-*========================================================================
-SET &&AL_DB.&CNT = 'AL'||&&XX|||'.FOC' ;
-SET &&USE_AL.&CNT = &&AIR_PATH||&&AL_DB.&CNT;
-*========================================================================
-*                SET-UP DIALOUGE MANAGER FOR 'S_' DATABASES
-*========================================================================
-SET &&SEG_DB.&CNT = 'S_'||&&XX|||'.FOC' ;
-SET &&USE_SEG.&CNT = &&AIR_PATH||&&SEG_DB.&CNT;
-*========================================================================
-*                SET-UP DIALOUGE MANAGER FOR 'M_' DATABASES
-*========================================================================
-SET &&AIR_DB.&CNT = 'A_'||&&XX|||'.FOC' ;
-SET &&USE_AIR.&CNT = &&AIR_PATH||&&AIR_DB.&CNT;
-*========================================================================
-*                SET-UP DIALOUGE MANAGER FOR 'D_' DATABASES
-*========================================================================
-SET &&TKT_DB.&CNT = 'T_'||&&XX|||'.FOC' ;
-SET &&USE_TKT.&CNT = &&AIR_PATH||&&TKT_DB.&CNT;
-SET &CNT = &CNT + 1;
-GETROLL
-*========================================================================
-*                        SET-UP USE STATEMENTS
-*========================================================================
-SET &&USE_AREF = &&COM_PATH || 'AIR_REF.FOC';
-SET &&USE_TREF = &&COM_PATH || 'TRP_REF.FOC';
-SET &&USE_NREF = &&COM_PATH || 'NHR_REF.FOC';
-SET &&USE_CLNT = &&COM_PATH || 'CLIENT.FOC';
-SET &&USE_BRCH = &&COM_PATH || 'BRANCH.FOC';
-SET &&USE_ROLL = &&COM_PATH || 'ROLLUPX.FOC';
-SET &&USE_CCAT = &&COM_PATH || 'CLS_CAT.FOC';
-SET &&USE_ARPT = &&COM_PATH || 'AIRPORT.FOC';
-SET &&USE_AEMB = &&COM_PATH || 'EMBARPT.FOC';
-SET &&USE_ARTE = &&COM_PATH || 'RTEARPT.FOC';
-SET &&USE_ANDO = &&COM_PATH || 'NDOARPT.FOC';
-SET &&USE_ANDD = &&COM_PATH || 'NDDARPT.FOC';
-SET &&USE_MILG = &&COM_PATH || 'MILEAGE.FOC';
-SET &&USE_ARLN = &&COM_PATH || 'AIRLINE.FOC';
-SET &&USE_CITY = &&COM_PATH || 'CITY.FOC';
-SET &&USE_CEMB = &&COM_PATH || 'EMBCITY.FOC';
-SET &&USE_CRTE = &&COM_PATH || 'RTECITY.FOC';
-SET &&USE_CNDO = &&COM_PATH || 'NDOCITY.FOC';
-SET &&USE_CNDD = &&COM_PATH || 'NDDCITY.FOC';
-SET &&USE_VAIR = &&COM_PATH || 'VAL_AIR.FOC';

USE 
 &&USE_AL1  AS 'AH_50' ;
 &&USE_AL2  AS 'AH_50' ;
 &&USE_AL3  AS 'AH_50' ;
 &&USE_AL4  AS 'AH_50' ;
 &&USE_AL5  AS 'AH_50' ;
 &&USE_AL6  AS 'AH_50' ;
 &&USE_AL7  AS 'AH_50' ;
 &&USE_AL8  AS 'AH_50' ;
 &&USE_AL9  AS 'AH_50' ;
 &&USE_AL10 AS 'AH_50' ;
 &&USE_AL11 AS 'AH_50' ;
 &&USE_AL12 AS 'AH_50' ;
 &&USE_AL13 AS 'AH_50' ;
 &&USE_AL14 AS 'AH_50' ;
 &&USE_AL15 AS 'AH_50' ;
 &&USE_AL16 AS 'AH_50' ;
 &&USE_AL17 AS 'AH_50' ;
 &&USE_AL18 AS 'AH_50' ;
 &&USE_AL19 AS 'AH_50' ;
 &&USE_AL20 AS 'AH_50' ;
 &&USE_AL21 AS 'AH_50' ;
 &&USE_AL22 AS 'AH_50' ;
 &&USE_AL23 AS 'AH_50' ;
 &&USE_AL24 AS 'AH_50' ;
 &&USE_AL25 AS 'AH_50' ;

 &&USE_SEG1  AS 'SR_50' ;
 &&USE_SEG2  AS 'SR_50' ;
 &&USE_SEG3  AS 'SR_50' ;
 &&USE_SEG4  AS 'SR_50' ;
 &&USE_SEG5  AS 'SR_50' ;
 &&USE_SEG6  AS 'SR_50' ;
 &&USE_SEG7  AS 'SR_50' ;
 &&USE_SEG8  AS 'SR_50' ;
 &&USE_SEG9  AS 'SR_50' ;
 &&USE_SEG10 AS 'SR_50' ;
 &&USE_SEG11 AS 'SR_50' ;
 &&USE_SEG12 AS 'SR_50' ;
 &&USE_SEG13 AS 'SR_50' ;
 &&USE_SEG14 AS 'SR_50' ;
 &&USE_SEG15 AS 'SR_50' ;
 &&USE_SEG16 AS 'SR_50' ;
 &&USE_SEG17 AS 'SR_50' ;
 &&USE_SEG18 AS 'SR_50' ;
 &&USE_SEG19 AS 'SR_50' ;
 &&USE_SEG20 AS 'SR_50' ;
 &&USE_SEG21 AS 'SR_50' ;
 &&USE_SEG22 AS 'SR_50' ;
 &&USE_SEG23 AS 'SR_50' ;
 &&USE_SEG24 AS 'SR_50' ;
 &&USE_SEG25 AS 'SR_50' ;

 &&USE_AIR1  AS 'AIR_MAIN' ;
 &&USE_AIR2  AS 'AIR_MAIN' ;
 &&USE_AIR3  AS 'AIR_MAIN' ;
 &&USE_AIR4  AS 'AIR_MAIN' ;
 &&USE_AIR5  AS 'AIR_MAIN' ;
 &&USE_AIR6  AS 'AIR_MAIN' ;
 &&USE_AIR7  AS 'AIR_MAIN' ;
 &&USE_AIR8  AS 'AIR_MAIN' ;
 &&USE_AIR9  AS 'AIR_MAIN' ;
 &&USE_AIR10 AS 'AIR_MAIN' ;
 &&USE_AIR11 AS 'AIR_MAIN' ;
 &&USE_AIR12 AS 'AIR_MAIN' ;
 &&USE_AIR13 AS 'AIR_MAIN' ;
 &&USE_AIR14 AS 'AIR_MAIN' ;
 &&USE_AIR15 AS 'AIR_MAIN' ;
 &&USE_AIR16 AS 'AIR_MAIN' ;
 &&USE_AIR17 AS 'AIR_MAIN' ;
 &&USE_AIR18 AS 'AIR_MAIN' ;
 &&USE_AIR19 AS 'AIR_MAIN' ;
 &&USE_AIR20 AS 'AIR_MAIN' ;
 &&USE_AIR21 AS 'AIR_MAIN' ;
 &&USE_AIR22 AS 'AIR_MAIN' ;
 &&USE_AIR23 AS 'AIR_MAIN' ;
 &&USE_AIR24 AS 'AIR_MAIN' ;
 &&USE_AIR25 AS 'AIR_MAIN' ;

 &&USE_TKT1   AS 'TKT_MAIN' ;
 &&USE_TKT2   AS 'TKT_MAIN' ;
 &&USE_TKT3   AS 'TKT_MAIN' ;
 &&USE_TKT4   AS 'TKT_MAIN' ;
 &&USE_TKT5   AS 'TKT_MAIN' ;
 &&USE_TKT6   AS 'TKT_MAIN' ;
 &&USE_TKT7   AS 'TKT_MAIN' ;
 &&USE_TKT8   AS 'TKT_MAIN' ;
 &&USE_TKT9   AS 'TKT_MAIN' ;
 &&USE_TKT10  AS 'TKT_MAIN' ;
 &&USE_TKT11  AS 'TKT_MAIN' ;
 &&USE_TKT12  AS 'TKT_MAIN' ;
 &&USE_TKT13  AS 'TKT_MAIN' ;
 &&USE_TKT14  AS 'TKT_MAIN' ;
 &&USE_TKT15  AS 'TKT_MAIN' ;
 &&USE_TKT16  AS 'TKT_MAIN' ;
 &&USE_TKT17  AS 'TKT_MAIN' ;
 &&USE_TKT18  AS 'TKT_MAIN' ;
 &&USE_TKT19  AS 'TKT_MAIN' ;
 &&USE_TKT20  AS 'TKT_MAIN' ;
 &&USE_TKT21  AS 'TKT_MAIN' ;
 &&USE_TKT22  AS 'TKT_MAIN' ;
 &&USE_TKT23  AS 'TKT_MAIN' ;
 &&USE_TKT24  AS 'TKT_MAIN' ;
 &&USE_TKT25  AS 'TKT_MAIN' ;

 &&USE_ARLN
 &&USE_MILG
 &&USE_CCAT
 &&USE_AEMB
 &&USE_CEMB
 &&USE_ARTE
 &&USE_CRTE
 &&USE_ANDO
 &&USE_CNDO
 &&USE_ANDD
 &&USE_CNDD
 &&USE_VAIR
 &&USE_AREF
 &&USE_TREF
 &&USE_NREF
 &&USE_CLNT
END
-*========================================================================
-*                <---------------- STEP2 ---------------->
-*========================================================================
-*                      PERFORM DATA EXTRACT AND HOLD
-*========================================================================
-IF &&LP1 GE 1 THEN GOTO MORE ;
-RUN

-SET &&LP0 = 0;
-SET &&XP0 = 0;
-SET &&LP1 = &&LP0 + 1;
-SET &&XP1 = &&XP0 + 1;
-SET &&DBASE = 'TCHOLD1' ;
-GOTO GETDATA
-RUN

-MORE
-SET &&LP1 = &&LP1 + 1 ;
-SET &&XP1 = &&XP1 + 1 ;
-SET &&DB1   = 'TCHOLD';
-SET &&DCNT  = &&LP1 ;
-SET &&DBASE = &&DB1||&&DCNT ;
-RUN

-GETDATA
DEFINE FILE SR_50
0440_RUF/A1 = IF LEVEL1 EQ '0440RUFFALO' THEN 'Y' ELSE
              IF LEVEL2 EQ '0015010100000' OR '0042022600000' OR
              '0015050000000' OR '0015010100000' OR '0015010000000' OR
              '0015062000000' OR '0015022000000' OR '0015010100000' OR
              '0013025300000' OR '0015044000000' OR '0015017900000' OR
              '0015017900000' OR '0010062200000' OR '0015010000000' OR
              '0015044000000' OR '0021044000000' OR '0015062000000' OR
              '0015010600000' OR '0032022500000' OR '0015012100000' OR
              '0014025200000' OR '0017044000000' OR '0005044000000' OR
              '0042022700000' OR '2402240000000' OR '0033044000000' 
              THEN 'Y' ELSE 'N';
CGBOOK/A20 = IF (ONLINE_TRADITIONAL EQ 'ONLINE') THEN 'ONLINE' ELSE
                'TRADITIONAL BOOKED';
COMM_SEG/D12.2S   = SEG_COM;
CP_CL_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'   
                              'D' 'DISCOUNT'  'B' 'BUSINESS');
CTY_PR/A1 = IF ORG_DST EQ 'FP2FP2' THEN 'Y' ELSE
            IF ORG_DST EQ 'FP3FP3' THEN 'Y' ELSE
            IF ORG_DST EQ 'EX1EX1' THEN 'Y' ELSE
            IF ORG_DST EQ 'DT1DT1' THEN 'Y' ELSE 
            IF ORG_DST EQ 'EX5EX5' THEN 'Y' ELSE 'N';
DEMB_APT/A21=EDIT(EMB_APT.AIRPORT_NAME,'999999999999999999999$$$$');
DIR_CITY_CD/A50 = EMB_CTY.EMB_CITY || '-' || RTE_CTY.RTE_CITY;
DIR_CITY_NM/A50 = EMB_CTY.CITY_NAME | RTE_CTY.CITY_NAME; 
DIR_CP_CD/A50 = CITYPAIR.EMBARK || '-' || CITYPAIR.ROUTE;
DIR_CP_NM/A50 = EMB_APT.AIRPORT_NAME | RTE_APT.AIRPORT_NAME;
DPT/A2 = EDIT (DPT_TIME, '99$$');
DRTE_APT/A21=EDIT(RTE_APT.AIRPORT_NAME,'999999999999999999999$$$$');
EMB_APT/A21=EDIT(NDO_APT.AIRPORT_NAME,'999999999999999999999$$$$');
FARE_PAID/D12.2S  = SEG_AMT;
-*ADDED FOR CONAGRA CITYPAIR REPORT FOR ICG COMMERCE
FARE_BAS1/A1 = EDIT (CLASS, '9');
-* ADDED FOR INTERNET BOOKINGS
IBOOK/A33 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE BOOKINGS' ELSE
    IF DOD_DESC CONTAINS 'EXCHANGE' THEN 'EXCHANGE FROM AN INTERNET BOOKING' ELSE
    IF DOD_DESC CONTAINS 'WEB' THEN 'WEB BOOKINGS' ELSE 'TRADITIONAL BOOKINGS'; 
LEVEL_DESC/A60 = 'AROLL_DSC2';
ND_CITY_CD/A50 = NDO_CTY.NDO_CITY || '-' || NDD_CTY.NDD_CITY;
ND_CITY_NM/A50 = NDO_CTY.CITY_NAME | NDD_CTY.CITY_NAME;
ND_CP_CD/A50  = NDO_APT.ND_ORG || '-' || NDD_APT.ND_DST;
NET_COMM/D12.2S   = (SEG_AMT - SEG_COM);
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
NEW_SEG_COUNT/D8CS = SEG_COUNT;
RTE_APT/A21=EDIT(NDD_APT.AIRPORT_NAME,'999999999999999999999$$$$');
TOTAL_FARE/D12.2S = SEG_AMT + SEG_TAX;
VASAVINGS/D11.2CS = SEG_DISCOUNT - TOTAL_FARE;
XPSNGR_NM/A15 = EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
XTRAN_DT/MDYY = TRN_DATE;

-* ****DEFINES ON PREVIOUS DEFINES****

DISSAVINGS/D11.2CS= IF EMBARK EQ 'DT1' THEN 0 ELSE
                         TOTAL_FARE - SEG_LOWEST;

ND_CP_NM/A50 = EMB_APT|'        '|RTE_APT;
ORGDST/A50 = DIR_CITY_CD ;
SAVINGS/D11.2CS   = SEG_STANDARD - TOTAL_FARE;
-******** NEW DEFINES TO CORRECT CITY/AIRPORT NAME/CODE ISSUE JK ******

-******* PRODUCES DIRECTIONAL CITY CODES ***************
DIR_CTY_CDO/A25 = EMB_CTY.EMB_CITY;
DIR_CTY_CDD/A25 = RTE_CTY.RTE_CITY;

-******* PRODUCES DIRECTIONAL CITY NAMES ***************
DIR_CTY_NMO/A25 = EMB_CTY.CITY_NAME;
DIR_CTY_NMD/A25 = RTE_CTY.CITY_NAME;

-********* PRODUCES DIRECTIONAL AIRPORT CODES ********
DIR_APT_CDO/A25 = CITYPAIR.EMBARK;
DIR_APT_CDD/A25 = CITYPAIR.ROUTE;

-********* PRODUCES DIRECTIONAL AIRPORT NAMES ********
DIR_APT_NMO/A25 = EMB_APT.AIRPORT_NAME;
DIR_APT_NMD/A25 = RTE_APT.AIRPORT_NAME;

-********* PRODUCES NON-DIRECTIONAL CITY CODES ********
ND_CTY_CDO/A25 = NDO_CTY.NDO_CITY;
ND_CTY_CDD/A25 = NDD_CTY.NDD_CITY;

-********* PRODUCES NON-DIRECTIONAL CITY NAMES ********
ND_CTY_NMO/A25 = NDO_CTY.CITY_NAME;
ND_CTY_NMD/A25 = NDD_CTY.CITY_NAME;

-******** PRODUCES NON-DIRECTIONAL AIRPORT CODES ****
ND_APT_CDO/A25 = NDO_APT.ND_ORG;
ND_APT_CDD/A25 = NDD_APT.ND_DST;

-******** PRODUCES NON-DIRECTIONAL AIRPORT NAMES ****
ND_APT_NMO/A25 = EDIT(NDO_APT.AIRPORT_NAME, '999999999999999999999$$$$');
ND_APT_NMD/A25 = EDIT(NDD_APT.AIRPORT_NAME, '999999999999999999999$$$$');  

END

TABLEF FILE SR_50
PRINT 
  0440_RUF
  AIR_KEY	
  AIRLINE	
  AIRLINE.AIRLINE AS 'AIR_LINE'	
  AIRLINE.AIRLINE_NAME 	
  AR_BRANCH
  CGBOOK
  CITYPAIR.AIRLINE AS 'CP_AIRLINE'	
  CITYPAIR.CLASS AS 'CP_CLASS'	
  CITYPAIR.EMBARK AS 'CP_EMB'         	
  CITYPAIR.INTL_DOM AS 'INTL_DOM'	
  CITYPAIR.NET_COMM AS 'NET_COMM'	
  CITYPAIR.ROUTE AS 'CP_RTE'	
  CITYPAIR.SEG_MILES/D9C AS 'MILES'	
  CLASS	
  CLS_CAT.CAT_DESC AS 'CAT_DESC'	
  COMM_SEG	
  CP_CL_CAT
  CTY_PR
  DEMB_APT	
  DISSAVINGS	
  DOC_TYPE
  DOD_TYPE
  DPT
  DPT_TIME
  DRTE_APT	
  EMB_APT
  FARE_BAS1	
  FARE_PAID  AS 'CP_FARE_PD'
  IBOOK
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL_DESC
  ND_CP_CD
  ND_CP_NM
  NET_FARE	
  NET_TKT_CNT
  NEW_SEG_COUNT	
  ORGDST	
  PASSNGR_NAME
  REFUSE_KEY
  ROLLUP_CODE	
  RTE_APT	
  SAVINGS	
  SEG_COUNT	
  SEG_NBR	
  TKT_NUM	
  TOTAL_FARE	
  TRN_DATE   	
  VASAVINGS 	
  XPSNGR_NM	
  XTRAN_DT    
-******* NEW FIELDS *********
  DIR_CTY_CDO
  DIR_CTY_CDD
  DIR_CTY_NMO
  DIR_CTY_NMD
  DIR_APT_CDO
  DIR_APT_CDD
  DIR_APT_NMO
  DIR_APT_NMD
  ND_CTY_CDO
  ND_CTY_CDD
  ND_CTY_NMO
  ND_CTY_NMD
  ND_APT_CDO
  ND_APT_CDD
  ND_APT_NMO
  ND_APT_NMD 

WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
WHERE VOID_DATE EQ 0
   
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS

ON TABLE HOLD AS '&&DBASE' FORMAT FOCUS
END
-RUN
-*========================================================================
-*                <---------------- STEP3 ---------------->
-*========================================================================
-*              UPDATE PR_STAMP TO 'Z' = Rollup Code Processed
-*========================================================================
DEFINE FILE ROLLUPX
XL01/A1 = 'Z' ;
END
TABLE FILE ROLLUPX
BY ROLLUP_CODE NOPRINT
PRINT ROLLUP_CODE
      XL01 AS 'PR_STAMP' 
IF UPD_ENABLE EQ 'X'
IF QTR_ENABLE EQ ' '
IF PR_STAMP EQ 'T'
IF RECORDLIMIT EQ 25
ON TABLE HOLD 
END
MODIFY FILE ROLLUPX
FIXFORM FROM HOLD
MATCH ROLLUP_CODE
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
USE CLEAR *
-GOTO TCLOOP
-RUN

-STOP
EX SUUSES
-RUN
EX TCDESC2
-RUN

FILEDEF TCUSE DISK TCUSE.FEX
-RUN

-SET &ZCNT = 1;
-WRITE TCUSE USE ADD
-REPEAT ENDREPEAT &&DCNT TIMES;
-SET &&TDAT = 'TCHOLD' | '&ZCNT.EVAL' | '.FOC' | ' AS TCHOLD1' ;
-WRITE TCUSE &&TDAT
-SET &ZCNT = &ZCNT + 1 ;
-ENDREPEAT
-WRITE TCUSE END
-CLOSE TCUSE

-INCLUDE TCUSE
-RUN

USE ADD CONG.FOC  AS TCHOLD1
        KIMB.FOC  AS TCHOLD1
        METX.FOC  AS TCHOLD1
        UPTTL.FOC AS TCHOLD1
END
-RUN

DEFINE FILE TCHOLD1
  BUSINESS/I8S = IF CP_CL_CAT EQ 'B' THEN SEG_COUNT ELSE 0;
  COACH/I8S    = IF CP_CL_CAT EQ 'C' THEN SEG_COUNT ELSE 0;
  DSCNT/I8S    = IF CP_CL_CAT EQ 'D' THEN SEG_COUNT ELSE 0;
  FIRST/I8S    = IF CP_CL_CAT EQ 'F' THEN SEG_COUNT ELSE 0;
  OTHER/I8S    = IF CP_CL_CAT EQ 'O' THEN SEG_COUNT ELSE 0;
  SEGMT/I8S    = FIRST + BUSINESS + COACH + DSCNT + OTHER ;
  LABEL1/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMO ELSE ' ';

 LABEL2/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMD ELSE ' ';
END
-RUN

TABLE FILE TCHOLD1
SUM BUSINESS	
    COACH	
    COMM_SEG	
    CP_FARE_PD AS 'FARE'
    DEMB_APT 	
    DISSAVINGS
    DPT_TIME
    DRTE_APT	
    DSCNT	
    EMB_APT
    FARE_BAS1
    FIRST
    LABEL1
    LABEL2
    LEVEL3	
    LEVEL_DESC
    MILES	
    NET_COMM	
    NET_FARE 	
    NET_TKT_CNT	
    NEW_SEG_COUNT	
    OTHER
    RTE_APT	
    SAVINGS	
    SEGMT	
    TOTAL_FARE	
    VASAVINGS
BY ORGDST
BY CP_AIRLINE
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
PRINT BUSINESS	
      COACH	
      COMM_SEG	
      CP_AIRLINE
      DEMB_APT	
      DISSAVINGS
      DPT_TIME
      DRTE_APT	
      DSCNT	
      EMB_APT
      FARE
      FARE_BAS1	
      FIRST
      LABEL1
      LABEL2
      LEVEL3
      LEVEL_DESC	
      MILES	
      NET_COMM	
      NET_FARE 	
      NET_TKT_CNT	
      NEW_SEG_COUNT	
      OTHER
      RTE_APT
      SAVINGS	
      SEGMT	
      TOTAL_FARE	
      VASAVINGS
BY ORGDST
RANKED BY HIGHEST &&RANK_METH   
ON TABLE HOLD AS HOLD2
END
-RUN


DEFINE FILE HOLD2
  DRANK/D5      = RANK ;
  ARANKX/A6     = FTOA(DRANK, '(D5)', 'A6');
  ARANK/A5      = EDIT(ARANKX,'$99999');
  RANK_LIMIT/I5 = &&RANK_LIMIT2 ;
  RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                  IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN

TABLE FILE HOLD2
PRINT BUSINESS	
      COACH	
      COMM_SEG	
      CP_AIRLINE
      DEMB_APT 	
      DISSAVINGS
      DPT_TIME
      DRTE_APT	
      DSCNT	
      EMB_APT
      FARE_BAS1
      FARE	
      FIRST
      LABEL1
      LABEL2
      LEVEL3
      LEVEL_DESC	
      MILES	
      NET_COMM	
      NET_FARE	
      NET_TKT_CNT	
      NEW_SEG_COUNT	
      OTHER
      RTE_APT
      SAVINGS	
      SEGMT	
      TOTAL_FARE	
      VASAVINGS
BY ORGDST
BY RANK_VALUE
ON TABLE HOLD AS HOLD3
END
-RUN


DEFINE FILE HOLD3
AIRLINE_PRINT/A12 = IF RANK_VALUE LE '&&RANK_LIMIT2' 
	THEN CP_AIRLINE ELSE 'OTHER';
END
-RUN

TABLE FILE HOLD3
SUM BUSINESS	
    COACH	
    COMM_SEG
    DEMB_APT	
    DISSAVINGS
    DPT_TIME
    DRTE_APT	
    DSCNT	
    EMB_APT
    FARE
    FARE_BAS1	
    FIRST
    LABEL1
    LABEL2
    LEVEL3
    LEVEL_DESC	
    MILES	
    NET_COMM	
    NET_FARE	
    NET_TKT_CNT	
    NEW_SEG_COUNT	
    OTHER	
    RTE_APT	
    SAVINGS	
    SEGMT	
    TOTAL_FARE	
    VASAVINGS
BY ORGDST 
BY RANK_VALUE
BY AIRLINE_PRINT 
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE HOLD1
SUM &&RANK_METH
BY ORGDST
ON TABLE HOLD AS RHOLD1 
END
-RUN

TABLE FILE RHOLD1
PRINT &&RANK_METH
RANKED BY HIGHEST &&RANK_METH AS 'SEG_RANK'
BY ORGDST
ON TABLE HOLD AS RHOLD2
END
-RUN


MATCH FILE HOLD4
PRINT AIRLINE_PRINT 	
      BUSINESS	
      COACH	
      COMM_SEG
      DEMB_APT	
      DISSAVINGS
      DPT_TIME
      DRTE_APT	
      DSCNT	
      EMB_APT
      FARE	
      FARE_BAS1
      FIRST
      LABEL1
      LABEL2
      LEVEL3	
      LEVEL_DESC
      MILES	
      NET_COMM	
      NET_FARE 	
      NET_TKT_CNT	
      NEW_SEG_COUNT	
      OTHER	
      RANK_VALUE
      RTE_APT
      SAVINGS	
      SEGMT	
      TOTAL_FARE	
      VASAVINGS
BY ORGDST 
ON TABLE HOLD
RUN
FILE RHOLD2
SUM SEG_RANK
BY ORGDST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN


TABLE FILE HOLD5
PRINT AIRLINE_PRINT	
      BUSINESS	
      COACH	
      COMM_SEG
      DEMB_APT	
      DISSAVINGS
      DPT_TIME
      DRTE_APT	
      DSCNT	
      EMB_APT	
      FARE
      FARE_BAS1	
      FIRST
      LABEL1
      LABEL2
      LEVEL3
      LEVEL_DESC	
      MILES	
      NET_COMM	
      NET_FARE 	
      NET_TKT_CNT	
      NEW_SEG_COUNT	
      ORGDST	
      OTHER	
      RANK_VALUE
      RTE_APT	
      SAVINGS	
      SEGMT	
      TOTAL_FARE	
      VASAVINGS	
RANKED BY HIGHEST SEG_RANK 
ON TABLE HOLD AS HOLD6
END
-RUN

DEFINE FILE HOLD6
  ORG_LIMIT/I2 = &&RANK_LIMIT ;
  OLIMIT/A2    = EDIT(ORG_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF ORGDST NE LAST ORGDST THEN (LAST LIST + 1) 
    ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999'); 
  ORG_VALUE/A6 = IF ORG_LIMIT EQ 0 THEN BRANK ELSE
               IF LIST GT ORG_LIMIT THEN 'OTHER' ELSE FRANK;
END
-RUN

TABLE FILE HOLD6
SUM BUSINESS	
    COACH	
    COMM_SEG
    DEMB_APT	
    DISSAVINGS
    DPT_TIME
    DRTE_APT	
    DSCNT	
    EMB_APT
    FARE
    FARE_BAS1	
    FIRST
    LABEL1
    LABEL2
    LEVEL3
    LEVEL_DESC	
    MILES	
    NET_COMM	
    NET_FARE  	
    NET_TKT_CNT	
    NEW_SEG_COUNT	
    OLIMIT	
    ORG_LIMIT	
    OTHER	
    RTE_APT	
    SAVINGS	
    SEG_RANK	
    SEGMT	
    TOTAL_FARE	
    VASAVINGS
BY ORG_VALUE
BY ORGDST
BY RANK_VALUE
BY AIRLINE_PRINT
ON TABLE HOLD AS HOLD7
END
-RUN

DEFINE FILE HOLD7
-INCLUDE STDTIME1
  DEMBARK_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN DEMB_APT ELSE 'ALL OTHER';
  DROUTE_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN DRTE_APT ELSE 'ALL OTHER';
  EMBARK_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN EMB_APT ELSE 'ALL OTHER'; 
  ORG_PRINT/A55 =IF ORG_VALUE LT (OLIMIT) THEN ORGDST ELSE 'ALL OTHER';
  RO_PRINT/A12   =IF ORG_VALUE LT (OLIMIT) THEN AIRLINE_PRINT ELSE ' ';
  ROUTE_APT/A21 = IF ORG_VALUE LT (OLIMIT) THEN RTE_APT ELSE 'ALL OTHER';
  EMB_LBL/A25 = IF ORG_VALUE LT (OLIMIT) THEN LABEL1 ELSE 'ALL OTHER';
  RTE_LBL/A25 = IF ORG_VALUE LT (OLIMIT) THEN LABEL2 ELSE 'ALL OTHER';
END
-RUN

TABLE FILE HOLD7
SUM BUSINESS	
    COACH	
    COMM_SEG
    DEMBARK_APT    	
    DISSAVINGS
    DPT_TIME
    DROUTE_APT	
    DSCNT	
    EMBARK_APT
    EMB_LBL
    FARE
    FARE_BAS1	
    FIRST
    LEVEL3
    LEVEL_DESC	
    MILES	
    NET_COMM	
    NET_FARE	
    NET_TKT_CNT
    NEW_SEG_COUNT
    ORGDST	
    OTHER	
    ROUTE_APT
    RTE_LBL
    SAVINGS	
    SEG_RANK	
    SEGMT
    TIME_DPT	
    TOTAL_FARE	
    VASAVINGS
BY ORG_VALUE     
BY ORG_PRINT  
BY RANK_VALUE
BY RO_PRINT
ON TABLE HOLD AS HOLD8
END
-RUN

TABLE FILE HOLD7
SUM MILES AS 'C_MILES'
    NET_COMM AS 'C_COMM'
    FARE AS 'C_FARE' 
    NET_TKT_CNT AS 'C_SEGMT'
    TOTAL_FARE AS 'T_FARE'
BY ORG_PRINT  
ON TABLE HOLD AS HOLD9
END
-RUN

MATCH FILE HOLD8
PRINT BUSINESS	
      COACH	
      COMM_SEG
      DEMBARK_APT	
      DISSAVINGS
      DPT_TIME
      DROUTE_APT	
      DSCNT
      EMBARK_APT
      EMB_LBL	
      FARE
      FARE_BAS1	
      FIRST
      LEVEL3
      LEVEL_DESC	
      MILES	
      NET_COMM	
      NET_FARE 	
      NET_TKT_CNT 	
      NEW_SEG_COUNT	
      ORGDST
      ORG_VALUE	
      OTHER	
      RANK_VALUE	
      RANK_VALUE	
      RO_PRINT	
      ROUTE_APT
      RTE_LBL
      SAVINGS	
      SEG_RANK	
      SEGMT
      TIME_DPT	
      TOTAL_FARE	
      VASAVINGS
BY ORG_PRINT 
ON TABLE HOLD
RUN
FILE HOLD9
SUM C_COMM
    C_FARE
    C_MILES
    C_SEGMT
    T_FARE
BY ORG_PRINT 
AFTER MATCH HOLD AS HOLD10 OLD-OR-NEW
END
-RUN

-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN

-TYPE BEFORE DEFINES

DEFINE FILE HOLD10 
  PER_FARE/D8.1=    IF ((C_FARE LT 1) AND (C_FARE GT (-1))) THEN 0
                    ELSE ((FARE/C_FARE)*100);
  PER_TFARE/D8.1=   IF ((T_FARE LT 1) AND (T_FARE GT (-1))) THEN 0
                    ELSE ((TOTAL_FARE/T_FARE)*100);
  SEGPER/D8.1 =     IF ((C_SEGMT LT 1) AND (C_SEGMT GT (-1))) THEN 0
                    ELSE ((NET_TKT_CNT/C_SEGMT)*100);
  PER_NFARE/D8.1=   IF ((C_COMM LT 1) AND (C_COMM GT (-1))) THEN 0
                    ELSE ((NET_COMM/C_COMM)*100);
  MILE_PCT/D8.1=    IF ((C_MILES LT 1) AND (C_MILES GT (-1))) THEN 0
                    ELSE ((MILES/C_MILES)*100);
  COSTPER_MILE/D8.2=IF ((MILES LT 1) AND (MILES GT (-1))) THEN 0
                    ELSE (FARE/MILES);
  AVGSEG_AMT/D8.2 = IF ((SEGMT LT 1) AND (SEGMT GT (-1))) THEN 0
                    ELSE (NET_FARE/SEGMT);
  AVGFLT/D8.2 =     IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0
                    ELSE (NET_FARE/NET_TKT_CNT);

-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END

TABLE FILE HOLD10
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT 

ON TABLE SET STYLE *

-INCLUDE &&SMSTY 
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW='CPALL';
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-XXIT


