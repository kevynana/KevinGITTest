USE CLEAR *
FILEDEF * CLEAR

SET MESSAGE=ON
-INCLUDE SETECHO
-* File ADMINDRV.FEX
-* Driver Program for the eTTek Review Admin. Functions
-*
-****************************************************************************
-* Changable Global Variables
-*   &&LOCDRV  - contains the drive letter for the local machine that will
-*               be used for this job.
-SET &&LOCDRV = 'K:';
-*
-*   &&SRVDRV  - contains the drive letter for the server that will be used
-*               for this job. This will be the drive letter that the local
-*               machine has mapped for the TNT directory on the server.
-SET &&SRVDRV = 'K:';
-*
-*   &&SRVR - contains the name of the WebFocus Server.  Used for Remote
-*            calls.
-SET &&SRVR = 'WEBFOCUS01';
-*   &&TMP_PATH - contains the path on the local machine where the AH*, CH*,
-*                HH*, HP*, and HX* files are backed up to.
-SET &&TMP_PATH = &&LOCDRV || '\TNT\TTRACKER\TEMP\';
-*
-*   &&HPF_PATH - contains the path on the local machine where the AH*, CH*,
-*                HH*, HP*, and HX* files will be process from.
-SET &&HPF_PATH = &&LOCDRV || '\TNT\TTRACKER\DATA\';
-*
-*   &&SRV_PATH - contains the path on the server where the AH*, CH*,
-*                HH*, HP*, and HX* files will be copied from and to.
-SET &&SRV_PATH = &&SRVDRV || '\TNT\TTRACKER\DATA\';
-*
-*   &&SRVSU_PATH - contains the path on the server where the data located
-*                 under control of the SU machine is located
-SET &&SRVSU_PATH = &&SRVDRV || '\TTRACKER\SUDATA\';
-*
-*   &&LOCAPP_PATH - contains the path on the local machine which will be used
-*                   for misc. processing
-SET &&LOCAPP_PATH = 'C:\IBI\APPS\TTADMIN\';
-*
-*   &&COM_PATH - contains the path on the local machine where common
-*                databases will be processed from.
-SET &&COM_PATH = &&LOCDRV || '\TNT\TTRACKER\SYSTEM\DATA\';
-*
-*   &&SRVCOM_PATH - contains the path on the server where common databases
-*                   are located.
-SET &&SRVCOM_PATH = &&SRVDRV || '\TNT\TTRACKER\SYSTEM\DATA\';
-SET &&AS400 = &&LOCDRV || '\AS400\';
-*
-****************************************************************************
-*
-*****************************************************************************
-* eTTek Review Data Hierarchy Load from AS400 File Procedures
-*
-* 1. Check files to see if update is needed. (eTTek Review Support)
-* a) Do a DBM TTPHIEA0 and make sure that company is included in the file.
-* b) Run a query to get a list of rollups and effective dates
-* 	1. From a command line submit the HIERCOUNTS query in the library
-* 	SACLIB.
-* 	2. Watch for a completed normally message and find report to print.
-* 	3. Look for the rollup you are updating and check for the last
-* 	effective date.
-* 	4. Go out to WEBFOCUS01:\TNT\TTRACKER\DATA and see when the HP was
-* 	last updated.
-* 	5. If the effective date is greater than the update on WEBFOCUS01
-* 	then you will need to continue on.
-*
-* 2. Copy rollup file from TTPHIEA0 to TTPTHIA0.
-* a) Call TTR501
-* 	1. Enter in the rollup code you are updating and enter.
-*	2. Watch for completed normally message.
-*
-* 3. Copy file from AS400 to WEBFOCUS01\TNT\TTRACKER\SYSTEM\DATA\.
-* a) Check dbm TTPTHIA0 to make sure it contains only the company submitted.
-* b) View TT file on WEBFOCUS01\AS400 to make sure it contains the company(s)
-*    submitted.
-* c) Copy file from WEBFOCUS01\AS400 to WEBFOCUS01\TNT\TTRACKER\SYSTEM\DATA\
-* d) Move the file from WEBFOCUS01\AS400 to WEBFOCUS01\DATA\HIERARCHIES\MONTH
-*
-* 4. Run hierarchy update in WebFocus
-* a) From Webfocus02 (test box) under TTADMIN.
-* b) Open PROCEDURES.
-* c) Run ADMINDRV.
-* d) Open FOCUS session window.
-* e) Select Hierarchy Update.
-* f) From dropdown box chose rollup-code (ex. FUJI-R) to update.
-* g) Enter file you are working with (ex. TTFUJI) and the effective date.
-*    The effective date will be the beginning of the month/week in which
-*    management reports are being submitted.  New clients will have an
-*    effective date of 19900101.
-* h) This can take anywhere from 5 minutes to several hours, depending on
-*    how deep the update is.
-*
-* 5. Check files for accuracy
-* a) From focus run a query on HP (company) and HX (company) files.  Make
-*    sure that paths for query are looking at WEBFOCUS01\TNT\TRACKER\DATA
-* b) Check file to make sure it is accurate. Make sure there are no
-*    duplicates within the file and also make sure the effective date is
-*    correct and make sure the correct hierarchy file was being used.
-*    If files are correct, continue on. If not, contact Steve.
-*****************************************************************************
-*
-SET &&TEMP_PATH=TEMPPATH(50,'A50');
-SET &&INSTPASS = &&TEMP_PATH || 'INSTPASS.FTM';
-RUN
-*

-BACK1

FILEDEF INSTPASS DISK &&INSTPASS
-RUN

-WRITE INSTPASS xx
-RUN

FILEDEF INSTPASS CLEAR
-RUN

FILEDEF INSTPASS DISK &&INSTPASS
-RUN

-* Display Admin Menu Screen
EX H50_MOD

-RUN

-READ INSTPASS &&IDCODE.2

-* Determine which process to execute
-*   HP - Hierarchy Update
-*   SF - SetFile Maintenance Process
-*   xx - Exit
-IF &&IDCODE EQ 'xx' GOTO XEXIT;

-IF &&IDCODE EQ 'HP' GOTO HIER_UPD;

-IF &&IDCODE EQ 'SF' GOTO SETFILES;

-GOTO XEXIT

-* Hierarchy Update Process
-HIER_UPD

-* Execute a request on the server against the Rollup Table to retrieve all
-* active Rollups.
REMOTE DEST=&&SRVR
-REMOTE BEGIN
TABLE FILE ROLLUP
SUM ROLLUP
BY ROLLUP NOPRINT
ON TABLE PCHOLD AS ROLLCDES
WHERE UPD_ENABLE EQ 'X'
END
-RUN
-REMOTE END

-RUN

-SET &&ROLLCODES = &&TEMP_PATH || 'ROLLCDES.FTM';
FILEDEF ROLLCDES DISK &&ROLLCODES

FILEDEF INSTPASS CLEAR
-RUN

FILEDEF INSTPASS DISK &&INSTPASS
-RUN

-WRITE INSTPASS xx

FILEDEF INSTPASS CLEAR
-RUN

FILEDEF INSTPASS DISK &&INSTPASS
-RUN

-DEFAULT &&IDCODE='XX';
-DEFAULT &&RCODE='ABCD-R';
-DEFAULT &&HFILE='ABCDHP';
-DEFAULT &&EFF_DATE='20000101';
-* Display Available Rollups for Hierarachy Update process
EX HP_SEL
-RUN

-* Load values from screen into variables
-READ INSTPASS &&IDCODE.2 &&RCODE.8 &&HFILE.8 &&EFF_DATE.8
-IF &&IDCODE EQ 'BK' GOTO BACK1;

-SET &FILECHK = &&COM_PATH || &&HFILE || '.DAT';
STATE &FILECHK
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;


-SET &&ROLLUP=&&RCODE;
-SET &&RINSTCHK = &&TEMP_PATH || 'RINSTCHK.FTM';
FILEDEF RINSTCHK DISK &&RINSTCHK
-RUN
ERASE &&RINSTCHK.EVAL
FILEDEF RINSTCHK CLEAR
-RUN

-* Updates the UPD_STAT flag in Rollup on the Server to 'X' to prevent
-* reports from execution.
-* Execute request on server to verify that there are not jobs currently
-* with an Execute Flag of 'E'
REMOTE DEST=&&SRVR
-REMOTE BEGIN
SET MESSAGE=OFF
TABLE FILE ROLLUP
PRINT ROLLUP_CODE
WHERE ROLLUP_CODE EQ '&&RCODE'
ON TABLE HOLD AS HOLD001
END
-RUN
MODIFY FILE ROLLUP
FIXFORM FROM HOLD001
MATCH ROLLUP_CODE
  ON NOMATCH REJECT
  ON MATCH COMPUTE UPD_STAT = 'X';
  ON MATCH UPDATE UPD_STAT
DATA ON HOLD001
END
-RUN
JOIN SET_KEY IN RPT_INST TO SET_KEY IN RPT_SET AS J1.
-RUN
TABLE FILE RPT_INST
PRINT RPT_NAME
      INSTANCE_ID
      USER_NAME
      SET_NAME
      RPT_SERVER
      EXECUTE_FLAG
      ROLLUP_CODE
WHERE EXECUTE_FLAG EQ 'Y'
WHERE ROLLUP_CODE EQ '&&RCODE'
ON TABLE PCHOLD AS RINSTCHK
END
-RUN
-REMOTE END
TABLE FILE RINSTCHK
PRINT *
ON TABLE HOLD AS HOLDXXX
END
-RUN
-* commented out by Steve
-* -GOTO TRY
-* -RUN

-* If there are records with an execute flag of 'E' display a report and end.
-IF &RECORDS GT 0 GOTO ERR_RPT1;
-RUN

-* Actual Hierarchy update process
-TYPE HIER_UPD
EX HIER_UPD

-RUN

REMOTE DEST=&&SRVR
-REMOTE BEGIN
-INCLUDE EX_IA50
-RUN
-REMOTE END
-RUN
-TRY
-REMOTE BEGIN
SET ASNAMES=ON
SET MESSAGE=OFF
DEFINE FILE ROLLUP
XL01/A8 = '&&EFF_DATE' ;
XL02/I8 = EDIT(XL01) ;
XL03/I8YYMD = XL02 ;
XL04/YYMD = XL03 ;
XL05/A1 = ' ' ;
END

TABLE FILE ROLLUP
PRINT ROLLUP_CODE AS 'ROLLUP_CODE'
      XL04 AS 'INST_EFF'
      XL05 AS 'UPD_STAT'
WHERE ROLLUP_CODE EQ '&&RCODE'
ON TABLE HOLD AS PASS1
END
-RUN
MODIFY FILE ROLLUP
FIXFORM ROLLUP_CODE/8 INST_EFF/YYMD UPD_STAT/1
MATCH ROLLUP_CODE
  ON NOMATCH REJECT
  ON MATCH UPDATE INST_EFF UPD_STAT
DATA ON PASS1
END
REMOTE END
-RUN

-GOTO BACK1


-* Start of Set File Process
-SETFILES
-SET &CPYF_ROD = &&SRVSU_PATH.EVAL || 'ROLLUP.FOC';
-SET &CPYT_ROD = &&LOCAPP_PATH.EVAL || 'ROLLUP.FOC';
-SET &CPYF_ROM = &&SRVSU_PATH.EVAL || 'ROLLUP.MAS';
-SET &CPYT_ROM = &&LOCAPP_PATH.EVAL || 'ROLLUP.MAS';
-SET &CPYF_RSD = &&SRVSU_PATH.EVAL || 'ROLLSET.FOC';
-SET &CPYT_RSD = &&LOCAPP_PATH.EVAL || 'ROLLSET.FOC';
-SET &CPYF_RSM = &&SRVSU_PATH.EVAL || 'ROLLSET.MAS';
-SET &CPYT_RSM = &&LOCAPP_PATH.EVAL || 'ROLLSET.MAS';
-SET &CPYF_STD = &&SRVCOM_PATH.EVAL || 'STRVIW.FOC';
-SET &CPYT_STD = &&LOCAPP_PATH.EVAL || 'STRVIW.FOC';
-SET &CPYF_STM = &&SRVCOM_PATH.EVAL || 'STRVIW.MAS';
-SET &CPYT_STM = &&LOCAPP_PATH.EVAL || 'STRVIW.MAS';

-SET &COPY1 = &&TEMP_PATH.EVAL || 'COPY1.BAT';
FILEDEF COPY1 DISK &COPY1
-RUN
-WRITE COPY1 COPY &CPYF_ROD.EVAL &CPYT_ROD.EVAL
-WRITE COPY1 COPY &CPYF_ROM.EVAL &CPYT_ROM.EVAL
-WRITE COPY1 COPY &CPYF_RSD.EVAL &CPYT_RSD.EVAL
-WRITE COPY1 COPY &CPYF_RSM.EVAL &CPYT_RSM.EVAL
-WRITE COPY1 COPY &CPYF_STD.EVAL &CPYT_STD.EVAL
-WRITE COPY1 COPY &CPYF_STM.EVAL &CPYT_STM.EVAL
-RUN
-* Execute the DOS Bat file to copy the files
DOS COPY1.BAT
-RUN

-SET &LOGFILE = &&TEMP_PATH || 'LOGFILE.DAT';
ERASE &LOGFILE.EVAL
-RUN
-SET &TMPFLD=' ';

FILEDEF LOGFILE CLEAR
FILEDEF LOGFILE DISK &LOGFILE
-RUN

-WRITE LOGFILE &TMPFLD.EVAL

FILEDEF LOGFILE CLEAR
FILEDEF LOGFILE DISK &LOGFILE
-RUN

EX SETF_MNT
-RUN

-SET &SRVFILE=&&SRVCOM_PATH || 'RSETMNT.DAT';
COPY &LOGFILE &SRVFILE
-RUN

REMOTE DEST=&&SRVR
-REMOTE BEGIN
-SET &SRVPATH='D:\TNT\TTRACKER\SYSTEM\DATA\RSETMNT.DAT';
FILEDEF RSETMNT DISK &SRVPATH

MODIFY FILE ROLLSET
COMPUTE UPDTYPE/A3=;
COMPUTE ENDREC/A1=;
FIXFORM UPDTYPE/3 X36
       IF UPDTYPE EQ 'DEL' PERFORM DELETE_REC
  ELSE IF UPDTYPE EQ 'UPD' PERFORM UPDATE_REC
  ELSE IF UPDTYPE EQ 'ADD' PERFORM ADD_REC;
GOTO TOP

CASE ADD_REC
FIXFORM X-39 UPDTYPE/3 ROLLUP_CODE/8 GLOBAL_PARM/8 X20
MATCH ROLLUP_CODE GLOBAL_PARM
  ON NOMATCH INCLUDE
  ON MATCH REJECT
ENDCASE

CASE DELETE_REC
FIXFORM X-39 UPDTYPE/3 ROLLUP_CODE/8 GLOBAL_PARM/8 X20
MATCH ROLLUP_CODE GLOBAL_PARM
  ON NOMATCH REJECT
  ON MATCH DELETE
ENDCASE

CASE UPDATE_REC
FIXFORM X-39 UPDTYPE/3 ROLLUP_CODE/8 GLOBAL_PARM/8 RPT_GROUP/3 RPT_STREAM/8
FIXFORM USE_PARM/8 X1
MATCH ROLLUP_CODE GLOBAL_PARM
  ON NOMATCH REJECT
  ON MATCH UPDATE RPT_GROUP RPT_STREAM USE_PARM
ENDCASE

DATA ON RSETMNT
END
REMOTE DEST=&&SRVR
-REMOTE BEGIN
TABLE FILE ROLLUP
PRINT ROLLUP_CODE
WHERE ROLLUP_CODE EQ '&&RCODE'
ON TABLE HOLD AS HOLD001
END
-RUN
MODIFY FILE ROLLUP
FIXFORM FROM HOLD001
MATCH ROLLUP_CODE
  ON NOMATCH REJECT
  ON MATCH COMPUTE UPD_STAT = ' ';
  ON MATCH UPDATE UPD_STAT
DATA ON HOLD001
END
-REMOTE END
-RUN
-* ADDED TABLE REQUEST TO KEEP OUTPUT FROM SERVER BEING DISPLAYED
TABLE FILE ROLLSET
PRINT *
WHERE RECORDLIMIT EQ 1
ON TABLE PCHOLD AS HOLDXXX
END
-RUN
-REMOTE END


-GOTO BACK1
-* End of Set File process


-* Error Report if the Input File does not exist.
-NOFILE
DEFINE FILE ROLLCDES
FNAME/A60='&FILECHK.EVAL';
END
TABLE FILE ROLLCDES
"The Hierarchy Update process has been cancelled, the input file could"
"not be located:"
PRINT FNAME AS 'Qualified File Name Not Found'
BY ROLLUP AS 'Company'
WHERE ROLLUP EQ '&&RCODE'
ON TABLE SET ONLINE-FMT HTML
END
-RUN
-GOTO XEXIT


-* Error Report if there are records with an execute flag of 'E'.
-ERR_RPT1
DEFINE FILE RINSTCHK
SET_OWNER/A20=IF USER_NAME EQ ' ' THEN 'Unassigned' ELSE USER_NAME;
END
TABLE FILE RINSTCHK
"The Hierarchy Update Process cannot take place at this time"
"because there are Report Instances currently with an Execute"
"Flag of 'E'.  Please try again later."
" "
"Current Instances with an Execute Flag of 'E'"
PRINT SET_OWNER AS SET,OWNER
      SET_NAME AS 'SET NAME'
      RPT_NAME AS 'REPORT NAME'
      INSTANCE_ID AS 'INSTANCE ID'
BY ROLLUP_CODE AS ROLLUP
BY EXECUTE_FLAG AS EXECUTE,FLAG
BY RPT_SERVER AS 'EXECUTED,BY'
ON TABLE SET ONLINE-FMT HTML
END
-RUN

-XEXIT
