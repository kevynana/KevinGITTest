-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-* File DRDTLTVC.FEX
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*----           ----                 ---------------------
-*8/31/00        SAC                   Initial Creation
-*12/2/03        SAC                   Added code for NO RECORDS
-*****************************************************************  
-*
-*
-*************************************************************************
-*DATE         NAME             DESCRIPTION
-*10/04/00    IBISTL-SB         ADDED SET STATEMENTS TO GET THE VALUE
-*                              INTO THIS DRILLED FEX
-*10/5/00     IBISTL-SB         ADDED THE LOGIC FOR LOCATING THE SET FILE
-*                              IN THE WEBPATH AND THEN EXECUTING THE SET
-*                              FILE
-*10/06/00    IBISTL-SB         CHANGED THE PARAMETER IN THE &&FIELD
-*                              STATEMENT TO MIXED CASE.
-*10/06/00    IBISTL-SB         REMOVED THE ';' FROM THE SET ASNAMES 
-*                              STATEMENT
-* 4/30/01    TANDT-SS          ADDED CODE TO CALCULATIONS TO KEEP NEGATIVE
-*                              NUMBER IN PLACE FOR BACK-OUT OF CANCELLATIONS
-*                              AND DUPLICATES
-* 7/24/01    TANDT-SS          CHANGED DEFINE FOR INTL_DOM FLAG.
-* 7/27/01    TANDT-SS          CHANGED TO LOOK AT CITY.INTL_DOM INSTEAD OF
-*                              COUNTRY CODE
-* 1/29/02    TANDT-JK          CHANGED THE NBR_DAYS DEFINE TO TAKE INTO 
-*                              ACCOUNT NEGATIVE BOOKINGS
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-* 5/1603    DEB                CHANGED INTL_DOM DEFINE 
-*                              IF (CITY.INTL_DOM EQ 'D') THEN 'D' ELSE 'I'; 
-*7/26/04    JOY                Added/Changed Norecords logic
-*08/16/04   DEB                CHANGED TO MAKE AVG DAILY RATE DRILLABLE
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-* ADDED TRANTYPE INCLUDE - JK 12/18/14 STORY ID S-06492
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

-*************************************************************************

-DEFAULT &&CPFLAG ='XX';
-DEFAULT &&DIFLAG ='XX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&UDIDGRP = 'C';


-SET &&FIELD = &ROW_LABEL;
-SET &&DIFLAG = &PARM2;
-SET &&CPFLAG = &PARM1; 

-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

-***************************************************
-* CHECK FOR NON-DRILLABLE FIELD
-***************************************************
-* -IF &&FIELD EQ 'Avg. Rate/Day' THEN FIRST ELSE SECOND;

-SECOND

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;

EX RPTRESET
-RUN
-*
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT

-*-SET &&SETF = &SETFNM;
        

-*EX TSSETDB SETFILE=DRDTLTVS

-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX TSSETDB SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-* CM (CURRENT MONTH), PM (PRIOR MONTH), CY (CURRENT YEAR), PY (PRIOR YEAR)
-****
-* &&DIFLAG WILL EQUAL ONE OF THE FOLLOWING:\
-* D (DOMESTIC), I (INTERNATIONAL), B (INTERNATIONAL AND DOMESTIC)
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&SUBHEAD = 'DOMESTIC AND INTERNATIONAL';
-SET &&DIFLAGCHK = ' ';


-IF &&CPFLAG EQ 'CM' THEN GOTO CURMONTH
-  ELSE IF &&CPFLAG EQ 'PM' THEN GOTO PRRMONTH
-  ELSE IF &&CPFLAG EQ 'CY' THEN GOTO CURYEAR
-  ELSE IF &&CPFLAG EQ 'PY' THEN GOTO PRRYEAR
-  ELSE GOTO CURMONTH; 

-CURMONTH
-SET &&DCHK='WHERE INV_DT GE '&&CMFMDT.EVAL' AND INV_DT LE '&&CMTODT.EVAL'';
-SET &&FROMDT = &&CMFMDT;
-SET &&TODT = &&CMTODT;
-GOTO ENDDATE

-PRRMONTH
-SET &&DCHK='WHERE INV_DT GE '&&PMFMDT.EVAL' AND INV_DT LE '&&PMTODT.EVAL'';
-SET &&FROMDT = &&PMFMDT;
-SET &&TODT = &&PMTODT;
-GOTO ENDDATE

-CURYEAR
-SET &&DCHK='WHERE INV_DT GE '&&CYFMDT.EVAL' AND INV_DT LE '&&CYTODT.EVAL'';
-SET &&FROMDT = &&CYFMDT;
-SET &&TODT = &&CYTODT;
-GOTO ENDDATE

-PRRYEAR
-SET &&DCHK='WHERE INV_DT GE '&&PYFMDT.EVAL' AND INV_DT LE '&&PYTODT.EVAL'';
-SET &&FROMDT = &&PYFMDT;
-SET &&TODT = &&PYTODT;
-GOTO ENDDATE

-ENDDATE

-IF &&DIFLAG EQ 'D' THEN GOTO DOMESTIC  
-  ELSE IF &&DIFLAG EQ 'I' THEN GOTO INTERNTL
-  ELSE GOTO FIELDS;

-DOMESTIC
-SET &&DIFLAGCHK = 'WHERE INTL_DOM EQ ''D''';
-SET &&SUBHEAD = 'DOMESTIC ONLY';
-GOTO FIELDS

-INTERNTL
-SET &&DIFLAGCHK = 'WHERE INTL_DOM EQ ''I''';
-SET &&SUBHEAD = 'INTERNATIONAL ONLY';


-*****************IBISTL-SB***************************
-FIELDS
-IF &&FIELD EQ 'Limo Bookings' THEN GOTO BOOK
-  ELSE IF &&FIELD EQ 'Limo Amount' THEN GOTO RENTAMT
-  ELSE IF &&FIELD EQ 'Rental Amount' THEN GOTO RENTAMT
-  ELSE GOTO BOOK;
-*****************IBISTL-SB*****************************
-GOTO OUTSIDE


-RENTAMT
-SET &&RPT_TTL1 = '                   LIMO RENTAL AMOUNT DETAIL    ';
-GOTO OUTSIDE



-BOOK
-SET &&RPT_TTL1 = '                    LIMO BOOKINGS DETAIL    ';
-GOTO OUTSIDE

-OUTSIDE

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN

-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

EX LIMUSE
-RUN

-*-SET &&RPTGRP = 'CAR';
-*EX UDIDGEN
-*-RUN


-*FILEDEF TRAV DISK D:\TNT\TTRACKER\TEMP\TRAV.TXT
-*-RUN


-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;

INTL_DOM/A1 =IF (PICK_CTY.INTL_DOM EQ 'I') THEN 'I' ELSE 'D';
-* INTL_DOM/A1 =IF (PICK_CTY.COUNTRY_CODE EQ 'US' OR ' ') THEN 'D' ELSE 'I';
-* INTL_DOM/A1 =IF (AIRPORT.INTL_DOM EQ 'D' OR ' ') THEN 'D' ELSE 'I';
DROP_OFF_DT/MDYY = DROP_DATE; 
PICK_UP_DT/MDYY = PICKUP_DATE;
NBR_DAYS/P12 = IF NUM_DAYS LT 0 AND NUM_CARS LT 0
                THEN ((NUM_DAYS * NUM_CARS) * (-1)) ELSE
                NUM_DAYS * NUM_CARS;         
INV_DT/YYMD = INVOICE_DATE;
-INCLUDE TRANTYPE  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  LIM_MAIN.CAR_KEY
  BOOKINGS
  CAR_KEY     
  CAR_TYPE    
  DAILY_RATE  
  DROP_CTY.CITY_NAME AS 'DROP_CTY_NAME'       
  DROP_OFF_DT 
  INVOICE_DATE  
  NBR_DAYS    
  PASSNGR_NAME        
  PICK_CTY.CITY_NAME AS 'PICK_CTY_NAME'	
  PICK_UP_DT   
  REFUSE_KEY  
  RENTAL_AMT	
  VENDOR_NAME
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

&&DCHK
&&DIFLAGCHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD

END
-RUN


 

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

-TYPE After Extract
DEFINE FILE RPTHOLD
NOWTOD/A8 WITH NBR_DAYS = HHMMSS (NOWTOD);
END
-RUN
-TYPE Before Report

TABLE FILE RPTHOLD
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM 
NBR_DAYS AS 'DAYS'
BOOKINGS AS 'BOOKINGS'
RENTAL_AMT AS 'RENTAL,AMOUNT'
COMPUTE AVGDR/D12.2CS = IF RENTAL_AMT LT 0 AND NBR_DAYS LT 0
 THEN ((RENTAL_AMT/NBR_DAYS) * (-1)) 
 ELSE RENTAL_AMT/NBR_DAYS; AS 'AVERAGE,DAILY,RATE'

-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY VENDOR_NAME AS 'VENDOR'
BY CAR_TYPE AS 'CAR'
BY PICK_UP_DT AS 'PICKUP,DATE'
BY PICK_CTY_NAME AS 'PICKUP,CITY'
BY DROP_OFF_DT AS 'DROP,DATE'
BY DROP_CTY_NAME AS 'DROP,CITY'
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME SUMMARIZE AS 'TOTAL FOR'
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
"&&FOOTER"
"eTTek Review &DATE AT <NOWTOD <90 &&FOOTR"
-* "&&CRIGHT"

-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END


-XXIT
