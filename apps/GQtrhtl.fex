-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 01/27/04   DEB  CHANGED INT_DOM DEFINE TO MATCH TRAVEL SUMMARY DEFINE
-****************************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT

  LEVEL_DESC/A60  = &&LDESC;
  NBR_DAYS/D12 = IF (NUM_DAYS LT 0) AND (NUM_ROOMS LT 0) THEN
                    ((NUM_DAYS * NUM_ROOMS) * (-1)) ELSE
                    (NUM_DAYS * NUM_ROOMS);
  NBR_ROOMS/D12 = NUM_ROOMS;
  NEW_TOTAL_AMT/P12.2CS = TOTAL_AMT;
-*  INT_DOM/A1 = IF COUNTRY_CODE EQ 'US' THEN 'D' ELSE 'I';   
  INT_DOM/A1 =IF (CITY.INTL_DOM EQ 'I') THEN 'I' ELSE 'D';
 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT CURR_DATE 
  INVOICE_DATE	
  INT_DOM
  LEVEL_DESC	
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
    
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL)
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS QTRHTL
END
-RUN


TABLE FILE QTRHTL
PRINT 
  INVOICE_DATE	
  INT_DOM
  LEVEL_DESC	
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
BY CURR_DATE
ON TABLE HOLD AS QTRHTL1 FORMAT FOCUS INDEX CURR_DATE
END
-RUN


-* Global Reporting JOIN
JOIN QTRHTL1.CURR_DATE IN QTRHTL1 TO GLOBAL.DATE IN GLOBAL AS J1
END


DEFINE FILE QTRHTL1
NAMEC/A30 = CNAME;
 

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
AMT_TOTALX/D20.6 = NEW_TOTAL_AMT * CURR1X;


END

TABLE FILE QTRHTL1
PRINT CURR_DATE
  INVOICE_DATE	
  INT_DOM
  LEVEL_DESC	
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
  NAMEC
  AMT_TOTALX/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HTLHLD
END
-RUN




DEFINE FILE HTLHLD
CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE &&FXDATE.EVAL AND
               INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
END

TABLE FILE HTLHLD
PRINT CURRENT	
  INT_DOM
  NBR_DAYS	
  NBR_ROOMS
  NEW_TOTAL_AMT
  AMT_TOTALX/D16.2CS
  NAMEC
  PRIOR
BY LEVEL_DESC
ON TABLE HOLD AS HTLHLD1
END
-RUN

DEFINE FILE HTLHLD1
-* DEFINES FOR HEADINGS
HTLDAY/A50 = 'Number Of Hotel Nights';
HTLROOM/A50 = 'Number Of Rooms Booked';
HTLAVG/A50 = 'Average Hotel Rate Per Night';
DHTLAVG/A50 = 'Average Domestic Hotel Rate Per Night';
IHTLAVG/A50 = 'Average International Hotel Rate Per Night';
FLAG/A1 = 'A';
-* CURRENT
CHNGT/P12C = IF CURRENT EQ 'Y' THEN NBR_DAYS ELSE 0;
CHDNGT/P12C = IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'D') THEN NBR_DAYS ELSE 0;
CHINGT/P12C = IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'I') THEN NBR_DAYS ELSE 0;
CHROOM/P12C = IF CURRENT EQ 'Y' THEN NBR_ROOMS ELSE 0;
CHAMT/P12.2 = IF CURRENT EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;

CHDAMT/P12.2C = IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'D') THEN 
                 NEW_TOTAL_AMT ELSE 0;
CHIAMT/P12.2C = IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'I') THEN 
                 NEW_TOTAL_AMT ELSE 0;
GCHAMT/P16.2 = IF CURRENT EQ 'Y' THEN AMT_TOTALX ELSE 0;
GCHDAMT/P16.2C = IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'D') THEN 
                 AMT_TOTALX ELSE 0;
GCHIAMT/P16.2C = IF (CURRENT EQ 'Y') AND (INT_DOM EQ 'I') THEN 
                 AMT_TOTALX ELSE 0;


-* PRIOR
PHNGT/P12C = IF PRIOR EQ 'Y' THEN NBR_DAYS ELSE 0;
PHDNGT/P12C = IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'D') THEN NBR_DAYS ELSE 0;
PHINGT/P12C = IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'I') THEN NBR_DAYS ELSE 0;
PHROOM/P12C = IF PRIOR EQ 'Y' THEN NBR_ROOMS ELSE 0;
PHAMT/P12.2 = IF PRIOR EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
PHDAMT/P12.2 = IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'D') THEN 
                  NEW_TOTAL_AMT ELSE 0;
PHIAMT/P12.2 = IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'I') THEN
                  NEW_TOTAL_AMT ELSE 0;
GPHAMT/P16.2 = IF PRIOR EQ 'Y' THEN AMT_TOTALX ELSE 0;
GPHDAMT/P16.2 = IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'D') THEN 
                  AMT_TOTALX ELSE 0;
GPHIAMT/P16.2 = IF (PRIOR EQ 'Y') AND (INT_DOM EQ 'I') THEN
                  AMT_TOTALX ELSE 0;


END

TABLE FILE HTLHLD1
SUM HTLDAY
HTLROOM
HTLAVG
DHTLAVG
IHTLAVG
CHNGT
CHDNGT
CHDAMT
CHROOM
CHAMT
PHNGT
PHROOM
PHAMT
GCHAMT/D16.2CS
GPHAMT/D16.2CS

COMPUTE CHAVG/P12.2 = IF ((CHNGT LT 1) AND (CHNGT GT (-1))) THEN 0 ELSE
                         (CHAMT/CHNGT);
COMPUTE CHDAVG/P12.2= IF ((CHDNGT LT 1) AND (CHDNGT GT (-1))) THEN 0 ELSE
                         (CHDAMT/CHDNGT);
COMPUTE CHIAVG/P12.2= IF ((CHINGT LT 1) AND (CHINGT GT (-1))) THEN 0 ELSE
                         (CHIAMT/CHINGT);
COMPUTE PHAVG/P12.2 = IF ((PHNGT LT 1) AND (PHNGT GT (-1))) THEN 0 ELSE
                         (PHAMT/PHNGT);
COMPUTE PHDAVG/P12.2 = IF ((PHDNGT LT 1) AND (PHDNGT GT (-1))) THEN 0 ELSE
                         (PHDAMT/PHDNGT);
COMPUTE PHIAVG/P12.2 = IF ((PHINGT LT 1) AND (PHINGT GT (-1))) THEN 0 ELSE
                         (PHIAMT/PHINGT);
COMPUTE GCHAVG/P16.2 = IF ((CHNGT LT 1) AND (CHNGT GT (-1))) THEN 0 ELSE
                         (GCHAMT/CHNGT);
COMPUTE GCHDAVG/P16.2= IF ((CHDNGT LT 1) AND (CHDNGT GT (-1))) THEN 0 ELSE
                         (GCHDAMT/CHDNGT);
COMPUTE GCHIAVG/P16.2= IF ((CHINGT LT 1) AND (CHINGT GT (-1))) THEN 0 ELSE
                         (GCHIAMT/CHINGT);
COMPUTE GPHAVG/P16.2 = IF ((PHNGT LT 1) AND (PHNGT GT (-1))) THEN 0 ELSE
                         (GPHAMT/PHNGT);
COMPUTE GPHDAVG/P16.2 = IF ((PHDNGT LT 1) AND (PHDNGT GT (-1))) THEN 0 ELSE
                         (GPHDAMT/PHDNGT);
COMPUTE GPHIAVG/P16.2 = IF ((PHINGT LT 1) AND (PHINGT GT (-1))) THEN 0 ELSE
                         (GPHIAMT/PHINGT);






-* INCREASE DECREASE COMPUTES
COMPUTE IDHNGT/P12.2 = IF ((PHNGT LT 1) AND (PHNGT GT (-1))) THEN 0 ELSE
                          (((CHNGT-PHNGT)/PHNGT)*100);
COMPUTE IDHRM/P12.2 = IF ((PHROOM LT 1) AND (PHROOM GT (-1))) THEN 0 ELSE
                         (((CHROOM-PHROOM)/PHROOM)*100);
COMPUTE IDHAVG/P12.2 = IF ((PHAVG LT 1) AND (PHAVG GT (-1))) THEN 0 ELSE
                          (((CHAVG-PHAVG)/PHAVG)*100);
COMPUTE IDHDAVG/P12.2 = IF ((PHDAVG LT 1) AND (PHDAVG GT (-1))) THEN 0 ELSE
                           ((CHDAVG-PHDAVG)/PHDAVG)*100;
COMPUTE IDHIAVG/P12.2 = IF ((PHIAVG LT 1) AND (PHIAVG GT (-1))) THEN 0 ELSE
                           ((CHIAVG-PHIAVG)/PHIAVG)*100;



COMPUTE GIDHAVG/P16.2 = IF ((GPHAVG LT 1) AND (GPHAVG GT (-1))) THEN 0 ELSE
                          (((GCHAVG-GPHAVG)/GPHAVG)*100);
COMPUTE GIDHDAVG/P16.2 = IF ((GPHDAVG LT 1) AND (GPHDAVG GT (-1))) THEN 0 ELSE
                           ((GCHDAVG-GPHDAVG)/GPHDAVG)*100;
COMPUTE GIDHIAVG/P16.2 = IF ((GPHIAVG LT 1) AND (GPHIAVG GT (-1))) THEN 0 ELSE
                           ((GCHIAVG-GPHIAVG)/GPHIAVG)*100;
BY FLAG
ON TABLE HOLD AS HTLTTL FORMAT FOCUS INDEX FLAG
END
-RUN



