-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-*  09/12/2016  REJ  S-24432 Changes for ER5 testing.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.
-* 10/02/2017 RSB S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-*                         Removed the old cright_footer and replaced with -INCLUDE FOOTERSM
-*                         With the embedded stylesheets added: TYPE=FOOTING, LINE=&FTR_DateTimeLine, STYLE=BOLD,POSITION=(&FTR_DatePosit),$ 
-**********************************************************************
 
-* Non Compliance Multi-level Drilldown
 
SET ASNAMES = ON
 
-INCLUDE SETECHO

-* FTR_DateTimePosit sets the position of the Post-trip date/time stamp text within the footer image.
-* The default line number (&FTR_DateTimeLine) in the FOOTING for the date/time stamp text is: 4.
-INCLUDE FTR_DateTimePosit
-*
 
-DEFAULT &FPARM = ' ';
-SET &TOTAL_LEVELS = &TOTAL_LEVELS;
-SET &TOTAL_LEV = &TOTAL_LEVELS + 1;
-SET &NEW_LEV = &C_LEVEL + 1;
-SET &FLD1 = IF &NEW_LEV GE &TOTAL_LEV THEN 'AROLL_LEV' || '&C_LEVEL.EVAL' ELSE 'AROLL_LEV' || '&NEW_LEV.EVAL';
-SET &FLD1A = 'AROLL_LEV' || '&C_LEVEL.EVAL';
-SET &FLDD = IF &NEW_LEV GE &TOTAL_LEV THEN 'AROLL_DSC' || '&C_LEVEL.EVAL' ELSE 'AROLL_DSC' || '&NEW_LEV.EVAL';
-SET &FLDD1 = 'AROLL_DSC' || '&C_LEVEL.EVAL';
-SET &W1 = 'WHERE ' | '&FLD1A.EVAL EQ ' | ''&SORT1.EVAL''';
-SET &LEVS_LEFT = &LEV_LEFT - 1;
-SET &LEV_LEFT = &LEVS_LEFT;
-SET &C_LEVEL = &C_LEVEL;
-SET &BREAK = &BREAK.EVAL;
-SET &SETFNM = &SETFNM;
-SET &INSTID = &INSTID;
-SET &STREAM = &STREAM;
-SET &RUN_FLAG = &RUN_FLAG;
-SET &&ROLLUP = &ROLUP;
-SET &&UNAME = &user;
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
EX RESET
-RUN
 
-IF &RUN_FLAG EQ 'M' THEN GOTO MULTIPLE;
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
 
-SET &IID = &INSTID;
 
-**********DETERMINE DATE AND TIME FOR FILE TO SAVE
-SET &NOWTM = HHMMSS ('A8');
-SET &NOWTM = EDIT(&NOWTM,'99$99$99');
-SET &NOWDT = &YYMD;
-SET &NOWDTTM = &NOWDT || '_' || &NOWTM;
-SET &STRMID = &STREAM || '_' || &IID;
-SET &&DTTMSTRM = &NOWDTTM || '_' || &STRMID;
-SET &&DTTMID = &NOWDTTM || '_' || &IID;
 
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
-RUN
 
-CHKFILE
STATE &FILEXIST
-IF &RETCODE NE 0 GOTO NOFILE;
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
-GOTO AMULTI;
 
 
-MULTIPLE;
 
-SET &IID = &INSTID;
 
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-* -SET &FILEXIST = &&WEBPATH || '&SETFNM.EVAL' || '.FTM';
-SET &FILEXIST = &FILEXIST;
 
-CHKFILE
STATE &FILEXIST
-IF &RETCODE NE 0 GOTO NOFILE;
 
-* -SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
 
-AMULTI;
 
EX SETDM SETFILE=&&SETF
-RUN
 
 
-IF &&FROMAVRP NE 'Y' GOTO :DRD1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:DRD1
 
-INCLUDE GetForDrillDown
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
-SET &&RPT_TTL2 = <&FLDD1.EVAL;
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DDTA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-:DDTA1
-RUN
 
 
-INCLUDE SELCATQT
-RUN
 
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
-IF &NEW_LEV GE &TOTAL_LEV THEN GOTO LEVEND;
 
-* Air extract portion
 
EX AIRUSE
-RUN
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
 
DEFINE FILE &&EXTRACT
  MXTRAD/A1 = IF (DOD_TYPE NE 'H' OR 'I' OR 'J' OR 'M' OR 'X' OR 'A' OR 'L' OR
                  'N' OR 'S') THEN 'Y' ELSE 'N';
  FARE_PAID/D12 = SEG_AMT + SEG_TAX;
  FLAG/A1 = 'A';
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
 
-* DEFINES ON PREVIOUS DEFINES
DECLINE_AMT/D12.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         TKT_AMOUNT - LOWEST_AMT;
MXADV/A1 = IF (ADV_PURCH LT 7) THEN 'Y' ELSE 'N';
MXADV1/A1 = IF (ADV_PURCH GE 7) AND (ADV_PURCH LT 14) THEN 'Y' ELSE 'N';
MXADV_TKT/D8CS = IF (MXADV EQ 'Y') THEN NET_TKT_CNT ELSE 0;
MXADV_TKT1/D8CS = IF (MXADV1 EQ 'Y') THEN NET_TKT_CNT ELSE 0;
MXTRA_TKT/D8CS = IF (MXTRAD EQ 'Y') THEN NET_TKT_CNT ELSE 0;
CGDOM_TKT/D8CS = IF (CITYPAIR.INTL_DOM EQ 'D') THEN NET_TKT_CNT ELSE 0;
LS100/D8CS = IF ((DECLINE_AMT GE 100) AND (DECLINE_AMT LE 200)) OR
                ((DECLINE_AMT LE (-100)) AND (DECLINE_AMT GE (-200)))
                THEN NET_TKT_CNT ELSE 0;
LS201/D8CS = IF (DECLINE_AMT GE 201) AND (DECLINE_AMT LE 300) OR
                (DECLINE_AMT LE (-201)) AND (DECLINE_AMT GE (-300))
                THEN NET_TKT_CNT ELSE 0;
LS301/D8CS = IF (DECLINE_AMT GE 301) OR (DECLINE_AMT LE (-301))
                THEN NET_TKT_CNT ELSE 0;
REFUNDABLE/D8CS = IF NONREF_FLAG EQ 'R' OR ' ' THEN NET_TKT_CNT ELSE 0;
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT
  AROLL_LEV1
  AROLL_LEV2
  AROLL_LEV3
  AROLL_LEV4
  AROLL_LEV5
  AROLL_LEV6
  AROLL_LEV7
  AROLL_DSC1
  AROLL_DSC2
  AROLL_DSC3
  AROLL_DSC4
  AROLL_DSC5
  AROLL_DSC6
  AROLL_DSC7
  CGDOM_TKT
  FARE_PAID
  LS100
  LS201
  LS301
  MXADV_TKT
  MXADV_TKT1
  MXTRA_TKT
  NET_TKT_CNT
  REFUNDABLE
WHERE VOID.VOID_DATE EQ 0
-INCLUDE &AIRDATES
&W1
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
 
-INCLUDE RPTPARMS
ON TABLE HOLD AS ARPTHLD
END
-RUN
 
TABLE FILE ARPTHLD
SUM CGDOM_TKT
  FARE_PAID
  LS100
  LS201
  LS301
  MXADV_TKT
  MXADV_TKT1
  MXTRA_TKT
  NET_TKT_CNT
  REFUNDABLE
BY &FLD1
BY &FLDD
BY &FLDD1
ON TABLE HOLD AS MDNONHLD
END
-RUN
 
-* Hotel Extract
 
 
-IF &&FROMAVRP NE 'Y' GOTO :DDTA2;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-:DDTA2
-RUN
 
EX HTLUSE
-RUN
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';
 
DEFINE FILE &&EXTRACT
  CITY_ST/A40 = PROP_STATE||(' '| PROP_CITY);
  NBR_DAYS/D12=NUM_DAYS;
  NBR_ROOMS/D12 = NUM_ROOMS;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
  REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
 
-* Defines on Previous Defines
 
RATE_RM1/A1 = IF ROOM_RATE GT 150  OR
                 ROOM_RATE LT (-150) THEN 'Y' ELSE 'N';
 
-* Defines on previously defines defines
TOTAL_RATE/D14.2C = IF RATE_RM1 EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
TOTAL_ROOM/D12 = IF RATE_RM1 EQ 'Y' THEN NNROOMS ELSE 0;
TOTAL_NON/D12 = IF REFUSE_CD NE 'A' OR 'N' OR 'S' OR 'M' OR 'T' OR 'R'
                THEN NNROOMS ELSE 0;
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT AROLL_LEV1
      AROLL_LEV2
      AROLL_LEV3
      AROLL_LEV4
      AROLL_LEV5
      AROLL_LEV6
      AROLL_LEV7
      AROLL_DSC1
      AROLL_DSC2
      AROLL_DSC3
      AROLL_DSC4
      AROLL_DSC5
      AROLL_DSC6
      AROLL_DSC7
      NNROOMS
      NEW_TOTAL_AMT
      TOTAL_NON
      TOTAL_RATE
      TOTAL_ROOM
-INCLUDE &HTLDATES
&W1
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
 
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTLHOLD
END
-RUN
 
TABLE FILE HTLHOLD
SUM NNROOMS
    NEW_TOTAL_AMT
    TOTAL_NON
    TOTAL_RATE
    TOTAL_ROOM
BY &FLD1
BY &FLDD
BY &FLDD1
ON TABLE HOLD AS MDNONHTL
END
-RUN
 
-* Car Extract Portion
 
-IF &&FROMAVRP NE 'Y' GOTO :DDTA3;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-:DDTA3
-RUN
 
EX CARUSE
-RUN
-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';
 
DEFINE FILE &&EXTRACT
BOOKINGS/D12 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
-* ****DEFINES ON PREVIOUS DEFINES****
NOMIDBK/D12S = IF  (CAR_CAT NE 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY'
                  OR 'COMPACT') THEN BOOKINGS
                  ELSE 0;
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT AROLL_LEV1
      AROLL_LEV2
      AROLL_LEV3
      AROLL_LEV4
      AROLL_LEV5
      AROLL_LEV6
      AROLL_LEV7
      AROLL_DSC1
      AROLL_DSC2
      AROLL_DSC3
      AROLL_DSC4
      AROLL_DSC5
      AROLL_DSC6
      AROLL_DSC7
      BOOKINGS
      NOMIDBK
-INCLUDE &CARDATES
&W1
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS CARHOLD
END
-RUN
 
TABLE FILE CARHOLD
SUM  BOOKINGS
     NOMIDBK
BY &FLD1
BY &FLDD
BY &FLDD1
ON TABLE HOLD AS MDNONCAR
END
-RUN
 
MATCH FILE MDNONHLD
PRINT CGDOM_TKT
  FARE_PAID
  LS100
  LS201
  LS301
  MXADV_TKT
  MXADV_TKT1
  MXTRA_TKT
  NET_TKT_CNT
  REFUNDABLE
BY &FLD1
BY &FLDD
BY &FLDD1
ON TABLE HOLD
RUN
FILE MDNONHTL
SUM NNROOMS
    NEW_TOTAL_AMT
    TOTAL_NON
    TOTAL_RATE
    TOTAL_ROOM
BY &FLD1
BY &FLDD
BY &FLDD1
AFTER MATCH HOLD AS MAH OLD-AND-NEW
END
-RUN
 
MATCH FILE MAH
PRINT CGDOM_TKT
  FARE_PAID
  LS100
  LS201
  LS301
  MXADV_TKT
  MXADV_TKT1
  MXTRA_TKT
  NET_TKT_CNT
  NNROOMS
  NEW_TOTAL_AMT
  REFUNDABLE
  TOTAL_NON
  TOTAL_RATE
  TOTAL_ROOM
BY &FLD1
BY &FLDD
BY &FLDD1
ON TABLE HOLD
RUN
FILE MDNONCAR
SUM BOOKINGS
    NOMIDBK
BY &FLD1
BY &FLDD
BY &FLDD1
AFTER MATCH HOLD AS MDNONHLD1 OLD-AND-NEW
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
DEFINE FILE MDNONHLD1
NOWTOD/A8 WITH &FLD1 = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN
 
TABLE FILE MDNONHLD1
-INCLUDE &&PAHDR
SUM MXADV_TKT AS 'DAY,ADVANCE,PURCHASE,0-6,TICKETS'
COMPUTE
MXADVP/P12CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE
                  ((MXADV_TKT/NET_TKT_CNT)*100); AS '% OF,0-6,TICKETS'
MXADV_TKT1 AS 'DAY,ADVANCE,PURCHASE,7-13,TICKETS'
COMPUTE
MXADVP1/P12CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE
                  ((MXADV_TKT1/NET_TKT_CNT)*100); AS '% OF,7-13,TICKETS'
REFUNDABLE AS 'REFUNDABLE,TICKETS'
COMPUTE
REFP/P12CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE
                ((REFUNDABLE/NET_TKT_CNT)*100); AS '% OF,REFUNDABLE,TICKETS'
MXTRA_TKT AS 'TRADITIONAL,TICKETS'
COMPUTE
MXTRAP/P12CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE
                  ((MXTRA_TKT/NET_TKT_CNT)*100); AS '% OF,TRADITIONAL,TICKETS'
LS100 AS 'LOST SAVINGS,$100-$200,TICKETS'
COMPUTE
LS100P/P12CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE
                  ((LS100/NET_TKT_CNT)*100); AS
                  '% OF TICKETS WITH,LOST SAVINGS,$100-$200'
LS201 AS 'LOST SAVINGS,$201-$300,TICKETS
COMPUTE
LS201P/P12CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE
                  ((LS201/NET_TKT_CNT)*100); AS
                  '% OF TICKETS WITH,LOST SAVINGS,$201-$300'
LS301 AS 'LOST SAVINGS,$301+'
COMPUTE
LS301P/P12CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE
                  ((LS301/NET_TKT_CNT)*100); AS
                  '% OF TICKETS WITH,LOST SAVINGS,$301+'
TOTAL_ROOM AS 'NUMBER OF,ROOMS GREATER,THAN $150'
COMPUTE HAVG/P12CS = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                        ((TOTAL_ROOM/NNROOMS)*100);
                        AS '% OF ROOMS,GREATER THAN $150'
TOTAL_NON AS 'NUMBER OF,NON-PREFERRED ROOMS'
COMPUTE
HNONP/P12CS = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                 ((TOTAL_NON/NNROOMS)*100);
                 AS '% OF,NON-PREFERRED ROOMS'
NOMIDBK AS 'NUMBER OF,BOOKINGS,LARGER THAN,MIDSIZE'
COMPUTE
NOMIDP/P12CS = IF ((BOOKINGS LT 1) AND (BOOKINGS GT (-1))) THEN 0 ELSE
                  ((NOMIDBK/BOOKINGS)*100);
                  AS '% OF,CARS LARGER THAN,MIDSIZE'
BY &FLD1 AS 'LEVEL'
BY &FLDD AS 'DESCRIPTION'
FOOTING
-INCLUDE FOOTERSM
 
ON TABLE SET STYLE *
TYPE=DATA, COLUMN=N1, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='AVI118DR' SORT1=&FLD1 LEV_LEFT=&LEV_LEFT C_LEVEL=&NEW_LEV.EVAL TOTAL_LEVELS=&TOTAL_LEVELS \
FILEXIST ='&FILEXIST' RUN_FLAG='M' BREAK=&BREAK.EVAL SETFNM='&SETFNM' FPARM='&FPARM' \
INSTID='&INSTID' STREAM='&STREAM' \
ROLUP='&ROLUP' user = '&user.EVAL'), $
 
 
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.300000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=22, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=22, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=3, JUSTIFY=LEFT, $
 
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,  $
-*      BACKCOLOR=(RGB(242 242 242) 'WHITE' ),$
 
 
 
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$
 
TYPE=TITLE, JUSTIFY=LEFT, $
 
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=FOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=SUBHEAD,
     STYLE=BOLD,
$
 
TYPE=SUBTOTAL,
     STYLE=BOLD,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $
 
TYPE=GRANDTOTAL, JUSTIFY=LEFT, STYLE=BOLD, $
 
 
 
TYPE=REPORT, COLUMN=P3, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P4, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P5, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P6, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P7, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P8, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P9, JUSTIFY=LEFT, SQUEEZE=0.9, $
TYPE=REPORT, COLUMN=P10, JUSTIFY=LEFT, SQUEEZE=0.9, $
TYPE=REPORT, COLUMN=P11, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P12, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P13, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P14, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P15, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P16, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P17, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P18, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P19, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P20, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P21, JUSTIFY=LEFT, SQUEEZE=0.8, $
TYPE=REPORT, COLUMN=P22, JUSTIFY=LEFT, SQUEEZE=0.8, $
 
 TYPE=FOOTING, LINE=&FTR_DateTimeLine, STYLE=BOLD,POSITION=(&FTR_DatePosit),$
 
ENDSTYLE
ON &FLDD RECOMPUTE MULTILINES AS 'TOTAL FOR'
&&OUTLINE1
&&OUTLINE2


END
-RUN
-GOTO XXIT
 
-LEVEND;
 
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE COMPANY'S LAST LEVEL OF HIERARCHY HAS BEEN REACHED</H2></CENTER>
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>YOUR BROWSER'S BACK BUTTON</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
 
-XXIT
 
 
