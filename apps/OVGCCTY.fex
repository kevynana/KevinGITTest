-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492
-* S-08064 - 4/3/2015 - removed All Other from Top Market graph

-INCLUDE SETECHO
SET ASNAMES = ON

DEFINE FILE MR_50
ND_CP_CD/A10  = ND_ORG || '-' || ND_DST;
FARE_PAID/D15.2 = SEG_AMT + SEG_TAX ;
MRK_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                     (FARE_PAID GE 0) AND
                     (SEG_COUNT EQ 1) AND
                     (CONJ_REL EQ 1) THEN 1 ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (EX_FLAG NE ' ') THEN (-1) ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (RF_FLAG NE ' ') THEN (-1) ELSE 0;

CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
CMAMT/D15.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
PMAMT/D15.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CMCNT/D8CS = IF CURRENT EQ 'Y' THEN MRK_CNT ELSE 0;
PMCNT/D8CS = IF PRIOR EQ 'Y' THEN MRK_CNT ELSE 0;
-INCLUDE TRANTYPE
END
-RUN

TABLEF FILE MR_50
PRINT AIRLINE_NAME
      CMAMT
      CMCNT
      CMCNT
      PMAMT
      PMCNT
      ND_CP_CD
      TKT_DATE
WHERE VOID.VOID_DATE EQ 0
WHERE (TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL)
   OR (TRN_DATE GE &&FXDATE.EVAL AND TRN_DATE LE &&TXDATE.EVAL)
&&WHERE1
&&WHERE2
&&WHERE3



-INCLUDE RPTPARMS
ON TABLE HOLD AS OVMHOLD
END
-RUN

TABLE FILE OVMHOLD
PRINT AIRLINE_NAME
      CMAMT
      CMCNT
      CMCNT
      PMAMT
      PMCNT
      ND_CP_CD
BY TKT_DATE
ON TABLE HOLD AS OVMHLD FORMAT FOCUS INDEX TKT_DATE
END
-RUN

-* Global Reporting JOIN
JOIN OVMHLD.TKT_DATE IN OVMHLD TO GLOBAL.DATE IN GLOBAL AS J1


DEFINE FILE OVMHLD
CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
  CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
  CAMT/D20.6 = CMAMT * CURR1X;
  PAMT/D20.6 = PMAMT * CURR1X;

-* DEFINES ON PREVIOUSLY DEFINES DEFINES

  CAMTX/P15.2 = CAMT;
  PAMTX/P15.2 = PAMT;
END
-RUN

TABLE FILE OVMHLD
PRINT AIRLINE_NAME
      CAMTX
      CMCNT
      CMCNT
      NAMEC
      PAMTX
      PMCNT
BY ND_CP_CD
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS OVMHLD1
END
-RUN


TABLE FILE OVMHLD1
SUM CAMTX CMCNT NAMEC
BY ND_CP_CD
ON TABLE HOLD AS CPHOLD1 
END
-RUN

TABLE FILE OVMHLD1
SUM PAMTX PMCNT  NAMEC
BY ND_CP_CD
ON TABLE HOLD AS CPHOLD2 
END
-RUN

MATCH FILE CPHOLD1
PRINT CAMTX
      CMCNT
      NAMEC
BY ND_CP_CD
ON TABLE HOLD
RUN
FILE CPHOLD2
PRINT PAMTX
      PMCNT
      NAMEC
BY ND_CP_CD
AFTER MATCH HOLD AS CPHOLD3 OLD-OR-NEW
END

TABLE FILE CPHOLD3
SUM CAMTX
    CMCNT
    PAMTX
    PMCNT 
    NAMEC
BY ND_CP_CD
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS CPHOLD4 
END
-RUN


TABLE FILE CPHOLD4
PRINT *
RANKED BY HIGHEST CMCNT NOPRINT
ON TABLE HOLD AS CPHOLD5 
END
-RUN


-SET &RECFOUND = &RECORDS;

DEFINE FILE CPHOLD5
  LIST/D9 = IF ND_CP_CD NE LAST ND_CP_CD THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
-*************************************************************************
-*DATE          NAME               DESCRIPTION
-*10/11/00    IBISTL-SB         CHANGED THE ARANK FIELD TO 'A11'
-*10/11/00    IBISTL-SB         CHANGED RANK_VALUE FIELD
-*************************************************************************
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 3 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL/A30 = IF RANK_VALUE NE 'OTHER' THEN ND_CP_CD
                      ELSE 'All Other';
END
-RUN

TABLE FILE CPHOLD5
SUM CMCNT
    CAMTX
    PMCNT
    PAMTX
    NAMEC
BY RANK_VALUE NOPRINT
BY RANK_LABEL
ON TABLE HOLD AS CPHOLD6 
END
-RUN


TABLE FILE CPHOLD6
SUM CMCNT
    CAMTX
    PMCNT
    PAMTX
    NAMEC
BY RANK_LABEL
WHERE RANK_LABEL NE 'All Other'
ON TABLE HOLD AS CPHOLD7
END
-RUN

GRAPH FILE CPHOLD7
SUM CMCNT AS 'Current'
    PMCNT AS 'Prior'
BY RANK_LABEL AS ' '
ON TABLE SET 3D OFF
ON TABLE SET GRAPHSTYLE *
setGraphType(24);
setLegendDisplay(true);
setLegendPosition(2);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),false);
setFontSize(getLegendText(),15);
setFontStyle(getLegendText(),0);
setLegendMarkerPosition(1);
setO1LabelDisplay(true);
setFontSizeAbsolute(getO1Label(),true); 
setAutofit(getO1Label(),false);
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),15);
setO1LabelRotate(0);
setO1MajorGridDisplay(false);
setO1MinorGridDisplay(true);
setFontSizeAbsolute(getY1Label(),true);
setAutofit(getY1Label(),false);
setY1LabelDisplay(true);
setY1LabelFormat(1);
setFontSize(getY1Label(),15);
setFontStyle(getY1Label(),0);
setY1AxisSide(0);
setFontSizeAbsolute(getTitle(),true); 
setAutofit(getTitle(),false);
setTitleString("Top Markets");
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),0);
setFontSize(getTitle(),15);
setSubtitleString(" ");
setDataTextDisplay(false);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 115 160 247));

ENDSTYLE
ON GRAPH SAVE AS MARKET FORMAT SVG
END
-RUN

-IF &&OUTFMT NE 'PDF' GOTO NOGRAPH1 ELSE GOTO GRAPH1;
-NOGRAPH1;
-SET &&M_GIF = 'N';
-GOTO GRAPHN1;
-GRAPH1;
-SET &&M_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';
-GRAPHN1;


 


DEFINE FILE CPHOLD6
LABEL1/A30 = 'Top Markets';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL1 ELSE LABEL1||'-'||NAMEC;
CAT_ORDER/I2 = 3;
END
-RUN

TABLE FILE CPHOLD6
PRINT CATEGORY/A61 AND RANK_LABEL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CAMTX/P15C AS 'VOL_CURR' AND 
CMCNT/P9CS AS 'TKT_CURR' AND PAMTX/P15C  AS 'VOL_PRIOR' 
AND PMCNT/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS CPHOLD7 
END
-RUN

TABLE FILE CPHOLD7
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS CPHOLD8 
END
MODIFY FILE OV1_GC
FIXFORM FROM CPHOLD8
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON CPHOLD8
END
-RUN

-***************************************************************************
-* Start of New Code to add Total Lines
-***************************************************************************
DEFINE FILE CPHOLD5
LABEL1/A30 = 'Top Markets';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL1 ELSE LABEL1||'-'||NAMEC;
RANK_LABEL/A30 = 'Total';
CAT_ORDER/I2 = 3;
END
-RUN
TABLE FILE CPHOLD5
SUM CMCNT
    CAMTX
    PMCNT
    PAMTX
    NAMEC
BY CATEGORY
BY CAT_ORDER
BY RANK_LABEL
ON TABLE HOLD AS CPHOLD6 
END
-RUN

TABLE FILE CPHOLD6
PRINT CATEGORY/A61 AND RANK_LABEL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CAMTX/P15C AS 'VOL_CURR' AND 
CMCNT/P9CS AS 'TKT_CURR' AND PAMTX/P15C  AS 'VOL_PRIOR' 
AND PMCNT/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = 5;    
ON TABLE HOLD AS CPHOLD7 
END
-RUN

TABLE FILE CPHOLD7
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS CPHOLDT
END
-RUN

MODIFY FILE OV1_GC
FIXFORM FROM CPHOLDT
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON CPHOLDT
END
-RUN


