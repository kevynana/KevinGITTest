-**********************************************************************
-* Conagra SLT reports
-*-**********************************************************************
-*1/28/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.
-* 02/24/2017 - DLV - S-22486 CHANGED ettek Review to Post-trip Reporting

 
-TYPE "IN CGGSLT1"
 
-INCLUDE SETECHO
SET ASNAMES=ON
 
-SET &&RPTSUF = 'SLTBYPASSNGER';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');
 
 
 
-INCLUDE FDEFRPTS
-RUN
 
 
CREATE FILE CGSLT1
-RUN
 
-*********************************************************************************
-* Gather/Add Air Info to CGSLT File
-*********************************************************************************
 
DEFINE FILE SR_50
  ADV/A8 = IF (ADV_PURCH LT 7) THEN '0-6' ELSE 'OTHER';
  BRK_LVL/I2 = &&BRK_LVL;
  -* LEVEL_DESC/A60 = &&LDESC;
  LEVEL_DESC/A60 = AROLL_DSC5;
 
  FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D12    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
-* Defines on Previous
CURRENT/A1 = IF (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) THEN 'Y' ELSE 'N';
PRIOR/A1 = IF (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL) THEN 'Y' ELSE 'N';
-* Defines on Previous
CFARE/D12 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
CNET_TKTS/D12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CNET_TKTD/D12 = IF (INTDOM EQ 'D') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
CDOMFP/D12.2 = IF (INTDOM EQ 'D') AND (CURRENT EQ 'Y') THEN FARE_PAID ELSE 0;
CADVT/D12 = IF (ADV EQ '0-6') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
CONL/D12 = IF (ONLINE_TRADITIONAL EQ 'ONLINE') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
CTRAD/D12 = IF (ONLINE_TRADITIONAL EQ 'TRADITIONAL') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PFARE/D12 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
PNET_TKTS/D12 = IF PRIOR EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PNET_TKTD/D12 = IF (INTDOM EQ 'D') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PDOMFP/D12.2 = IF (INTDOM EQ 'D') AND (PRIOR EQ 'Y') THEN FARE_PAID ELSE 0;
PADVT/D12 = IF (ADV EQ '0-6') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PONL/D12 = IF (ONLINE_TRADITIONAL EQ 'ONLINE') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PTRAD/D12 = IF (ONLINE_TRADITIONAL EQ 'TRADITIONAL') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
END
-RUN
 
TABLEF FILE SR_50
PRINT AROLL_DSC6
      CFARE
      CNET_TKTS
      CNET_TKTD
      CDOMFP
      CADVT
      CONL
      CTRAD
      LEVEL_DESC
      PFARE
      PNET_TKTS
      PNET_TKTD
      PDOMFP
      PADVT
      PONL
      PTRAD
WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)
WHERE VOID_DATE EQ 0
-INCLUDE RPTPARMS
ON TABLE HOLD AS TRANONE
END
-RUN
 
TABLE FILE TRANONE
SUM CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
BY LEVEL_DESC
BY AROLL_DSC6
ON TABLE HOLD AS TRANTWO
END
-RUN
 
TABLE FILE TRANTWO
SUM CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
BY LEVEL_DESC
BY AROLL_DSC6
ON TABLE SAVE
END
-RUN
 
MODIFY FILE CGSLT1
FIXFORM LEVEL_DESC/60 AROLL_DSC6/60 CFARE/12 CNET_TKTS/12 CNET_TKTD/12 CDOMFP/12 CADVT/12 CONL/12 CTRAD/12 PFARE/12 PNET_TKTS/12 PNET_TKTD/12 PDOMFP/12 PADVT/12 PONL/12 PTRAD/12
MATCH LEVEL_DESC AROLL_DSC6
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
-*********************************************************************************
-* Gather/Add Car Info to CGSLT File
-*********************************************************************************
 
-IF &&FROMAVRP NE 'Y' GOTO :TA2;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:TA2
 
EX CARUSE
-RUN
 
DEFINE FILE CR_50
BOOKINGS/D12 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
 -* LEVEL_DESC/A60 = &&LDESC;
 LEVEL_DESC/A60 = AROLL_DSC5;
 PAX_NAME/A25 = EDIT(PASSNGR_NAME, '999999999999999999999999');
 RENTAL_DAY/D12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
 NEW_RENTAL_AMT/D12.2 = RENTAL_AMT;
 CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
 PRIOR/A1 = IF INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
-* Defines on Previous
CBOOKINGS/D12 = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
CRENTDAYS/D12 =  IF CURRENT EQ 'Y' THEN RENTAL_DAY ELSE 0;
CRENTAMT/D12.2 = IF CURRENT EQ 'Y' THEN NEW_RENTAL_AMT ELSE 0;
PBOOKINGS/D12 = IF PRIOR EQ 'Y' THEN BOOKINGS ELSE 0;
PRENTDAYS/D12 =  IF PRIOR EQ 'Y' THEN RENTAL_DAY ELSE 0;
PRENTAMT/D12.2 = IF PRIOR EQ 'Y' THEN NEW_RENTAL_AMT ELSE 0;
 
END
 
-RUN
TABLEF FILE CR_50
PRINT AROLL_DSC6
      CBOOKINGS
      CRENTDAYS
      CRENTAMT
      PBOOKINGS
      PRENTDAYS
      PRENTAMT
      LEVEL_DESC
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL) AND (INVOICE_DATE LE &&TXDATE.EVAL)
-INCLUDE RPTPARMS
ON TABLE HOLD AS CAR1
END
-RUN
 
 
TABLE FILE CAR1
SUM CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT
COMPUTE CAVGL/D8S = IF ((CBOOKINGS LT 1) AND (CBOOKINGS GT (-1))) THEN 0 ELSE
                       (CRENTDAYS/CBOOKINGS);
COMPUTE PAVGL/D8S = IF ((PBOOKINGS LT 1) AND (PBOOKINGS GT (-1))) THEN 0 ELSE
                       (PRENTDAYS/PBOOKINGS);
BY LEVEL_DESC
BY AROLL_DSC6
ON TABLE HOLD AS CAR2
END
-RUN
 
TABLE FILE CAR2
SUM CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
BY LEVEL_DESC
BY AROLL_DSC6
ON TABLE SAVE
END
-RUN
 
MODIFY FILE CGSLT1
FIXFORM LEVEL_DESC/60 AROLL_DSC6/60 CRENTDAYS/12 CRENTAMT/12 PRENTDAYS/12 PRENTAMT/12 CAVGL/8 PAVGL/8
MATCH LEVEL_DESC AROLL_DSC6
ON MATCH UPDATE CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
-*********************************************************************************
-* Gather/Add HTL Info to CGSLT File
-*********************************************************************************
 
-IF &&FROMAVRP NE 'Y' GOTO :TA3;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:TA3
 
EX HTLUSE
-RUN
 
DEFINE FILE HR_50
 CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
 -* LEVEL_DESC/A60 = &&LDESC;
 LEVEL_DESC/A60= AROLL_DSC5;
 NBR_DAYS/D12=NUM_DAYS;
 NEW_TOTAL_AMT/D12.2 = TOTAL_AMT;
 NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
 PAX_NAME/A25 = EDIT(PASSNGR_NAME, '999999999999999999999999');
 PRIOR/A1 = IF INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
-********** Defines on Previous -*****************************************************
BOOKINGS/D8 = IF NEW_TOTAL_AMT GE 0 THEN 1 ELSE (-1);
CHNIGHTS/D12 = IF CURRENT EQ 'Y' THEN NNROOMS ELSE 0;
PHNIGHTS/D12 = IF PRIOR EQ 'Y' THEN NNROOMS ELSE 0;
CHAMT/D12 = IF CURRENT EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
PHAMT/D12 = IF PRIOR EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
CBOOK/D12 = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
PBOOK/D12 = IF PRIOR EQ 'Y' THEN BOOKINGS ELSE 0;
END
-RUN
 
 
TABLEF FILE HR_50
PRINT AROLL_DSC6
      CHNIGHTS
      CHAMT
      CBOOK
      LEVEL_DESC
      PHNIGHTS
      PHAMT
      PBOOK
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL) AND (INVOICE_DATE LE &&TXDATE.EVAL)
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTL1
END
-RUN
 
TABLE FILE HTL1
SUM CHNIGHTS CHAMT CBOOK PHNIGHTS PHAMT PBOOK
COMPUTE CHAVGL/D8S = IF ((CBOOK LT 1) AND (CBOOK GT (-1))) THEN 0 ELSE
                        (CHNIGHTS/CBOOK);
COMPUTE PHAVGL/D8S = IF ((PBOOK LT 1) AND (PBOOK GT (-1))) THEN 0 ELSE
                        (PHNIGHTS/PBOOK);
 
BY LEVEL_DESC
BY AROLL_DSC6
ON TABLE HOLD AS HTL2
END
-RUN
 
TABLE FILE HTL2
SUM CHNIGHTS CHAMT PHNIGHTS PHAMT CHAVGL PHAVGL
BY LEVEL_DESC
BY AROLL_DSC6
ON TABLE SAVE
END
-RUN
 
 
MODIFY FILE CGSLT1
FIXFORM LEVEL_DESC/60 AROLL_DSC6/60 CHNIGHTS/12 CHAMT/12 PHNIGHTS/12 PHAMT/12 CHAVGL/8 PHAVGL/8
MATCH LEVEL_DESC AROLL_DSC6
ON MATCH UPDATE CHNIGHTS CHAMT PHNIGHTS PHAMT CHAVGL PHAVGL
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
 
DEFINE FILE CGSLT1
FLAG/A1 = 'A';
END
-RUN
 
TABLE FILE CGSLT1
SUM CFARE CRENTAMT CHAMT PFARE PRENTAMT PHAMT
COMPUTE CTTOTACH = CFARE+CRENTAMT+CHAMT;
COMPUTE PTTOTACH = PFARE+PRENTAMT+PHAMT;
BY FLAG
ON TABLE HOLD AS CGSLTT
END
-RUN
 
DEFINE FILE CGSLT1
FLAG/A1 = 'A';
END
-RUN
 
TABLE FILE CGSLT1
PRINT CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
    CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
    CHNIGHTS PHNIGHTS CHAMT PHAMT CHAVGL PHAVGL LEVEL_DESC AROLL_DSC6
BY FLAG
ON TABLE HOLD AS CGSLT1
END
-RUN
 
MATCH FILE CGSLT1
PRINT CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
    CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
    CHNIGHTS PHNIGHTS CHAMT PHAMT CHAVGL PHAVGL LEVEL_DESC AROLL_DSC6
BY FLAG
ON TABLE HOLD
RUN
FILE CGSLTT
SUM CTTOTACH
    PTTOTACH
BY FLAG
AFTER MATCH HOLD AS CGSLT2 OLD-AND-NEW
END
-RUN
 
TABLE FILE CGSLT2
SUM CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
    CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
    CHNIGHTS PHNIGHTS CHAMT PHAMT CHAVGL PHAVGL
BY LEVEL_DESC
RANKED BY HIGHEST CNET_TKTS
BY AROLL_DSC6
ON TABLE HOLD AS RNK1
END
-RUN
 
DEFINE FILE RNK1
    RANK_LIMIT/I5 = 10;
    CRANK/D5     = RANK;
    BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
    LIST/D6      = IF LEVEL_DESC EQ LAST LEVEL_DESC THEN (LAST LIST + 1) ELSE CRANK;
    FRANKX/A7    = FTOA(LIST, '(D6)', 'A7');
    FRANK/A6     = EDIT(FRANKX,'$999999');
    RANK_VAL/A9 = IF LIST GT RANK_LIMIT THEN 'OTHER' ELSE FRANK;
    RANK_DSC/A60 = IF RANK_VAL NE 'OTHER' THEN AROLL_DSC6 ELSE 'ALL OTHER';
END
-RUN
 
TABLE FILE RNK1
SUM CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
    CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
    CHNIGHTS PHNIGHTS CHAMT PHAMT CHAVGL PHAVGL
BY LEVEL_DESC
BY RANK_VAL
BY RANK_DSC
ON TABLE HOLD AS RNK2
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
DEFINE FILE RNK2
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
-RUN
 
TABLE FILE RNK2
HEADING
" "
"<10 &&RPT_TTL1"
"<10 &&RPT_TTL2"
" "
" "
" <34 PERIOD1: <40 &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
" <34 PERIOD2: <40 &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
" "
"</1"
"<LEVEL_DESC"
" "
SUM
-* Total Air Car Hotel Spend Amounts and Variance
COMPUTE CTOTACH/D12M = CFARE + CRENTAMT + CHAMT; AS 'Current,Air Car,Hotel Spend'
COMPUTE PTOTACH/D12M = PFARE + PRENTAMT + PHAMT; AS 'Prior,Air Car,Hotel Spend'
COMPUTE TOTACHVAR/P8.1S% = IF ((PTOTACH LT 1) AND (PTOTACH GT (-1))) THEN 0 ELSE
                             ((CTOTACH - PTOTACH)/PTOTACH)*100; AS  'Variance'
-* -*Domestic Average Ticket Price and Variance
-* COMPUTE CDAVGTP/D12M = IF ((CNET_TKTD LT 1) AND (CNET_TKTD GT (-1))) THEN 0 ELSE
-*                           (CDOMFP/CNET_TKTD); AS 'Current Domestic,Average Ticket,Price'
-* COMPUTE PDAVGTP/D12M = IF ((PNET_TKTD LT 1) AND (PNET_TKTD GT (-1))) THEN 0 ELSE
-*                           (PDOMFP/PNET_TKTD); AS 'Prior Domestic,Average Ticket,Price'
-* COMPUTE DOMATPVAR/P8.1S% = IF ((PDAVGTP LT 1) AND (PDAVGTP GT (-1))) THEN 0 ELSE
-*                               ((CDAVGTP - PDAVGTP)/PDAVGTP)*100; AS 'Variance'
 
-*0-6 Ticket %s and Variance
-* COMPUTE CADVP/P8.1S% = IF ((CNET_TKTS LT 1) AND (CNET_TKTS GT (-1))) THEN 0 ELSE
-*                           (CADVT/CNET_TKTS)*100; AS 'Current % of Tickets,Purchased,0-6 Days'
-* COMPUTE PADVP/P8.1S% = IF ((PNET_TKTS LT 1) AND (PNET_TKTS GT (-1))) THEN 0 ELSE
-*                           (PADVT/PNET_TKTS)*100; AS 'Prior % of Tickets,Purchased,0-6 Days'
-* COMPUTE ADVPVAR/P8.1S% = IF ((PADVP LT 1) AND (PADVP GT (-1))) THEN 0 ELSE
-*                             ((CADVP-PADVP)/PADVP)*100; AS 'Variance'
 
-*Total Air Spend and Variance %
CFARE/D12M AS 'Current,Air,Spend' PFARE/D12M AS 'Prior,Air,Spend'
COMPUTE AIRSPVAR/P8.1S% = IF ((PFARE LT 1) AND (PFARE GT (-1))) THEN 0 ELSE
                             ((CFARE-PFARE)/PFARE)*100; AS 'Variance'
-*Net Tickets and Variance %
CNET_TKTS AS 'Current,Net,Tickets' PNET_TKTS AS 'Prior,Net,Tickets'
COMPUTE NETTVAR/P8.1S% = IF ((PNET_TKTS LT 1) AND (PNET_TKTS GT (-1))) THEN 0 ELSE
                            ((CNET_TKTS-PNET_TKTS)/PNET_TKTS)*100; AS 'Variance'
-*Traditional Tickets and Variance %
-* CTRAD AS 'Current,Traditional,Net Tickets' PTRAD AS 'Prior,Traditional,Net Tickets'
-* COMPUTE TRADVARP/P8.1S% = IF ((PTRAD LT 1) AND (PTRAD GT (-1))) THEN 0 ELSE
-*                             ((CTRAD-PTRAD)/PTRAD)*100; AS 'Variance'
-*Traditional Tickets % and Variance %
-* COMPUTE CTRADP/P8.1S% = IF ((CNET_TKTS LT 1) AND (CNET_TKTS GT (-1))) THEN 0 ELSE
-*                            (CTRAD/CNET_TKTS)*100; AS 'Current %,of Traditional,Tickets'
-* COMPUTE PTRADP/P8.1S% = IF ((PNET_TKTS LT 1) AND (PNET_TKTS GT (-1))) THEN 0 ELSE
-*                            (PTRAD/PNET_TKTS)*100; AS 'Prior %,of Traditonal,Tickets'
-* COMPUTE TRADPVAR/P8.1S% = IF ((PTRADP LT 1) AND (PTRADP GT (-1))) THEN 0 ELSE
-*                              ((CTRADP-PTRADP)/PTRADP)*100; AS 'Variance'
 
-*Online Tickets and Variance %
-* CONL AS 'Current,Online,Net Tickets' PONL AS 'Prior,Online,Net Tickets'
-* COMPUTE ONLVARP/P8.1S% = IF ((PONL LT 1) AND (PONL GT (-1))) THEN 0 ELSE
-*                             ((CONL-PONL)/PONL)*100; AS 'Variance'
-*Online Tickets % and Variance %
-* COMPUTE CONLDP/P8.1S% = IF ((CNET_TKTS LT 1) AND (CNET_TKTS GT (-1))) THEN 0 ELSE
-*                            (CONL/CNET_TKTS)*100; AS 'Current %,of Online,Tickets'
-* COMPUTE PONLDP/P8.1S% = IF ((PNET_TKTS LT 1) AND (PNET_TKTS GT (-1))) THEN 0 ELSE
-*                            (PONL/PNET_TKTS)*100; AS 'Prior %,of Online,Tickets'
-* COMPUTE ONLPVAR/P8.1S% = IF ((PONLDP LT 1) AND (PONLDP GT (-1))) THEN 0 ELSE
-*                             ((CONLDP-PONLDP)/PONLDP)*100; AS 'Variance'
 
-* Total Hotel Spend and Variance %
CHAMT/D12M AS 'Current,Hotel,Spend' PHAMT/D12M AS 'Prior,Hotel,Spend'
COMPUTE HTLSVAR/P8.1S% = IF ((PHAMT LT 1) AND (PHAMT GT (-1))) THEN 0 ELSE
                           ((CHAMT-PHAMT)/PHAMT)*100; AS 'Variance'
 
-* Average Hotel Rate Per Night and Variance %
COMPUTE CHAVGR/P12CSM = IF ((CHNIGHTS LT 1) AND (CHNIGHTS GT (-1))) THEN 0 ELSE
                           (CHAMT/CHNIGHTS); AS 'Current,Avg Rate,Per Night'
COMPUTE PHAVGR/P12CSM = IF ((PHNIGHTS LT 1) AND (PHNIGHTS GT (-1))) THEN 0 ELSE
                           (PHAMT/PHNIGHTS); AS 'Prior,Avg Rate,Per Night'
COMPUTE HTLRVAR/P8.1S% = IF ((PHAVGR LT 1) AND (PHAVGR GT (-1))) THEN 0 ELSE
                            ((CHAVGR-PHAVGR)/PHAVGR)*100; AS 'Variance'
 
-* Average Lenght of Stay and Variance %
CHAVGL AS 'Current,Avg Length,of Stay' PHAVGL AS 'Prior,Avg Length,of Stay'
COMPUTE HTLSVAR/P8.1S% = IF ((PHAVGL LT 1) AND (PHAVGL GT (-1))) THEN 0 ELSE
                            ((CHAVGL-PHAVGL)/PHAVGL)*100; AS 'Variance'
-* Total Car Spend and Variance %
CRENTAMT AS 'Current,Car,Spend' PRENTAMT AS 'Prior,Car,Spend'
COMPUTE CARSVAR/P8.1S% = IF ((PRENTAMT LT 1) AND (PRENTAMT GT (-1))) THEN 0 ELSE
                            ((CRENTAMT-PRENTAMT)/PRENTAMT)*100; AS 'Variance'
-* Average Daily Rate and Variance %
COMPUTE CCAVGR/P12CSM = IF ((CRENTDAYS LT 1) AND (CRENTDAYS GT (-1))) THEN 0 ELSE
                           (CRENTAMT/CRENTDAYS); AS 'Current,Avg Daily,Rate'
COMPUTE PCAVGR/P12CSM = IF ((PRENTDAYS LT 1) AND (PRENTDAYS GT (-1))) THEN 0 ELSE
                           (PRENTAMT/PRENTDAYS); AS 'Prior,Avg Daily,Rate'
COMPUTE CARRVAR/P8.1S% = IF ((PCAVGR LT 1) AND (PCAVGR GT (-1))) THEN 0 ELSE
                            ((CCAVGR-PCAVGR)/PCAVGR)*100; AS 'Variance'
-* Average length of Rental and Variance %
CAVGL AS 'Current,Avg Rental,Length' PAVGL AS 'Prior,Avg Rental,Length'
COMPUTE CARSVAR/P8.1S% = IF ((PAVGL LT 1) AND (PAVGL GT (-1))) THEN 0 ELSE
                            ((CAVGL-PAVGL)/PAVGL)*100; AS 'Variance'
-* % of Company's Total Spend and Variance %
-* COMPUTE CTOTSP/P8.1S% = IF ((CTTOTACH LT 1) AND (CTTOTACH GT (-1))) THEN 0 ELSE
-*                            (CTOTACH/CTTOTACH)*100; AS 'Current %,of Company's,Total Spend'
-* COMPUTE PTOTSP/P8.1S% = IF ((PTTOTACH LT 1) AND (PTTOTACH GT (-1))) THEN 0 ELSE
-*                            (PTOTACH/PTTOTACH)*100; AS 'Prior %,of Company's,Total Spend'
-* COMPUTE TOTSPVAR/P8.1S% = IF ((PTOTSP LT 1) AND (PTOTSP GT (-1))) THEN 0 ELSE
-*                              ((CTOTSP-PTOTSP)/PTOTSP)*100; AS 'Variance'
BY LEVEL_DESC NOPRINT
BY RANK_VAL NOPRINT
BY RANK_DSC AS ' '
&&OUTLINE1
&&OUTLINE2
FOOTING BOTTOM
"Post-trip Reporting &DATE AT <NOWTOD <60 &&FOOTR"
ON LEVEL_DESC RECOMPUTE MULTILINES AS 'TOTAL FOR'
ON LEVEL_DESC PAGE-BREAK
ON TABLE SET PAGE-NUM OFF
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
 
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=1.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=1.000000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=8, COLOR=BLACK,
    BACKCOLOR=NONE, GRID=ON, STYLE=NORMAL, $
TYPE=TITLE, FONT=TIMES NEW ROMAN, SIZE=9, BACKCOLOR=RGB(128 255 255), $
TYPE=FOOTING, SIZE=6, $
TYPE=TITLE, SIZE=9, STYLE=BOLD+UNDERLINE, $
TYPE=HEADING, SIZE=10, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=REPORT, WRAP= 1,$
TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(&&TNTTRPOS), SIZE=(&&TNTTRSIZ), $
TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLTRPOS), SIZE=(&&ROLTRSIZ), $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, COLOR=BLACK, BACKCOLOR=RGB(128 255 255),$
TYPE=SUBTOTAL, SIZE=8, STYLE=BOLD, COLOR=BLACK, BACKCOLOR=RGB(128 255 255),$
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN
 
 
 
 
 
