-* File MGMTCYC.FEX
-****************************************************************************
-* This program will automate the running of management cycle reports
-* through Report Broker.
-*
-* DATE           NAME             DESCRIPTION
-* 4/18/01        TANDT-SS         NEW PROGRAM
-*
-****************************************************************************

SET ASNAMES=ON

-* JOIN CLEAR *
END
-RUN

-*------------------------------------
-* Find sets to run from RPT_SET
-*------------------------------------
TABLE FILE RPT_SET
PRINT *
WHERE ROLLUP_CODE EQ 'ABCD-R'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'MONTHLY'
ON TABLE HOLD AS SET1
END
-RUN

DEFINE FILE SET1
 NEW_ZIP_FLAG/A1 = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE 'Y';
 NEWSET_EX_FLAG/A1 = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE 'Y';
 NEWSET_FM_DATE/YYMD = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE 20010301;
 NEWSET_TO_DATE/YYMD = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE 20010331;
END
-RUN

TABLE FILE SET1
PRINT SET_KEY
 ROLLUP_CODE
 SET_ID
 SET_FREQ
 SET_NAME
 SET_TITLE1
 SET_TITLE2
 SET_ESC
 PRINTER_TYP
 NEWSET_FM_DATE AS 'FM_DATE'
 NEWSET_TO_DATE AS 'TO_DATE'
 OUTPUT_DEST
 OUTPUT_FORMAT
 NEWSET_EX_FLAG AS 'EXECUTE_FLAG'
 CONTROL_ID
 RUN_SEQ
 USER_NAME
 USER_SECURITY
 NEW_ZIP_FLAG AS 'ZIP_FLAG'
ON TABLE HOLD AS SETA
END
-RUN

-*------------------------------------
-* Update RPT_SET
-*------------------------------------
MODIFY FILE RPT_SET
FIXFORM FROM SETA
MATCH SET_KEY
ON MATCH UPDATE FM_DATE TO_DATE EXECUTE_FLAG ZIP_FLAG
ON NOMATCH REJECT
DATA ON SETA
END
-RUN


-*--------------------------------------------
-* Find reports to run in RPT_INST
-*--------------------------------------------
TABLE FILE RPT_SET
PRINT *
WHERE ROLLUP_CODE EQ 'ABCD-R'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'MONTHLY'
ON TABLE HOLD AS SET1A
END
-RUN

JOIN SET_KEY IN SET1A TO ALL RPT_INST.SET_KEY IN RPT_INST AS JOINSET1

DEFINE FILE SET1A
 NEW_FM_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN ' ' ELSE 20010301;
 NEW_TO_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN ' ' ELSE 20010331;
 NEW_CMFM_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20010301 ELSE ' ';
 NEW_CMTO_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20010331 ELSE ' ';
 NEW_PMFM_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20000301 ELSE ' ';
 NEW_PMTO_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20000331 ELSE ' ';
 NEW_CYFM_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20010101 ELSE ' ';
 NEW_CYTO_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20010331 ELSE ' ';
 NEW_PYFM_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20000101 ELSE ' ';
 NEW_PYTO_DATE/YYMD = IF RPT_TYPE EQ 'STS' THEN 20000331 ELSE ' ';
 NEW_RPT_SERVER/A20 = 'master';
 NEW_EXECUTE_FLAG/A1 = 'Y';
 NEW_DISTRB_FLAG/A2 = 'E';
END
-RUN

TABLE FILE SET1A
PRINT  INST_KEY
 RPT_INST.ROLLUP_CODE
 SET_ID
 RPT_ID
 INSTANCE_ID
 SET_KEY
 RPT_INST.OUTPUT_DEST
 RPT_INST.OUTPUT_FORMAT
 NEW_EXECUTE_FLAG AS 'EXECUTE_FLAG'
 SRD_FLAG
 RPT_NAME
 RPT_GROUP
 RPT_TYPE
 RPT_STREAM
 RPT_HEAD1
 RPT_HEAD2
 RPT_FOOT1
 RPT_FOOT2
 STYLE1
 STYLE2
 RPT_TITLE1
 RPT_TITLE2
 TITLE_ESC
 BODY_ESC
 STYLE3
 RPT_LOOK
 NEW_RPT_SERVER AS 'RPT_SERVER'
 GLOBAL_PARM
 USE_PARM
 NEW_CMFM_DATE AS 'CMFM_DATE'
 NEW_CMTO_DATE AS 'CMTO_DATE'
 NEW_PMFM_DATE AS 'PMFM_DATE'
 NEW_PMTO_DATE AS 'PMTO_DATE'
 NEW_CYFM_DATE AS 'CYFM_DATE'
 NEW_CYTO_DATE AS 'CYTO_DATE'
 NEW_PYFM_DATE AS 'PYFM_DATE'
 NEW_PYTO_DATE AS 'PYTO_DATE'
 LEVEL_KEY
 LEV_SELECT
 SELECT_LEV
 RPT_INST.RUN_SEQ
 NEW_FM_DATE AS 'FM_DATE'
 NEW_TO_DATE AS 'TO_DATE'
 PAIR_DB
 PRINTER_TYP
 RPT_INST.CONTROL_ID
 COVER_DEST
 SUMM_DEST
 RANK_DEST
 SECURE_FLAG
 NEW_DISTRB_FLAG AS 'DISTRB_FLAG'
 LOGO_1
 LOGO_2
ON TABLE HOLD AS INST1
END
-RUN

-*--------------------------------------------
-* Update RPT_INST
-*--------------------------------------------
MODIFY FILE RPT_INST
FIXFORM FROM INST1
MATCH INST_KEY
ON MATCH UPDATE EXECUTE_FLAG RPT_SERVER CMFM_DATE CMTO_DATE
ON MATCH UPDATE PMFM_DATE PMTO_DATE CYFM_DATE CYTO_DATE
ON MATCH UPDATE PYFM_DATE PYTO_DATE FM_DATE TO_DATE DISTRB_FLAG
ON NOMATCH REJECT
DATA ON INST1
END
-RUN

-*----------------------------------------------
-* Copy reports to run to RPT_QUE
-*----------------------------------------------
TABLE FILE RPT_SET
PRINT *
WHERE ROLLUP_CODE EQ 'ABCD-R'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'MONTHLY'
ON TABLE HOLD AS SET2
END
-RUN

-* JOIN CLEAR *
END
-RUN

JOIN SET_KEY IN SET2 TO ALL RPT_INST.SET_KEY IN RPT_INST AS JOINSET2

DEFINE FILE SET2
 ROLL_SEQ/I6=0;
END
-RUN

TABLE FILE SET2
PRINT  INST_KEY
 RPT_INST.ROLLUP_CODE
 USER_NAME
 SET_ID
 INSTANCE_ID
 RPT_INST.EXECUTE_FLAG AS 'EX_FLAG'
 SET_NAME
 RPT_NAME
 RPT_GROUP
 RPT_TYPE
 RPT_STREAM
 GLOBAL_PARM
 ROLL_SEQ
 SET2.RUN_SEQ AS 'SET_SEQ'
 RPT_INST.RUN_SEQ AS 'RPT_SEQ'
ON TABLE HOLD AS INST3
END
-RUN

-*----------------------------
-* Update RPT_QUE
-*----------------------------
MODIFY FILE RPT_QUE
FIXFORM FROM INST3
MATCH INST_KEY
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON INST3
END
-RUN
-*-EXIT