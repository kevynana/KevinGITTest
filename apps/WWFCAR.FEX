-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*4/26/2017-JEM-S-33318-updated logic for no records report


-INCLUDE SETECHO
-SET ASNAMES = ON;
-SET HOLDATTR = ON;
-RUN

-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';

DEFINE FILE &&EXTRACT
  BKD_ONLINE/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';
  LOC_CD/A2 = '01';
  GL_CODE/A4 = '5315';
  DEPT_CD/A3 = EDIT(LEVEL1, '999$$$$$$$$$$$$');
  PROJ_CD/A8 = EDIT(LEVEL1, '$$$99999999$$$$');
  FUND_SRC/A4 = EDIT(LEVEL1, '$$$$$$$$$$$9999');
  AGR_CD/A4 = '0000';
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  PAX/A5 = EDIT(XPSNGR_NM, '99999$$$$$$$$$$');
  LINE_DESC/A38 = XPSNGR_NM||TKT_NUM||PNR_LOCATOR|' '|'C';
  FEE/P5.2S = IF BKD_ONLINE EQ 'Y'  AND TKT_NUM EQ '0000000000' AND PNR_LOCATOR NE LAST PNR_LOCATOR THEN 5 ELSE 0;
  VND_CD/A3 = VENDOR_CODE;
  C_ITIN/A67 = PICKUP_CITY;
  XTRAN_DT/MDYY = INVOICE_DATE; 
  XDPT_DT/MDYY= PICKUP_DATE;
END
-RUN

TABLEF FILE &&EXTRACT
PRINT  
  C_ITIN
  FEE
  LOC_CD
  GL_CODE
  DEPT_CD
  PROJ_CD
  FUND_SRC
  AGR_CD
  INVOICE_DATE
  LEVEL2
  LINE_DESC
  PAX
  PNR_LOCATOR
  TKT_NUM
  VND_CD
  XPSNGR_NM
  XDPT_DT
  XTRAN_DT

-INCLUDE &CARDATES    
WHERE TKT_NUM EQ '0000000000'
WHERE BKD_ONLINE EQ 'Y'
-********************************************************************
-* Where statements to select data specified in user profile
-* *******************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS CARHOLD
END
-RUN

-SET &&CFLG = IF &LINES EQ 0 THEN 'N' ELSE 'Y';
-IF &LINES EQ 0 THEN GOTO NOCREC;

DEFINE FILE CARHOLD
FLAG/A1 = 'C';
END
TABLE FILE CARHOLD
PRINT AGR_CD
  C_ITIN
  FEE
  LOC_CD
  GL_CODE
  DEPT_CD
  PROJ_CD
  FUND_SRC
  LEVEL2
  LINE_DESC
  PAX
  PNR_LOCATOR
  TKT_NUM
  VND_CD AS 'VAL_AIRLINE'
  XDPT_DT
  XPSNGR_NM
  INVOICE_DATE AS 'XTRAN_DT' 
BY FLAG
-IF &&AFLG EQ 'N' THEN GOTO NOAIR;
WHERE NOT PNR_LOCATOR IN FILE AIRHLD2
-NOAIR
ON TABLE HOLD AS WWFC FORMAT FOCUS  
END
-RUN



DEFINE FILE WWFC
PNR_LOC/A8 = '''' |PNR_LOCATOR|'''';
END
TABLE FILE WWFC
PRINT PNR_LOC
ON TABLE SAVE AS CARHLD2
END
-RUN

-NOCREC;
-SET &&CARREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';
