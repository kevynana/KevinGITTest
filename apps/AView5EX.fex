-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File RNKEXTRT.FEX
-********************************************************************
-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-* DATE      NAME              DESCRIPTION
-* 4/3/01    TANDT-SS          ADDED COUNTRY_CODE AND STAY TO ALLOW
-*                             RUN OF CONAGRA COUNTRY ACTIVITY REPORT
-* 4/20/01   TANDT-JK          ADDED PASSNGR_NAME
-* 6/7/01    TANDT-DB          CHANGED DEFINE DUE TO BALACING ISSUES
-*                             (EX1-EX1)
-* 8/27/01   TANDT-JK          ADDED DOD_TYPE
-* 2/4/02    TANDT-JK          ADDED TRIP_LENGTH
-* 4/29/02   TANDT-SC DB       CHANGED MRK_TKT_CNT DEFINE TO CORRECTLY BACK
-*                             OUT PARTIAL REFUNDS AND EXCHANGES
-* 1/17/03   DB                ADDED TRIPS DEFINE
-* 3/27/03   DEB               ADDED AMTK_TKT_CNT DEFINE FOR METX
-* 10/27/03  JK                ADDED PSNGR_CNT DEFINE
-*12/02/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*06/29/16 - RSB - S-20630 - New Employee ID database field
-*                           Added EMP_ID
-********************************************************************
SET ASNAMES = ON

-SET &&IDLEVELBREAK = '';


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

-SET HOLDATTR = ON;

-INCLUDE SETECHO
-RUN

DEFINE FILE &&EXTRACT
  COMM_SEG/D12.2S   = SEG_COM;
  CGC/A6 = EDIT(CREDIT_CARD, '999999$$$$$$$$$$$$');
  CGC1/A3 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$$$$999');
  CGC2/A9 = 'XXXXXXXXX';

-****************************************************************************
-* 10/3/00  IBISTL-RJ    FIELD WAS BEING TRUNCATED IN FOCUS 6, NEED TO RUN A 
-*                       SUBROUTINE IN WEBFOCUS TO TRUNCATE.
  DIR_CP_NMX/A72 = '(' || CITYPAIR.EMBARK || '/' 
           || CITYPAIR.ROUTE || ') ' | 
           EMB_APT.AIRPORT_NAME ||'---' || RTE_APT.AIRPORT_NAME;
  DIR_CP_NM/A50 = SUBSTR(72,DIR_CP_NMX,1,50,50,'A50');
-*
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC1/A80      = &&LDESC;
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_COMM/D12.2S     = (SEG_AMT - SEG_COM);
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_SEG_LOWEST/D9 = SEG_LOWEST;
  NEW_SEG_MILES/D9 = SEG_MILES;
  NEW_STAY/D9 = STAY;
  NEW_TRIP_LENGTH/D12 = TRIP_LENGTH;
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TOTAL_FARE/D12.2S = SEG_AMT + SEG_TAX;
  TRAN_MONTH/MT = TRN_DATE;
  TRP_LEN/D8CS = TRIP_LENGTH * NET_TKT_CNT;
  VASAVINGS/D11.2CS = SEG_DISCOUNT - TOTAL_FARE;
  XMILES/I8C = MILES; 
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
  YCLASS/A9 = IF CLASS EQ 'Y' THEN 'Y' ELSE 'ALL OTHER';

-* ****DEFINES ON PREVIOUS DEFINES****
  AIRLINE_FEE/D8.2CS   = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;
  CGCARD/A18 = CGC||CGC2||CGC1;
  CPMILE/D12.2CS = IF ((SEG_MILES LT 1) AND (SEG_MILES GT (-1))) THEN 0 ELSE
                      (FARE_PAID/SEG_MILES);
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  UA_TKT_CNT/D8CS = IF VAL_AIRLINE EQ 'UA' THEN NET_TKT_CNT ELSE 0;
  TRIPS/D8CS = IF STAY GT 0 THEN 1 ELSE
               IF STAY LT 0 THEN (-1) ELSE 0;
  AMTK_TKT_CNT/D8CS = IF VAL_AIRLINE EQ '2V' THEN NET_TKT_CNT ELSE 0; 
-INCLUDE TRANTYPE  
-INCLUDE IDLEVDEFINE 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT   AIR_KEY 
        AIRLINE 
        AIR_MAIN.INTL_DOM   
        AIRLINE_FEE 
        AIRLINE_NAME
        AMTK_TKT_CNT
        AROLL_DSC4
        AR_BRANCH
        BR_CL_IDX
        CC_NUM
        CGCARD
        CLASS
        CLIENT_NUM  
        COMM_SEG
        EMB_CTY.COUNTRY_CODE AS 'EMB_CNTRY_CODE'
        CPMILE  
        CREDIT_CARD
        DIR_CP_NM
        DISSAVINGS
        DOC_TYPE    
        DOD_TYPE
        EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'    
        EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
        FARE_BAS        
        FARE_PAID
        GSA_GTR 
        LEVEL_DESC  
        EMP_ID  
        LEVEL1  
        LEVEL2
        LEVEL3  
        MILES   
        MRK_TKT_CNT
        NET_COMM    
        NET_FARE    
        NET_TKT_CNT             
        NEW_SEG_LOWEST
        NEW_SEG_MILES
        NEW_STAY
        NEW_TRIP_LENGTH
        ORG_DST
        PASSNGR_NAME
        REF_EXCH_CNT    
        REFUSE_CODE
        REFUSE_KEY  
        RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
        RTE_CTY.COUNTRY_CODE AS 'RTE_CNTRY_CODE'        
        SAVINGS 
        SEG_AMT 
        SEG_COUNT   
        SEG_DISCOUNT    
        SEG_LOWEST
        SEG_NBR 
        SEG_STANDARD    
        SEG_TAX 
        STAY
        TICKET_BRANCH   
        TKT_AMOUNT  
        TKT_NUM 
        TKT_PURCH
        TKT_TYPE    
        TOTAL_FARE  
        TRAN_MONTH
        TRIPS   
        TRN_DATE 
        TRP_LEN
        UA_TKT_CNT  
        VAL_AIRLINE 
        VASAVINGS   
        XPSNGR_NM   
        XTRAN_DT
        YCLASS
        XMILES

-INCLUDE &AIRDATES

WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD 

END
-RUN


-*-EXIT


        
