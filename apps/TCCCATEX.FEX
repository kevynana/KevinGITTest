-* 2/7/14 CREATED FOR NEW TOTAL TRANSACTION REPORT BY ROLLUP AND CATEGORY
-* 8/23/16-S-22623-JEM-Added Business total/annualized/prior number of rental days
-INCLUDE SETECHO
SET ASNAMES = ON
-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================


DEFINE FILE CR_50

EDATE/YYMD=  &&TODT.EVAL ;
EOM_DATE/YYMD=DATEMOV(EDATE,'EOM');
SDATE/YYMD = &&FROMDT.EVAL;
BOM_DATE/YYMD=DATEMOV(SDATE,'BOM');
BOY_DATE1/YYMD=DATEMOV(SDATE, 'BOY');

ANNUAL_BEGIN/YYMD = &&FROMDT.EVAL;
P_ANNUAL_BEGIN/YYMD=DATEMOV(DATEADD(BOY_DATE1, 'M', (-12)), 'BOM');

P_ANNUAL_END1/YYMD=DATEMOV(DATEADD(P_ANNUAL_BEGIN, 'M', (+0)), 'EOM');

P_ANNUAL_END2/YYMD=DATEMOV(DATEADD(P_ANNUAL_END1, 'M', (+11)), 'EOY');
 
YD/A1 = IF (INVOICE_DATE GE  &&FROMDT.EVAL)  AND (INVOICE_DATE LE  &&TODT.EVAL ) THEN 'Y' ELSE 'N';

P_YD/A1 = IF (INVOICE_DATE GE P_ANNUAL_BEGIN ) AND
            (INVOICE_DATE LE P_ANNUAL_END2) THEN 'Y' ELSE 'N';

  NEW_RENTL_AMT/D14.2C = RENTAL_AMT;

  RENTAL_DAYS/P12 =  IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                  ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;  
  
-*  YTD

 YCAMT/D14.2C = IF YD EQ 'Y' THEN NEW_RENTL_AMT ELSE 0;
 YRENTL/D12C = IF YD EQ 'Y' THEN RENTAL_DAYS ELSE 0;
  
-*  PRIOR  

 PYCAMT/D14.2C = IF P_YD EQ 'Y' THEN NEW_RENTL_AMT ELSE 0;
 PYRENTL/D12C = IF P_YD EQ 'Y' THEN RENTAL_DAYS ELSE 0;

ROLLCD/A8 = '&&CURROLL.EVAL';
END


TABLE  FILE CR_50
SUM YCAMT YRENTL
PYCAMT PYRENTL
-* P_ANNUAL_BEGIN 
-* P_ANNUAL_END2
-* EOM_DATE
COMPUTE CDAYS/I6=(EOM_DATE - ANNUAL_BEGIN) + 1;

BY ROLLCD
BY CATEGORY
WHERE CATEGORY OMITS 'GLOBAL' OR 'TRAVELFUND' OR 'FINANCIAL'
-* &AIRDATES CANNOT BE USED FOR THIS REPORT B/C IT WILL RUN FOR CURRENT MONTH ONLY INSTEAD OF YTD,QTR, ETC
-* -INCLUDE &AIRDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCARYD
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACT DATA FROM ABOVE
-*========================================================================

DEFINE FILE TCARYD
NEW_CAT/A15 = IF CATEGORY EQ 'CONF PLANNING' OR 'CONFERENCE' OR 'MAPS' OR 'OTHER' OR 'SITE INSPEC' THEN 'MEETING' ELSE 
              IF CATEGORY EQ 'NITTOB' OR 'NONBILL' OR 'TOYOB' OR 'TTHUB' THEN 'BUSINESS' ELSE CATEGORY;
END
TABLE FILE TCARYD
SUM YCAMT/D12.2 PYCAMT/D12.2 YRENTL/P12 PYRENTL/P12
COMPUTE NDAYS/I6 = FST.CDAYS;
COMPUTE DIVBY/I6=IF NDAYS GT 365 THEN NDAYS ELSE 365; NOPRINT
COMPUTE CPROSP1/D12.2   = ((YCAMT/FST.CDAYS)*DIVBY) ;
COMPUTE CPRODY1/P12 = ((YRENTL/FST.CDAYS)*DIVBY);
BY ROLLCD
BY NEW_CAT AS 'CATEGORY'
ON TABLE HOLD AS TCARYD2
END
-RUN


DEFINE FILE TCARYD2
CBUS_CAMT/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN YCAMT ELSE 0;
CBUS_CDAY/P12 = IF CATEGORY EQ 'BUSINESS' THEN YRENTL ELSE 0;
CMTG_CAMT/D12.2 = IF CATEGORY EQ 'MEETING' THEN YCAMT ELSE 0;
PBUS_CAMT/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN PYCAMT ELSE 0;
PBUS_CDAY/P12 = IF CATEGORY EQ 'BUSINESS' THEN PYRENTL ELSE 0;
PMTG_CAMT/D12.2 = IF CATEGORY EQ 'MEETING' THEN PYCAMT ELSE 0;
BCPROSP1/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN CPROSP1 ELSE 0;
BCPRODY1/P12 = IF CATEGORY EQ 'BUSINESS' THEN CPRODY1 ELSE 0;
MCPROSP1/D12.2 = IF CATEGORY EQ 'MEETING' THEN CPROSP1 ELSE 0;
END
TABLE FILE TCARYD2
SUM YCAMT PYCAMT CPROSP1
    CBUS_CAMT PBUS_CAMT BCPROSP1 CMTG_CAMT PMTG_CAMT  MCPROSP1
    CBUS_CDAY CPRODY1 BCPRODY1 PBUS_CDAY YRENTL PYRENTL
BY ROLLCD
ON TABLE HOLD AS TCCAR2
END
-RUN


DEFINE FILE TCCAR2
  NMB/I5 = NMB + 1;  
END
TABLE FILE TCCAR2
SUM YCAMT
    YRENTL
    PYCAMT
    PYRENTL
    CPROSP1
    CPRODY1
    CBUS_CAMT 
    CBUS_CDAY
    PBUS_CAMT
    PBUS_CDAY
    BCPROSP1 
    BCPRODY1
    CMTG_CAMT
    PMTG_CAMT  
    MCPROSP1
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCCAR3
END
-RUN

MODIFY FILE TCCARYD2
FIXFORM FROM TCCAR3
DATA ON TCCAR3
END
-RUN

-NEXTONE
