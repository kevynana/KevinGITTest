-* 4/1/14 - DEB - CREATED FOR New Exchange report based on AMN usage
-*5/23/14  DEB   ADDED EX5 TO EX_RF_FLAG AND TCOUNT DEFINE
-* ADDED TRANTYPE INCLUDE - JK 12/18/14 STORY ID S-06492
-* 3/19/15 -  S-06584 - CHANGED TO USE EXTRACT ROUTINE PROVIDED BY RANDY PSOTA - DEB
-*                      CHANGED TO SORT BY CLIENT_NAME TO BALANCE SUMMARY REPORT
-*  10/10/2016  REJ  S-24432 ER5 Testing updates.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
 
 
-INCLUDE SETECHO
 
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&LSEL = 'XX';
 
-SET &&DTSTY = 'DT';
-SET &&PAX = &ROW_LABEL;
-SET &&FIELD = &FLAG;
-SET &&CLIENTNM = &LSEL;
 
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
 
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
 
-TYPE &RETCODE
 
-IF &RETCODE NE 0 GOTO NOFILE;
-**********************************************************************
-* USE ORIGINAL SETFILE
-**********************************************************************
-*EX TSSETDB SETFILE=DRTRNSF
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-RUN
-SET &&SETF='TEMPDRIL';
EX SETDBLS SETFILE=&&SETF
-RUN
 
-IF &&FROMAVRP EQ '' GOTO :OTER5;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:OTER5
 
-INCLUDE GetForDrillDown
-RUN
 
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
 
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
 
 
 
-****
 
-SET &&FULLREFCHK = ' ';
-SET &&FULLEXCHK = ' ';
-SET &&PARTEXCHK = ' ';
-SET &&AIRFEECHK = ' ';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKT,CNT';
-SET &&FAREAMT = 'FARE_AMT AS FARE,AMOUNT';
-SET &&TKTN = 'TICKET_NMBR AS TICKET,NUMBER';
-SET &&TKTN1 = ' ';
-SET &&CLINAME = 'WHERE CLIENT_NAME EQ ''&&CLIENTNM.EVAL''';
 
 
 
-FIELDS
-IF &&FIELD EQ 'DFU' THEN GOTO DOMFULLUSED
-  ELSE IF &&FIELD EQ 'IFU' THEN GOTO INTFULLUSED
-  ELSE IF &&FIELD EQ 'DFRC' THEN GOTO DOMFRC
-  ELSE IF &&FIELD EQ 'IFRC' THEN GOTO INTFRC
-  ELSE IF &&FIELD EQ 'DPU' THEN GOTO DOMPUSED
-  ELSE IF &&FIELD EQ 'IPU' THEN GOTO INPUSED
-  ELSE IF &&FIELD EQ 'DPRC' THEN GOTO DOMPRC
-  ELSE IF &&FIELD EQ 'IPRC' THEN GOTO INTPRC
-  ELSE GOTO DOMFULLUSED;
 
-GOTO OUTSIDE
 
-DOMFULLUSED
-SET &&RPT_TTL1 = 'Domestic Full Used Tickets';
-SET &&EXCHK = 'WHERE EXG_ISS NE 0';
-SET &&INTDOMCHK= 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&NTCOUNT = 'EXG_ISS AS ''EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''D''';
 
-GOTO OUTSIDE
 
 
-INTFULLUSED
-SET &&RPT_TTL1 = 'International Full Used Tickets';
-SET &&EXCHK = 'WHERE EXG_ISS NE 0';
-SET &&INTDOMCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
 
-SET &&NTCOUNT = 'EXG_ISS AS ''EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''I''';
 
-GOTO OUTSIDE
 
 
-DOMFRC
-SET &&RPT_TTL1 = 'Domestic Full Res Change';
-SET &&EXCHK = 'WHERE EXG_ISS NE 0';
-SET &&INTDOMCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
 
-SET &&NTCOUNT = 'EXG_ISS AS ''EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE NOT TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''D''';
 
 
-GOTO OUTSIDE
 
 
-INTFRC
-SET &&RPT_TTL1 = 'International Full Used Tickets';
-SET &&EXCHK = 'WHERE EXG_ISS NE 0';
-SET &&INTDOMCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
 
-SET &&NTCOUNT = 'EXG_ISS AS ''EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE NOT TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''I''';
 
-GOTO OUTSIDE
 
-DOMPUSED
-SET &&RPT_TTL1 = 'Domestic Partial Used Tickets';
-SET &&EXCHK = 'WHERE TKT_TYPE EQ ''P-EX''';
-SET &&INTDOMCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
 
-SET &&NTCOUNT = 'PEXG_ISS1 AS ''PARTIAL,EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''D''';
 
-GOTO OUTSIDE
 
-INPUSED
-SET &&RPT_TTL1 = 'International Partial Used Tickets';
-SET &&EXCHK = 'WHERE TKT_TYPE EQ ''P-EX'' ';
 
-SET &&INTDOMCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
 
-SET &&NTCOUNT = 'PEXG_ISS1 AS ''PARTIAL,EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''I''';
 
-GOTO OUTSIDE
 
-DOMPRC
-SET &&RPT_TTL1 = 'Domestic Partial Used Tickets';
-SET &&EXCHK = 'WHERE TKT_TYPE EQ ''P-EX'' ';
-SET &&INTDOMCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
 
-SET &&NTCOUNT = 'PEXG_ISS1 AS ''PARTIAL,EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE NOT TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''D''';
 
-GOTO OUTSIDE
 
-INTPRC
-SET &&RPT_TTL1 = 'International Partial Used Tickets';
-SET &&EXCHK = 'WHERE TKT_TYPE EQ ''P-EX'' ';
-SET &&INTDOMCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
 
-SET &&NTCOUNT = 'PEXG_ISS1 AS ''PARTIAL,EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-SET &&EXCHGTYPE = 'WHERE NOT TKT_NUM IN FILE DOC3';
-SET &&EXDI = 'WHERE INTL_DOM EQ ''I''';
 
 
 
-OUTSIDE
 
 
 
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DCA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-RUN
-:DCA1
 
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
-*EX AIRUSE
EX AIRUSE
-RUN
 
-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN
 
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
 
DEFINE FILE &&EXTRACT
 
 ALN_NAME/A10 = IF VAL_AIR.VAL_AIRLINE EQ 'UA' OR 'LH' THEN 'UA/LH' ELSE 'OTHER';
 
 CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE'
                                ' ' 'DEFAULT');
 
 
  CLASS1/A25 = IF TKT_CL_CAT CONTAINS 'F' THEN 'FIRST' ELSE
               IF TKT_CL_CAT CONTAINS 'B' THEN 'BUSINESS' ELSE
               IF TKT_CL_CAT CONTAINS 'P' THEN 'PREMIUM ECONOMY' ELSE
               IF TKT_CL_CAT CONTAINS 'E' THEN 'ECONOMY' ELSE
               IF TKT_CL_CAT CONTAINS 'U' THEN 'UPGRADE' ELSE 'DEFAULT';
 
 
  COMM_SEG/D12.2S = SEG_COM;
  CITY/A6 = SUBSTR (176, CNON_STP, 1, 6, 6, CITY);
  DCTRY/A65 = EDIT(TKT_MAIN.CTRY_COD,'$$$9999999999999999999999999999999999999999999999999999999999999999');
  DGZONE/A64 = EDIT(TKT_MAIN.GEO_ZONE,'$$$$9999999999999999999999999999999999999999999999999999999999999999');
  FARE_PAID/D12.2CS   = IF VOID_DATE EQ 0 THEN SEG_AMT + SEG_TAX ELSE 0;
  FARE_PAID1/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC/A60      = &&LDESC;
  MXEXC1/A1 = IF (ORG_DST EQ 'BOSLGA') OR (ORG_DST EQ 'DCALGA') OR
                 (ORG_DST EQ 'BOSDCA') OR (ORG_DST EQ 'LGAPVD') OR
                 (ORG_DST EQ 'BOSEWR') THEN 'N' ELSE 'Y';
  MXEXC2/A1 = IF (ORG_DST EQ 'BOSLGA') OR (ORG_DST EQ 'DCALGA') OR
                 (ORG_DST EQ 'BOSDCA') THEN 'Y' ELSE 'N';
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
 
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) AND (VOID_DATE EQ 0) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') AND (VOID_DATE EQ 0) THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') AND (VOID_DATE EQ 0) THEN (-1) ELSE 0;
 
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT - SEG_COM ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_DISC/D12.2CS = SEG_DISCOUNT;
  OCTRY/A2 = EDIT(TKT_MAIN.CTRY_COD, '99');
  OGZONE/A3 = EDIT(TKT_MAIN.GEO_ZONE, '999');
 
  REF_EXCH_CNT/D8.2CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 0 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  TAX/D12.2S = SEG_TAX;
  TKTN1/A10 = EDIT(TKT_SORT, '9999999999');
  TKTNA/A10 = IF TKTN1 EQ TKT_NUM THEN TKT_NUM ELSE TKTN1;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  VASAVINGS/D12.2CS    = NEW_SEG_DISC - FARE_PAID;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        =
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
 
 
-* ****DEFINES ON PREVIOUS DEFINES****
  AFLAG/A1 = IF ALN_NAME EQ 'OTHER' THEN '2' ELSE '1';
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
 
-* Added for new AD reason code to split out the value added savings
-*   ADVSV/D12.2CS = IF REFUSE_CODE EQ 'AD' THEN VASAVINGS ELSE 0;
ADVSV/D12.2CS = SEG_AGENCY_DISC - FARE_PAID;
-*  NFVSV/D12.2CS = IF REFUSE_CODE NE 'AD' THEN VASAVINGS ELSE 0;
NFVSV/D12.2CS = VASAVINGS;
 
-*The following 2 defined fields are only used for saving/lost saving reports
-*02/04/2003
  SAVING1/P12.2CS = (STD_FARE - (TICKET_AMT + TAX_AMT));
  LSTSAVE1/P12.2CS = ((TICKET_AMT + TAX_AMT) - LOWEST_AMT);
-***** Added for Kiewit exchange by Validating airline Report
ADDCOLLECT/D12.2CSM = IF (VOID_DATE EQ 0) AND (SEG_NBR EQ '01') THEN EXCH_NET_FARE+EXCH_NET_TAX;
AIRLINE_FEE/D12.2CSM = IF (VOID_DATE EQ 0) AND (EMBARK EQ 'FP2' OR 'FP3') THEN FARE_PAID ELSE 0;
EXG_ISS/D12CS = IF (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1) AND (SEG_NBR EQ '01') AND (VOID_DATE EQ 0) THEN 1 ELSE 0;
-*PEXG_ISS/D12CS = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'P') AND (TKT_NUM NE LAST TKT_NUM) THEN 1 ELSE 0;
PEXG_ISS/D12CS = IF (EX_FLAG EQ 'P') AND (SEG_COUNT EQ -1) AND (SEG_NBR NE '01') AND (VOID_DATE EQ 0) THEN 1 ELSE 0;
 
FEXG_AMT/D12.2CSM = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'F') THEN FARE_PAID ELSE 0;
NET_AMT/D12.2CSM = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'F') THEN FARE_PAID ELSE 0;
FULL_RFNDS/D9CS = IF (RF_FLAG EQ 'F') AND (DOC_TYPE NE '1') AND (SEG_COUNT EQ -1) AND (SEG_NBR EQ '01') THEN 1 ELSE 0;
VOIDS/D9CS = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01') THEN 1 ELSE 0;
VOID_AMT/D12.2CS = IF VOID_DATE NE 0 THEN SEG_AMT + SEG_TAX ELSE 0;
-* CONG REPORT
EX_RF_FLAG/A1 = IF (((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 'FP2' OR 'EX5') AND
                     (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1')) THEN 'R' ELSE
                  IF (((EXSALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EMBARK NE 'DT1') AND
                      (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (RF_FLAG EQ ' ')))  THEN 'E'  ELSE ' ';
TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
-INCLUDE TRANTYPE
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT AIR_MAIN.INTL_DOM AS 'INTL_DOM'
    AIRLINE
    AIRLINE.AIRLINE_NAME AS 'ARLN_NAME'
    ARR_TIME
    CITY
    CITYPAIR.EMBARK AS 'EMB_APT_CD'
    CITYPAIR.ROUTE  AS 'RTE_APT_CD'
    CLASS
    CLASS1
    CLASS_CAT
    CLIENT_NAME
    COMM_SEG
    CONJ_REL
    DGZONE
    DCTRY
    DISSAVINGS
    DOC_TYPE
    DOC_NUMBER
    DPT_TIME
    EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
    EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
    EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'
    EX_FLAG
    EXCHG_SALE
    FARE_PAID
    FARE_PAID1
    FARE_BAS
    INT_CODE
    LEVEL_DESC
    LEVEL2
    MXEXC1
    MXEXC2
    MRK_TKT_CNT
    NET_FARE
    NET_TKT_CNT
    NEW_ADV_PURCH
    NEW_SEG_DISC
    NFVSV
    OCTRY
    OGZONE
    PASSNGR_NAME
    PNR_LOCATOR
    REF_EXCH_CNT
    REFUSE_KEY
    REFUSE_CODE
    RESV_BRANCH
    RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
    RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'
    RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'
    SEG_DESIG
    SEG_LOWEST
    SEG_NBR
    TAX
    TKT_MAIN.GEO_ZONE AS 'GEO_ZONE'
    TKT_MAIN.CTRY_COD AS 'CNTRY_CODE'
    TKT_DATE
    TKT_NUM
    TKTNA
    TKT_PURCH
    TKT_SORT
    TKT_TYPE
    TOUR_CODE
    TRN_DATE
    VAL_AIR.AIRLINE_NAME
    VAL_AIRLINE
    VASAVINGS
    XDPT_DT
    XTRAN_DT
    XPSNGR_NM
-* Kiewit Defines
    ADDCOLLECT
    AIRLINE_FEE
    EXG_ISS
    FEXG_AMT
    NET_AMT
    PEXG_ISS
    EX_RF_FLAG
    TKT_TYPE
    VOIDS
    VOID_AMT
    FULL_RFNDS
-INCLUDE &AIRDATES
WHERE VAL_AIRLINE NE 'WN'
WHERE (EX_RF_FLAG EQ 'E') OR (EMBARK EQ 'FP2')
 
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
 
ON TABLE HOLD AS EXHLD
END
-RUN
 
 
 
 
 
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
-TYPE After Extract
TABLE FILE EXHLD
SUM DOC_NUMBER INTL_DOM
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_DATE
BY TKT_NUM
BY DOC_NUMBER
WHERE EXCHG_SALE NE ' '
 
ON TABLE HOLD AS DOCHOLD
END
-RUN
 
 
 
TABLE FILE DOCHOLD
SUM DOC_NUMBER
BY DOC_NUMBER NOPRINT
WHERE DOC_NUMBER OMITS '0000000000'
ON TABLE SAVE AS DOCU
END
-RUN
 
 
-GOTO NewAmnExtract;
 
 
 
DEFINE FILE AMN_ITEM
 CLIENT/A8 = '&&ROLLUP.EVAL';
 ISSUEDATE/YYMD = HDATE(DATE_ISSUED, 'YYMD');
 -*EXPIREDATE/YYMD = HDATE(DATE_EXPIRES, 'YYMD');
 EXPIREDATE/YYMD = HDATE(STATUS_DATE, 'YYMD');
 APPLYDATE/YYMD = HDATE(DATE_SELECTED, 'YYMD');
 DEPART_DT/YYMD = HDATE(DEPART_DATE, 'YYMD');
 PASSNGR_NM/A33 = EDIT(PAX, '999999999999999999999999999999999');
 TRN_DATE/YYMD = ISSUEDATE;
 FST_DPT_DATE/YYMD = DEPART_DT;
 
-*Selections passed from GUI
 BR_CL_IDX/A8 = AGENT_BRANCH | CLIENT_CODE;
 PASSNGR_NAME/A33 = EDIT(PAX, '999999999999999999999999999999999');
 TKT_NUM/A10 = TICKET_NUM;
 VAL_AIRLINE/A3 = AIRLINE_CODE;
 AUX_TKT/A14 = AIRLINE_PNR;
 CUSED_TOT/D12.2 = IF STATUS_CODE EQ 'S' OR 'U' THEN TICKET_VALUE ELSE 0;
 XUNUSED_TOT/D12.2 = IF STATUS_CODE EQ 'X' THEN TICKET_VALUE ELSE 0;
 USED_CNT/D12 = IF TKT_NUM NE LAST TKT_NUM THEN 1 ELSE 0;
END
TABLE FILE AMN_ITEM
PRINT ROLLUP PASSNGR_NAME TYPE_CODE STATUS_CODE APPLYDATE
      AIRLINE_CODE TICKET_VALUE DEPART_DT
      EXPIREDATE NEW_TICKET_NUM NEW_TICKET_VALUE ISSUEDATE CLIENT AUX_TKT
      NEW_LEVEL1 NEW_LEVEL2 NEW_LEVEL3 CUSED_TOT XUNUSED_TOT USED_CNT
BY CLIENT
BY STATUS_CODE
BY TKT_NUM
WHERE ROLLUP EQ '&&ROLLUP.EVAL'
 WHERE TYPE_CODE NE 'R'
 WHERE STATUS_CODE EQ 'S' OR 'U'
 WHERE TKT_NUM IN FILE DOCU
ON TABLE HOLD AS AMNEXCHG
 END
-RUN
 
 
-NewAmnExtract
 
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$$$99');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$$$99');
 
 
-SET &FROMDATE = &FMM || '/' || &FMD || '/' || &FMY;
-SET &TODATE = &TOM || '/' || &TOD || '/' || &TOY;
-TYPE '&FROMDATE.EVAL'
-TYPE '&TODATE.EVAL'
 
 
 
 
 
ENGINE SQLMSS SET DEFAULT_CONNECTION AMNData
SQL SQLMSS EX USP_REVIEW_ETTEK_BANX  '&FROMDATE.EVAL', '&TODATE.EVAL', '&&ROLLUP.EVAL';
TABLE FILE SQLOUT
PRINT *
ON TABLE HOLD AS AMNHLD2 FORMAT FOCUS
END
-RUN
 
 
 
 
 
DEFINE FILE AMNHLD2
CLIENT/A8 = EDIT(rollup, '99999999');
TYPE_CODE/A1 = type_code;
STATUS_CODE/A1 = status_code;
PASSNGR_NAME/A33 = EDIT(pax, '999999999999999999999999999999999');
APPLYDATE/YYMD = HDATE(date_selected, 'YYMD');
AIRLINE_CODE/A2 = airline_code;
TICKET_VALUE/D12.2 = ticket_value;
DEPART_DT/YYMD = HDATE(depart_date, 'YYMD');
EXPIREDATE/YYMD = HDATE(date_expires, 'YYMD');
NEW_TICKET_NUM/A10 = new_ticket_num;
NEW_TICKET_VALUE/D12.2 = new_ticket_value;
ISSUEDATE/YYMD = HDATE(date_issued, 'YYMD');
ROLLUP/A8 = CLIENT;
BR_CL_IDX/A8 = client_branch | client_code;
TKT_NUM/A10 = ticket_num;
 AUX_TKT/A10 = IF EDIT(TKT_NUM, '9') EQ '2' THEN TKT_NUM ELSE ' ';
 AUX_PNR/A16 = airline_pnr;
 CUSED_TOT/D12.2 = IF STATUS_CODE EQ 'S' OR 'U'  THEN TICKET_VALUE ELSE 0;
 XUNUSED_TOT/D12.2 = IF STATUS_CODE EQ 'X'  THEN TICKET_VALUE ELSE 0;
 
 
NEW_LEVEL1/A15 = EDIT(new_level1, '999999999999999');
NEW_LEVEL2/A15 = EDIT(new_level2, '999999999999999');
NEW_LEVEL3/A15 = EDIT(new_level3, '999999999999999');
TNUM/A16 = IF STATUS_CODE EQ 'X' OR 'U' AND TKT_NUM EQ ' ' THEN AUX_PNR ELSE TKT_NUM;
LEVEL1/A15 = EDIT(new_level1, '999999999999999');
LEVEL2/A15 = EDIT(new_level2, '999999999999999');
LEVEL3/A15 = EDIT(new_level3, '999999999999999');
 USED_CNT/D12 = IF TKT_NUM NE LAST TKT_NUM THEN 1 ELSE 0;
 
END
TABLE FILE AMNHLD2
PRINT ROLLUP PASSNGR_NAME TYPE_CODE STATUS_CODE APPLYDATE
      AIRLINE_CODE TICKET_VALUE DEPART_DT
      EXPIREDATE NEW_TICKET_NUM NEW_TICKET_VALUE ISSUEDATE CLIENT AUX_TKT AUX_PNR
      NEW_LEVEL1 AS 'NW_LEV1' NEW_LEVEL2 AS 'NW_LEV2' NEW_LEVEL3 AS 'NW_LEV3' CUSED_TOT XUNUSED_TOT  TNUM
      LEVEL1 LEVEL2 LEVEL3 USED_CNT
BY CLIENT
BY STATUS_CODE
BY TKT_NUM
WHERE ROLLUP EQ '&&ROLLUP.EVAL'
 WHERE STATUS_CODE EQ 'S' OR 'U'
 WHERE TKT_NUM IN FILE DOCU
ON TABLE HOLD AS AMNEXCHG
END
-RUN
 
 
 
TABLE FILE AMNEXCHG
SUM NEW_TICKET_NUM USED_CNT TKT_NUM AS 'DOC_NUMBER' TKT_NUM
BY TKT_NUM NOPRINT
ON TABLE HOLD AS AMNHLD
END
-RUN
 
 
 
 
DEFINE FILE AMNHLD
TKN/A12 = ''''||TKT_NUM||'''';
END
TABLE FILE AMNHLD
PRINT TKN
BY TKN NOPRINT
ON TABLE SAVE AS DOC3
END
-RUN
 
 
 
 
 
 
-TYPE Before Report
 
DEFINE FILE EXHLD
TCOUNT/D8 = IF TKT_TYPE EQ 'P-EX' AND RTE_CTY_NM NE 'EX1' OR 'EX5' AND TKT_NUM NE LAST TKT_NUM THEN 1 ELSE 0;
END
-RUN
TABLE FILE EXHLD
COUNT TCOUNT
BY CLIENT_NAME
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_NUM
BY TKT_SORT
BY TRN_DATE
IF TKT_TYPE EQ 'P-EX'
ON TABLE HOLD AS HOLD1
END
-RUN
 
 
DEFINE FILE HOLD1
TCOUNT1/D8 = IF TCOUNT GT 1 THEN 1 ELSE TCOUNT;
NEW_PCNT/D8 = IF TKT_NUM NE LAST TKT_NUM THEN TCOUNT1 ELSE 0 ;
END
TABLE FILE HOLD1
PRINT NEW_PCNT AS 'TPEXC'
BY CLIENT_NAME
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_NUM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS PHOLD2
END
-RUN
 
 
 
 
 
TABLE FILE EXHLD
SUM NET_TKT_CNT AS 'TTKT'
FARE_PAID AS 'TFARE'
FULL_RFNDS AS 'TRFND'
AIRLINE_FEE AS 'TAFEE'
EXG_ISS AS 'TFEXC'
PEXG_ISS AS 'TPEXC'
TCOUNT
BY CLIENT_NAME
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN
 
MATCH FILE AIRHOLD1
PRINT TTKT
      TFARE
      TRFND
      TAFEE
      TFEXC
BY CLIENT_NAME
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD
RUN
FILE PHOLD2
PRINT TPEXC
BY CLIENT_NAME
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS PHOLD3 OLD-OR-NEW
END
-RUN
 
TABLE FILE EXHLD
PRINT AIRLINE
      AIRLINE_FEE
      CLASS_CAT
      CLIENT_NAME
      EMB_APT_NM
      FARE_PAID
      INTL_DOM
      REFUSE_CODE
      RTE_APT_NM
      TKT_NUM
      TKTNA
      TKT_TYPE
      DPT_TIME
      XDPT_DT
      XTRAN_DT
      TRN_DATE
      EXG_ISS
      PEXG_ISS
      FULL_RFNDS
      DOC_NUMBER
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN
 
MATCH FILE XTWO
PRINT AIRLINE
      AIRLINE_FEE
      CLASS_CAT
      CLIENT_NAME
      EMB_APT_NM
      FARE_PAID
      INTL_DOM
      REFUSE_CODE
      RTE_APT_NM
      TKT_NUM
      TKTNA
      TKT_TYPE
      DPT_TIME
      TRN_DATE
      XDPT_DT
      XTRAN_DT
      EXG_ISS
      PEXG_ISS
      FULL_RFNDS
      DOC_NUMBER
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE PHOLD3
SUM TTKT
    TFARE
    TRFND
    TAFEE
    TFEXC
    TPEXC
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW
END
-RUN
 
DEFINE FILE XTHREE
-*PEXG_ISS1/D12S = IF PEXG_ISS EQ 0 AND EMB_APT_NM OMITS 'EXCHANGE' AND TKT_NUM NE LAST TKT_NUM THEN 1 ELSE PEXG_ISS;
-*PEXG_ISS1/D12S = IF PEXG_ISS EQ 1 AND EMB_APT_NM OMITS 'EXCHANGE' AND TKT_NUM NE LAST TKT_NUM THEN 1 ELSE 0;
PEXG_ISS2/D12S = IF TKT_SORT NE LAST TKT_SORT THEN TPEXC ELSE 0;
PEXG_ISS1/D12S = IF PEXG_ISS2 GT 1 THEN 1 ELSE PEXG_ISS2;
-*PEXG_ISS EQ 1 AND FARE_PAID LT 1 AND TKT_NUM NE LAST TKT_NUM THEN 1 ELSE 0;
 
END
TABLE FILE XTHREE
PRINT AIRLINE
      AIRLINE_FEE
      CLASS_CAT
      CLIENT_NAME
      EMB_APT_NM
      FARE_PAID
      INTL_DOM
      LEVEL_DESC
      PASSNGR_NAME
      REFUSE_CODE
      RTE_APT_NM
      TKT_NUM
      TKTNA
      TKT_TYPE
      TRN_DATE
      DPT_TIME
      XDPT_DT
      XTRAN_DT
      EXG_ISS
      PEXG_ISS
      PEXG_ISS1
      FULL_RFNDS
      TTKT
      TFARE
      TRFND
      TAFEE
      TFEXC
      TPEXC
BY DOC_NUMBER
BY TKT_SORT
BY SEG_NBR
&&EXCHGTYPE
&&EXCHK
&&EXDI
&&CLINAME
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
 
 
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
 
JOIN ROLL WITH TKT_SORT IN AIRHOLD3 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN
 
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-SET &&SP = '|';
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&CSLP = IF &&RCNCT.EVAL GE 15 OR &&CSL.EVAL GE 10 THEN 61 ELSE 68;
-RUN
 
 
 
DEFINE FILE AIRHOLD3
NET_TKT_CNT1/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
TKTN/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKTNA ELSE ' ';
TTYPE/A4 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_TYPE ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;
 
-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/D12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
VOID_AMT/D12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TVOIDA ELSE 0;
ROLL/A8='&&ROLLUP.EVAL';
LVL/A60='&&LVL_DSC.EVAL';
RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
TDATE1/A8MDYY='&MDYY';
TDATE2/A10=EDIT(TDATE1,'99/99/9999');
TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
RD/A10 = 'Run Date:';
RT/A10 = 'Run Time:';
END
-RUN
 
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS';
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
-RUN
 
 
TABLE FILE AIRHOLD3
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT &&TKTN
&&TKTN1
REASON_CD AS 'RSN,CDE'
XDPT_DT AS 'DEPART,DATE'
EMB_APT_NM AS 'ORIGIN'
RTE_APT_NM AS 'DESTINATION'
AIRLINE AS 'CR'
CLASS_CAT AS 'CLASS,CATEGORY'
&&FAREAMT
&&NTCOUNT
 
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY LEVEL_DESC NOPRINT
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY XTKT_SRT NOPRINT BY TKT_SORT NOPRINT
BY TRN_DATE NOPRINT BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY SEG_NBR NOPRINT
BY TTYPE AS 'TKT,TYPE'
BY XTRAN_DTE AS 'TRAN,DATE'
 
-*
 
 
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
 
 
ON PASSNGR_NAME RECOMPUTE MULTILINES AS 'TOTAL FOR'
 
 
 
 
 
FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
"</4"
"<12 PAGE <TABPAGENO  <25 DRTTSM  <40 &M1.EVAL <60 &M2.EVAL  <75 &M3.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
" "
-INCLUDE SBFOOT
 
ON TABLE SET STYLE *
 
-INCLUDE &&PASTYD
 
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;
 
 
-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-GOTO XXIT;
 
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT
 
