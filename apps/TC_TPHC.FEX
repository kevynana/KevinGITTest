-* Updated all VAR %s to divide by prior counts/amounts instead of current Story ID - 07136 - JK 1/23/14
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO
 
CREATE FILE TCHCHN
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCHTLDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TPHCHNDD
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
-********************
-* Final report
-*******************
 
JOIN CHAIN_GROUP IN TCHCHN TO CHAIN_GROUP IN HTL_CGRP
-RUN
 
DEFINE FILE TCHCHN
GRC_NAME/A30 = IF GCHN_NAME EQ ' ' THEN CHAIN_NAME ELSE GCHN_NAME;
GCHN_NM/A30 = LCWORD (30, GRC_NAME, GCHN_NM);
END
TABLE FILE TCHCHN
SUM CBOOK/D12C
    PBOOK/D12C
    CDAYS/D12C
    PDAYS/D12C
    CAMT/D12CM
    PAMT/D12CM
BY GCHN_NM
ON TABLE HOLD AS HOLD1
END
-RUN
 
 
TABLE FILE HOLD1
PRINT CBOOK
    PBOOK
    CDAYS
    PDAYS
    CAMT
    PAMT
RANKED BY HIGHEST CDAYS NOPRINT
BY GCHN_NM
ON TABLE HOLD AS RNKHLD1
END
-RUN
 
 DEFINE FILE RNKHLD1
 -IF '&&SETF.EVAL' EQ 'TCTPHCH' GOTO OTRRNK;
 RANK_LIMIT/I5 = 10;
 -GOTO DONERNK;
 -OTRRNK;
 RANK_LIMIT/I5 = 9999;
 -DONERNK;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
 GNAME_PRINT/A30 = IF RANK_VALUE EQ 'OTHER' THEN 'All Other' ELSE GCHN_NM;
 FLAG/A1 = 'A';
END
-RUN
 
TABLE FILE RNKHLD1
SUM CBOOK
    PBOOK
    CDAYS
    PDAYS
    CAMT
    PAMT
BY FLAG
BY RANK_VALUE
BY GNAME_PRINT
ON TABLE HOLD AS RNKHLD2
END
-RUN
 
TABLE FILE RNKHLD1
SUM CBOOK AS 'TCBOOK'
    PBOOK AS 'TPBOOK'
    CDAYS AS 'TCDAYS'
    PDAYS AS 'TPDAYS'
BY FLAG
ON TABLE HOLD AS TTLH
END
-RUN
 
MATCH FILE RNKHLD2
PRINT CBOOK
    PBOOK
    CDAYS
    PDAYS
    CAMT
    PAMT
    RANK_VALUE
    GNAME_PRINT
BY FLAG
ON TABLE HOLD
RUN
FILE TTLH
SUM TCBOOK
    TPBOOK
    TCDAYS
    TPDAYS
BY FLAG
AFTER MATCH HOLD AS RNKHLD3 OLD-OR-NEW
END
-RUN
 
TABLE FILE RNKHLD3
SUM CBOOK
    PBOOK
    CDAYS
    PDAYS
    CAMT
    PAMT
    TCBOOK
    TPBOOK
    TCDAYS
    TPDAYS
 
-* AVERAGE NIGHTLY RATE
COMPUTE CAVNR/D12CM = IF ((CDAYS LT 1) AND (CDAYS GT (-1))) THEN 0 ELSE (CAMT/CDAYS);
COMPUTE PAVNR/D12CM = IF ((PDAYS LT 1) AND (PDAYS GT (-1))) THEN 0 ELSE (PAMT/PDAYS);
-* AVERAGE STAY
COMPUTE CAVS/D12.2C = IF ((CBOOK LT 1) AND (CBOOK GT (-1))) THEN 0 ELSE (CDAYS/CBOOK);
COMPUTE PAVS/D12.2C = IF ((PBOOK LT 1) AND (PBOOK GT (-1))) THEN 0 ELSE (PDAYS/PBOOK);
-* HOTEL CHAIN BOOKING %S
COMPUTE CHBP/D8.1% = IF ((TCBOOK LT 1) AND (TCBOOK GT (-1))) THEN 0 ELSE ((CBOOK/TCBOOK)*100);
COMPUTE PHBP/D8.1% = IF ((TPBOOK LT 1) AND (TPBOOK GT (-1))) THEN 0 ELSE ((PBOOK/TPBOOK)*100);
-* HOTEL CHAIN NIGHT %S
COMPUTE CHNP/D8.1% = IF ((TCDAYS LT 1) AND (TCDAYS GT (-1))) THEN 0 ELSE ((CDAYS/TCDAYS)*100);
COMPUTE PHNP/D8.1% = IF ((TPDAYS LT 1) AND (TPDAYS GT (-1))) THEN 0 ELSE ((PDAYS/TPDAYS)*100);
-* VARIANCE AMTS/COUNTS/%
COMPUTE VDY1/D12C = CDAYS - PDAYS;
COMPUTE VDY2/P8% = IF ((PDAYS LT 1) AND (PDAYS GT (-1))) THEN 0 ELSE ((VDY1/PDAYS)*100);
COMPUTE VAM1/D12CM = CAMT - PAMT;
COMPUTE VAM2/P8% = IF ((PAMT LT 1) AND (PAMT GT (-1))) THEN 0 ELSE ((VAM1/PAMT)*100);
COMPUTE VBK1/D12C = CBOOK - PBOOK;
COMPUTE VBK2/P8% = IF ((PBOOK LT 1) AND (PBOOK GT (-1))) THEN 0 ELSE ((VBK1/PBOOK)*100);
COMPUTE VANR1/P12CM = CAVNR - PAVNR;
COMPUTE VANR2/P8% = IF ((PAVNR LT 1) AND (PAVNR GT (-1))) THEN 0 ELSE ((VANR1/PAVNR)*100);
COMPUTE VARS1/P5.2C = CAVS - PAVS;
COMPUTE VARS2/P8% = IF ((CAVS LT 1) AND (CAVS GT (-1))) THEN 0 ELSE ((VARS1/PAVS)*100);
COMPUTE VARVPB/P8.1% = CHBP-PHBP;
COMPUTE VARVPN/P8.1% = CHNP-PHNP;
BY RANK_VALUE
BY GNAME_PRINT
ON TABLE HOLD AS HOLD4 FORMAT FOCUS
END
-RUN
 
DEFINE FILE RNKHLD3
RANK_VALUE/A5 = 'TOTAL';
GNAME_PRINT/A30 = 'TOTAL';
END
TABLE FILE RNKHLD3
SUM CBOOK
    PBOOK
    CDAYS
    PDAYS
    CAMT
    PAMT
    TCBOOK
    TPBOOK
    TCDAYS
    TPDAYS
 
-* AVERAGE NIGHTLY RATE
COMPUTE CAVNR/D12CM = IF ((CDAYS LT 1) AND (CDAYS GT (-1))) THEN 0 ELSE (CAMT/CDAYS);
COMPUTE PAVNR/D12CM = IF ((PDAYS LT 1) AND (PDAYS GT (-1))) THEN 0 ELSE (PAMT/PDAYS);
-* AVERAGE STAY
COMPUTE CAVS/D12.2C = IF ((CBOOK LT 1) AND (CBOOK GT (-1))) THEN 0 ELSE (CDAYS/CBOOK);
COMPUTE PAVS/D12.2C = IF ((PBOOK LT 1) AND (PBOOK GT (-1))) THEN 0 ELSE (PDAYS/PBOOK);
-* HOTEL CHAIN BOOKING %S
COMPUTE CHBP/D8.1%S = 100;
COMPUTE PHBP/D8.1%S = 100;
-* HOTEL CHAIN NIGHT %S
COMPUTE CHNP/D8.1%S = 100;
COMPUTE PHNP/D8.1%S = 100;
-* VARIANCE AMTS/COUNTS/%
COMPUTE VDY1/D12C = CDAYS - PDAYS;
COMPUTE VDY2/P8%S = 0;
COMPUTE VAM1/D12CM = CAMT - PAMT;
COMPUTE VAM2/P8%S = 0;
COMPUTE VBK1/D12C = CBOOK - PBOOK;
COMPUTE VBK2/P8%S = 0;
COMPUTE VANR1/P12CM = CAVNR - PAVNR;
COMPUTE VANR2/P8%S = 0;
COMPUTE VARS1/P5.2C = CAVS - PAVS;
COMPUTE VARS2/P8%S = 0;
COMPUTE VARVPB/P8.1%S = 0;
COMPUTE VARVPN/P8.1%S = 0;
BY RANK_VALUE
BY GNAME_PRINT
ON TABLE HOLD AS HOLD5 FORMAT FOCUS
END
-RUN
 
 
 
-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &HRANK1=&HLDPATH || 'HOLD4.FOC';
-SET &HRANK2=&HLDPATH || 'HOLD5.FOC';
-RUN
 
USE CLEAR *
-RUN
 
EX SUUSES
-RUN
 
USE ADD
&HRANK1 AS HOLD4
&HRANK2 AS HOLD4
END
-RUN
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
DEFINE FILE HOLD4
NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
 
TABLE FILE HOLD4
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
SUM CDAYS AS 'CURRENT,# NIGHTS'
    PDAYS AS 'PRIOR, # NIGHTS'
    VDY1 AS 'VAR'
    VDY2/D8%S AS '% VAR'
    CAMT AS 'CURRENT,HTL AMT'
    PAMT AS 'PRIOR, HTL AMT'
    VAM1 AS 'VAR'
    VAM2/D8%S AS '% VAR'
    CBOOK AS 'CURRENT,# BOOKINGS'
    PBOOK AS 'PRIOR, # BOOKINGS'
    VBK1 AS 'VAR'
    VBK2/D8%S AS '% VAR'
    CAVNR AS 'CURRENT,AVG RATE'
    PAVNR AS 'PRIOR,AVG RATE'
    VANR1 AS 'VAR'
    VANR2/P8%S AS '% VAR'
    CAVS AS 'CURRENT,AVG STAY'
    PAVS AS 'PRIOR,AVG STAY'
    VARS1 AS 'VAR'
    VARS2/D8%S AS '% VAR'
    CHBP AS 'CURRENT,BOOKINGS %'
    PHBP AS 'PRIOR,BOOKINGS %'
    VARVPB/D8.1%S AS 'VAR'
    CHNP AS 'CURRENT,NIGHTS %'
    PHNP AS 'PRIOR,NIGHTS %'
    VARVPN/P8.1%S  AS 'VAR'
BY RANK_VALUE NOPRINT
BY GNAME_PRINT AS 'BRAND'
 
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
 
 
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
