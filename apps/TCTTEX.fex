-*06/27/13        JK                 ADDED XPSNGR_NM DEFINE
-*7/22/13         JK                 ADDED MILES
-* 12/2/2016 - DLV - S-27656 - Added code for TCTPT500 to SET &&RANK_LIMIT

-INCLUDE SETECHO 
SET ASNAMES = ON 

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
MIR_SEQ1/A1 = EDIT(MIR_SEQ, '9$$$$$');
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
                           
TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
EXG_ISS/D8CS = IF (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1) AND 
                  (SEG_NBR EQ '01') THEN 1 ELSE 0; 
NEW_SEG_MILES/D12 = CITYPAIR.SEG_MILES;
NEW_SEG_DISC/D12.2CS = SEG_DISCOUNT;
MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;                           
ROLLCD/A8 = '&&ROLL';
XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');

END

TABLEF FILE &&EXTRACT
PRINT ROLL_NAME
  FARE_PAID
  NET_TKT_CNT
  NEW_SEG_MILES
  PASSNGR_NAME
  ROLLCD
  TKT_NUM
  TRN_DATE
  XPSNGR_NM
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCTT1
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;

TABLE FILE TCTT1
SUM FARE_PAID
    NET_TKT_CNT
    NEW_SEG_MILES AS 'MILES'
BY ROLLCD    
BY ROLL_NAME
BY PASSNGR_NAME
ON TABLE HOLD AS TCTT2
END
-RUN


-IF &&SETF EQ 'TCTPTVL1' GOTO TP500;

TABLE FILE TCTT2
PRINT *
BY ROLLCD
BY ROLL_NAME
RANKED BY HIGHEST NET_TKT_CNT NOPRINT
ON TABLE HOLD AS TCTT3
END
-RUN

-IF &&SETF EQ 'TCTPTVL' GOTO NEXTset;

-SET &&RANK_LIMIT = 500;
-GOTO StartDEF;

-NEXTset;

-SET &&RANK_LIMIT = 25;

-StartDEF;


DEFINE FILE TCTT3
 RANK_LIMIT/I5 = &&RANK_LIMIT;
 CRANK/D5 =   RANK;
 BRANK/A6 =   FTOA(CRANK, '(D5)' , 'A6');
 LIST/D6 = IF PASSNGR_NAME NE LAST PASSNGR_NAME THEN (LAST LIST + 1) ELSE LAST LIST;
 FRANKX/A6 = FTOA(LIST, '(D5)', 'A6');
 FRANK/A5=EDIT(FRANKX,'$99999');
 RANK_VALUE/A9 = IF (RANK_LIMIT EQ 0) THEN BRANK ELSE
                 IF LIST GT RANK_LIMIT THEN 'OTHER' ELSE FRANK;
 RANK_LABEL/A33 = IF RANK_VALUE NE 'OTHER' THEN PASSNGR_NAME ELSE ' ';
END
-RUN

TABLE FILE TCTT3
SUM NET_TKT_CNT FARE_PAID MILES
BY ROLLCD
BY ROLL_NAME
BY RANK_VALUE
BY RANK_LABEL
ON TABLE HOLD AS TCTT4
END
-RUN

-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
DEFINE FILE TCTT4
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCTT4
SUM FARE_PAID
    NET_TKT_CNT
    MILES
BY ROLLCD
BY ROLL_NAME
BY RANK_VALUE
BY RANK_LABEL
ON TABLE HOLD AS TCAS
END
-RUN


DEFINE FILE TCAS
 NMB/I5 = NMB + 1;
END

TABLE FILE TCAS
PRINT ROLL_NAME
      RANK_VALUE
      RANK_LABEL
      NET_TKT_CNT
      FARE_PAID
      MILES
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCA1
END
-RUN


MODIFY FILE TCTPTVL
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM TCA1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCA1
END
-RUN
-GOTO NEXTONE

-TP500;
DEFINE FILE TCTT2
RANK_VALUE/A9 = ' ';
RANK_LABEL/A33=' ';
ROLLCD/A8 = '&&ROLL';
END
-RUN

TABLE FILE TCTT2
SUM NET_TKT_CNT
    FARE_PAID
    MILES
BY ROLLCD
BY ROLL_NAME
BY PASSNGR_NAME
BY RANK_VALUE
BY RANK_LABEL
ON TABLE HOLD AS TCTT3
END
-RUN


DEFINE FILE TCTT3
NMB/I5 = NMB + 1;
END
-RUN
TABLE FILE TCTT3
PRINT ROLL_NAME
      RANK_VALUE
      RANK_LABEL
      NET_TKT_CNT
      FARE_PAID
      PASSNGR_NAME
      MILES
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCA1
END
-RUN

MODIFY FILE TCTPTVL
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM TCA1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCA1
END
-RUN
-GOTO NEXTONE

-NEXTONE

