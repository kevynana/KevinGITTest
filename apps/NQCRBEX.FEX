-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the new quarterly review - JK 8/2/05
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK

-******************************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT 


BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
NBR_DAYS/D8 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
                 
TC_AMT/D12.2  = RENTAL_AMT; 
CCQ/A1 = IF INVOICE_DATE GE '&&CQFMDT.EVAL' AND
                INVOICE_DATE LE '&&CQTODT.EVAL' THEN 'Y' ELSE 'N';
-* Defines on Previous Defines

CLIENTCODE/A8 = '&&ROLLUP.EVAL';
-INCLUDE QRFTRANC

END


TABLEF FILE &&EXTRACT
PRINT BOOKINGS       
      PICK_CTY.CITY_NAME/A20 AS 'PICK_CTY_NAME'	
      PICKUP_CITY
      NBR_DAYS
      VENDOR_CODE
      VENDOR_NAME
      CLIENTCODE AS 'ROLLUP_CODE'
      TC_AMT
      YEAR
      TRAN
      CURRENT
      CCQ
      PRIOR
      PYTD
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') OR
      (INVOICE_DATE GE '&&FXDATE.EVAL') AND (INVOICE_DATE LE '&&TXDATE.EVAL')
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
-INCLUDE RPTPARMS
ON TABLE HOLD AS HOLDCARB
END
-RUN


-IF &LINES EQ 0 THEN GOTO NOCBASE;


DEFINE FILE HOLDCARB
CDAY/D8 = IF CURRENT EQ 'Y' THEN NBR_DAYS ELSE 0;
PDAY/D8 = IF PYTD EQ 'Y' THEN NBR_DAYS ELSE 0;
CAMT/P12.2  = IF CURRENT EQ 'Y' THEN TC_AMT ELSE 0;
PAMT/P12.2  = IF PYTD EQ 'Y' THEN TC_AMT ELSE 0;
CBOOKINGS/D8 = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
PBOOKINGS/D8 = IF PYTD EQ 'Y' THEN BOOKINGS ELSE 0;
END

TABLE FILE HOLDCARB
SUM CDAY
    CAMT
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
BY ROLLUP_CODE
BY VENDOR_CODE
BY VENDOR_NAME
ON TABLE HOLD AS ALLBASE2
END
-RUN


TABLE FILE ALLBASE2
PRINT *
RANKED BY HIGHEST CDAY NOPRINT
ON TABLE HOLD AS ALLBASE3
END
-RUN


-SET &RECFOUND = &RECORDS;

DEFINE FILE ALLBASE3
  LIST/D9 = (LAST LIST + 1) ;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 8 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL1/A12 = IF RANK_VALUE NE 'OTHER' THEN VENDOR_CODE
                      ELSE 'ALL OTHER';
  VND_NAME/A12 = IF RANK_VALUE NE 'OTHER' THEN VENDOR_NAME
                      ELSE 'ALL OTHER';                                          
  RANK_LABEL/A12 = LCWORD(12, VND_NAME, RANK_LABEL);
END
-RUN


TABLE FILE ALLBASE3
SUM CDAY 
    CAMT 
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
    VND_NAME/A12 AS 'VENDOR_NAME'
    
    VENDOR_CODE
    
BY ROLLUP_CODE
BY RANK_VALUE 
BY RANK_LABEL1
BY RANK_LABEL
ON TABLE HOLD AS ALLBASE4
END
-RUN


-IF &&SKIPGRAPH EQ 'Y' THEN GOTO AROUNDCAR;
GRAPH FILE ALLBASE4
SUM CDAY AS ' '
ACROSS RANK_LABEL 
ON GRAPH SET GRAPHSTYLE *
setGraphType(55);
-*setPieRotate(90);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 145 207 255));
setLegendDisplay(false);
setTitleString("Car Days");
setAutofit(getTitle(),true);

setTextJustHoriz(getTitle(), 1);
setFontSizeAbsolute(getTitle(),true);
setFontStyle(getTitle(),0);
setPieTilt(65);
setPieDepth(95);
setPieFeelerTextFormat(2);
setPieLabelDisplay(3);
setPlace(true);
ENDSTYLE
ON GRAPH HOLD AS QRCAR FORMAT SVG
END
-RUN

-SET &&G_QRCAR=IF &LINES GT 0 THEN 'Y' ELSE 'N';

-AROUNDCAR;



TABLE FILE ALLBASE4
-*PRINT RANK_LABEL1 AS 'VENDOR_NAME'
PRINT VENDOR_CODE
BY RANK_VALUE NOPRINT
WHERE RANK_LABEL1 NE 'ALL OTHER'
WHERE VENDOR_CODE NE ' '
ON TABLE SAVE AS SAVCVDR
END
-RUN


TABLE FILE ALLBASE4
PRINT ROLLUP_CODE VND_NAME AS 'VENDOR_NAME' RANK_VALUE AS 'RANK' CDAY CBOOKINGS CAMT PDAY PBOOKINGS PAMT 
BY RANK_VALUE NOPRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRCVDR
FIXFORM ROLLUP_CODE/8 VENDOR_NAME/12 RANK/9 CDAY/8 CBOOKINGS/8 CAMT/12 PDAY/8 PBOOKINGS/8 PAMT/12
MATCH ROLLUP_CODE VENDOR_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


TABLE FILE HOLDCARB
SUM CAMT
    CDAY
    PICK_CTY_NAME
BY ROLLUP_CODE
BY PICKUP_CITY
WHERE CURRENT EQ 'Y'
ON TABLE HOLD AS CARCTY
END
-RUN


TABLE FILE CARCTY
PRINT *
RANKED BY HIGHEST CDAY
ON TABLE HOLD AS CARCTY2
END
-RUN


DEFINE FILE CARCTY2
  LIST/D9 = (LAST LIST + 1) ;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RNK_VAL/A9 = IF LIST GT 8 THEN 'OTHER' ELSE ARANK1;
  RNK_LBL1/A20 = IF RNK_VAL NE 'OTHER' THEN PICKUP_CITY
                      ELSE 'ALL OTHER';
  PC_NAME/A20 = IF RNK_VAL NE 'OTHER' THEN PICK_CTY_NAME
                      ELSE 'ALL OTHER';             
END
-RUN

TABLE FILE CARCTY2
SUM CDAY
    CAMT
    PC_NAME/A14 AS 'PICK_CTY_NAME'
BY ROLLUP_CODE
BY RNK_VAL 
BY RNK_LBL 
ON TABLE HOLD AS CARCTY3 
END
-RUN



TABLE FILE CARCTY3
PRINT RNK_LBL1 AS 'PICKUP_CITY'
BY RNK_VAL NOPRINT
WHERE RNK_LBL1 NE 'ALL OTHER'
ON TABLE SAVE AS SAVCITY
END
-RUN


TABLE FILE CARCTY3
PRINT ROLLUP_CODE PC_NAME AS PICK_CTY_NAME RNK_VAL AS 'RANK' CDAY CAMT
ON TABLE SAVE
END
-RUN



MODIFY FILE QRCCTY
FIXFORM ROLLUP_CODE/8 PICK_CTY_NAME/20 RANK/A9 CDAY/8 CAMT/12
MATCH ROLLUP_CODE PICK_CTY_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


DEFINE FILE HOLDCARB
RIGHT_TRAN/A7 = RJUST(7, TRAN, RIGHT_TRAN);
END
TABLE FILE HOLDCARB
SUM NBR_DAYS
    TC_AMT 
BY ROLLUP
BY YEAR
BY RIGHT_TRAN AS 'TRAN'
ON TABLE HOLD AS BQADR
END
-RUN

TABLE FILE BQADR
PRINT ROLLUP_CODE YEAR TRAN NBR_DAYS TC_AMT
ON TABLE SAVE
END
-RUN


MODIFY FILE QRCADR
FIXFORM ROLLUP_CODE/8 YEAR/4 TRAN/7 NBR_DAYS/8 TC_AMT/12
MATCH ROLLUP_CODE YEAR TRAN
ON NOMATCH INCLUDE 
DATA ON SAVE
END
-RUN



-NOCBASE

