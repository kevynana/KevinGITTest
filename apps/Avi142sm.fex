-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*
-****************************************************************************
-* DATE       INITIAL        DESCRIPTION
-*
-* 08/12/2016 RSB            S-19016 - Citypair with connections report
-*                                     (Copy of avi101sm.fex)
-* 08/07/2017 RSB S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-****************************************************************************
-*
SET ASNAMES = ON
-RUN
-*
-IF &&CURRENCY EQ 'USD' THEN GOTO NEXT;
-*
-SET &&SUBHEAD = <NAMEC;
-SET &BASF_CUR = 'Net Segment,Amount, (' | &&CURRENCY || ')';
-*
-SET &&S_SUBJ3 ='GFARE/D16.2C AS ' | '''&BASF_CUR.EVAL''';
-* 
-*****
-NEXT
-*****
-SET &HLDPATH=TEMPPATH(50,'A50');

-*USE CLEAR *
-RUN
-*
EX SUUSES
-RUN
-*
-SET &&RPTSUF = 'SMY';
-*
-INCLUDE FDEFRPTS
-RUN
-*
-* Get DIR_CP_CD with multiple connections across tickets or at least 1 non-stop and a connection.
-* Flaten out the connections by ticket between ORG-DST.
DEFINE FILE CONN_HOLD
 SEG_CON1    /A3 = IF (TKT_NUM NE LAST TKT_NUM) THEN CONNECTION ELSE SEG_CON1;
 SEG_CON2    /A7 = IF (TKT_NUM NE LAST TKT_NUM) THEN ' ' ELSE
                   IF ((TKT_NUM EQ LAST TKT_NUM) AND (SEG_NBR EQ '02') AND (CONNECTION NE LAST CONNECTION)) THEN LAST SEG_CON1 || '-' || CONNECTION ELSE LAST SEG_CON1;
 SEG_CON3    /A11= IF (TKT_NUM NE LAST TKT_NUM) THEN ' ' ELSE
                   IF ((TKT_NUM EQ LAST TKT_NUM) AND (SEG_NBR EQ '03') AND (CONNECTION NE LAST CONNECTION)) THEN LAST SEG_CON2 || '-' || CONNECTION ELSE LAST SEG_CON2;
END
-*
TABLE FILE CONN_HOLD
SUM   DIR_CP_CD
     COMPUTE SEG_CONN /A11 = IF SEG_CON3 NE ' ' THEN SEG_CON3 ELSE
                             IF SEG_CON2 NE ' ' THEN SEG_CON2 ELSE SEG_CON1; NOPRINT   
BY TKT_NUM  
BY TOTAL SEG_CONN  
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS CONN_CNT
END
-RUN
-*
TABLE FILE CONN_CNT
SUM  SEG_CONN   
    COMPUTE CONN_COUNT/I9= IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND (SEG_CONN NE LAST SEG_CONN)) THEN 1 ELSE 0;
BY DIR_CP_CD
BY TKT_NUM NOPRINT
BY SEG_CONN NOPRINT
WHERE TOTAL CONN_COUNT GT 0;
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS CONN_FILTER
END
-RUN
-*
-* Get the data with at least 1 non-stop and a connection or multiple airport connections across tickets.
TABLE FILE CONN_HOLD
SUM   DIR_CP_CD
      FLIGHT_HOURS
      LAYOVER_HOURS
      FARE_PAID
      EMBARK
      ROUTE
      FNL_DEST_AIRPORT
      STOP_CON
      SEG_AMT
      TAX_AMT
      TICKET_CODE
      SEG_COUNT
      CONJ_REL
      EX_FLAG
      RF_FLAG
      C_ITIN
      DPT_DATE
      AIRLINE_NAME
      REFUSE_CODE
      AROLL_LEV1
      AROLL_LEV2
      AROLL_LEV3
      AROLL_LEV4
      AROLL_LEV5
      AROLL_LEV6
      AROLL_LEV7
      AROLL_LEV8
      AROLL_LEV9
      AROLL_LEV10
      AROLL_LEV11 
      AROLL_LEV12
      AROLL_LEV13
      AROLL_LEV14
BY TKT_NUM
BY SEG_NBR
BY HIGHEST CONNECTION
WHERE DIR_CP_CD IN FILE CONN_FILTER;
ON TABLE HOLD AS CONN_HOLDX
END
-*
-* Flaten out the connections by ticket between ORG-DST.
DEFINE FILE CONN_HOLDX
 SEG_CON1    /A3 = IF (TKT_NUM NE LAST TKT_NUM) THEN CONNECTION ELSE SEG_CON1;
 SEG_CON2    /A7 = IF (TKT_NUM NE LAST TKT_NUM) THEN ' ' ELSE
                   IF ((TKT_NUM EQ LAST TKT_NUM) AND (SEG_NBR EQ '02') AND (CONNECTION NE LAST CONNECTION)) THEN LAST SEG_CON1 || '-' || CONNECTION ELSE LAST SEG_CON1;
 SEG_CON3    /A11= IF (TKT_NUM NE LAST TKT_NUM) THEN ' ' ELSE
                   IF ((TKT_NUM EQ LAST TKT_NUM) AND (SEG_NBR EQ '03') AND (CONNECTION NE LAST CONNECTION)) THEN LAST SEG_CON2 || '-' || CONNECTION ELSE LAST SEG_CON2;
END
-*
TABLE FILE CONN_HOLDX
SUM   DIR_CP_CD
      SEG_CON1
      SEG_CON2      
      SEG_CON3
     COMPUTE SEG_CONN  /A11 = IF SEG_CON3 NE ' ' THEN SEG_CON3 ELSE
                              IF SEG_CON2 NE ' ' THEN SEG_CON2 ELSE SEG_CON1; 
     COMPUTE NUM_CONNS  /I9 = IF ((TKT_NUM NE LAST TKT_NUM) AND (SEG_CONN NE ' ')) THEN 1 ELSE 
                              IF ((TKT_NUM EQ LAST TKT_NUM) AND (SEG_CONN NE ' ')) THEN NUM_CONNS + 1 ELSE 0;
     COMPUTE DIRECT_FLTS/I9 = IF (TKT_NUM NE LAST TKT_NUM) AND (SEG_CONN EQ ' ') THEN 1 ELSE 0;
     COMPUTE TOT_FLTS   /I9 = DIRECT_FLTS + NUM_CONNS;
      C_ITIN
      DIRECT_FLTS
      FLIGHT_HOURS
      LAYOVER_HOURS
      FARE_PAID
      EMBARK
      ROUTE
      FNL_DEST_AIRPORT
      STOP_CON
      SEG_AMT
      TAX_AMT
      TICKET_CODE
      SEG_COUNT
      CONJ_REL
      EX_FLAG
      RF_FLAG
      DPT_DATE
      AIRLINE_NAME
      REFUSE_CODE
      AROLL_LEV1
      AROLL_LEV2
      AROLL_LEV3
      AROLL_LEV4
      AROLL_LEV5
      AROLL_LEV6
      AROLL_LEV7
      AROLL_LEV8
      AROLL_LEV9
      AROLL_LEV10
      AROLL_LEV11 
      AROLL_LEV12
      AROLL_LEV13
      AROLL_LEV14
BY TKT_NUM
BY TOTAL SEG_CONN
ON TABLE HOLD AS CONN_HOLDY
END
-RUN
-*
TABLE FILE CONN_HOLDY
SUM   NUM_CONNS 
      TOT_FLTS
      DIRECT_FLTS
      FLIGHT_HOURS
      LAYOVER_HOURS
      FARE_PAID     
     COMPUTE FPAID     /D10.2N= (FARE_PAID / TOT_FLTS);
     COMPUTE FLGT_HOURS/D10.2 = (FLIGHT_HOURS / TOT_FLTS);
     COMPUTE LAYOVR    /D10.2S= IF LAYOVER_HOURS EQ 0 THEN 0 ELSE (LAYOVER_HOURS / NUM_CONNS);
     COMPUTE TOT_FLT   /D12.2 = (FLGT_HOURS + LAYOVR);
-*
-* Calculate cost and time savings between direct and connecting flights.
     COMPUTE DFLGT_HRS /D10.2= IF ((DIR_CP_CD NE LAST DIR_CP_CD) AND (SEG_CONN EQ ' ')) THEN TOT_FLT ELSE 
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND (LAST DFLGT_HRS GT 0)) THEN LAST DFLGT_HRS ELSE 0;
-*                                 
     COMPUTE DFARE     /D10.2= IF ((DIR_CP_CD NE LAST DIR_CP_CD) AND (SEG_CONN EQ ' ')) THEN FPAID ELSE 
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND (LAST DFARE GT 0)) THEN LAST DFARE  ELSE 0;
-*                                 
     COMPUTE COMP_HRS  /D10.2= IF ((DFLGT_HRS GT 0) OR (TOT_FLT GT 0)) AND (TOT_FLT GT DFLGT_HRS) THEN (TOT_FLT - DFLGT_HRS)  ELSE 
                               IF ((DFLGT_HRS GT 0) OR (TOT_FLT GT 0)) AND (DFLGT_HRS GT TOT_FLT) THEN (DFLGT_HRS - TOT_FLT) ELSE 0;    
-*                                 
     COMPUTE COMP_FARE /D10.2= IF ((DFARE GT 0) OR (FPAID GT 0)) AND (FPAID GT DFARE) THEN (FPAID - DFARE) ELSE 
                               IF ((DFARE GT 0) OR (FPAID GT 0)) AND (DFARE GT FPAID) THEN (DFARE - FPAID) ELSE 0;
-*                                 
     COMPUTE ALPHA_HRS /A12  = IF COMP_HRS  GT 0 THEN FTOA(COMP_HRS, '(D10.2)', ALPHA_HRS) ELSE ' ';
     COMPUTE ALPHA_FARE/A12  = IF COMP_FARE GT 0 THEN FTOA(COMP_FARE, '(D10.2)', ALPHA_FARE) ELSE ' ';
     COMPUTE TRIM_HRS  /A12  = TRIM('L', ALPHA_HRS, 12, ' ', 1, TRIM_HRS);
     COMPUTE TRIM_FARE /A12  = TRIM('L', ALPHA_FARE, 12, ' ', 1, TRIM_FARE);
-*
     COMPUTE HRS_RESLTS/A40  = IF ((DIR_CP_CD NE LAST DIR_CP_CD) AND (SEG_CONN EQ ' ')) THEN ' ' ELSE
                               IF ((DFLGT_HRS EQ 0) OR (TOT_FLT EQ 0)) THEN ' ' ELSE
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND ((DFLGT_HRS GT 0) OR (TOT_FLT GT 0)) AND (DFLGT_HRS GT TOT_FLT)) THEN 'Connection saved ' | TRIM_HRS || ' hours' ELSE
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND ((DFLGT_HRS GT 0) OR (TOT_FLT GT 0)) AND (TOT_FLT GT DFLGT_HRS)) THEN 'Direct flight saved ' | TRIM_HRS || ' hours' ELSE
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND ((DFLGT_HRS GT 0) OR (TOT_FLT GT 0)) AND (TOT_FLT EQ DFLGT_HRS)) THEN 'No time savings with either flight.' ELSE ' ';
-*    
     COMPUTE COST_RESLTS/A40 = IF ((DIR_CP_CD NE LAST DIR_CP_CD) AND (SEG_CONN EQ ' ')) THEN ' ' ELSE
                               IF ((FPAID EQ 0) AND (DFARE EQ 0)) THEN ' ' ELSE
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND ((DFARE GT 0) AND (FPAID GT 0)) AND (DFARE GT FPAID)) THEN 'Connection saved $ ' | TRIM_FARE ELSE
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND ((DFARE GT 0) AND (FPAID GT 0)) AND (FPAID GT DFARE)) THEN 'Direct flight saved $ ' | TRIM_FARE ELSE
                               IF ((DIR_CP_CD EQ LAST DIR_CP_CD) AND ((DFARE GT 0) AND (FPAID GT 0)) AND (FPAID EQ DFARE)) THEN 'No cost savings with either flight.' ELSE ' ';
-*    
     COMPUTE CMP_RESLTS/A88  = IF ((DIR_CP_CD NE LAST DIR_CP_CD) AND (SEG_CONN EQ ' ')) THEN ' ' ELSE
                               IF ((DFLGT_HRS EQ 0) AND (DFARE EQ 0)) THEN ' ' ELSE COST_RESLTS | HRS_RESLTS;
      EMBARK
      ROUTE
      FNL_DEST_AIRPORT
      C_ITIN
      STOP_CON
      SEG_AMT
      TAX_AMT
      TICKET_CODE
      SEG_COUNT
      CONJ_REL
      EX_FLAG
      RF_FLAG
      DPT_DATE
      AIRLINE_NAME
      REFUSE_CODE
      AROLL_LEV1
      AROLL_LEV2
      AROLL_LEV3
      AROLL_LEV4
      AROLL_LEV5
      AROLL_LEV6
      AROLL_LEV7
      AROLL_LEV8
      AROLL_LEV9
      AROLL_LEV10
      AROLL_LEV11 
      AROLL_LEV12
      AROLL_LEV13
      AROLL_LEV14
BY DIR_CP_CD
BY SEG_CONN
ON TABLE HOLD AS CONN_HOLDZ
END
-RUN
-*
DEFINE FILE CONN_HOLDZ
-*
 -**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH DIR_CP_CD = HHMMSS (NOWTOD);
-*
END
-*
-SET &&PAHDR  = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN
-*
TABLE FILE CONN_HOLDZ
-*
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</1"
-*
-******
-NOSPC;
-******
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10

   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10

-* Custom footer
-SET &FTR_AddlLines = 3;
FOOTING BOTTOM
"</1"
"This report only includes information for an ORG-DST if there is at least one direct flight and a connection or more than one connection location."
-*
-INCLUDE FOOTERSM
-*
-*
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT 
-*
-IF &&CURRENCY EQ 'TRL' THEN GOTO SMALLER;
-*
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
-*
-GOTO FINFONT;
-*
-********
-SMALLER
-********
ON TABLE SET STYLE *
-INCLUDE SM7
-*
-********
-FINFONT
-********
-*
-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-**********************************************************************
-IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;
-*
-IF &&SETF EQ 'KWTOPMP' OR 'MXTPNOD' OR 'ABTPMPND' THEN GOTO SKIP_DRILL;
-*
-INCLUDE &&DSTY
-*
-***********
-SKIP_DRILL
-***********
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
-*
-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;
-*
EX REPORT_3
-*
-********
-SKIP_R3
-********