-* file name: UpdHotelProperty.fex
-* Expand PROP_CODE from A6 to A7 in HOTL_PRP as well as the individual company hotel files (HTL_MAIN)
-*
-* Create 2 new masters (HOTLPRP2 AND HR_50N) before running this procedure.
-* HOTLPRP2.MAS should be in System\Data and HR_50N should be in TTracker\Data.
-* 
-SET &ECHO = ALL;
-DOS DEL D:\IBI\LOGFILE.TXT 
FILEDEF LOGFILE DISK D:\IBI\LOGFILE.TXT (APPEND

-*update HOTL_PRP
-SET &DBPATH = 'D:\tnt\TTracker\System\Data\';
-SET &OLDFILE = 'HOTL_PRP';
-SET &NEWFILE = 'HOTLPRP2';

-INCLUDE rebuildfile
-RUN

-WRITE LOGFILE &OLDFILE
-*-DOS DEL D:\tnt\TTracker\System\Data\HOTLPRP2.MAS

-SET &DBPATH = 'D:\tnt\TTracker\Data\';
-SET &NEWFILE = 'HR_50N';
-SET &OLDFILE = 'HR_50';

-*Update HR_50.mas
-SET &OLDMASTER = '&DBPATH.EVAL' | '&OLDFILE.EVAL' || '.MAS';
-SET &NEWMASTER = '&DBPATH.EVAL' | '&NEWFILE.EVAL' || '.MAS';
-DOS COPY &NEWMASTER &OLDMASTER
-RUN

-*Update the individual company hotel files
TABLE FILE ROLLUP
PRINT COMP QTR_ENABLE
BY ROLLUP_CODE
WHERE ROLLUP_CODE NE 'TOTAL-R' OR ' ' 
-*WHERE ROLLUP_CODE EQ 'ABCD-R' OR 'AGP-R' OR 'AFMG-R' OR 'ACTON-R' OR '48HOUR-R'
-*WHERE ROLLUP_CODE EQ '3HTECH-R' OR '454LIF-R' OR 'ABSPUM-R' OR 'ACTON-R' OR 'ABCD-R'

ON TABLE SAVE AS ALLROLLS
END
-RUN

-SET &LOOPTIMES = &LINES;

-REPEAT TOEND &LOOPTIMES TIMES
-READ ALLROLLS NOCLOSE &ROLLUP.8. &COMP.6. &QTR.1.

-IF '&QTR.EVAL' EQ 'X' THEN GOTO QTR_DB;

-SingleDB
-SET &OLDFILE = 'H_' | '&COMP.EVAL' || '';

-*master file
-SET &OLDMAS = '&DBPATH.EVAL' || '&OLDFILE.EVAL' || '.MAS';
-DOS STATE &OLDMAS
-IF &RETCODE EQ -1 THEN GOTO NEXTROLLUP;
-*focus file
-SET &OLDFOC = '&DBPATH.EVAL' || '&OLDFILE.EVAL' || '.FOC';
-DOS STATE &OLDFOC
-IF &RETCODE EQ -1 THEN GOTO NEXTROLLUP;


-INCLUDE rebuildfile
-RUN
-GOTO NEXTROLLUP;

-QTR_DB
-SET &CNT = 0;
-REPEAT INNERLOOP 12 TIMES
-SET &SUFIX = IF &CNT EQ 10 THEN 'Y' ELSE IF &CNT EQ 11 THEN 'Z' ELSE '&CNT.EVAL';
-SET &OLDFILE = 'H' | '&SUFIX.EVAL' | '&COMP.EVAL';
-*master file
-SET &OLDMAS = '&DBPATH.EVAL' || '&OLDFILE.EVAL' || '.MAS';
-DOS STATE &OLDMAS
-IF &RETCODE EQ -1 THEN GOTO NEXTROLLUP;
-*focus file
-SET &OLDFOC = '&DBPATH.EVAL' || '&OLDFILE.EVAL' || '.FOC';
-DOS STATE &OLDFOC
-IF &RETCODE EQ -1 THEN GOTO NEXTROLLUP;


-INCLUDE rebuildfile
-RUN
-SET &CNT = &CNT + 1;
-INNERLOOP

-NEXTROLLUP
-WRITE LOGFILE &ROLLUP 
-TOEND
-CLOSE ALLROLLS;
-CLOSE LOGFILE;
-RUN
-*-DOS DEL D:\tnt\TTracker\Data\HR_50N.MAS

