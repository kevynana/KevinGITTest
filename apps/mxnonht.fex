-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Due to hotel market format change, changed city_st to have an extra space
-* JK 3/23/04
-* 7/13/05 - DEB - INCLUDED REFUSE_CD 'R' OR 'T' IN NONPREF_RM DEFINE
-* 7/18/05 - STEVE - Took out REFUSE_CD 'T' IN NONPREF_RM DEFINE - per Robin Lewis
-*4/20/2017-JEM-S-33318-updated logic for no records report

TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT
  CITY_ST/A40 = PROP_STATE||(' '| ' '|PROP_CITY);
  LEVEL_DESC/A60  = &&LDESC;
  NBR_DAYS/D12=NUM_DAYS;
  NBR_ROOMS/D12 = NUM_ROOMS;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
  REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
 -*   PREF_NON/A15 = IF HTL_REF.PREFERRED EQ 'Y' THEN 'PREFERRED' ELSE 'NON-PREFERRED';
 

-* Defines on Previous Defines
NONPREF_RM/D12 = IF REFUSE_CD NE 'A' OR 'R' THEN NNROOMS ELSE 0;
-* NONPREF_RM/D12 = IF PREF_NON EQ 'NON-PREFERRED' THEN NNROOMS ELSE 0;
RATE_RM1/D12 = IF ((ROOM_RATE GE 150 AND ROOM_RATE LE 199) OR
                  (ROOM_RATE LE (-150) AND ROOM_RATE GE (-199))) 
                  THEN NNROOMS ELSE 0;
RATE_RM2/D12 = IF ((ROOM_RATE GE 200 AND ROOM_RATE LE 249) OR
                  (ROOM_RATE LE (-200) AND ROOM_RATE GE (-249)))
                  THEN NNROOMS ELSE 0;
RATE_RM3/D12 = IF (ROOM_RATE GE 250) THEN NNROOMS ELSE 0;
END
-RUN

TABLEF FILE &&EXTRACT
PRINT CITY_ST
      LEVEL2
      NNROOMS 
      NONPREF_RM
      RATE_RM1
      RATE_RM2  
      RATE_RM3   
-INCLUDE &HTLDATES  
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS
ON TABLE HOLD AS HHOLD
END
-RUN

TABLE FILE HHOLD
PRINT LEVEL2
      NNROOMS 
      NONPREF_RM
BY CITY_ST
ON TABLE HOLD AS HHOLD1
END
-RUN


DEFINE FILE METXHTL
HTL_MKT/A130 = IF H_MARKET NE ' ' THEN H_MARKET ELSE ST_CITY;
END

TABLE FILE METXHTL
PRINT HTL_MKT
BY ST_CITY AS 'CITY_ST'
WHERE ROLLUP_CODE EQ '&&RLUP'
ON TABLE HOLD AS MKTFILE
END
-RUN

MATCH FILE HHOLD1
PRINT LEVEL2
      NNROOMS
      NONPREF_RM
BY CITY_ST
ON TABLE HOLD
RUN
FILE MKTFILE
SUM HTL_MKT
BY CITY_ST
AFTER MATCH HOLD AS HFILE OLD-AND-NEW
END
-RUN

TABLE FILE HFILE
SUM NNROOMS
    NONPREF_RM
BY LEVEL2
WHERE CITY_ST NE ' '
ON TABLE HOLD AS HHOLD2
END
-RUN

TABLE FILE HHOLD
SUM  RATE_RM1
     RATE_RM2  
     RATE_RM3   
     NNROOMS AS 'NNROOMS1'
BY LEVEL2
ON TABLE HOLD AS HHOLD3
END
-RUN

MATCH FILE HHOLD2
PRINT NNROOMS
      NONPREF_RM
BY LEVEL2
ON TABLE HOLD
RUN
FILE HHOLD3
SUM RATE_RM1
    RATE_RM2
    RATE_RM3
    NNROOMS1
BY LEVEL2
AFTER MATCH HOLD AS MXNONHTL OLD-OR-NEW
END
-RUN


-SET &&HTLREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';



