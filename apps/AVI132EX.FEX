-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File AIREXTRT.FEX
-********************************************************************
-* This program extracts data for the Data Vault Air detail
-********************************************************************
-* MODIFICATIONS 
-* CREATED FROM AVIW74EX 
-*2/17/15 - DEB = S-07237  ADDED CODE FOR ABTRNFSM  
-*12/28/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels 
-*10/24/16- DLV  S-26029 - Added EMP_ID
-*09/25/2017 - DLV - S-37268 - Added EMBARKDEF define
-********************************************************************
-INCLUDE SETECHO

SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
ACCT/A4 = ' ';
ADDCOLLECT/D12.2CS = EXCH_NET_FARE + EXCH_NET_TAX;

BASE_FARE/D12.2CS = SEG_AMT;



FARE_PAID/D12.2C   = SEG_AMT + SEG_TAX;
LEVEL_DESC1/A80 = &&LDESC;
MV_V1/A10='XXXXXXXXXX';
MV_CC/A2 =EDIT(CREDIT_CARD, '99');
CC_LEN/I2 = ARGLEN(18, CREDIT_CARD, CC_LEN);
AA_CC/A7 = IF CC_LEN LT 18 THEN EDIT(CREDIT_CARD, '99$$$$$$$$$$99999')
ELSE EDIT(CREDIT_CARD, '99$$$$$$$$$$$99999');

MCOS/D9CS           = IF (DOC_TYPE EQ '1') AND
                           (EX_FLAG EQ ' ')  AND
                           (EXCHG_SALE NE ' ') THEN 1 ELSE 0;                              
MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
NEW_SEG_AMT/D12.2CS = SEG_AMT;
NET_COMM/D12.2S = (SEG_AMT - SEG_COM);
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

NET_TKT_CNT1/D9 =   IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND
                         (SEG_COUNT  EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 
                    IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND 
                         (EXCHG_SALE NE ' ') AND (RF_FLAG EQ ' ') THEN 1 ELSE 0;
NEW_ADV_PURCH/D9    = ADV_PURCH;
NEW_SEG_MILES/D9= SEG_MILES;
SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;
SEG_CNT/D8CS         = SEG_COUNT;
TKT_CNT/D9CS        = IF (SEG_NBR EQ '01') AND 
                           (FARE_PAID GE 0) AND 
                           (SEG_COUNT EQ 1) THEN 1 ELSE 0;    
TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                           (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' '; 
TRAN_MONTH/MT        = TRN_DATE;
TRAN_MY/MTYY         = TRN_DATE;
TRNM/MT = IF '&&DATETYPE.EVAL' EQ 'I' THEN TRN_DATE ELSE FST_DPT_DATE;

XARR_DT/MDYY         = ARR_DATE;

XDPT_DT/MDYY         = DPT_DATE;
XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
XTRAN_DT/MDYY        = TRN_DATE;
XFST_DPT/MDYY = FST_DPT_DATE;
VOIDS/D9CS          = IF (VOID_DATE NE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (SEG_NBR EQ '01') THEN 1 ELSE
                        IF (DOC_TYPE EQ '1') AND 
                           (VOID_DATE NE 0) THEN 1 ELSE 0;
-* DEFINES ON PREVIOUS DEFINES
AIRLINE_FEE/D12.2CS = IF EMBARK EQ 'FP2' OR 'FP3' 
     THEN FARE_PAID ELSE 0;
DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
  NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
  IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;
FF_MILES/D9S = IF FREQ_FLYER NE ' ' THEN NEW_SEG_MILES ELSE 0;

TSFARE_PAID/D12.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN 0 ELSE FARE_PAID ;
EX_IND/A1 = IF EXCHG_SALE NE ' ' THEN 'E' ELSE ' ';
NW_FARE_PAID/D12.2CS = IF VOID_DATE EQ 0 THEN FARE_PAID ELSE 0;
NW_TKT_CNT/D8CS = IF VOID_DATE EQ 0 THEN NET_TKT_CNT ELSE 0;
 
EMBARKDEF/A3 = CITYPAIR.EMBARK;
-INCLUDE TRANTYPE
-INCLUDE IDLEVDEFINE 

END

TABLEF FILE &&EXTRACT
PRINT AA_CC
  ACCT
  ADDCOLLECT
  AGENT_NUM
  AIR_KEY	
  AIR_MAIN.INTL_DOM
  AIRLINE
  AIRLINE_FEE
  BR_CL_IDX
  C_ITIN
  CC_NUM
-*  CITYPAIR.EMBARK      AS 'EMB_APT_CD'
  EMBARKDEF AS 'EMB_APT_CD'
  DISSAVINGS
  DOC_NUMBER
  DOC_TYPE  
  EMBARKDEF AS 'EMBARK' 
  EMP_ID
  EX_FLAG
  EX_RF_FLAG
  EX_IND
  EXCHG_SALE AS 'EXSALE'
  EXCHG_SALE
  EXCHGS
  FARE_BAS
  INVOICE_NUM
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  MCOS
  MRK_TKT_CNT
  NET_COMM
  NEW_SEG_AMT
  NET_TKT_CNT
  NET_TKT_CNT1
  NEW_SEG_MILES
  NW_FARE_PAID
  NW_TKT_CNT
  ONLINE_TRADITIONAL
  PASSNGR_NAME
  PNR_LOCATOR
  RF_FLAG
  SAVINGS
  SEG_AMT
  SEG_COM
  SEG_CNT
  SEG_NBR
  SEG_STANDARD
  SEG_TAX
  TKT_DATE	
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE
  TRNM
  TSFARE_PAID AS 'FARE_PAID'
  VOID_DATE
  VOIDS
  XARR_DT
  XDPT_DT
  XFST_DPT
  XPSNGR_NM
  XTRAN_DT	
  CREDIT_CARD
-INCLUDE &AIRDATES

-*WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
-IF &&SETF EQ 'ABTRNFEE' OR 'SBTRNFEE' OR 'BETRNFEE' OR 'FOTRNFEE' THEN GOTO NoWhere;
WHERE VOID_DATE EQ 0
-NoWhere
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD
END
-RUN



