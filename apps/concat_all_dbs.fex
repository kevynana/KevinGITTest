-*INCLUDE SETECHO
-* File concat_all_dbs.fex
-*
-* This Focexec will issue the USE statements for a company databases based
-* on &ROLL and &FID being passed to it.
-*
-* Parameters:
-*   &ROLL = Rollup Code
-*   &FID = File Type ID (Valid values are CAR, LIMO, HOTEL, SEG, MKT, or DST)
-*   &CLEAR = Set to Y to have other USEs cleared or N to issue a USE ADD.
-*            Default is Y.
-*
-* Execute using the following: EX CONCAT_ALL_DBS ROLL=ABCD-R,FID=SEG,CLEAR=Y
-*
 
-DEFAULT &ROLL='ABCD-R';
-DEFAULT &FID='CAR';
-DEFAULT &CLEAR='Y';
 
-SET &UCLR=IF &CLEAR EQ 'Y' THEN '' ELSE 'ADD';
 
TABLE FILE ROLLUP
PRINT QTR_ENABLE COMP
WHERE ROLLUP_CODE EQ '&ROLL.EVAL'
ON TABLE SAVE AS HQTR
END
-RUN
 
-READ HQTR &QTR.A1, &COMP.A6
 
-SET &XTIMES = IF &QTR EQ 'X' THEN 12 ELSE 1;
-SET &CTR = 0;
 
-SET &F4=IF &FID EQ 'CAR' THEN  'TTDATA/UC' || &COMP || '.FOC AS CARUDID' ELSE
-        IF &FID EQ 'LIMO' THEN 'TTDATA/UL' || &COMP || '.FOC AS CARUDID' ELSE
-        IF &FID EQ 'HOTEL' THEN 'TTDATA/UH' || &COMP || '.FOC AS HTLUDID' ELSE
-                                'TTDATA/UA' || &COMP || '.FOC AS AIRUDID';
-SET &F5='TTDATA/UF' || &COMP || '.FOC AS UDFILE';
-SET &F6='TTDATA/AL' || &COMP || '.FOC AS AH_50';
 
USE &UCLR.EVAL
-REPEAT DLOOP &XTIMES TIMES;
-SET &DBID=IF &QTR NE 'X' THEN '_' ELSE
-          IF &CTR EQ 10 THEN 'Y' ELSE
-          IF &CTR EQ 11 THEN 'Z' ELSE &CTR;
-SET &F1 = IF &FID EQ 'CAR' THEN 'TTDATA/C' || &DBID || &COMP || '.FOC AS CR_50' ELSE
-         IF &FID EQ 'HOTEL' THEN 'TTDATA/H' || &DBID || &COMP || '.FOC AS HR_50' ELSE
-         IF &FID EQ 'LIMO' THEN 'TTDATA/L' || &DBID || &COMP || '.FOC AS LR_50' ELSE
-         IF &FID EQ 'SEG' THEN 'TTDATA/S' || &DBID || &COMP || '.FOC AS SR_50' ELSE
-         IF &FID EQ 'MKT' THEN 'TTDATA/M' || &DBID || &COMP || '.FOC AS MR_50' ELSE
-         IF &FID EQ 'DST' THEN 'TTDATA/D' || &DBID || &COMP || '.FOC AS DR_50' ELSE 'XX';
-SET &F2=IF &FID EQ 'SEG' OR 'MKT' OR 'DST' THEN 'TTDATA/A' || &DBID || &COMP || '.FOC AS AIR_MAIN' ELSE '';
-SET &F3=IF &FID EQ 'SEG' OR 'MKT' OR 'DST' THEN 'TTDATA/T' || &DBID || &COMP || '.FOC AS TKT_MAIN' ELSE '';
&F1
&F2
&F3
-SET &CTR = &CTR + 1;
-DLOOP
&F4
&F5
&F6
END
-RUN
