-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
SET ASNAMES=ON

-SET &&RPTSUF = 'STAPLESREVIEW';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');

-INCLUDE FDEFRPTS
-RUN


MATCH FILE AIRTTL
PRINT CFARE
      PFARE
BY LEVEL_DESC
-* BY AROLL_DSC3
-* BY AROLL_DSC4
ON TABLE HOLD
RUN 
FILE CARTTL
SUM CCAMT
    PCAMT
BY LEVEL_DESC
-* BY AROLL_DSC3
-* BY AROLL_DSC4
AFTER MATCH HOLD AS AIRCAR OLD-OR-NEW
END
-RUN

MATCH FILE AIRCAR
PRINT CCAMT
      CFARE
      PCAMT
      PFARE
BY LEVEL_DESC
-* BY AROLL_DSC3
-* BY AROLL_DSC4
ON TABLE HOLD 
RUN
FILE HTLTTL
SUM CHAMT
    PHAMT
BY LEVEL_DESC
-* BY AROLL_DSC3
-* BY AROLL_DSC4
AFTER MATCH HOLD AS TTLHOLD OLD-OR-NEW
END
-RUN

TABLE FILE TTLHOLD
SUM CCAMT
    CHAMT
    CFARE
    PCAMT
    PHAMT
    PFARE
COMPUTE CTTL/P12C = CFARE + CCAMT + CHAMT;
        PTTL/P12C = PFARE + PCAMT + PHAMT;
        IDAIRD/P12C = CFARE - PFARE;
        IDCARD/P12C = CCAMT - PCAMT;
        IDHTLD/P12C = CHAMT - PHAMT;
        IDTTLD/P12C = CTTL - PTTL;
        IDAIRP/P12.2=IF ((PFARE LT 1) AND (PFARE GT (-1))) THEN 0 ELSE
                        ((IDAIRD/PFARE)*100);
        IDCARP/P12.2=IF ((PCAMT LT 1) AND (PCAMT GT (-1))) THEN 0 ELSE
                        ((IDCARD/PCAMT)*100);
        IDHTLP/P12.2=IF ((PHAMT LT 1) AND (PHAMT GT (-1))) THEN 0 ELSE
                        ((IDHTLD/PHAMT)*100);
        IDTTLP/P12.2=IF ((PTTL LT 1) AND (PTTL GT (-1))) THEN 0 ELSE
                        ((IDTTLD/PTTL)*100);
BY LEVEL_DESC        
-* BY AROLL_DSC3 
-* BY AROLL_DSC4
ON TABLE HOLD AS FINALTTL
END
-RUN

DEFINE FILE FINALTTL
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
-RUN

TABLE FILE FINALTTL
SUM 
CFARE AS '' CCAMT AS '' CHAMT AS '' CTTL AS '' PFARE AS '' PCAMT AS '' 
PHAMT AS '' PTTL AS '' IDAIRD AS '' IDAIRP AS ''  IDCARD AS '' IDCARP AS ''
IDHTLD AS '' IDHTLP AS '' IDTTLD AS ''  IDTTLP AS ''
BY LEVEL_DESC AS ' '
-* BY LEVEL_DESC NOPRINT
-* BY AROLL_DSC3 NOPRINT
-* BY AROLL_DSC4 AS ''
HEADING
"</5"
" "
"<10 &&RPT_TTL1"
"<10 &&RPT_TTL2"
" "
" "
" <34 PERIOD1: <40 &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
" <34 PERIOD2: <40 &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
"</2"
"<23 Period1 <31 Period2 <49 Increase/Decrease Prior Year"
"<5 Air <10 Car <15 Hotel <20 Total <25 Air <30 Car <35 Hotel <40 Total
 <45 Air <50 Car <55 Hotel <60 Total"
"<5 Dollar <10 Percent <15 Dollar <20 Percent <25 Dollar <30 Percent
 <35 Dollar <40 Percent"
FOOTING BOTTOM
"eTTek Review &DATE AT <NOWTOD <60 &&FOOTR"
"Copyright (c) 1996-2003, Travel and Transport - Confidential/Proprietary"

ON TABLE SET PAGE-NUM OFF
ON LEVEL_DESC RECOMPUTE MULTILINES AS ''
ON LEVEL_DESC SKIP-LINE
-* ON AROLL_DSC3 RECOMPUTE MULTILINES AS ''
-* ON AROLL_DSC3 SKIP-LINE
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=1.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=1.000000, BOTTOMMARGIN=0.250000, SQUEEZE=OFF, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=6, COLOR=BLACK, 
    BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=FOOTING, SIZE=6, $
TYPE=TITLE, SIZE=7, STYLE=BOLD, $
-* TYPE=HEADING, LINE=7, SIZE=8, FONT=TIMES NEW ROMAN, STYLE=BOLD+ITALIC, $
-* TYPE=HEADING, LINE=10, SIZE=7, STYLE=BOLD, $
TYPE=HEADING, SIZE=6, STYLE=BOLD, FONT=TIMES NEW ROMAN, $
TYPE=SUBTOTAL, SIZE=6, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=6, STYLE=BOLD, $
TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(&&TNTTRPOS), SIZE=(&&TNTTRSIZ), $
TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLTRPOS), SIZE=(&&ROLTRSIZ), $

ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN



