-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO
-*****************************************************************************
-* Created by JK 1/10/02
-* This view produces a transaction summary report that does not utilize
-* the internet/non-internet break
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*12/21/04   DEB                CHANGED IFLAG AND INTERNET_FLAG DEFINE TO 
-*                              INCLUDE ADDTL DOD_TYPE
-*10/11/05   DEB                ADDED DOD_TYPE H TO INTERNET_FLAG DEFINE
-*04/20/10   DEB                CHANGED ORIG_SALE DEFINE
-*1/24/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14    DEB                UPDATED NUMEROUS DEFINES TO INCLUDE EX5
-* 02/24/2017 - DLV - S-22486 CHANGED ettek Review to Post-trip Reporting
-* 08/08/2017 - RSB - S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-*                            

SET ASNAMES=ON
-RUN

-INCLUDE SetParmtime
-RUN

-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-**********************************************************************
-* DETERMINE IF WE HAVE WEB DEPLOYMENT OR LOCAL STAND ALONE
-* SET PATH FOR SAVING THE SETFILE
-**********************************************************************
-******LOCAL DEPLOYMENT
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ ' ||&&ROLLUP || '\ '||&&UNAME.EVAL|| '\';
-SET &&SETFNM= &&LOCPATH || &&DTTMSTRM ||'.FTM';
-GOTO COPYSETF;

-******WEB DEPLOYMENT
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL|| '\';
-SET &&SETFNM= &&WEBPATH || &&DTTMSTRM ||'.FTM';

-COPYSETF
-SET &&VARFWEB = &&WEB_PATH || &&SETF ||'.FEX';
COPY &&VARFWEB &&SETFNM
-INCLUDE SETFPARM
-RUN
-INCLUDE APNDSETF
-**********************************************************************
-**********************************************************************
-INCLUDE SetBDBsForDrill
-RUN

-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-RUN

TABLE FILE &&RPT_HOLD
PRINT 
  AIR_KEY	
  AIRLINE
  BR_CL_IDX	
  CLASS	
  DOC_TYPE
  DOD_DESC
  DOD_TYPE	
  EMBARK	
  EX_FLAG	
  EXCHG_SALE	
  GRAND_MSTR_CODE	
  GROUP_CODE	
  LEVEL1	
  LEVEL2	
  LEVEL3	
  MSTR_GRP_CODE	
  ONLINE_TRADITIONAL
  PASSNGR_NAME	
  REFUSE_KEY	
  RESV_BRANCH	
  RF_FLAG
  SEG_AMT	
  SEG_COM	
  SEG_COUNT
  SEG_NBR 	
  SEG_TAX 	
  TKT_NUM
  TRN_DATE	
  VAL_AIRLINE	
  VOID_DATE 	
  VOID_STATUS 	
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS TRANONE
END
-RUN

DEFINE FILE TRANONE
BLANK/A1 = ' ';	
DOC1/A15 = '   Mcos';	
DOC2/A15 = '   Tour Orders';	
DOC3/A15 = '   Handwrites';	
DOC4/A15 = '   Ptas';	
DOC5/A15 = '   Paper/Etkt';	
DOC9/A15 = '   Ticketless';	
EMCO_TAX/D12 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') THEN SEG_TAX ELSE 0;
EXCH/A15 = '   Exchg Sales';	

EXCH_AMT/D12 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE NE ' ') THEN 0 ELSE
  IF ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND 
  (EMBARK NE 'DT1' OR 'RF1' OR 'FP3')) THEN SEG_AMT ELSE
  IF ((SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') AND (EMBARK NE 'RF1')) 
  THEN SEG_AMT ELSE 0;

EXCH_MCO/D12 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') THEN SEG_AMT ELSE 0;

NEW_SALES/D12 = IF (SEG_AMT GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE 
  EQ ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5') THEN SEG_AMT ELSE 0;	

NEW_TAX/D12 = IF ((SEG_TAX GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE EQ 
  ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5')) THEN SEG_TAX ELSE 0;

MCO_AMT/D12 = IF (EX_FLAG EQ ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE
  NE ' ') THEN SEG_AMT ELSE 0;	

MCO_CNT/D9 = IF (EX_FLAG EQ ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE
  NE ' ') THEN 1 ELSE 0;

MCO_TAX/D12 = IF (EX_FLAG EQ ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE
  NE ' ') THEN SEG_TAX ELSE 0;

-* EXCH_TAX/D12 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE
-*   IF ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND 
-*   (EMBARK NE 'DT1' OR 'RF1' OR 'FP3') AND
-*   (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND 
-*   (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1') THEN SEG_TAX ELSE 0;	

EXCH_TAX/D12 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE NE ' ') THEN 0 ELSE
  IF ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND 
  (EMBARK NE 'DT1' OR 'RF1' OR 'FP3')) THEN SEG_TAX ELSE
  IF ((SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') AND (EMBARK NE 'RF1'))
  THEN SEG_TAX ELSE 0;

MCO_TOTAL/D12 = MCO_AMT + MCO_TAX;

NEW_SLS/D12 = NEW_SALES + MCO_AMT;

NEW_TX/D12 = NEW_TAX + MCO_TAX;

NEW_TOTAL/D12 = NEW_SLS + NEW_TX;

EXMC/A15 = '   Exchg Mcos';	

EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') THEN 1 ELSE 0; 

FARE_PAID/D12 = SEG_AMT + SEG_TAX;	
FULL/A15 = '   Full Refunds';	
FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;	
GRND/A15 = 'TOTALS';	
INTERNET_FLAG/A42 = DOD_DESC;
IFLAG/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';	

-*ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;	



ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND 
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;	



OTH_SALES/D9 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN ORIG_SALE 
  ELSE 0;
	
OTHR_AMT/D12 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_SALES 
  ELSE 0;
	
OTHR_TAX/D12 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_TAX 
  ELSE 0;
	
OTHR_TOTAL/D12 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_TOTAL 
  ELSE 0;
	
PAID_FARE/D12 = IF VOID_DATE EQ 0 THEN FARE_PAID ELSE 0;	
PART/A15 = '   Partial Rfnd';	
PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;	
PTA_AMT/D12 = IF DOC_TYPE EQ '4' THEN NEW_SALES ELSE 0;	
PTA_CNT/D9 = IF DOC_TYPE EQ '4' THEN ORIG_SALE ELSE 0;	
PTA_TAX/D12 = IF DOC_TYPE EQ '4' THEN NEW_TAX ELSE 0;	
PTA_TOTAL/D12 = IF DOC_TYPE EQ '4' THEN NEW_TOTAL ELSE 0;	
REFUND_AMT/D12 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_AMT ELSE 
  0;	
REFUND_TAX/D12 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_TAX ELSE 
  0;	
REFUND_TOTAL/D12 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN FARE_PAID ELSE 
  0;	
REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;	
RFND/A15 = 'TOTAL REFUNDS';	
TEXG/A15 = 'TOTAL EXCHANGES';	
TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;	
TKTLESS/D9 = IF DOC_TYPE EQ '9' THEN ORIG_SALE ELSE 0;	
TKTLS_AMT/D12 = IF DOC_TYPE EQ '9' THEN NEW_SALES ELSE 0;	
TKTLS_TAX/D12 = IF DOC_TYPE EQ '9' THEN NEW_TAX ELSE 0;	
TKTLS_TOTAL/D12 = IF DOC_TYPE EQ '9' THEN NEW_TOTAL ELSE 0;	
TOT_ETAX/D12 = EXCH_TAX + EMCO_TAX;	
TOT_EXCH/D12 = EXCH_AMT + EXCH_MCO;	
TOTL/A15 = 'TOTAL NEW SALES';	
TOUR_AMT/D12 = IF DOC_TYPE EQ '2' THEN NEW_SALES ELSE 0;	
TOUR_ORDERS/D9 = IF DOC_TYPE EQ '2' THEN ORIG_SALE ELSE 0;	
TOUR_TAX/D12 = IF DOC_TYPE EQ '2' THEN NEW_TAX ELSE 0;	
TOUR_TOTAL/D12 = IF DOC_TYPE EQ '2' THEN NEW_TOTAL ELSE 0;	
VOID/A15 = 'VOIDS';	
VOID_AMT/D12 = IF VOID_DATE NE 0 THEN SEG_AMT ELSE 0;	
VOID_TAX/D12 = IF VOID_DATE NE 0 THEN SEG_TAX ELSE 0;	
VOID_TOTAL/D12 = IF VOID_DATE NE 0 THEN FARE_PAID ELSE 0;	
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1)AND (SEG_NBR EQ '01') 
  THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) THEN 1 ELSE 0;	

-* Defines on previous DEFINEs
EMCO_TOTAL/D12 = EXCH_MCO + EMCO_TAX;

EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
  NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
  IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;	

EXCH_TOTAL/D12 = EXCH_AMT + EXCH_TAX;

FRFND_AMT/D12 = IF RF_FLAG EQ 'F' THEN REFUND_AMT ELSE 0;	
FRFND_TAX/D12 = IF RF_FLAG EQ 'F' THEN REFUND_TAX ELSE 0;	
FRFND_TOTAL/D12 = IF RF_FLAG EQ 'F' THEN REFUND_TOTAL ELSE 0;	
HNDWRT_AMT/D12 = IF DOC_TYPE EQ '3' THEN NEW_SALES ELSE 0;	
HNDWRT_TAX/D12 = IF DOC_TYPE EQ '3' THEN NEW_TAX ELSE 0;	
HNDWRT_TOTAL/D12 = IF DOC_TYPE EQ '3' THEN NEW_TOTAL ELSE 0;	
HNDWRTS/D9 = IF DOC_TYPE EQ '3' THEN ORIG_SALE ELSE 0;	
NET_AMT/D12 = NEW_SLS + REFUND_AMT + TOT_EXCH - VOID_AMT;	
NET_TAX/D12 = NEW_TX + REFUND_TAX + TOT_ETAX - VOID_TAX;	
PRFND_AMT/D12 = IF RF_FLAG EQ 'P' THEN REFUND_AMT ELSE 0;	
PRFND_TAX/D12 = IF RF_FLAG EQ 'P' THEN REFUND_TAX ELSE 0;	
PRFND_TOTAL/D12 = IF RF_FLAG EQ 'P' THEN REFUND_TOTAL ELSE 0;	
TOTAL_EXCHG/D12 = EXCH_TOTAL + EMCO_TOTAL;	
TOT_ECNT/D9 = EXCHGS + EXMCOS;	
TOT_TRANS/D9 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;	
NEW_TOTAL/D12 = NEW_SLS + NEW_TX;
-* Defines on previous DEFINEs
NET_TOTAL/D12 = NEW_TOTAL + REFUND_TOTAL + TOTAL_EXCHG - VOID_TOTAL;
FOOT1/A75 = 'NOTE: NEW SALES AND EXCHANGE SALES INCLUDE MCO'S';
FOOT2/A75 =  'TOTAL TRANSACTION COUNT = NEW SALES + EXCHANGES + REFUNDS + VOIDS';
FOOT3/A75 = '-  TOTAL DOLLAR AMOUNT = NEW SALES + EXCHANGES + REFUNDS - VOIDS';
FOOT4/A75 = '-  TICKETS ISSUED ON TRAVEL SUMMARY = NEW SALES + EXCHANGE SALES - MCO'S';
FOOT5/A75 = '-  VOIDS ARE INCLUDED IN NEW SALES, EXCHANGE SALES, & REFUNDS';
FOOT6/A75 =  '-  VOID COUNT INCLUDES MCOS - TRAVEL SUMMARY VOID COUNT DOES'; 
-**************************************************************************
-*DATE           NAME           DESCRIPTION OF CHANGE
-*----           ----           ---------------------
-*7/31/00      IBISTL-KR        ADDED DEFINE FOR NOWTOD 
-**************************************************************************
NOWTOD/A8 WITH TKT_NUM = HHMMSS (NOWTOD);
BLANK1/A5 = ' ';

END
TABLE FILE TRANONE
SUM  DOC1 AS '' 
 MCO_CNT AS ''     MCO_AMT AS ''  MCO_TAX AS ''   MCO_TOTAL AS '' 
OVER DOC2 AS ''
 TOUR_ORDERS AS '' TOUR_AMT AS '' TOUR_TAX AS '' TOUR_TOTAL AS '' 
OVER DOC3 AS '' 
 HNDWRTS AS ''   HNDWRT_AMT AS '' HNDWRT_TAX AS '' HNDWRT_TOTAL AS ''  
OVER DOC4 AS '' 
 PTA_CNT AS ''    PTA_AMT AS ''   PTA_TAX AS ''   PTA_TOTAL AS ''
OVER DOC5 AS '' 
 OTH_SALES AS ''  OTHR_AMT AS ''  OTHR_TAX AS ''  OTHR_TOTAL AS '' 
OVER DOC9 AS '' 
 TKTLESS AS ''  TKTLS_AMT AS ''  TKTLS_TAX AS '' TKTLS_TOTAL AS ''
OVER TOTL AS '' 
 ORIG_SALES AS '' NEW_SLS AS ''  NEW_TX AS ''   NEW_TOTAL AS ''  
OVER BLANK AS ''
OVER EXCH AS '' 
 EXCHGS AS ''      EXCH_AMT AS ''  EXCH_TAX AS ''  EXCH_TOTAL AS ''
OVER EXMC AS ''   
 EXMCOS AS ''      EXCH_MCO AS ''  EMCO_TAX AS ''  EMCO_TOTAL AS ''
OVER TEXG AS ''
 TOT_ECNT AS ''    TOT_EXCH AS ''  TOT_ETAX AS ''  TOTAL_EXCHG AS ''
OVER BLANK AS ''
OVER FULL AS '' 
 FULL_RFNDS AS '' FRFND_AMT AS ''  FRFND_TAX AS '' FRFND_TOTAL AS ''
OVER PART AS ''
 PART_RFNDS AS '' PRFND_AMT AS ''  PRFND_TAX AS '' PRFND_TOTAL AS ''
OVER RFND AS '' 
 REFUNDS AS ''   REFUND_AMT AS '' REFUND_TAX AS '' REFUND_TOTAL AS ''
OVER BLANK AS ''
OVER VOID AS '' 
 VOIDS AS ''       VOID_AMT AS ''  VOID_TAX AS ''  VOID_TOTAL AS ''  
OVER BLANK AS ''
OVER GRND AS '' 
 TOT_TRANS AS ''    NET_AMT AS ''  NET_TAX AS ''   NET_TOTAL AS ''
OVER BLANK AS ''
-IF &&OUTFMT NE 'PDF' GOTO SkipOVER;
OVER BLANK AS ' '
OVER BLANK AS ' '
OVER BLANK AS ' '
OVER BLANK AS ' '
OVER BLANK AS ' '
OVER BLANK AS ' '
OVER FOOT1 AS ' '
OVER FOOT2 AS ' '
OVER FOOT3 AS ' '
OVER FOOT4 AS ' '
OVER FOOT5 AS ' '
OVER FOOT6 AS ' '
-SkipOVER;
-IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' GOTO XLHDR;


HEADING
"</5"
"<18 TRANSACTION SUMMARY REPORT"
"<10 &&RPT_TTL1"
"<10 &&RPT_TTL2"
"<23 &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"</2"
"<23 COUNT <+4 BASE FARE <+8 TAX <+4 FARE PAID </1"
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----           ---------------------
-*7/31/00      IBISTL-KR        REPLACED &FOCSYSTOD WITH NOWTOD
-**************************************************************************
-INCLUDE FOOTERDT
-*
ON TABLE SET PAGE-NUM OFF
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-*10/16/00      IBISTL-KR            ADDED DRILL DOWN PARMS
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
 

ON TABLE SET STYLE *

-INCLUDE &&DTSTY  
-* -INCLUDE DTTEST
-***********************IBISTL DRILLDOWN**********************
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N1, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N1 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N6, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N6 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL'\
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N11, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N11 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N16, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N16 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N21, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N21 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N26, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N26 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N37, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N37 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N42, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N42 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N53, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N53 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N58, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N58 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, STYLE=-UNDERLINE, COLUMN=N69, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N69 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
-***********************IBISTL DRILLDOWN**********************
TYPE=DATA, COLUMN=N87,COLOR=RGB(70 136 177), SIZE=7, FONT='ARIAL', $
TYPE=DATA, COLUMN=N88,COLOR=RGB(70 136 177), SIZE=7, FONT='ARIAL', $
TYPE=DATA, COLUMN=N89,COLOR=RGB(70 136 177), SIZE=7, FONT='ARIAL', $
TYPE=DATA, COLUMN=N90,COLOR=RGB(70 136 177), SIZE=7, FONT='ARIAL',$
TYPE=DATA, COLUMN=N91,COLOR=RGB(70 136 177), SIZE=7, FONT='ARIAL',$
TYPE=DATA, COLUMN=N92,COLOR=RGB(70 136 177), SIZE=7, FONT='ARIAL',$
TYPE=DATA, COLUMN=N93,COLOR=RGB(70 136 177), SIZE=7, FONT='ARIAL',$

ENDSTYLE

&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN

-GOTO XXIT;

-XLHDR;

-SET &&SP = '|';
-SET &&SP1A = IF &&ROLLNME EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP1 = IF &&ROLLCS EQ ' ' THEN ' ' ELSE &&SP1A;
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&CSLPA = IF &&RCNCT.EVAL GE 10 OR &&CSL.EVAL GE 10 THEN 60 ELSE 68;
-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' THEN 78 ELSE &&CSLPA;
-SET &&ROLLCS11 = TRUNCATE(&&ROLLCS);
-SET &&RCNCT1 = TRUNCATE(&&ROLLCNCT);
-SET &&BLANK = ' ';


HEADING
" "
"&&ROLLNME &&SP1 &&ROLLCS11 &&SP3 &&RCNCT1 &&SP4 &&ROLLPHN"
"&&DTDESC &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
" "
"&&RPT_TTL1"
"&&RPT_TTL2"
" "
" "
"<BLANK1 COUNT <+0> BASE FARE <+0> TAX <+0> FARE PAID"

FOOTING 
" "
" "
"NOTE: NEW SALES AND EXCHANGE SALES INCLUDE MCO'S"
"-  TOTAL TRANSACTION COUNT = NEW SALES + EXCHANGES + REFUNDS + VOIDS"
"-  TOTAL DOLLAR AMOUNT = NEW SALES + EXCHANGES + REFUNDS - VOIDS"
"-  TICKETS ISSUED ON TRAVEL SUMMARY = NEW SALES + EXCHANGE SALES - MCO'S"
"-  VOIDS ARE INCLUDED IN NEW SALES, EXCHANGE SALES, & REFUNDS" 
"-  VOID COUNT INCLUDES MCOS - TRAVEL SUMMARY VOID COUNT DOES NOT"
-*
-INCLUDE FOOTERDT
-*
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE TREXL

-***********************IBISTL DRILLDOWN**********************
TYPE=DATA, COLUMN=N1, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N1 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N6, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N6 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL'\
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N11, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N11 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N16, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N16 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N21, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N21 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N26, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N26 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N37, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N37 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N42, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N42 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N53, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N53 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N58, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N58 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
TYPE=DATA, COLUMN=N69, \ 
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTRANC' TRANTYPE=N69 FRMDT=&&FROMDT TODT=&&TODT \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
-***********************IBISTL DRILLDOWN**********************

ENDSTYLE

&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN

-GOTO XXIT;

-XXIT;

-*-EXIT
