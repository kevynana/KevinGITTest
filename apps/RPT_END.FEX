-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File RPT_END.FEX
-*
-*****   This routine resets the Report Execute Flag after completion of a
-*****     report execution. It also will be where a history record of the
-*****     execution gets generated.
-*****   AUTHOR:  Dave Kuzanek
-*****   PRODUCTION DATE:
-**********************************************************************
-*DATE		NAME		DESCRIPTION OF CHANGE			
-*----------------------------------------------------------------------
-*09/20/00	ibistl_jyn	delete the rpt_que record 
-*10/3/2000     IBISTL-RJ       CLEAR THE ZIP_FLAG ONCE A SET IS PROCESSED
-*10/10/00      IBISTL-RJ       CLEAR THE BROWSER OVERIDE FROM THE 
-*                              DISTRIBUTION FLAG
-***********************************************************************


MAINTAIN FILE RPT_INST AND RPT_SET AND CONTROL AND BRPT_QUE

COMPUTE INST_PARM/A72 = ;

REPOSITION CONTROL.CONTROL_ID
FOR ALL NEXT CONTROL.CONTROL_ID INTO Cntl_Stack

REPOSITION RPT_INST.INST_KEY
STACK CLEAR RI_Stack
NEXT RPT_INST.INST_KEY I_CODE.PRT_LN1 I_SUMM.SUMM_SUBJ1 INTO RI_Stack
  WHERE (RPT_INST.EXECUTE_FLAG EQ 'E') AND
        (RPT_INST.RPT_SERVER EQ Cntl_Stack(1).USER_ID) AND
        (RPT_INST.INST_KEY EQ Cntl_Stack(1).INST_KEY);

REPOSITION RPT_SET.SET_KEY
STACK CLEAR RS_Stack
NEXT RPT_SET.SET_KEY INTO RS_Stack
  WHERE RPT_SET.SET_KEY EQ RI_Stack.SET_KEY

COMPUTE RI_Stack(1).EXECUTE_FLAG = '';
COMPUTE RI_Stack(1).RPT_SERVER = '';
-* 10/10/00  IBISTL-RJ   CLEAR THE BROWSER OVERIDE FROM THE DISTRIBUTION
-*                       FLAG
COMPUTE RI_Stack(1).DISTRB_FLAG = 
       MASK(RI_Stack(1).DISTRB_FLAG,'9') | ' ';
-*
UPDATE RPT_INST.EXECUTE_FLAG RPT_INST.RPT_SERVER 
       RPT_INST.DISTRB_FLAG FROM RI_STACK(1)
COMMIT

-**********************************************************************
-*DATE		NAME		DESCRIPTION OF CHANGE			
-*----------------------------------------------------------------------
-*09/20/00	ibistl_jyn	delete the rpt_que record 
-***********************************************************************
REPOSITION BRPT_QUE.INST_KEY
STACK CLEAR QueStack
NEXT BRPT_QUE.INST_KEY INTO QueStack
  WHERE (BRPT_QUE.INST_KEY EQ RI_STACK.INST_KEY);

Delete BRPT_QUE.RPT_QUE.INST_KEY from QueStack(1) ;
-***********************************************************************

REPOSITION RPT_INST.INST_KEY
STACK CLEAR RI_Stack
FOR ALL NEXT RPT_INST.INST_KEY INTO RI_Stack
  WHERE (RPT_INST.EXECUTE_FLAG EQ 'Y') AND
        (RPT_INST.RPT_SERVER EQ Cntl_Stack(1).USER_ID) AND
        (RPT_INST.SET_KEY EQ RS_Stack(1).SET_KEY)
IF RI_Stack.FocCount EQ 0 THEN
BEGIN
  COMPUTE RS_Stack.EXECUTE_FLAG = '';
  IF RS_Stack(1).ZIP_FLAG EQ 'Y'
     BEGIN
        TYPE ON ZIPCHECK
           "<1 <Cntl_Stack(1).USER_ID <21 <RS_STACK(1).SET_KEY";
     ENDBEGIN
-* 10/3/2000  IBISTL-RJ  CLEAR THE ZIP_FLAG ONCE A SET IS PROCESSED
  COMPUTE RS_Stack.ZIP_FLAG = '';
  UPDATE RPT_SET.EXECUTE_FLAG RPT_SET.ZIP_FLAG FROM RS_STACK(1)
  COMMIT
ENDBEGIN


GOTO END RESET


END
