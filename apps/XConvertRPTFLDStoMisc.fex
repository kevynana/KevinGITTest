-**************************************************************************************************
-* file name: ConvertRPTFLDStoMisc.fex
-*            convert some fields from RPT_FLDS.FOC into SQL rpt_flds_misc table.
-*
-* SQL tables: RPT_FLDS_MISC
-*    column names in SQL: INST_KEY, CURRENCY_CODE
-*  (from air segment) CITYPAIRFLD, AIR_RANKBY, AIR_RANKBY_TOP1, AIR_RANKBY_TOP2, AIR_FORECAST
-*  (from car segment) CAR_RANKBY, CAR_RANKBY_TOP1, CAR_RANKBY_TOP2, CAR_FORECAST
-*  (from htl segment) HTL_RANKBY, HTL_RANKBY_TOP1, HTL_RANKBY_TOP2, HTL_FORECAST
-*         
-* Retrieve related columns from air, car and htl segments and put into rpt_flds_misc table.
-* 
-**************************************************************************************************
-SET &ECHO = ALL;
SET ASNAME = ON

-INCLUDE ConvertOldPath
-RUN

-**************************************************************
-* Convert the following fields from AIR,CAR & HTL segments
-**************************************************************
-* retrieve misc from air segment
TABLE FILE RPT_FLDS
PRINT CURRENCY 
CITYPAIRFLD AS 'CITYPAIRFIELD' AIR.RANKMETHOD AS 'AIR_RANKBY' 
AIR.RANKBYTOP1 AS 'AIR_RANKBY_TOP1' 
AIR.RANKBYTOP2 AS 'AIR_RANKBY_TOP2'  AIRFORECAST AS 'AIR_FORECAST'
BY INST_KEY
WHERE CITYPAIRFLD NE 0 OR  AIR.RANKMETHOD NE 0 OR 
AIR.RANKBYTOP1 NE 0 OR AIR.RANKBYTOP2 NE 0 OR AIRFORECAST NE 0 
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE RPT_FLDS_MISC
FIXFORM FROM HOLD
MATCH INST_KEY
  ON MATCH UPDATE CURRENCY CITYPAIRFIELD AIR_RANKBY AIR_RANKBY_TOP1 AIR_RANKBY_TOP2 AIR_FORECAST
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN

-* retrieve misc from car segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT CAR.RANKMETHOD AS 'CAR_RANKBY' 
CAR.RANKBYTOP AS 'CAR_RANKBY_TOP1' CARFORECAST AS 'CAR_FORECAST'
BY INST_KEY
WHERE CAR.RANKMETHOD NE 0 OR CAR.RANKBYTOP NE 0 OR CARFORECAST NE 0 
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE RPT_FLDS_MISC
FIXFORM FROM HOLD
MATCH INST_KEY
  ON MATCH UPDATE CAR_RANKBY CAR_RANKBY_TOP1 CAR_FORECAST
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN

-* retrieve misc from htl segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT HTL.RANKMETHOD AS 'HTL_RANKBY' HTL.RANKBYTOP1 AS 'HTL_RANKBY_TOP1' 
HTL.RANKBYTOP2 AS 'HTL_RANKBY_TOP2' HTLFORECAST AS 'HTL_FORECAST'
BY INST_KEY
WHERE HTL.RANKMETHOD NE 0 OR HTL.RANKBYTOP1 NE 0 OR HTL.RANKBYTOP2 NE 0 OR HTLFORECAST NE 0
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN
MODIFY FILE RPT_FLDS_MISC
FIXFORM FROM HOLD
MATCH INST_KEY
  ON MATCH UPDATE HTL_RANKBY HTL_RANKBY_TOP1 HTL_RANKBY_TOP2 HTL_FORECAST
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN


