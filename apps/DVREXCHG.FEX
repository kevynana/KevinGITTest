-*-* 5/10/10 - changed Level1 to be handled like all other Levels - Deb
-*5/23/14 - DEB   - ADDED EX5 TO RADV_PURCH DEFINE
-*7/12/16 -DLV -  S-20630   Added EMP_ID to report  
-*03/09/17 - DLV - S-22486 - Updated IMAGE define to use server report is being ran from



-DEFAULT &DSC = 'Exchange';
-SET &&FROMDT = EDIT(&&FROMDT,'9999999999');
-SET &&TODT = EDIT(&&TODT,'9999999999');
-SET &RPTTITLE = IF '&&ROLLUP.EVAL' NE 'OTHER-R' THEN  'Travel and Transport Exchange Tracking Report for &&ROLLNAME.EVAL Account'
- ELSE 'Travel and Transport Exchange Tracking Report for Client &&CLIENT.EVAL Account';

-IF &&EXCHGRECS EQ 0 THEN GOTO NoExchange2;

DEFINE FILE EXCHANGE
  PASNGR_NAME/A15 = IF TKT_NUM NE LAST TKT_NUM THEN XPSNGR_NM ELSE ' ';
  TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
  TRN_DT/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;
  PAR_DT/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN PARENT_DATE ELSE 0;
  XT_DTE/A8 = EDIT(TRN_DATE);
  XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
  XINV_NUM/A7 = IF TKT_NUM NE LAST TKT_NUM THEN INVOICE_NUM ELSE ' ';
  XNON_REF/A15 = IF TKT_NUM NE LAST TKT_NUM THEN NON_REF ELSE ' ' ;
-* ****DEFINES ON PREVIOUS DEFINES****
  SRT_DT/A100 = TKT_SORT||XT_DTE||LEVEL_DESC||XPSNGR_NM;
  DOC_NMBR/A10 = IF (DOC_NUMBER EQ '0000000000') THEN ' ' ELSE               
               IF (EMBARK EQ 'FP2') THEN ' ' ELSE
               IF (SRT_DT NE LAST SRT_DT) THEN DOC_NUMBER ELSE ' ';
  AUX_NMBR/A10 = IF (AUX_TKTLS_NUM EQ '0000000000') THEN ' ' ELSE               
               IF (EMBARK EQ 'FP2') THEN ' ' ELSE
               IF (SRT_DT NE LAST SRT_DT) THEN AUX_TKTLS_NUM ELSE ' ';
  EXCH_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN EFARE ELSE 0;
 
  FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0; 
  ORIG_SALE/D12.2C  = IF (EMBARK EQ 'FP2') THEN 0 ELSE 
                      IF (SRT_DT NE LAST SRT_DT) THEN ORG_SALE ELSE 0;
  ORIG_SL/D12.2CS = IF (EMBARK EQ 'FP2') THEN 0 ELSE 
                      IF (DOC_TYPE EQ '1') THEN 0 ELSE
                   IF (SRT_DT NE LAST SRT_DT) THEN ORG_SALE ELSE 0;
  LOST_SAV_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN DTSAVE ELSE 0;
  SAVE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSAVE ELSE 0;
  RADV_PURCH/I8CS = IF (EMBARK EQ 'FP2' OR 'EX1' OR 'RF1' OR 'EX5') THEN 0
  ELSE IF (TKT_TYPE EQ 'P-RF' OR 'P-EX') THEN 0
  ELSE IF (SEG_NBR EQ '00') THEN 0
  ELSE IF (TKT_SORT NE LAST TKT_SORT) AND (FARE_AMT GT 0) THEN NEW_ADV_PURCH
  ELSE IF (TKT_SORT NE LAST TKT_SORT) AND (FARE_AMT LT 0) 
  THEN NEW_ADV_PURCH * (-1) ELSE 0;
  RREFUSE_CODE/A2 = IF TKT_SORT NE LAST TKT_SORT THEN REFUSE_CODE ELSE ' ';
-*  ADD_AMT/D12.2CS = IF (DOC_NUMBER EQ '0000000000') THEN FARE_AMT ELSE
-*                    IF (DOC_NUMBER NE '0000000000')  THEN (FARE_AMT - ORIG_SALE) ELSE 0;
  ADD_AMT2/D12.2CS= IF (DOC_NUMBER EQ '0000000000') THEN FARE_AMT ELSE
                    IF TKT_TYPE EQ 'F-EX' THEN 0 ELSE
                    IF TKT_NUM NE LAST TKT_NUM THEN ADDCOLLECT ELSE 0;
  ADD_AMT/D12.2 = IF TKT_NUM NE LAST TKT_NUM THEN ADDCOLLECT ELSE 0;

  NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
-*IMAGE/A80 = '<img src=HTTPS://www.ettekreview.com/asp/NEW_HDR.jpg>';
IMAGE/A80 = '<img src=' || '&&SRVR.EVAL' || '/asp/NEW_HDR.PNG>';
END

TABLE FILE EXCHANGE
HEADING 
"<IMAGE>"
"</5"
"&RPTTITLE.EVAL"
"&&DTDESC.EVAL &&FROMDT - &&TODT " 
SUM BR_CL_IDX AS 'ACCT'
  LEVEL1 &&LV1
  XPSNGR_NM AS 'TRAVELER'
  EMP_ID &EMPID
  XTRAN_DT AS 'INVOICED'
  XFST_DPT AS 'TRAVEL'
  INVOICE_NUM AS 'INVOICE'  
  VAL_AIRLINE AS 'A/L'
  C_ITIN AS 'ITINERARY'
  TKT_NUM AS 'TICKET'
  OLD_ITIN AS 'ORIG ITINERARY'  DOC_NUMBER AS 'ORIG TICKET'
  ADD_AMT  AS 'AMT'
  ORIG_SALE AS 'ORIG AMT'
-*  FARE_AMT
-*  ADD_AMT2
-*  AIRLINE_FEE
COMPUTE TTL/D12.2S = ADD_AMT + ORIG_SALE; AS 'TOTAL AMT'
 LEVEL2 &&LV2 
 LEVEL3 &&LV3 
 LEVEL4 &&LV4 
 LEVEL5 &&LV5
 LEVEL6 &&LV6
 LEVEL7 &&LV7
 TRIP_PURPOSE &&TRIPPRP
 TRP_DESC &&TRIPRSN
BY BR_CL_IDX NOPRINT
BY XPSNGR_NM NOPRINT
BY XTRAN_DT NOPRINT
BY INVOICE_NUM NOPRINT
BY TKT_NUM NOPRINT
BY DOC_NUMBER NOPRINT
WHERE EX_FLAG NE 'F' OR 'P'

ON TABLE COLUMN-TOTAL

-*WHERE DOC_NMBR NE ' '
-*WHERE ORIG_SALE NE ' '
-*WHERE ADD_AMT NE ' '

-INCLUDE FOOTERDT

-*ON TABLE PAGE-BREAK AND SUBFOOT
ON TABLE SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
-*-INCLUDE &&DTSTY
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=1.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=1.000000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=8, COLOR=BLACK, GRID=ON, STYLE=NORMAL,WRAP= OFF, $
TYPE=TITLE, FONT=TIMES NEW ROMAN, SIZE=9, BACKCOLOR=RGB(170 170 170), $
TYPE=FOOTING, SIZE=6, $
TYPE=TITLE, SIZE=9, STYLE=BOLD+UNDERLINE, $
TYPE=HEADING, SIZE=10, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=14, LINE=8, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=10, LINE=9, STYLE=BOLD, FONT=TIMES NEW ROMAN,$

-*TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(&&TNTTRPOS), SIZE=(&&TNTTRSIZ), $
-*TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLTRPOS), SIZE=(&&ROLTRSIZ), $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, COLOR=BLACK, BACKCOLOR=RGB(170 170 170),$
TYPE=REPORT, TITLETEXT=&DSC,$

ENDSTYLE
&&OUTLINE1
-*&&OUTLINE2

END
-RUN

-IF &FOCERRNUM EQ 205  OR &LINES EQ 0 THEN GOTO NoExchange2;
-GOTO XXITExchg2;
-NoExchange2
-SET &TITLETEXT = 'Exchange   ';
-INCLUDE DVRNoRecord
-XXITExchg2
