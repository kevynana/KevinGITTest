-*  10/21/2016  REJ  S-22499 Updated for Post-trip Reporting change
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
EXPIRE_REPORTS=1
-DEFAULT &&RLUP = 'ABCD-R';
SET PAGE-NUM=OFF
SET EMPTYREPORT=ON
TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.
-RUN
 
TABLE FILE ROLLUPX
PRINT LOGO_2
BY ROLLUP_CODE NOPRINT
WHERE ROLLUP_CODE EQ '&&RLUP.EVAL'
ON TABLE SAVE
END
-RUN
-READ SAVE &LOGO2.8.
-SET &LOGO2 = '&LOGO2.EVAL' || '.GIF';
 
DEFINE FILE COVERPG
CATD/A3 = EDIT (CAT_DESC, '999$$$$$$$$$$$$');
END
 
TABLE FILE COVERPG
PRINT CP_TYPE1 CP_TYPE2 CP_DESC
BY CAT_DESC
BY CP_TYPE1 NOPRINT
IF ROLLUP EQ '&&RLUP'
IF CATD EQ 'CAR'
ON TABLE HOLD AS CARCOV FORMAT FOCUS
END
-RUN
 
DEFINE FILE CARCOV
CNT/I5 = CNT + 1;
ICNT/I5 = IMOD(CNT, 2, ICNT);
BCFLAG/A1 = IF ICNT EQ 0 THEN 'Y' ELSE 'N';
END
 
TABLE FILE CARCOV
HEADING
" "
" "
" "
"Post-trip Reporting - Run Date: &DATE "
"CAR COVERPAGE FOR: &&RLUP    <24 Report Name: CAR_COV.FEX"
-*" "
PRINT CP_DESC AS '' IN 10 BCFLAG NOPRINT
BY CAT_DESC NOPRINT
BY CP_TYPE1 AS ''
ON CAT_DESC SKIP-LINE
 
ON CAT_DESC SUBHEAD
" "
"<ST.CAT_DESC "
" "
"Code        <10 Description "
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL,$
TYPE=DATA, FONT=TIMES NEW ROMAN, SIZE=8, BACKCOLOR=RGB(234 234 234),WHEN=BCFLAG EQ 'N', $
TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, STYLE = BOLD ,$
TYPE=HEADING, LINE=4, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=SUBHEAD, BY=1, LINE=2, OBJECT=FIELD, ITEM=1, FONT=ARIAL, SIZE=10,STYLE=BOLD+ITALIC+UNDERLINE, COLOR=RGB(0 128 192), $
TYPE=SUBHEAD, LINE=4, OBJECT=TEXT, FONT=ARIAL, SIZE=8,STYLE=BOLD, $
TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=BLACK, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
-* TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(.2 .4 ), SIZE=(1.5 .4), $
TYPE=REPORT, IMAGE=&LOGO2, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
ENDSTYLE
END
-RUN
