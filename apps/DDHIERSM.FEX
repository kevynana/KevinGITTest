
-**********************************************************************
-* Summary by Hierarchy - Air
-* 7/8/13 - Added defines/carried thru fields for CCI exchange fields
-* 9/23/13 Added code to be able to run by travel date - Deb
-*1/7/14    UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-* 3/18/14 Shortened LVL_PRINT so report displays on 1 page in pdf - Deb
-*3/20/14  Added logic for Petsmart report - JK
-* 5/8/14 Removed Period2 from PSSMHIER report - Deb 
-*6/4/14 - CREATED FOR NEW REPORT - DDHIERSM - DEB
-*6/6/14 - CORRECTED NO HTML ISSUE - DEB
-*6/11/14 Changed some code due to AVG adv pur days not balancing detail-Deb
-*11/11/14 - Researched/fixed %s over 100% - JK
-* ADDED TRANTYPE INCLUDE - JK 12/16/14 STORY ID S-06492
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-* Story ID S-10086 Added &&overdrill flag logic for use in 5.0 to choose to for the report to be drillable or non-drillable - 9/15/15 - JM
-* 02/24/31/17 DLV S-22486 - updated position of header and footer NEW_HDR NEW_FTR
-*4/25/2017-JEM-S-33318-updated logic for no records report
-* 05/24/2017 JEM S-34216 - updating to make styling/heading/positioning more consistent
-*10/04/2017 DLV D-01656 - Updated to run correctly in excel format
-**********************************************************************
-INCLUDE SETECHO
-*
-* FTR_DateTimePosit sets the position of the Post-trip date/time stamp text within the footer image.
-* The default line number (&FTR_DateTimeLine) in the FOOTING for the date/time stamp text is: 4.
-INCLUDE FTR_DateTimePosit
-*
SET ASNAMES = ON 
SET SPACE = 1
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');
-SET &&SP = '|';
-SET &&SP1A = IF &&ROLLNME EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP1 = IF &&ROLLCS EQ ' ' THEN ' ' ELSE &&SP1A;
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&REL = &&ROLLEMAIL.LENGTH;

-SET &&RCSL = &&RCNCT.EVAL + &&CSL.EVAL;

-SET &&CSLPA = IF '&&ROLLUP.EVAL' EQ 'ABCD-R' THEN 80 ELSE 
-              IF '&&ROLLEMAIL.EVAL' NE ' ' AND &&REL GT 20 THEN 68 ELSE
-              IF &&RCSL LE 21 THEN 80 ELSE 
-              IF &&RCSL LE 31 THEN 75 ELSE 68;

-* -SET &&CSLPA = IF &&RCNCT.EVAL GE 15 OR &&CSL.EVAL GE 10 THEN 68 ELSE 75;
-*-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' THEN 88 ELSE &&CSLPA;

-SET &&CSLP = IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' AND &&ORIENTATION LIKE 'L%' THEN 88 
- ELSE IF '&&ROLLUP.EVAL' EQ 'BOOZ-R' AND &&ORIENTATION LIKE 'P%' THEN 68 
- ELSE IF '&&ROLLUP.EVAL' EQ 'HDRR-R' AND &&ORIENTATION LIKE 'L%' THEN 80
- ELSE IF &&ORIENTATION LIKE 'P%' THEN &&CSLPA-15 ELSE &&CSLPA;
-SET &&BLANK = ' ';
-SET &&ROLLPHNEML = IF '&&ROLLEMAIL.EVAL' GT ' ' THEN '&&ROLLEMAIL.EVAL' ELSE '&&ROLLPHN.EVAL';
-RUN

-SET &WHATDATE = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'TRN_DATE' ELSE 'FST_DPT_DATE';
 
 

CREATE FILE DDHIER
-RUN


-*********************************************************************************
-* Gather/Add Air Info to QRHIER File
-*********************************************************************************

DEFINE FILE SR_50
  CURRENT/A1 = IF &WHATDATE.EVAL GE &&FROMDT.EVAL AND &WHATDATE.EVAL LE &&TODT.EVAL THEN 'Y' ELSE 'N';
  PRIOR/A1 = IF &WHATDATE.EVAL GE &&FXDATE.EVAL AND &WHATDATE.EVAL LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
  CGADV/A8 = IF (ADV_PURCH LT 7) THEN '0-6' ELSE 
             IF (ADV_PURCH GE 7) AND (ADV_PURCH LE 13) THEN '7-13' ELSE
             IF (ADV_PURCH GE 14) AND (ADV_PURCH LE 20) THEN '14-20' ELSE
                '21+';  
 MXADV/A8  = IF ADV_PURCH LT 14 THEN 'Y' ELSE 'N';                  
 BRK_LVL/I2 = &&BRK_LVL;
  LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = GETTOK(LEVEL_DESC1, 60, -1, ')', 25, LEVEL_DESC);
  
  FARE_PAID/P12.2 = SEG_AMT + SEG_TAX;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;

  NET_TKT_CNT/P12    = IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (FARE_PAID GE 0) AND
                          (SEG_COUNT EQ 1) AND 
                          (CONJ_REL EQ 1) THEN 1 ELSE 
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND 
                          (EX_FLAG EQ 'F') THEN (-1) ELSE 
                       IF (TICKET_CODE EQ '1') AND
                          (SEG_NBR EQ '01') AND
                          (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

  SAVINGS/P12.2     = SEG_STANDARD - FARE_PAID;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
 

  NEW_SEG_MILES/D12 = SEG_MILES;

 -* Defines on Previous Defines
  DISSAVINGS/P12.2 = IF EMBARK EQ 'DT1' THEN 0 ELSE
                   FARE_PAID-SEG_LOWEST;


-* Defines added for CCI exchange fields
  F_EXCH_CNT/D8C    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 0;
CFEXCH/P12CS = IF CURRENT EQ 'Y' THEN F_EXCH_CNT ELSE 0;  
CAIR_FEE/P12.2 = IF CURRENT EQ 'Y' THEN AIRLINE_FEE ELSE 0;
-INCLUDE TRANTYPE
END
-RUN




TABLEF FILE SR_50
PRINT CGADV
  CURRENT
  PRIOR
  DISSAVINGS
  DOD_TYPE
  AIR_MAIN.INTL_DOM AS 'INTDOM'
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR 
  TKT_NUM
  TRN_DATE
  CFEXCH
  CAIR_FEE
  RF_FLAG
  EX_FLAG
  TKT_DATE
  MXADV
WHERE (&WHATDATE.EVAL GE &&FROMDT.EVAL) AND (&WHATDATE.EVAL LE &&TODT.EVAL) OR
      (&WHATDATE.EVAL GE &&FXDATE.EVAL) AND (&WHATDATE.EVAL LE &&TXDATE.EVAL)
WHERE VOID_DATE EQ 0
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN


TABLE FILE RPTHOLD
PRINT CGADV
  CFEXCH
  CAIR_FEE
  DISSAVINGS
  DOD_TYPE
  INTDOM
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR 
  
  TKT_NUM
  TRN_DATE
  RF_FLAG
  EX_FLAG  
  MXADV
BY CURRENT
BY PRIOR
BY LEVEL_DESC
ON TABLE HOLD AS TRANONE 
END
-RUN

TABLE FILE TRANONE
PRINT CGADV
  CFEXCH
  CAIR_FEE
  DISSAVINGS
  DOD_TYPE
  INTDOM
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR  
  TKT_NUM
  TRN_DATE
  RF_FLAG
  EX_FLAG  
  MXADV
BY CURRENT
BY PRIOR
BY LEVEL_DESC
BY TKT_NUM
ON TABLE HOLD AS DDONE 
END
-RUN
 
TABLE FILE DDONE
SUM CGADV
  CFEXCH
  CAIR_FEE
  DISSAVINGS
  DOD_TYPE
  INTDOM
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  NEW_ADV_PURCH
  NEW_SEG_MILES
  NONREF_FLAG
  ONLINE_TRADITIONAL
  SEG_COUNT
  SEG_NBR  
  TKT_NUM
  TRN_DATE
  RF_FLAG
  EX_FLAG  
  MXADV
BY CURRENT
BY PRIOR
BY LEVEL_DESC
BY TKT_NUM
WHERE (DISSAVINGS GE 300) OR (DISSAVINGS LE (-300))
ON TABLE HOLD AS DDTWO
END
-RUN



-SET &&NOLSV300 = IF &RECORDS EQ 0 THEN 'Y' ELSE 'N';


DEFINE FILE TRANONE
NEW_ADV_PURCH1/D9 = IF RF_FLAG EQ ' ' THEN NEW_ADV_PURCH ELSE
                  IF EX_FLAG EQ ' ' THEN NEW_ADV_PURCH ELSE (NEW_ADV_PURCH * (-1));
-*REC_CNT/D8CS = IF NET_TKT_CNT EQ 1 THEN 1 ELSE 0;
-*TTLDAYS/D8 = NEW_ADV_PURCH*REC_CNT;

TTLDAYS/D8 = NEW_ADV_PURCH*NET_TKT_CNT;
CAVOL/P12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
CMILES/P12 = IF CURRENT EQ 'Y' THEN NEW_SEG_MILES ELSE 0;
PAVOL/P12.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CNTKT/P12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CNTKTD/P12 = IF CURRENT EQ 'Y' AND INTDOM EQ 'D' THEN NET_TKT_CNT ELSE 0;
PNTKT/P12 = IF PRIOR EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CADVT/P12 = IF (CURRENT EQ 'Y') AND 
                (CGADV EQ '0-6') THEN NET_TKT_CNT ELSE 0;
CADVT1/P12 = IF (CURRENT EQ 'Y') AND
                 (CGADV EQ '7-13') THEN NET_TKT_CNT ELSE 0;
DDCADVT1/P12 = IF (CURRENT EQ 'Y') AND (MXADV EQ 'Y') THEN NET_TKT_CNT ELSE 0;
-*                 (CGADV EQ '07 - 13') THEN NET_TKT_CNT ELSE 0;                 
CONL/P12 = IF (CURRENT EQ 'Y') AND (ONLINE_TRADITIONAL EQ 'ONLINE') AND (INTDOM EQ 'D') THEN
                  NET_TKT_CNT ELSE 0;
CNR/P12 = IF (CURRENT EQ 'Y') AND (NONREF_FLAG EQ 'N') THEN 
              NET_TKT_CNT ELSE 0;
CTTLDAYS/D8 = IF CURRENT EQ 'Y' THEN TTLDAYS ELSE 0;                
END
-RUN


TABLE FILE TRANONE
SUM CADVT
    CADVT1
    CAVOL
    DDCADVT1
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL   
    CFEXCH/D12
    CAIR_FEE
    CTTLDAYS
BY LEVEL_DESC
ON TABLE HOLD AS TRANTWO 
END
-RUN



-IF '&&NOLSV300.EVAL' EQ 'Y' THEN GOTO NoHighLSV;

DEFINE FILE DDTWO
NEW_ADV_PURCH1/D9 = IF RF_FLAG EQ ' ' THEN NEW_ADV_PURCH ELSE
                    IF EX_FLAG EQ ' ' THEN NEW_ADV_PURCH ELSE (NEW_ADV_PURCH * (-1));
-*REC_CNT/D8CS = IF NET_TKT_CNT EQ 1 THEN 1 ELSE 0;
-*TTLDAYS/D8 = NEW_ADV_PURCH*REC_CNT;
TTLDAYS/D8 = NEW_ADV_PURCH*NET_TKT_CNT;

CAVOL/P12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
CMILES/P12 = IF CURRENT EQ 'Y' THEN NEW_SEG_MILES ELSE 0;
PAVOL/P12.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CNTKT/P12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CNTKTD/P12 = IF CURRENT EQ 'Y' AND INTDOM EQ 'D' THEN NET_TKT_CNT ELSE 0;
PNTKT/P12 = IF PRIOR EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CADVT/P12 = IF (CURRENT EQ 'Y') AND 
                (CGADV EQ '0-6') THEN NET_TKT_CNT ELSE 0;
CADVT1/P12 = IF (CURRENT EQ 'Y') AND
                 (CGADV EQ '7-13') THEN NET_TKT_CNT ELSE 0;
DDCADVT1/P12 = IF (CURRENT EQ 'Y') AND (MXADV EQ 'Y') THEN NET_TKT_CNT ELSE 0;
-*                 (CGADV EQ '7-13') THEN NET_TKT_CNT ELSE 0;                 
DDCLSAV/P12.2 = IF CURRENT EQ 'Y' THEN DISSAVINGS ELSE 0;

CONL/P12 = IF (CURRENT EQ 'Y') AND (ONLINE_TRADITIONAL EQ 'ONLINE') AND (INTDOM EQ 'D') THEN
                  NET_TKT_CNT ELSE 0;
CNR/P12 = IF (CURRENT EQ 'Y') AND (NONREF_FLAG EQ 'N') THEN 
              NET_TKT_CNT ELSE 0;
CTTLDAYS/D8 = IF CURRENT EQ 'Y' THEN TTLDAYS ELSE 0;                
END
-RUN


TABLE FILE DDTWO
SUM CADVT
    CADVT1
    CAVOL
    DDCADVT1
    DDCLSAV  
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL   
    CFEXCH/D12
    CAIR_FEE
    CTTLDAYS
BY LEVEL_DESC
ON TABLE HOLD AS DDTHREE 
END
-RUN


-GOTO ContMatch;

-NoHighLSV
DEFINE FILE TRANTWO
DDCLSAV/P12.2 = 0;
END
TABLE FILE TRANTWO
SUM CADVT
    CADVT1
    CAVOL
    DDCADVT1
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL   
    CFEXCH/D12
    CAIR_FEE
    CTTLDAYS
    DDCLSAV
BY LEVEL_DESC
ON TABLE HOLD AS DDTHREE
END
-RUN

-IF &RECORDS EQ 0 GOTO NOREC;



-ContMatch
 
MATCH FILE TRANTWO
SUM CADVT
    CADVT1
    CAVOL
    DDCADVT1
    CMILES
    CNR
    CNTKT
    CONL
    PNTKT
    CNTKTD
    CNR
    PAVOL   
    CFEXCH 
    CAIR_FEE
    CTTLDAYS
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE DDTHREE
SUM DDCLSAV
BY LEVEL_DESC
AFTER MATCH HOLD AS XTWO OLD-OR-NEW
END
-RUN

TABLE FILE XTWO
PRINT CAVOL  CNTKT PNTKT DDCLSAV CMILES PAVOL  CFEXCH CAIR_FEE  DDCADVT1 CONL CNTKTD CNR CTTLDAYS  LEVEL_DESC   
RANKED BY HIGHEST CAVOL
ON TABLE HOLD AS RNK1
END
-RUN


-INCLUDE OTHPARMS
-RUN
 
DEFINE FILE RNK1
-* RANK_LIMIT/I2 = 9999;
 RANK_LIMIT/I2 = &&RANK_LIMIT;
 RLIMIT/A2    = EDIT(RANK_LIMIT) ;
 CRANK/D5     = RANK;
 BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
 LIST/D6 = IF LEVEL_DESC NE LAST LEVEL_DESC THEN (LAST LIST + 1) 
           ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999'); 
 RANK_VALUE/A9 = IF RANK_LIMIT EQ 0 THEN BRANK ELSE
                IF LIST GT RANK_LIMIT THEN 'ALL OTHER' ELSE FRANK;
 LVL_PRINT/A25 =IF RANK_VALUE LT RLIMIT THEN LEVEL_DESC ELSE 'ALL OTHER';
END
-RUN

TABLE FILE RNK1
SUM CAVOL  CNTKT PNTKT DDCLSAV CMILES PAVOL  CFEXCH CAIR_FEE  DDCADVT1 CONL CNTKTD CNR CTTLDAYS  LEVEL_DESC   
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS RNK2
END
-RUN




TABLE FILE RNK2
PRINT LVL_PRINT RANK_VALUE CAVOL  CNTKT PNTKT DDCLSAV CMILES PAVOL  CFEXCH CAIR_FEE  DDCADVT1 CONL CNTKTD CNR CTTLDAYS      
ON TABLE SAVE
END
-RUN

 


MODIFY FILE DDHIER
LOG DUPL MSG OFF
FIXFORM LVL_PRINT/25 RANK_VALUE/9 CAVOL/12  CNTKT/12 PNTKT/12 DDCLSAV/12 CMILES/12 PAVOL/12  CFEXCH/12 CAIR_FEE/12  DDCADVT1/12 CONL/12 CNTKTD/12 CNR/12 CTTLDAYS/8  

MATCH RANK_VALUE LVL_PRINT
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN




TABLE FILE RNK1
SUM LST.LIST LVL_PRINT 
WHERE LVL_PRINT NE 'ALL OTHER'
ON TABLE HOLD AS LASTRANK
END
-RUN


-IF &RECORDS EQ 0 THEN GOTO NOADDP;

 
DEFINE FILE LASTRANK
NRANK/D6 = LIST + 1;
NRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
FRANK/A6  = EDIT(NRANKX,'$999999'); 
CAVOL/P12.2 =  0;
CMILES/P12 =  0;
PAVOL/P12.2 =  0;
CNTKT/P12 =  0;
CNTKTD/P12 =  0;
PNTKT/P12 =  0;
DDCLSAV/P12.2 =  0;
DDCADVT1/P12 = 0;
CONL/P12 =  0;
CNR/P12 =  0; 
CFEXCH/P12 = 0;
CAIR_FEE/P12.2 = 0;
CTTLDAYS/P8 = 0;
 RANK_VALUE/A9 = 'ALL OTHER' ;
 LVL_PRINT/A25=  'ALL OTHER'; 
END
TABLE FILE LASTRANK
PRINT NRANK LVL_PRINT RANK_VALUE CAVOL  CNTKT PNTKT DDCLSAV CMILES PAVOL  CFEXCH CAIR_FEE  DDCADVT1 CONL CNTKTD CNR CTTLDAYS     
ON TABLE HOLD AS LASTRANK1 
END
-RUN
 
TABLE FILE LASTRANK1
PRINT LVL_PRINT RANK_VALUE CAVOL  CNTKT PNTKT DDCLSAV CMILES PAVOL  CFEXCH CAIR_FEE  DDCADVT1 CONL CNTKTD CNR CTTLDAYS     
ON TABLE SAVE
END
-RUN

MODIFY FILE DDHIER
LOG DUPL MSG OFF
FIXFORM LVL_PRINT/25 RANK_VALUE/9 CAVOL/12  CNTKT/12 PNTKT/12 DDCLSAV/12 CMILES/12 PAVOL/12  CFEXCH/12 CAIR_FEE/12  DDCADVT1/12 CONL/12 CNTKTD/12 CNR/12 CTTLDAYS/8 
MATCH RANK_VALUE LVL_PRINT
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN 

 


-NOADDP
DEFINE FILE RNK2
LVLP/A27 = ''''||LVL_PRINT||'''';
END
-RUN
TABLE FILE RNK2
PRINT LVLP
BY LVL_PRINT NOPRINT
ON TABLE SAVE AS LEVELF
END
-RUN





-*********************************************************************************
-* Gather/Add Car Info to QRHIER File Based upon 
-*********************************************************************************

EX CARUSE
-RUN


-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN

-INCLUDE RESETCPM
-RUN


-SET &WHATDATEC = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'INVOICE_DATE' ELSE 'PICKUP_DATE';



DEFINE FILE CR_50
 LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = GETTOK(LEVEL_DESC1, 60, -1, ')', 25, LEVEL_DESC);
 RENTAL_DAYS/P12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
END

-RUN
TABLE FILE CR_50
SUM RENTAL_DAYS
    RENTAL_AMT
BY LEVEL_DESC
WHERE (&WHATDATEC.EVAL GE &&FROMDT.EVAL) AND (&WHATDATEC.EVAL LE &&TODT.EVAL)

-INCLUDE RPTPARMS
ON TABLE HOLD AS CAR1
END
-RUN
 
 



TABLE FILE CAR1
SUM RENTAL_AMT/P12.2 AS 'CARVOL'
    RENTAL_DAYS/P12 AS 'CARDAY'
BY LEVEL_DESC
ON TABLE HOLD AS CAR2
END
-RUN


-******************************************************
-* Matching Hierarchy Levels
-******************************************************

TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARMREC
END
-RUN




TABLE FILE CARMREC
SUM CARVOL
    CARDAY
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS CARMREC1
END
-RUN

TABLE FILE CARMREC1
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE DDHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12 CARDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL CARDAY
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN

-**********************************************************************
-* Non Matching information ALL OTHER
-**********************************************************************

TABLE FILE CAR2
SUM CARVOL
    CARDAY
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS CARNREC
END
-RUN

 

DEFINE FILE CARNREC
LVL_PRINT/A25 = 'ALL OTHER';
END
-RUN
TABLE FILE CARNREC
SUM CARVOL CARDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE DDHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 CARVOL/12 CARDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE CARVOL CARDAY
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN
 

-*********************************************************************************
-* Gather/Add HTL Info to QRHIER File
-*********************************************************************************
FILEDEF RPTPARMS CLEAR
-RUN 

EX HTLUSE
-RUN

-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN

-INCLUDE RESETHPM
-RUN

-SET &WHATDATEH = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'INVOICE_DATE' ELSE 'CHKIN_DATE';
 


DEFINE FILE HR_50
 LEVEL_DESC1/A60 = &&LDESC;
  LEVEL_DESC/A25 = GETTOK(LEVEL_DESC1, 60, -1, ')', 25, LEVEL_DESC);
 NBR_DAYS/P12=NUM_DAYS;
 NEW_TOTAL_AMT/P12.2 = TOTAL_AMT;
 NNROOMS/P12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
END
-RUN

TABLE FILE HR_50
SUM NEW_TOTAL_AMT
    NNROOMS
BY LEVEL_DESC
WHERE (&WHATDATEH.EVAL GE &&FROMDT.EVAL) AND (&WHATDATEH.EVAL LE &&TODT.EVAL) 
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTL1
END
-RUN
 


-**************************************************************************
-* Matching Hotel Records
-**************************************************************************
TABLE FILE HTL1
SUM NEW_TOTAL_AMT AS 'HTLVOL'
    NNROOMS AS 'HTLDAY'
BY LEVEL_DESC
ON TABLE HOLD AS HTL2
END
-RUN


TABLE FILE HTL2
SUM HTLVOL
   HTLDAY
BY LEVEL_DESC
WHERE LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLMREC
END
-RUN

TABLE FILE HTLMREC
SUM HTLVOL HTLDAY
BY LEVEL_DESC AS 'LVL_PRINT'
ON TABLE HOLD AS HTLMREC1
END
-RUN

TABLE FILE HTLMREC1
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE DDHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 HTLVOL/12 HTLDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE HTLDAY HTLVOL
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN

-**********************************************************************
-* Non Matching ALL OTHER Records
-**********************************************************************
TABLE FILE HTL1
SUM NEW_TOTAL_AMT AS 'HTLVOL'
    NNROOMS AS 'HTLDAY'
BY LEVEL_DESC
WHERE NOT LEVEL_DESC IN FILE LEVELF
ON TABLE HOLD AS HTLNREC
END
-RUN


DEFINE FILE HTLNREC
LVL_PRINT/A25 = 'ALL OTHER';
END
-RUN
TABLE FILE HTLNREC
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE HOLD AS HTLNREC1
END
-RUN

TABLE FILE HTLNREC1
SUM HTLVOL HTLDAY
BY LVL_PRINT
ON TABLE SAVE
END
-RUN

MODIFY FILE DDHIER
LOG NOMATCH MSG OFF
FIXFORM LVL_PRINT/25 HTLVOL/12 HTLDAY/12
MATCH LVL_PRINT
ON MATCH UPDATE HTLDAY HTLVOL
ON NOMATCH REJECT
DATA ON SAVE
END
-RUN


-SUMMARY


DEFINE FILE DDHIER
RANK_VALUEA/A9 = 'TOTAL';
LVL_PRINTA/A25 = 'TOTAL';
END
-RUN

TABLE FILE DDHIER
SUM CAVOL/P12.2M 
  PCT.CAVOL/P4 AS 'PCTVOL'
  CNTKT 
  DDCLSAV/P12.2M AS 'CLSAV'
  PCT.CFEXCH/P4 AS 'PCTEX'
  CAIR_FEE/P12.2M
    CTTLDAYS/P8
COMPUTE ADVAP/P8 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE 
            (CTTLDAYS/CNTKT); 

COMPUTE CTLS/P12 =  IF CAVOL EQ 0 THEN 0 ELSE ((DDCLSAV/CAVOL)*100); 
COMPUTE CAVGT/P12.2M = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE CAVOL/CNTKT;   
COMPUTE PAVGT/P12.2M = IF ((PNTKT LT 1) AND (PNTKT GT (-1))) THEN 0 ELSE PAVOL/PNTKT; NOPRINT
COMPUTE AAVGV/P12.2M = CAVGT - PAVGT; NOPRINT
COMPUTE AAVGV1/P12 = IF PAVGT EQ 0 THEN 0 ELSE (((CAVGT-PAVGT)/PAVGT)*100); 
COMPUTE CADVP2/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((DDCADVT1/CNTKT)*100); 
COMPUTE CONLP/P12  = IF CNTKTD EQ 0 THEN 0 ELSE ((CONL/CNTKTD)*100); 
COMPUTE CNRP/P12  = IF CNTKT EQ 0 THEN 0 ELSE ((CNR/CNTKT)*100); 

COMPUTE CPMILE/P4.2 = IF CMILES EQ 0 THEN 0 ELSE (CAVOL/CMILES);
COMPUTE CARAR/P12.2M = IF ((CARDAY LT 1) AND (CARDAY GT (-1))) THEN 0 ELSE CARVOL/CARDAY; 
COMPUTE HTLARR/P12.2M = IF ((HTLDAY LT 1) AND (HTLDAY GT (-1))) THEN 0 ELSE HTLVOL/HTLDAY; 
COMPUTE TACH/P12.2M = CAVOL + CARVOL + HTLVOL;  
BY RANK_VALUEA AS 'RANK_VALUE'
-* BY LVL_PRINTA AS 'LVL_PRINT'
BY LVL_PRINTA
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS TTL1
END
-RUN





TABLE FILE TTL1
SUM CAVOL/P12.2M PCTVOL CNTKT/P12 DDCLSAV/P12 CTLS/P12 CAVGT/P12 AAVGV1/P12
     CADVP2/P12 CONLP/P12 CNRP/P12 CPMILE/P10.2 PCTEX  CAIR_FEE/P12.2M CTTLDAYS/P8 ADVAP/P8 CARAR/P12 HTLARR/P12 TACH/P12
BY RANK_VALUE 
BY LVL_PRINT
ON TABLE HOLD AS TTL2
END
-RUN

TABLE FILE TTL2
PRINT CAVOL PCTVOL CNTKT DDCLSAV CTLS CAVGT AAVGV1  CADVP2 
      CONLP CNRP CPMILE PCTEX CAIR_FEE CTTLDAYS ADVAP CARAR HTLARR TACH
BY RANK_VALUE
BY LVL_PRINT

ON TABLE HOLD AS TTL3 FORMAT FOCUS
END
-RUN


DEFINE FILE DDHIER
NWLVL_PRINT/A25 = EDIT(LVL_PRINT, '9999999999999999999999999');
END
TABLE FILE DDHIER
SUM CAVOL/P12.2M 
  PCT.CAVOL/P4 AS 'PCTVOL'
  CNTKT 
  DDCLSAV/P12.2M AS 'CLSAV'
  PCT.CFEXCH/P4 AS 'PCTEX'
  CAIR_FEE/P12.2M
  CTTLDAYS/P8
  DDCADVT1
COMPUTE ADVAP/P8 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE 
            (CTTLDAYS/CNTKT); 

COMPUTE CTLS/P12 =  IF CAVOL EQ 0 THEN 0 ELSE ((DDCLSAV/CAVOL)*100); 
COMPUTE CAVGT/P12.2M = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0 ELSE CAVOL/CNTKT;   
COMPUTE PAVGT/P12.2M = IF ((PNTKT LT 1) AND (PNTKT GT (-1))) THEN 0 ELSE PAVOL/PNTKT; NOPRINT
COMPUTE AAVGV/P12.2M = CAVGT - PAVGT; NOPRINT
COMPUTE AAVGV1/P12 = IF PAVGT EQ 0 THEN 0 ELSE (((CAVGT-PAVGT)/PAVGT)*100); 
COMPUTE CADVP2/P12 = IF CNTKT EQ 0 THEN 0 ELSE ((DDCADVT1/CNTKT)*100); 
COMPUTE CONLP/P12  = IF CNTKTD EQ 0 THEN 0 ELSE ((CONL/CNTKTD)*100); 
COMPUTE CNRP/P12  = IF CNTKT EQ 0 THEN 0 ELSE ((CNR/CNTKT)*100); 
COMPUTE CPMILE/P4.2 = IF CMILES EQ 0 THEN 0 ELSE (CAVOL/CMILES);
COMPUTE CARAR/P12.2M = IF ((CARDAY LT 1) AND (CARDAY GT (-1))) THEN 0 ELSE CARVOL/CARDAY; 
COMPUTE HTLARR/P12.2M = IF ((HTLDAY LT 1) AND (HTLDAY GT (-1))) THEN 0 ELSE HTLVOL/HTLDAY; 
COMPUTE TACH/P12.2M = CAVOL + CARVOL + HTLVOL;  
BY RANK_VALUE
BY NWLVL_PRINT AS 'LVL_PRINT'
ON TABLE HOLD AS TLEV1
END
-RUN



TABLE FILE TLEV1


PRINT CAVOL/P12 PCTVOL/P4 CNTKT/P12 CLSAV/P12 CTLS/P12 CAVGT/P12 AAVGV1/P12  CADVP2/P12 
      CONLP/P12 CNRP/P12 CPMILE/P10.2 PCTEX CAIR_FEE/P12.2 CTTLDAYS/P8 ADVAP/P8 CARAR/P12 HTLARR/P12 TACH/P12
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV2 
END
-RUN


TABLE FILE TLEV2

PRINT CAVOL PCTVOL CNTKT CLSAV CTLS CAVGT AAVGV1  CADVP2 
      CONLP CNRP CPMILE PCTEX CAIR_FEE CTTLDAYS ADVAP CARAR HTLARR TACH
BY RANK_VALUE
BY LVL_PRINT
ON TABLE HOLD AS TLEV3 FORMAT FOCUS
END
-RUN



-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &PHOLD1=&HLDPATH || 'TLEV3.FOC';
-SET &PHOLD2=&HLDPATH || 'TTL3.FOC';
-RUN

USE CLEAR *
-RUN

EX SUUSES
-RUN

USE ADD 
&PHOLD1 AS TLEV3
&PHOLD2 AS TLEV3
END
-RUN


DEFINE FILE TLEV3
RANK_VAL1/A9 = IF RANK_VALUE EQ 'TOTAL' THEN 'ZZ' ELSE RANK_VALUE;
END
-RUN

TABLE FILE TLEV3
SUM CAVOL PCTVOL CNTKT CLSAV CTLS CAVGT AAVGV1  CADVP2 
    CONLP CNRP CPMILE PCTEX CAIR_FEE CTTLDAYS ADVAP CARAR HTLARR TACH
COMPUTE ZFLAG/A1 = IF (RANK_VAL1 EQ 'ALL OTHER') AND (TACH EQ 0) THEN 'Y' ELSE 'N';    
BY RANK_VAL1
BY LVL_PRINT
ON TABLE HOLD AS TLEV4
END
-RUN

-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF0

-SET &&RPTSUF = 'HIERSUM';
-INCLUDE FDEFRPTS
-RUN

JOIN ROLL WITH RANK_VAL1 IN TLEV4 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN

DEFINE FILE TLEV4
NOWTOD/A8 WITH RANK_VAL1 = HHMMSS (NOWTOD);
ROLL/A8='&&ROLLUP.EVAL';
  RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
  RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
  RN1/A60=TRIM('B', '&&RPT_TTL2.EVAL', 60, ' ', 1, 'A60');
  TDATE1/A8MDYY='&MDYY';
  TDATE2/A10=EDIT(TDATE1,'99/99/9999');
  TIME1/A8=HHMMSS('A8');
  TIME2/A8=EDIT(TIME1,'99$:99$:99');
  RD/A10 = 'Run Date:';
  RT/A10 = 'Run Time:';
  DTDESC/A20='Transaction Dates';
  CUR/A15='Current Period:';
  PRI/A15='Prior Period:';
  SPACE/A1 = 'A';
  KEY/A1 = IF RANK_VAL1 EQ 'TOTAL' THEN 'B' ELSE 'A';
END
-RUN


TABLE FILE TLEV4
HEADING 
-IF '&&OUTFMT.EVAL' EQ 'PDF' THEN GOTO HEAD2P;    


"&&ROLLNME &&SP1 &&ROLLCS &&SP3 &&RCNCT &&SP4 &&ROLLPHNEML"
"&&DTDESC  Period1: &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"

-SET &&ROLLCNCT = 'LENGTH';

"&&RPT_TTL1"
"&&RPT_TTL2"


-GOTO SkipHeadP

-HEAD2P
"<86 &&DTDESC"
"<42 <RN <83 <CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"<42 <RN1 <83 <PRI  &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
"<&&CSLP &&ROLLNME <+0> &&SP <+0> &&ROLLCS <+0> &&SP3 <+0> &&ROLLCNCT <+0> &&SP4 <+0> &&ROLLPHNEML"
-* "<35 &&RPT_TTL1"
-* "<35 &&RPT_TTL2"
" "
-* "Client: <6 <ROLL_NAME <42 <RN <90 <DTDESC"
-* "Contact: <6 <ROLL_CONTACT <40 <RN1 <90 <CUR  &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
-* "Phone: <6 <RPHONE <90 <PRI       &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
-* " "
-SkipHeadP
 
 
SUM CAVOL/P12CM AS 'Total,Volume' 
    PCTVOL/P4% AS '% of,Total,Volume'
    CNTKT/P12C AS 'Net,Tickets'
    CLSAV/P12CM AS 'Total,Lost,Savings'
    CTLS/P4% AS '% of,LS to,Air,Volume'
    CAVGT/P12CM AS 'Avg,Ticket,Price'    
    CADVP2/P4% AS '% of,Net Tickets,Purchased,0-13 Days' 
    ADVAP/P8 AS 'Avg,Nbr of,Bkg,Days'

    CONLP/P4% AS '% Dom,Online'
    CNRP/P4% AS '% of,NR'    
    CARAR/P12CM AS 'Avg,Car,Rate'
    HTLARR/P12CM AS 'Avg,Hotel,Cost Per,Night'
    TACH/P12CM AS 'Total Air,Car &,Hotel,Spend'
BY RANK_VAL1 NOPRINT
BY KEY NOPRINT
BY LVL_PRINT AS 'HIERARCHY'
WHERE ZFLAG EQ 'N'

ON TABLE SET PAGE-NUM OFF
&&OUTLINE1
&&OUTLINE2
-*
-INCLUDE FOOTERSM
-*
-* "<60 &M1.EVAL"
-*
ON TABLE PAGE-BREAK AND SUBFOOT
-* " "
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-IF '&&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO NOB;

UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.050000, RIGHTMARGIN=0.050000,
    TOPMARGIN=0.00000, BOTTOMMARGIN=0.010000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $ 



TYPE=REPORT, FONT='ARIAL', SIZE=7, STYLE=NORMAL, BACKCOLOR=NONE, JUSTIFY=LEFT, $


TYPE=DATA,
     GRID=OFF,
     SIZE=7,
     STYLE=NORMAL,
     BACKCOLOR=('WHITE' RGB(239 239 239)),
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR=RGB(5 76 149),
     BORDER-BOTTOM-COLOR=RGB(5 76 149),
     BORDER-LEFT-COLOR=RGB(5 76 149),
     BORDER-RIGHT-COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
     WHEN=N1 NE 'ZZ',
$
-NOB 

-IF &&OVERDRILL EQ 'Y' GOTO OVERDRILL;

TYPE=DATA, COLUMN=N4, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='A'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$
TYPE=DATA, COLUMN=N6, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='N'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$ 
TYPE=DATA, COLUMN=N7, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='D'     \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$
TYPE=DATA, COLUMN=N9, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='T'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$

TYPE=DATA, COLUMN=N10, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='X'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$ 
 
 

TYPE=DATA, COLUMN=N11, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='P'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$ 
TYPE=DATA, COLUMN=N12, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='O'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$ 
 
TYPE=DATA, COLUMN=N13, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMair1' ROW_LABEL=N1 LSEL=LVL_PRINT PARM1='R'    \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$
TYPE=DATA, COLUMN=N14, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMCAR' ROW_LABEL=N1 LSEL=LVL_PRINT   \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  
$
TYPE=DATA, COLUMN=N15, STYLE=-UNDERLINE, COLOR=RGB(0 0 138), \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='drSMHTL' ROW_LABEL=N1 LSEL=LVL_PRINT   \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
DTTYPE='&&DATETYPE.EVAL'\
ROLUP='&&ROLLUP' user='&&UNAME'),  

-OVERDRILL;
$
-IF '&&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO NOB2P;

TYPE=HEADING,
     SIZE=8,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$

TYPE=HEADING,
     SIZE=9,
     LINE=2,
     ITEM=1,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$

TYPE=HEADING,
     SIZE=9,
     LINE=3,
     ITEM=1,
     JUSTIFY=LEFT,
     COLOR=RGB(70 136 177),
     STYLE=BOLD,
     FONT='ARIAL',
$

TYPE=TITLE,
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
     GRID=ON,
     SIZE=7,
     COLOR=RGB(5 76 149),
     JUSTIFY=LEFT,
$
-GOTO PDFFMTP

-NOB2P
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000, 
    TOPMARGIN=0.300000, BOTTOMMARGIN=0.050000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
    
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=9, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=9, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8, 
     BACKCOLOR=(RGB(242 242 242) 'WHITE' ),$
-JOYP
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$
     
TYPE=TITLE, JUSTIFY=LEFT, $     
    
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$     
-*
TYPE=FOOTING, SIZE=7, FONT='ARIAL', COLOR=RGB(70 136 177), $
-*
-*
TYPE=SUBHEAD,
     STYLE=BOLD,
$

TYPE=SUBTOTAL, 
     STYLE=BOLD, 
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $
     
TYPE=GRANDTOTAL, STYLE=BOLD, $

-GOTO XSTYP

-PDFFMTP

TYPE=DATA, BACKCOLOR=NONE, GRID=OFF, SIZE=8,STYLE=BOLD, FONT='TIMES NEW ROMAN', JUSTIFY=LEFT, WHEN=N1 EQ 'ZZ',$
TYPE=SUBTOTAL, STYLE=BOLD, $
-*
TYPE=FOOTING, SIZE=7, FONT='ARIAL', COLOR=RGB(70 136 177), $
-*
TYPE=TABFOOTING, FONT=TIMES NEW ROMAN, SIZE=6, STYLE=BOLD, COLOR=RGB(70 136 177), $

TYPE=REPORT, IMAGE=NEW_HDR.jpg, POSITION=(0 0), SIZE=(2.8 .37),PRESERVERATIO=ON, $ 
TYPE=REPORT, IMAGE=NEW_FTR.jpg, POSITION=(0 8.0), SIZE=(10.5 .58), PRESERVERATIO=ON, $     
-*
TYPE=FOOTING, LINE=&FTR_DateTimeLine, STYLE=BOLD,POSITION=(&FTR_DatePosit),$ 
-*

-* TYPE=REPORT,GRID=OFF, $
-XSTYP
ENDSTYLE

END
-RUN
 

SET SPACE = 2

-GOTO XXIT;







-NOREC
-IF &&FROMAVRP NE 'Y' GOTO :AVSF2;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF2

-INCLUDE NORECRPT
-RUN
-EXIT



-XXIT

