-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**


-* 7/13/05 - DEB - INCLUDED REFUSE_CD 'R' TO TOTAL_NON DEFINE
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/8/14 Updated Pref_non define to use exclude_flags
-* 9/19/14 Updated to use exclude flags in % - JK
-*01/19/16 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*4/20/2017-JEM-S-33318-updated logic for no records report

TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
ON TABLE SAVE AS XROLL
END
-RUN
-READ XROLL &&RLUP.A8.

SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';


-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
  CITY_ST/A40 = PROP_STATE||(' '| PROP_CITY);
  LEVEL_DESC1/A80  = &&LDESC;
  NBR_DAYS/D12=NUM_DAYS;
  NBR_ROOMS/D12 = NUM_ROOMS;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
  REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
-* PREF_NON/A15 = IF CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'PREFERRED' ELSE 'NON-PREFERRED';
  PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
  PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
  
  PREF_NON/A15 = IF PNP_FLAG EQ 'I'  THEN 'PREFERRED' ELSE 'NON-PREFERRED';

-* Defines on Previous Defines

RATE_RM1/A1 = IF ROOM_RATE GT 150  OR
                 ROOM_RATE LT (-150) THEN 'Y' ELSE 'N';

-* Defines on previously defines defines
TOTAL_RATE/D14.2C = IF RATE_RM1 EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
TOTAL_ROOM/D12 = IF RATE_RM1 EQ 'Y' THEN NNROOMS ELSE 0;
-* TOTAL_NON/D12 = IF REFUSE_CD NE 'A' OR 'N' OR 'S' OR 'M' OR 'T' OR 'R'
-*                 THEN NNROOMS ELSE 0;
NNROOMS3/P12CS = IF PNP_IE EQ 'I' THEN NNROOMS ELSE 0;
TOTAL_NON/D12 = IF PREF_NON EQ 'NON-PREFERRED' THEN NNROOMS ELSE 0; 
TOTAL_NON1/D12 = IF PNP_FLAG EQ 'E' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 0;
-INCLUDE IDLEVDEFINE 

END
-RUN

TABLEF FILE &&EXTRACT
PRINT CURR_DATE
      LEVEL_DESC
      NNROOMS 
      NNROOMS3
      NEW_TOTAL_AMT
      TOTAL_NON
      TOTAL_NON1
      TOTAL_RATE
      TOTAL_ROOM
      RATE_RM1
-INCLUDE &HTLDATES  
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS HTLHOLD
END
-RUN

TABLE FILE HTLHOLD
PRINT LEVEL_DESC
      NNROOMS 
      NNROOMS3
      NEW_TOTAL_AMT
      TOTAL_NON
      TOTAL_NON1
      TOTAL_RATE
      TOTAL_ROOM
      RATE_RM1
BY CURR_DATE
ON TABLE HOLD AS HTLHOLD2
END
-RUN

-* Global Reporting JOIN
JOIN HTLHOLD2.CURR_DATE IN HTLHOLD2 TO GLOBAL.DATE IN GLOBAL AS J1
END


DEFINE FILE HTLHOLD2
NAMEC/A30 = CNAME;
 

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
AMT_TOTALX/D20.6 = NEW_TOTAL_AMT * CURR1X;
RATE_TOTALX/D20.6B = TOTAL_RATE * CURR1X;
END

TABLE FILE HTLHOLD2
PRINT LEVEL_DESC
      NNROOMS 
      NNROOMS3
      NEW_TOTAL_AMT
      TOTAL_NON
      TOTAL_NON1
      TOTAL_RATE
      TOTAL_ROOM
      RATE_RM1
      CURR_DATE 
      NAMEC
      AMT_TOTALX/D16.2CS
      RATE_TOTALX/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HHOLD
END
-RUN

TABLE FILE HHOLD
SUM TOTAL_NON
    TOTAL_NON1
    TOTAL_ROOM
    NNROOMS
    NNROOMS3
    NAMEC
BY LEVEL_DESC
ON TABLE HOLD AS ABNONHTL
END
-RUN

-SET &&HTLREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';
