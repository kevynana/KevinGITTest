-*09/26/2017 - DLV - S-37268 - updated define for ND_CP_ND ND_APT_CDO ND_APT_CDD

-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**


-INCLUDE SETECHO
-TYPE EXTRACT1
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
-SET &WHATDATE = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'TRN_DATE' ELSE 'FST_DPT_DATE';
-SET &&ORGDST = 'ND_CP_NM';
-SET &&AIR1 = 'B6';
-SET &&AIR2 = ' ';
-SET &&AIR3 = ' ';
-SET &&AIR4 = ' ';

DEFINE FILE &&EXTRACT
COMM_SEG/D12.2S   = SEG_COM;
-* CP_CL_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'   
-*                               'D' 'DISCOUNT'  'B' 'BUSINESS');


CP_CL_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
		               'D' 'DEFAULT'   'B' 'BUSINESS'
		               'E' 'ECONOMY'   'U'  'UPGRADE' 
                               ' ' 'DEFAULT');   

DIR_CITY_CD/A50 = EMB_CTY.EMB_CITY || '-' || RTE_CTY.RTE_CITY;
DIR_CITY_NM/A50 = EMB_CTY.CITY_NAME | RTE_CTY.CITY_NAME; 
DIR_CP_CD/A50 = CITYPAIR.EMBARK || '-' || CITYPAIR.ROUTE;
DIR_CP_NM/A50 = EMB_APT.AIRPORT_NAME | RTE_APT.AIRPORT_NAME;
EMB_APT/A21=EDIT(NDO_APT.AIRPORT_NAME,'999999999999999999999$$$$');
FARE_PAID/D12.2S  = SEG_AMT;
GEO1/A3 =  EDIT(TKT_MAIN.GEO_ZONE, 
'999$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$');    
ND_CITY_CD/A50 = NDO_CTY.NDO_CITY || '-' || NDD_CTY.NDD_CITY;
ND_CITY_NM/A50 = NDO_CTY.CITY_NAME | NDD_CTY.CITY_NAME;
-*ND_CP_CD/A50  = NDO_APT.ND_ORG || '-' || NDD_APT.ND_DST;
ND_CP_CD/A50  = ND_ORG || '-' || ND_DST;

NET_COMM/D12.2S = (SEG_AMT - SEG_COM);
-* NET_COMM/D12.2S   = IF (SEG_AMT LT 0) AND (SEG_COM GT 0) THEN 
-*                    (SEG_AMT + SEG_COM) ELSE (SEG_AMT - SEG_COM);
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
NEW_SEG_COUNT/D8CS = SEG_COUNT;
OCTRY/A2 = EDIT(TKT_MAIN.CTRY_COD, '99');
OGZONE/A3 = EDIT(TKT_MAIN.GEO_ZONE, '999');
RTE_APT/A21=EDIT(NDD_APT.AIRPORT_NAME,'999999999999999999999$$$$');
TOTAL_FARE/D12.2S = SEG_AMT + SEG_TAX;
VALUA/A1 =   IF (AIRLINE EQ 'UA') AND 
                (FLIGHT GE '5000') AND
                (FLIGHT LE '5499') THEN 'Y' ELSE 
             IF (AIRLINE EQ 'UA') AND
                (FLIGHT GE '5500') AND
                (FLIGHT LE '5899') THEN 'Y' ELSE
             IF (AIRLINE EQ 'UA') AND
                (FLIGHT GE '6800') AND
                (FLIGHT LE '7019') THEN 'Y' ELSE
             IF (AIRLINE EQ 'UA') AND
                (FLIGHT GE '7020') AND
                (FLIGHT LE '7149') THEN 'Y' ELSE
             IF (AIRLINE EQ 'UA') AND
                (FLIGHT GE '7150') AND
                (FLIGHT LE '7899') THEN 'Y' ELSE
             IF (AIRLINE EQ 'UA') AND
                (FLIGHT GE '7900') AND
                (FLIGHT LE '7899') THEN 'Y' ELSE 'N';
VASAVINGS/D11.2CS = SEG_DISCOUNT - TOTAL_FARE;
XPSNGR_NM/A15 = EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
XTRAN_DT/MDYY = TRN_DATE;
KILO/D9      = CITYPAIR.SEG_MILES * 1.60934 ;
XTRAN_YY/YY          = &WHATDATE;
XTRAN_M/M            = &WHATDATE;  

-* ****DEFINES ON PREVIOUS DEFINES****

DISSAVINGS/D11.2CS= IF EMBARK EQ 'DT1' THEN 0 ELSE
                         TOTAL_FARE - SEG_LOWEST;
ND_CP_NM/A50 = EMB_APT|'  '|RTE_APT;
ORGDST/A50 = &&ORGDST;
SAVINGS/D11.2CS   = SEG_STANDARD - TOTAL_FARE;  

-******** NEW DEFINES TO CORRECT CITY/AIRPORT NAME/CODE ISSUE JK ******

-******* PRODUCES DIRECTIONAL CITY CODES ***************
DIR_CTY_CDO/A25 = EMB_CTY.EMB_CITY;
DIR_CTY_CDD/A25 = RTE_CTY.RTE_CITY;

-******* PRODUCES DIRECTIONAL CITY NAMES ***************
DIR_CTY_NMO/A25 = EMB_CTY.CITY_NAME;
DIR_CTY_NMD/A25 = RTE_CTY.CITY_NAME;

-********* PRODUCES DIRECTIONAL AIRPORT CODES ********
DIR_APT_CDO/A25 = CITYPAIR.EMBARK;
DIR_APT_CDD/A25 = CITYPAIR.ROUTE;

-********* PRODUCES DIRECTIONAL AIRPORT NAMES ********
DIR_APT_NMO/A25 = EMB_APT.AIRPORT_NAME;
DIR_APT_NMD/A25 = RTE_APT.AIRPORT_NAME;

-********* PRODUCES NON-DIRECTIONAL CITY CODES ********
ND_CTY_CDO/A25 = NDO_CTY.NDO_CITY;
ND_CTY_CDD/A25 = NDD_CTY.NDD_CITY;

-********* PRODUCES NON-DIRECTIONAL CITY NAMES ********
ND_CTY_NMO/A25 = NDO_CTY.CITY_NAME;
ND_CTY_NMD/A25 = NDD_CTY.CITY_NAME;

-******** PRODUCES NON-DIRECTIONAL AIRPORT CODES ****
-*ND_APT_CDO/A25 = NDO_APT.ND_ORG;
-*ND_APT_CDD/A25 = NDD_APT.ND_DST;
ND_APT_CDO/A25 = ND_ORG;
ND_APT_CDD/A25 = ND_DST;

-******** PRODUCES NON-DIRECTIONAL AIRPORT NAMES ****
ND_APT_NMO/A25 = EDIT(NDO_APT.AIRPORT_NAME, '999999999999999999999$$$$');
ND_APT_NMD/A25 = EDIT(NDD_APT.AIRPORT_NAME, '999999999999999999999$$$$');
END


TABLEF FILE &&EXTRACT
PRINT 
  AIR_KEY   
  AIRLINE   
  AIRLINE.AIRLINE AS 'AIR_LINE' 
  AIRLINE.AIRLINE_NAME  
  AR_BRANCH 
  CITYPAIR.AIRLINE AS 'CP_AIRLINE'  
  CITYPAIR.CLASS AS 'CP_CLASS'  
  CITYPAIR.INTL_DOM AS 'INTL_DOM'   
  CITYPAIR.NET_COMM AS 'NET_COMM'   
  CITYPAIR.SEG_MILES/D9C AS 'MILES' 
  CLASS 
  CLS_CAT.CAT_DESC AS 'CAT_DESC'    
  CP_CL_CAT 
  DISSAVINGS    
  DOC_TYPE  
  DST_APC
  EMB_APT.GEO_ZONE AS 'EGEO'
  EMBARK
  FARE_PAID AS 'CP_FARE_PD'
  KILO  
  ND_CP_CD  
  ND_CP_NM
  NET_FARE  
  NET_TKT_CNT   
  NEW_SEG_COUNT 
  ORG_APC
  ORGDST
  ROUTE 
  RTE_APT.GEO_ZONE AS 'RGEO'
  REFUSE_KEY    
  SAVINGS   
  SEG_COUNT
  SEG_DESIG 
  SEG_NBR
  TKT_DATE  
  TKT_NUM   
  TOTAL_FARE    
  TRN_DATE
  VALUA     
  VASAVINGS     
  XPSNGR_NM 
  XTRAN_DT
  XTRAN_M
  XTRAN_YY
-******* NEW FIELDS *********
  DIR_CTY_CDO
  DIR_CTY_CDD
  DIR_CTY_NMO
  DIR_CTY_NMD
  DIR_APT_CDO
  DIR_APT_CDD
  DIR_APT_NMO
  DIR_APT_NMD
  ND_CTY_CDO
  ND_CTY_CDD
  ND_CTY_NMO
  ND_CTY_NMD
  ND_APT_CDO
  ND_APT_CDD
  ND_APT_NMO
  ND_APT_NMD  
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS

ON TABLE HOLD AS MKSHRHLD
END
-RUN

DEFINE FILE MKSHRHLD
LABEL1/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMO ELSE ' ';

 LABEL2/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMD ELSE ' ';
ORGDSTN/A55 = LABEL1||'-'||LABEL2;               
 ROLLCD/A8 = '&&ROLL' ;
 NMB/I2 = NMB + 1;
               
END               

TABLE FILE MKSHRHLD
SUM NET_COMM  
  MILES 
  CP_FARE_PD
  NET_FARE  
  NET_TKT_CNT   
  TOTAL_FARE  
BY ROLLCD
BY NMB
BY ORGDSTN
BY LABEL1
BY LABEL2
BY AIRLINE_NAME  
BY AIR_LINE
BY CP_AIRLINE
ON TABLE HOLD AS MKTSHR1
END
-RUN

TABLE FILE MKTSHR1
PRINT ORGDSTN 
      LABEL1
      LABEL2
      AIRLINE_NAME
      AIR_LINE
      CP_AIRLINE
      CP_FARE_PD
      NET_FARE
      NET_COMM
      TOTAL_FARE
      NET_TKT_CNT
      MILES
BY ROLLCD
BY NMB
ON TABLE HOLD AS MKTSHR1
END
-RUN

MODIFY FILE TCMS
FIXFORM FROM MKTSHR1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON MKTSHR1
END
-RUN




