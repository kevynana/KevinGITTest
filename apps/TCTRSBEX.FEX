-* 2/23/05 - JK Added TNT_COMPANY eq '1' or '2'
-*04/20/10   DEB                CHANGED ORIG_SALE DEFINE
-*1/30/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*1/30/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14    DEB    ADDED EX5 TO NUMEROUS DEFINES


SET TEMPERASE = OFF

-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
DEFINE FILE &&EXTRACT
XFST_DPT/MDYY = FST_DPT_DATE;
XTRAN_DT/MDYY        = TRN_DATE;

END

TABLE FILE &&EXTRACT
PRINT 
   AGENT_NUM
-* AIR_KEY
   C_ITIN
   DOC_TYPE
   DOD_TYPE
   DOD_DESC
   EMBARK
   EX_FLAG
   EXCHG_SALE
   RF_FLAG
   PASSNGR_NAME
   PNR_LOCATOR
   SEG_AMT/D12.2
   SEG_COM/D12.2
   SEG_COUNT
   SEG_NBR
   SEG_TAX/D12.2
   TKT_SORT
   TRN_DATE
   TKT_NUM
   TICKET_BRANCH
   VOID_DATE
   VOID_STATUS
   XFST_DPT
   XTRAN_DT
-* BY TKT_SORT NOPRINT
BY AIR_KEY
-INCLUDE &AIRDATES

&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-IF &&IKEY.EVAL NE 438235 OR 438268 OR 438269 THEN GOTO SKIP;
WHERE AIR_MAIN.INTL_DOM EQ 'I'
WHERE VOID_DATE EQ 0
-SKIP
-INCLUDE RPTPARMS
ON TABLE HOLD AS TC_TRAN 
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;
 

 

-INCLUDE TCPFXUSE
-RUN
 


TABLE FILE PFHOLDA
PRINT FEE_AMT/D12.2CS
BY AIR_KEY
ON TABLE HOLD AS FEEHOLD
END
-RUN
 


MATCH FILE TC_TRAN
PRINT AGENT_NUM C_ITIN DOC_TYPE DOD_TYPE EMBARK EX_FLAG EXCHG_SALE 
  PASSNGR_NAME PNR_LOCATOR RF_FLAG SEG_AMT
  SEG_COM SEG_COUNT SEG_NBR SEG_TAX TKT_SORT TRN_DATE TKT_NUM TICKET_BRANCH
  VOID_DATE VOID_STATUS XTRAN_DT XFST_DPT DOD_DESC

BY AIR_KEY
ON TABLE HOLD
RUN 
FILE FEEHOLD
PRINT FEE_AMT
BY AIR_KEY
AFTER MATCH HOLD AS TC_TRAN1 OLD
END
-RUN




DEFINE FILE TC_TRAN1

ROLLCD/A8 = '&&ROLL'; 


FLAG/A1 = 'A';
 COMM/D12.2 = IF VOID_DATE EQ 0 THEN SEG_COM ELSE 0;

EMCO_TAX/D15 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_TAX 
  ELSE 0;	
EXCH_AMT/D15 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF 
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR 
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND 
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_AMT ELSE 0;	
EXCH_MCO/D15 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_AMT 
  ELSE 0;	
EXCH_TAX/D15 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF 
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR 
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND 
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_TAX ELSE 0;	
EXMCOS/D15 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE 
	NE ' ') THEN 1 ELSE 0;  
FARE_PAID/D15.2 = SEG_AMT + SEG_TAX;	
FULL_RFNDS/D15 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (TKT_NUM 
          NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
IFLAG/A42 = DOD_DESC;

-*IFLAG/A33 = IF DOD_TYPE EQ 'I' OR 'J' THEN 'ONLINE BOOKINGS' ELSE
-*     IF DOD_TYPE EQ 'M' OR 'X' THEN 'EXCHANGE FROM AN INTERNET BOOKING' ELSE
-*     IF DOD_TYPE EQ 'A' OR 'L' OR 'N' OR 'S' THEN 'WEB BOOKINGS' ELSE
-*     'TRADITIONAL BOOKINGS'; 	
NEW_SALES/D15 = IF (SEG_AMT GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE 
  EQ ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5') THEN SEG_AMT ELSE 0;	
NEW_TAX/D15 = IF ((SEG_TAX GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE EQ 
  ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5')) THEN SEG_TAX ELSE 0;	
NEW_TOTAL/D15 = NEW_SALES + NEW_TAX;	
-*ORIG_SALE/D15 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;


ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND 
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;	

PAID_FARE/D15.2 = IF VOID_DATE EQ 0 THEN FARE_PAID ELSE 0;	
TSFARE_PAID/D15.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN 0 ELSE PAID_FARE ;


PART_RFNDS/D15 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;	
REFUND_AMT/D15 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_AMT ELSE 0;	
REFUND_TAX/D15 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_TAX ELSE 0;	
REFUND_TOTAL/D15 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN FARE_PAID ELSE 0;	
REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;	
TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;	


TOTL/A15 = 'TOTAL NEW SALES';	

TRAN_DT/MDYY = TRN_DATE;	
VOID_AMT/D15 = IF VOID_DATE NE 0 THEN SEG_AMT ELSE 0;	
VOID_TAX/D15 = IF VOID_DATE NE 0 THEN SEG_TAX ELSE 0;	
VOID_TOTAL/D15 = IF VOID_DATE NE 0 THEN FARE_PAID ELSE 0;	
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01')
 THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) THEN 1 ELSE 0;	

-* Defines on previous DEFINEs
AIRLINE_FEE/D12.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN PAID_FARE ELSE 0;
TOT_ETAX/D15 = EXCH_TAX + EMCO_TAX;	
TOT_EXCH/D15 = EXCH_AMT + EXCH_MCO;
EMCO_TOTAL/D15 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN FARE_PAID 
  ELSE 0;	
EXCHGS/D15 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
 NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE IF (DOC_TYPE EQ '1') AND 
(EXCHG_SALE EQ 'E') THEN 0 ELSE IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE 
  ' ') THEN TKT_CNT ELSE 0;
EXCH_TOTAL/D15 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE 
  IF (((EXCHG_SALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EMBARK NE 'DT1' OR 
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ') AND (EX_FLAG EQ 'F' OR 'P') AND 
  (SEG_COUNT LE 0)) OR (EMBARK EQ 'EX1' OR 'EX5')) THEN FARE_PAID ELSE 0;
MCO_AMT/D15 = IF DOC_TYPE EQ '1' THEN NEW_SALES ELSE 0;	
MCO_CNT/D15 = IF DOC_TYPE EQ '1' THEN ORIG_SALE ELSE 0;		
MCO_TAX/D15 = IF DOC_TYPE EQ '1' THEN NEW_TAX ELSE 0;	
MCO_TOTAL/D15 = IF DOC_TYPE EQ '1' THEN NEW_TOTAL ELSE 0;
MCOS/D15CS           = IF (DOC_TYPE EQ '1') AND
                           (EX_FLAG EQ ' ')  AND
                           (EXCHG_SALE NE ' ') THEN 1 ELSE 0;	
NET_AMT/D15 = NEW_SALES + REFUND_AMT + TOT_EXCH - VOID_AMT;	
NET_TAX/D15 = NEW_TAX + REFUND_TAX + TOT_ETAX - VOID_TAX;	
TOTAL_EXCHG/D15 = EXCH_TOTAL + EMCO_TOTAL;
TOT_EXAMT/D15 = EXCH_AMT + EXCH_MCO;	
TOT_EXTAX/D15 = EXCH_TAX + EMCO_TAX;
TOT_ECNT/D15 = EXCHGS + EXMCOS;	
-*TOT_TRANS/D15 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;	
TOT_TRANS/D15 = ORIG_SALE + VOIDS + EXCHGS + REFUNDS;



  
-* Defines on previous DEFINEs
NET_AMT/D15 = NEW_SALES + REFUND_AMT + TOT_EXAMT - VOID_AMT;
NET_TAX/D15 = NEW_TAX + REFUND_TAX + TOT_EXTAX - VOID_TAX;
NET_TOTAL/D15 = NEW_TOTAL + REFUND_TOTAL + TOTAL_EXCHG - VOID_TOTAL;
NET_TRAN/D15 = ORIG_SALE + EXCHGS - REFUNDS - VOIDS;	


END 



-* -IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================


TABLE FILE TC_TRAN1
SUM
ORIG_SALE 
EXCHGS 
EXMCOS 
REFUNDS 
MCOS  
VOIDS 
TOT_TRANS 
NEW_SALES 
NEW_TAX 
NEW_TOTAL 
EXCH_AMT 
EXCH_TAX 
EXCH_TOTAL  
EXCH_MCO 
EMCO_TAX 
EMCO_TOTAL 
REFUND_AMT 
REFUND_TAX 
REFUND_TOTAL 
MCO_AMT 
MCO_TAX 
MCO_TOTAL 
VOID_AMT 
VOID_TAX 
VOID_TOTAL 
NET_AMT 
NET_TAX 
NET_TOTAL 
PAID_FARE
TSFARE_PAID
FEE_AMT
AIRLINE_FEE 
C_ITIN
COMM
PNR_LOCATOR
XTRAN_DT
XFST_DPT

BY ROLLCD 
BY PASSNGR_NAME
BY TKT_NUM
ON TABLE HOLD AS TCT1
END
-RUN




MODIFY FILE TCSBTRAN
FIXFORM FROM TCT1
DATA ON TCT1
END
-RUN






-NEXTONE

 
