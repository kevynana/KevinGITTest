-**************************************************************************************************
-* CHANGED PROGRAM TO USE JOIN DUE TO RECORD LIMITATION ON IN FILE COMMAND
-* DEB 4/14/05
-*  10/19/2016  REJ  S-22499 Updated the logo to Post Trip Reporting.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-**************************************************************************************************
 
-INCLUDE SETECHO
SET ASNAMES=ON
EXPIRE_REPORTS=1
SET PAGE-NUM=OFF
SET EMPTYREPORT=ON
 
TABLE FILE CONTROL
PRINT SECURITY_LEVEL
BY ROLLUP_CODE
BY USER_ID
ON TABLE SAVE AS XUSER
END
-RUN
 
-READ XUSER &&RLUP.A8. &&USR.A20.
-RUN
 
TABLE FILE ROLLUPX
PRINT LOGO_2
BY ROLLUP_CODE NOPRINT
WHERE ROLLUP_CODE EQ '&&RLUP.EVAL'
ON TABLE SAVE
END
-RUN
-READ SAVE &LOGO2.8.
-SET &LOGO2 = '&LOGO2.EVAL' || '.GIF';
 
JOIN SET_KEY IN RPT_SET TO ALL SET_KEY IN RPT_INST
DEFINE FILE RPT_SET
DISTRB/A10 = DECODE DISTRB_FLAG ('T' 'SERVER' 'F' 'FTP'
                                 'E' 'EMAIL');
OTYPE/A22 = IF SETTINGS.OUTPUT_FORMAT EQ 'EXCEL' THEN 'EXCEL' ELSE
            IF SETTINGS.OUTPUT_FORMAT EQ 'EXL2K' THEN 'EXCEL 2000'
            ELSE IF SETTINGS.OUTPUT_FORMAT EQ 'PDF' THEN 'PDF'
            ELSE IF SETTINGS.OUTPUT_FORMAT EQ 'PPT' THEN 'PowerPoint'
            ELSE 'UNKNOWN';
REPORT/A35= EDIT(RPT_NAME,'99999999999999999999999999999999999$$$$$$$$$$$$$$$');
END
 
TABLE FILE RPT_SET
PRINT INST_KEY DISTRB OTYPE SET_FREQ
SET_NAME GLOBAL_PARM REPORT ROLLUP_CODE USER_NAME
BY INST_KEY NOPRINT
IF ROLLUP_CODE EQ '&&RLUP'
IF USER_NAME EQ '&&USR'
ON TABLE HOLD AS RPTSET FORMAT FOCUS INDEX INST_KEY
END
-RUN
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
TABLE FILE RPTSET
PRINT DISTRB
      OTYPE
      SET_FREQ
      SET_NAME
      GLOBAL_PARM
      REPORT
      ROLLUP_CODE
      USER_NAME
BY INST_KEY
ON TABLE HOLD AS XONE FORMAT FOCUS INDEX INST_KEY
END
-RUN
 
-SET &BLANK = '                           ';
-IF &&RLUP EQ 'TOTAL-R' THEN GOTO TOTAL;
 
JOIN INST_KEY IN RPT_FLDS TO INST_KEY IN XONE
 
TABLE FILE RPT_FLDS
PRINT HIEORG2.MEMBER AS 'MEMBER'
      HIEORG1.SEL_LEVEL AS 'SEL_LEVEL'
      HIEORG2.CATG AS 'CATG'
      HIEORG2.SEQ_NO AS 'SEQ_NO'
BY INST_KEY
WHERE RPT_FLDS.INST_KEY EQ XONE.INST_KEY
ON TABLE HOLD AS XTWO
END
-RUN
 
MATCH FILE XONE
PRINT DISTRB
      OTYPE
      SET_FREQ
      SET_NAME
      GLOBAL_PARM
      REPORT
      ROLLUP_CODE
      USER_NAME
BY INST_KEY
ON TABLE HOLD
RUN
FILE XTWO
PRINT MEMBER SEL_LEVEL CATG SEQ_NO
BY INST_KEY
AFTER MATCH HOLD AS RPTSET1 OLD
END
-RUN
 
TABLE FILE RPTSET1
HEADING
" "
" "
" "
"Post-trip Reporting"
"&DATE ........ &TOD        REPORT LISTING FOR : &&USR"
"REPORT NAME:  RPT_LST.FEX"
" "
" "
BY USER_NAME        AS 'USER NAME'
BY SET_FREQ         AS 'FREQUENCY'
BY SET_NAME         AS 'SET NAME'
 
BY GLOBAL_PARM      AS 'REPORT ID'
PRINT REPORT        AS 'REPORT NAME'
      MEMBER        AS 'HIERARCHY'
      SEL_LEVEL     AS 'LEVEL'
      OTYPE         AS 'FORMAT'
      DISTRB        AS 'OUTPUT'
      CATG          AS 'CATEGORY   '
-* WHERE SEQ_NO EQ 1
ON SET_NAME SKIP-LINE
ON SET_NAME
    SUBFOOT
    "&BLANK.EVAL"
 
WHERE USER_NAME NE ' ' AND SET_NAME NE ' '
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=6, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=DATA, SIZE=6, COLUMN=N4, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N5, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N6, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N7, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N8, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N9, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N10, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
 
TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, STYLE = BOLD ,$
TYPE=HEADING, LINE=4, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=1, STYLE=BOLD, BACKCOLOR=SILVER, GRID=ON, JUSTIFY=CENTER,  $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
-*TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
TYPE=REPORT, IMAGE=&LOGO2, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
END
-RUN
 
-GOTO DONE;
 
 
-TOTAL
TABLE FILE XONE
PRINT INST_KEY
BY INST_KEY NOPRINT
ON TABLE SAVE
END
-RUN
 
TABLE FILE RPT_FLDS
PRINT ROLL2.SEQ_NO AS 'SEQ_NO'
BY INST_KEY
WHERE INST_KEY IN FILE SAVE
ON TABLE HOLD AS XTWO
END
-RUN
 
MATCH FILE XONE
PRINT DISTRB
      OTYPE
      SET_FREQ
      SET_NAME
      GLOBAL_PARM
      REPORT
      ROLLUP_CODE
      USER_NAME
BY INST_KEY
ON TABLE HOLD
RUN
FILE XTWO
PRINT SEQ_NO
BY INST_KEY
AFTER MATCH HOLD AS TOTSET1 OLD
END
-RUN
 
 
TABLE FILE TOTSET1
HEADING
" "
" "
" "
"TRAVEL & TRANPORT, INC. - Post-trip Reporting"
"&DATE ........ &TOD        REPORT LISTING FOR : &&USR"
"REPORT NAME:  RPT_LST.FEX"
" "
" "
BY USER_NAME          AS 'USER NAME'
 
BY SET_FREQ         AS 'FREQUENCY'
BY SET_NAME         AS 'SET NAME'
BY GLOBAL_PARM      AS 'REPORT ID'
PRINT REPORT        AS 'REPORT NAME'
      OTYPE         AS 'FORMAT'
      DISTRB        AS 'OUTPUT'
 
WHERE SEQ_NO EQ 1
WHERE USER_NAME NE ' ' AND SET_NAME NE ' '
ON SET_NAME SKIP-LINE
ON SET_NAME
    SUBFOOT
    "&BLANK.EVAL"
ON TABLE SET ONLINE-FMT PDF
 
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=6, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=DATA, SIZE=6, COLUMN=N4, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N5, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N6, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N7, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N8, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N9, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=DATA, SIZE=6, COLUMN=N10, BACKCOLOR=( RGB(242 242 242) 'WHITE' ),$
TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, STYLE = BOLD ,$
TYPE=HEADING, LINE=4, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=8, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=14, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=1, STYLE=BOLD, BACKCOLOR=SILVER, GRID=ON, JUSTIFY=CENTER,  $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
-*TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
TYPE=REPORT, IMAGE=&LOGO2, POSITION=(.5 .5 ), SIZE=(1.5 .4), $
END
 
-GOTO DONE;
 
-NORECORDS
DEFINE FILE RPT_SET
EMPTY/A60 = 'NO REPORTS SET UP FOR THIS USER                             ';
EMPTY/A60 WITH USER_NAME = 'NO REPORTS SET UP FOR THIS USER';
END
 
TABLE FILE RPT_SET
PRINT EMPTY AS ''
BY USER_NAME NOPRINT
WHERE RECORDLIMIT EQ 1
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET STYLE *
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=12, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=REPORT, IMAGE=&LOGO2, POSITION=(.2 .2 ), SIZE=(1.5 .4), $
END
-RUN
 
-DONE
