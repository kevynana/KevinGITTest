-*1/29/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-* ADDED TRANTYPE INCLUDE - JK 12/19/14 STORY ID S-06492
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

-TYPE  drSMair1
-INCLUDE SETECHO
-DEFAULT &&OTFLAG ='XX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&LSEL = 'XX';
-SET &&DIFLAG = &PARM1;
 
-SET &&DTSTY = 'DT';

-SET &&LSEL = &LSEL;


-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';


-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-SET &&UNAME = &user;
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
EX RESET
-RUN


********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-**********************************************************************
-*
-**********************************************************************
-CONT


-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE GetForDrillDown
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   
 

-IF &&LSEL NE 'TOTAL' THEN GOTO NotTotal;

-SET &LCHK= ' ';
-GOTO Next;

-NotTotal;


TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN


-READ SAVE &LEVEL.2.

-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &LCHK='WHERE ' | &AROLL_DSC.EVAL | ' EQ ' | '''&&LSEL.EVAL''';

 
-Next



-****
-* &&DIFLAG WILL EQUAL ONE OF THE FOLLOWING:\
-* D (DOMESTIC), I (INTERNATIONAL), B (INTERNATIONAL AND DOMESTIC)
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK = ' ';
-SET &&DIFLAGCHK = ' ';
-SET &&AIRFEECHK = ' ';
-SET &&SUBHEAD = ' ';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NT_TKT_CNT AS NET,TKT,CNT';
-SET &&FAREAMT = 'FARE_AMT AS FARE,PAID';
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SAVINGS';
-SET &&NETVOLUME = '';
-SET &&TAXES = '';
-SET &&STANDARD = ' ';
-SET &&SAVINGS = ' ';
-SET &&LOWEST = 'LOW_TOT AS LOWEST,AMOUNT';
-SET &&LSTKT = 'WHERE (LSTSVNGS GE .001) OR (LSTSVNGS LE -.001)'; 
-SET &&DISCOUNT = ' ';
-SET &&VALUEADD = ' ';
-SET &&DCHK='WHERE TRN_DATE GE '&&FROMDT.EVAL' AND TRN_DATE LE '&&TODT.EVAL'';
-SET &&DIFLAGCHK = '';
-SET &&RPT_TTL1 = '                  LOST SAVINGS DETAIL';
-SET &&SUBHEAD = 'DOMESTIC AND INTERNATIONAL';
 
-IF &&DIFLAG EQ 'D' THEN GOTO DOM
- ELSE IF &&DIFLAG EQ 'I' THEN GOTO INTL
- ELSE GOTO ENDOT;
 
-DOM
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&SUBHEAD = 'DOMESTIC ONLY';

-GOTO ENDOT

-INTL
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&SUBHEAD = 'INTERNATIONAL ONLY';
-GOTO ENDOT



-ENDOT
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN 

EX AIRUSE
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
 
DEFINE FILE SR_50
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  FARE_PAID/D15.2  = SEG_AMT + SEG_TAX;
  IFLAG/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'I' ELSE 'N';
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  TKT_TYPE/A4         = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  MONTH/MT = TRN_DATE;
  LSTSVNGS/P12.2C    = IF EMBARK EQ 'DT1' THEN 0 ELSE
                        FARE_PAID - SEG_LOWEST;

-INCLUDE TRANTYPE
END
-RUN
 
TABLEF FILE SR_50
PRINT
  AIRLINE
  LSTSVNGS
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  FARE_PAID
  NET_TKT_CNT
  PASSNGR_NAME
  REFUSE_CODE
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  TKT_NUM
  TKT_TYPE
  TRN_DATE
  DPT_TIME
  XDPT_DT
  XTRAN_DT
  MONTH
  TKT_SORT
  SEG_NBR
  SEG_LOWEST
  TKT_DATE
  &&LDESC
  ROLLUP_CODE
  
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&LSTKT 
&&DCHK
&&VOIDCHK
&&REFUNDCHK
&&EXCHANGECHK
&&DIFLAGCHK
&&AIRFEECHK
&LCHK
-INCLUDE RPTPARMS
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS RPTHOLD
END
-RUN


 



-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN


 
 
TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT'
    FARE_PAID AS 'TFARE'
   LSTSVNGS AS 'TLOST'
   SEG_LOWEST AS 'TLOW'
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN
 
TABLE FILE RPTHOLD
PRINT AIRLINE
  LSTSVNGS
  DPT_TIME
  EMB_APT_NM
  FARE_PAID
  NET_TKT_CNT
  REFUSE_CODE
  RTE_APT_NM
  SEG_LOWEST
  TKT_NUM
  TKT_TYPE
  TRN_DATE
  XDPT_DT
  XTRAN_DT
  &&LDESC
  ROLLUP_CODE
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN
 
 
MATCH FILE XTWO
PRINT AIRLINE
  LSTSVNGS
  DPT_TIME
  EMB_APT_NM
  FARE_PAID
  NET_TKT_CNT
  REFUSE_CODE
  RTE_APT_NM
  SEG_LOWEST
  TKT_NUM
  TKT_TYPE
  XDPT_DT
  XTRAN_DT
  &&LDESC
  ROLLUP_CODE
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TFARE
      TTKT
      TLOST
      TLOW
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN
 
TABLE FILE AIRHOLD2
PRINT AIRLINE
  LSTSVNGS
  DPT_TIME
  EMB_APT_NM
  FARE_PAID
  NET_TKT_CNT
  PASSNGR_NAME
  REFUSE_CODE
  RTE_APT_NM
  SEG_LOWEST
  TFARE
  TLOST
  TLOW
  TKT_NUM
  TKT_TYPE
  TRN_DATE
  TTKT
  XDPT_DT
  XTRAN_DT
  &&LDESC
  ROLLUP_CODE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
DEFINE FILE AIRHOLD3
NT_TKT_CNT/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN PASSNGR_NAME ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/D15.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
LOST_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOST ELSE 0;
LOW_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;

NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END
-RUN

TABLE FILE AIRHOLD3
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'RSN,CDE'
&&DEPARTD
EMB_APT_NM AS 'ORG'
RTE_APT_NM AS 'DST'
AIRLINE AS 'CR'
&&FAREAMT
&&LOWEST
&&LSTSVGS

-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY TICKET_NMBR AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'

-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
ON XTKT_SRT SKIP-LINE
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
 
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;


-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-XXIT

