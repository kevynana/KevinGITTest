-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* Created for BLV's finacial report request JK 7/30/03

-**********************************************************************
-*DATE           NAME               DESCRIPTION
-* 8/8/03          DEB              ADDED CCARD2 CC_NUM SEG_AMT
-* 8/8/03          DEB              SEG_TAX INVOICE_NUM BR_CL_IDX
-* 3/24/04         JK               MADE ADDITIONS FOR THE NEW KOHLS REPORT
-*12/21/04         DEB              CHANGED OTFLAG DEFINE TO INCLUDE ADDTL
-*                                  DOD_TYPE
-*10/11/05         DEB              ADDED DOD_TYPE H TO OTFLAG
-*1/22/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*06/29/16         RSB              S-20630 - New Employee ID database field
-*                                  Added EMP_ID
-**********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
  CCARD2/A6 =EDIT(CC_NUM, '$$999999$$$$$$$$$$');
  CITY/A6=SUBSTR (176, CDIR_STP, 1, 6, 176, CITY);
  TSFLAG/A1=SUBSTR (11, TKT_SORT, 11, 11, 11, TSFLAG);
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC/A60      = &&LDESC;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NRFLAG/A15 = IF NONREF_FLAG EQ 'N' THEN 'NON-REFUNDABLE' ELSE
               IF NONREF_FLAG EQ 'R' OR ' '  THEN 'REFUNDABLE' ELSE ' ';
  OTFLAG/A33 = ONLINE_TRADITIONAL;

  ROLL/A1 = EDIT (AROLL_LEV4, '9$$$$$$$$$$$$$$');
  ROLL1/A60 = IF ROLL EQ '1' THEN 'STORES' ELSE
              IF ROLL EQ '2' THEN 'REGIONS' ELSE
              IF ROLL EQ '3' THEN 'TERRITORIES' ELSE
              IF ROLL EQ '4' THEN 'DISTRICTS' ELSE
              IF ROLL EQ '6' THEN 'DISTRICT CODES' ELSE
              IF ROLL EQ '7' THEN 'DISTRICT SECURITY' ELSE AROLL_DSC4;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';

  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  BOOK_DT/MDYY        = RESV_DATE;

-* DEFINES ON PREVIOUS DEFINES
AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3'
     THEN FARE_PAID ELSE 0;
DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
                      
-INCLUDE TRANTYPE  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT
  ADV_PURCH AS 'NEW_ADV_PURCH'
  AIR_MAIN.INTL_DOM AS 'INT_DOM'
  AIRLINE_FEE
  AROLL_DSC5
  AROLL_LEV5
  BOOK_DT
  BR_CL_IDX
  CITY
  CCARD2
  CC_NUM
  DISSAVINGS
  EMBARK
  EX_FLAG
  FARE_PAID
  INVOICE_NUM
  EMP_ID
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL_DESC
  NET_TKT_CNT
  NRFLAG
  OTFLAG
  PASSNGR_NAME	
  REFUSE_CODE
  REFUSAL_DESC
  ROLL1
  SEG_AMT
  SEG_TAX
  SEG_LOWEST/D12.2CS AS 'LOW_AMT'	
  SEG_COUNT
  SEG_NBR
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE
  TSFLAG	
  XTRAN_DT
  XDPT_DT	

-INCLUDE &AIRDATES

WHERE VOID_DATE EQ 0

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD
END
-RUN


