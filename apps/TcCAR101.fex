-**************************************************************
-* Date       Author    Modification
-**************************************************************
-* 01/23/2012 GSE       Removed parens from around CLIMIT to
-*                      prevent errors in 7.7.03
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-**************************************************************
-INCLUDE SETECHO
 
CREATE FILE TCCRNK
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCCARDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCC101EX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
-SET &&RANK_METH = IF &&RANK_METH EQ 'CAR_AMT' THEN 'CAR_AMT'
- ELSE IF &&RANK_METH EQ 'NMBR_DAYS' THEN 'NMBR_DAYS'
- ELSE 'NMBR_DAYS';
 
TABLE FILE TCCRNK
SUM BOOKINGS
    NBR_CARS
    NBR_DAYS/I8 AS 'NMBR_DAYS'
    RENTAL_AMT/D12.2 AS 'CAR_AMT'
BY PICK_CTY_NAME
BY VENDOR_NAME
BY GZONE
ON TABLE HOLD AS HOLD1
END
-RUN
 
 
TABLE FILE HOLD1
PRINT BOOKINGS
    CAR_AMT/D12.2
    NBR_CARS
    NMBR_DAYS
    VENDOR_NAME
    GZONE
BY PICK_CTY_NAME
RANKED BY HIGHEST &&RANK_METH NOPRINT
BY VENDOR_NAME
ON TABLE HOLD AS HOLD2
END
-RUN
 
DEFINE FILE HOLD2
  DRANK/D5      = RANK;
  ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
  ARANK/A5=EDIT(ARANKX,'$99999');
  RANK_LIMIT/I5 = 5;
  RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN
 
TABLE FILE HOLD2
SUM BOOKINGS
    CAR_AMT
    NBR_CARS
    NMBR_DAYS
BY PICK_CTY_NAME
BY RANK_VALUE
BY VENDOR_NAME
BY GZONE
ON TABLE HOLD AS HOLD3
END
-RUN
 
DEFINE FILE HOLD3
VENDOR_PRINT/A25 = IF RANK_VALUE LE '5' THEN VENDOR_NAME ELSE 'ALL OTHER';
END
 
TABLE FILE HOLD3
SUM BOOKINGS
    CAR_AMT
    NBR_CARS
    NMBR_DAYS
BY PICK_CTY_NAME
BY RANK_VALUE
BY VENDOR_PRINT
BY GZONE
ON TABLE HOLD AS HOLD4
END
-RUN
 
TABLE FILE HOLD1
SUM BOOKINGS
    CAR_AMT
    NBR_CARS
    NMBR_DAYS
BY PICK_CTY_NAME
ON TABLE HOLD AS RHOLD1
END
-RUN
 
TABLE FILE RHOLD1
SUM BOOKINGS
    CAR_AMT
    NBR_CARS
    NMBR_DAYS
RANKED BY HIGHEST &&RANK_METH AS 'DAYS_RANK'
BY PICK_CTY_NAME
ON TABLE HOLD AS RHOLD2
END
-RUN
 
MATCH FILE HOLD4
PRINT BOOKINGS
      CAR_AMT
      NBR_CARS
      NMBR_DAYS
      RANK_VALUE
      VENDOR_PRINT
      GZONE
BY PICK_CTY_NAME
ON TABLE HOLD
RUN
FILE RHOLD2
SUM DAYS_RANK
BY PICK_CTY_NAME
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN
 
TABLE FILE HOLD5
SUM BOOKINGS
    CAR_AMT
    NBR_CARS
    NMBR_DAYS
RANKED BY HIGHEST DAYS_RANK
BY PICK_CTY_NAME
BY RANK_VALUE
BY VENDOR_PRINT
BY GZONE
ON TABLE HOLD AS HOLD6
END
-RUN
 
DEFINE FILE HOLD6
  CTY_LIMIT/I2 = &&RANK_LIMIT;
  CLIMIT/A2    = EDIT(CTY_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF PICK_CTY_NAME NE LAST PICK_CTY_NAME THEN
                        (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6=EDIT(FRANKX,'$999999');
  CTY_VALUE/A6 = IF CTY_LIMIT EQ 0 THEN BRANK ELSE
               IF RANK GT CTY_LIMIT THEN 'OTHER' ELSE BRANK;
  CTY_PRINT/A35 = IF CTY_VALUE LT CLIMIT THEN PICK_CTY_NAME ELSE 'ALL OTHER';
  VENDOR_RANK/A5 = IF CTY_VALUE LT CLIMIT THEN RANK_VALUE ELSE ' ';
  VNDR/A30 = IF CTY_VALUE LT CLIMIT THEN VENDOR_PRINT ELSE  'OTHER';
END
-RUN
 
TABLE FILE HOLD6
SUM BOOKINGS
    CAR_AMT
    NBR_CARS
    NMBR_DAYS
BY CTY_VALUE
BY CTY_PRINT
BY VENDOR_RANK
BY VNDR
BY GZONE
ON TABLE HOLD AS HOLD7
END
-RUN
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
 
DEFINE FILE HOLD7
NOWTOD/A8 WITH CTY_VALUE = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE HOLD7
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
 &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
 
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
