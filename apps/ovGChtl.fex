-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
DEFINE FILE HR_50
NNROOMS/D10 = IF  (NUM_DAYS LT 0) AND
                  (NUM_ROOMS LT 0) THEN
                  (NUM_DAYS * NUM_ROOMS) * (-1) ELSE
                  (NUM_DAYS * NUM_ROOMS);

TH_AMT/D15.2 = TOTAL_AMT;

CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =   IF INVOICE_DATE GE &&FXDATE.EVAL AND
                INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';

CDAY/D10 = IF CURRENT EQ 'Y' THEN NNROOMS ELSE 0;
PDAY/D10 = IF PRIOR EQ 'Y' THEN NNROOMS ELSE 0;
CAMT/D15.2 = IF CURRENT EQ 'Y' THEN TH_AMT ELSE 0;
PAMT/D15.2 = IF PRIOR EQ 'Y' THEN TH_AMT ELSE 0;
END
-RUN

TABLEF FILE HR_50
PRINT CAMT
      CDAY
      CHAIN_NAME
      CURRENT
      CURR_DATE
      PAMT
      PDAY
      PRIOR
WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL OR
      INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL
&&WHERE1
&&WHERE2
&&WHERE3



-INCLUDE RPTPARMS
ON TABLE HOLD AS OVHHOLD 
END


TABLE FILE OVHHOLD
PRINT CAMT
      CDAY
      CHAIN_NAME
      CURRENT
      PAMT
      PDAY
      PRIOR
BY CURR_DATE
ON TABLE HOLD AS OVHHOLD1 FORMAT FOCUS INDEX CURR_DATE
END
-RUN


-* Global Reporting JOIN
JOIN OVHHOLD1.CURR_DATE IN OVHHOLD1 TO GLOBAL.DATE IN GLOBAL AS J1

DEFINE FILE OVHHOLD1
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
  CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
  NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;
  CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
  CHTLAMT/D20.6 = CAMT * CURR1X;
  PHTLAMT/D20.6 = PAMT * CURR1X;
 
-* DEFINES ON PREVIOUS DEFINES 

  CHAMTX/D20.6 = CHTLAMT;
  PHAMTX/D20.6 = PHTLAMT;

-* DEFINES ON PREVIOUSLY DEFINED DEFINES
  CHAMT1/D15.2 = CHAMTX;
  PHAMT1/D15.2 = PHAMTX;
 END
-RUN


TABLE FILE OVHHOLD1
SUM CDAY
    CHAMT1
    NAMEC
BY CHAIN_NAME
WHERE CURRENT EQ 'Y'
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HHOLD1 
END
-RUN

TABLE FILE OVHHOLD1
SUM PDAY
    PHAMT1
BY CHAIN_NAME
WHERE PRIOR EQ 'Y'
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HHOLD2  
END
-RUN

-* JOIN CLEAR *
-* -RUN

MATCH FILE HHOLD1
PRINT CDAY
      CHAMT1
      NAMEC
BY CHAIN_NAME
ON TABLE HOLD
RUN
FILE HHOLD2
PRINT PDAY
      PHAMT1
BY CHAIN_NAME
AFTER MATCH HOLD AS HHOLD3 OLD-OR-NEW
END

TABLE FILE HHOLD3
SUM CDAY
    CHAMT1
    PDAY
    PHAMT1
    NAMEC
BY CHAIN_NAME
ON TABLE HOLD AS HHOLD4 
END
-RUN

TABLE FILE HHOLD4
PRINT *
RANKED BY HIGHEST CDAY NOPRINT
ON TABLE HOLD AS HHOLD5 
END
-RUN


-SET &RECFOUND = &RECORDS;



DEFINE FILE HHOLD5
  LIST/D9 = IF CHAIN_NAME NE LAST CHAIN_NAME THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 3 THEN 'OTHER' ELSE ARANK1;
  CHAIN/A25 = EDIT(CHAIN_NAME, '9999999999999999999999999$$$$$');
  RANK_LABEL1/A25 = IF RANK_VALUE NE 'OTHER' THEN CHAIN ELSE 'All Other';
  RANK_LABEL2/A25 = LCWORD(25, RANK_LABEL1, RANK_LABEL2);
  RANK_LABEL/A25 = IF RANK_LABEL2 EQ 'Hilton Usa' THEN 'Hilton USA' ELSE RANK_LABEL2;
END
-RUN

TABLE FILE HHOLD5
SUM CDAY
    CHAMT1
    NAMEC
    PDAY
    PHAMT1
BY RANK_VALUE NOPRINT
BY RANK_LABEL
ON TABLE HOLD AS HHOLD6 
END
-RUN



GRAPH FILE HHOLD6
SUM CDAY AS ' '
ACROSS RANK_LABEL 
ON GRAPH SET LOOKGRAPH PIE
ON GRAPH SET GRAPHEDIT OFF
ON GRAPH SET GRAPHSTYLE *
setGraphType(56);
setLegendDisplay(true);
setAutofit(getLegendText(),true);
setLegendPosition(1);
setLegendMarkerPosition(2);
setFontSizeAbsolute(getTitle(),true);
setAutofit(getTitle(),false);
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),1);
setFontSize(getTitle(),15);
setTitleString("Hotel Summary");
setTextJustHoriz(getTitle(), 1);
setSubtitleString("Based Upon Number of Nights");
setTextJustHoriz(getSubtitle(), 1);
setFontSizeAbsolute(getSubtitle(),true); 
setAutofit(getSubtitle(),true);
setFontStyle(getSubtitle(),1);
setFontSize(getSubtitle(),15);
setPieFeelerTextDisplay(2);
setPieFeelerTextFormat(1);
setPieLabelDisplay(0);
setAutofit(getPieLabelDisplay(),true);
setFontStyle(getPieLabelDisplay(),1);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 115 160 247));
ENDSTYLE
ON GRAPH SET BARNUMB OFF
ON GRAPH SET 3D OFF
ON GRAPH SET GRID ON
ON GRAPH SAVE AS HOTEL FORMAT SVG
END
-RUN

-IF &&OUTFMT NE 'PDF' GOTO NOGRAPH1 ELSE GOTO GRAPH1;
-NOGRAPH1;
-SET &&H_GIF = 'N';
-GOTO GRAPHN1;
-GRAPH1;
-SET &&H_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';
-GRAPHN1;


DEFINE FILE HHOLD6
LABEL/A30 = 'Hotel Chains';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 6;
END
-RUN

TABLE FILE HHOLD6
PRINT CATEGORY/A61 AND RANK_LABEL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CHAMT1/P15C AS 'VOL_CURR' AND 
CDAY/P9CS AS 'TKT_CURR' AND PHAMT1/P15C AS 'VOL_PRIOR' 
AND PDAY/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS HHOLD7 
END
-RUN

TABLE FILE HHOLD7
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS HHOLD8 
END
MODIFY FILE OV1_GC
FIXFORM FROM HHOLD8
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HHOLD8
END
-RUN

-***********************************************************************************
-* Start of code added for total line addition
-***********************************************************************************
DEFINE FILE HHOLD6
LABEL/A30 = 'Hotel Chains';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 6;
RANK_LABEL1/A30 = 'Total';
END
-RUN

TABLE FILE HHOLD6
SUM CDAY
    CHAMT1
    NAMEC
    PDAY
    PHAMT1
BY CATEGORY
BY CAT_ORDER
BY RANK_LABEL1
ON TABLE HOLD AS HHOLDT
END
-RUN

TABLE FILE HHOLDT
PRINT CATEGORY/A61 AND RANK_LABEL1/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CHAMT1/P15C AS 'VOL_CURR' AND 
CDAY/P9CS AS 'TKT_CURR' AND PHAMT1/P15C AS 'VOL_PRIOR' 
AND PDAY/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = 5;    
ON TABLE HOLD AS HHOLDT1
END
-RUN

TABLE FILE HHOLDT1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS HHOLDT2
END
MODIFY FILE OV1_GC
FIXFORM FROM HHOLDT2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HHOLDT2
END
-RUN






