-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-* Focexec is used to help debug problems with the eTTek Review
-* reports.  This focexec will simulate the code that is executed
-* through Report Caster for jobs that are scheduled in a 'Run Now'
-* state.  Once the variables are supplied this job can be executed 
-* multiple times without having to log back into the User Interface
-* of the application and reschedule the report(s).
-*
-*
-* Changable variables
-*
-*   &&ROLLUP - Company you are running for
-SET &&ROLLUP='DSTM-R';
-*
-*   &&USERID - Userid of person running reports
-SET &&USERID='rjones';
-*
-*   &&OUTFMT - Output Format of Reports
-SET &&OUTFMT='PDF';
-*
-*   &&OUTTO - Ouput destination 
-SET &&OUTTO='TC';
-*
-*   &&RP_PM - Report Group type
-SET &&RP_PM='STS';
-*
-*   &&RT_PM - Report name 
-SET &&RT_PM='DSTM';
-*
-*   &&SE_PM - Set file name
-SET &&SE_PM='TSDSTM';
-*
-*   &&SK_PM - Set Key
-SET &&SK_PM='DSTM-R  DSTM  ma20010423094754';
                            
-*
-*   &&SN_PM - Set name
-SET &&SN_PM='Travel Summary';
-*
-*   &&ST_PM - Stream
-SET &&ST_PM='TRVLSUM1';
-*
-SET &&IX_PM='DSTM-R  ma200104230948';

-*   &&IN_PM - Instance Key
-SET &&IN_PM=
-'DSTM-R  DSTM  ma20010423094754STSMaster           DSTM-R  ma200104230948';
-*PRINC-R PRINC wb20020131124158STSMaster           PRINC wb20020131124142

-SET &&FM_PM='';
-SET &&FX_PM='';
-SET &&TO_PM='';
-SET &&TX_PM='';
-SET &&CMFM='05/01/2002';
-SET &&CMTO='05/31/2002';
-SET &&PMFM='05/01/2001';
-SET &&PMTO='05/31/2001';
-SET &&CYFM='01/01/2002';
-SET &&CYTO='05/31/2002';
-SET &&PYFM='01/01/2001';
-SET &&PYTO='05/31/2001';
-*
TABLE FILE ROLLUP
ON TABLE SET PAGE-NUM OFF
PRINT  ROLLUP_CODE COMP UPD_STAT CLSELECT
WHERE ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE NOTOTAL
ON TABLE HOLD AS HOLDPASS
END
-RUN
-READ HOLDPASS &X.8 &COMP.6 &USTAT.1 &CLSEL.1
-RUN
-SET &COMPID = 'c:\TNT\TTRACKER\DATA\AL' || &COMP || '.FOC AS HIER50';
USE ADD
&COMPID.EVAL
END
-RUN

CREATE FILE CONTROL
-RUN

-TYPE Before first Control MODIFY
MODIFY FILE CONTROL
FIXFORM ROLLUP_CODE/A8
FIXFORM COMP/A6
FIXFORM UPD_STAT/A1
FIXFORM CL_SEL/A1
COMPUTE CONTROL_ID/A3  =  'PGM';
COMPUTE USER_ID/A20 = '&&USERID.EVAL';
COMPUTE SECURITY_LEVEL/I1 = 9;
COMPUTE WEB_RUN/A1 = 'Y';
COMPUTE WDOMAIN/A50 = 'https://www.etraveltracker.com/asp/';
MATCH CONTROL_ID
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HOLDPASS
END
-RUN
CREATE FILE CATEGORY
JOIN ROLLUP_CODE IN CONTROL TO ROLLUP_CODE IN CLIENT AS AA
TABLE FILE CLIENT
BY CATEGORY NOPRINT
SUM CATEGORY
WHERE ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE HOLD AS HOLDCAT
END
-RUN
MODIFY FILE CATEGORY
FIXFORM CATLIST/A15
MATCH CATLIST
ON NOMATCH INCLUDE
ON MATCH INCLUDE
DATA ON HOLDCAT
END
JOIN CLEAR AA
-RUN

DEFINE FILE RPT_INST
E_FLAG/A1 WITH INST_KEY ='N';
R_SERVER/A20 WITH INST_KEY ='&&USERID.EVAL';
END
TABLE FILE RPT_INST
PRINT E_FLAG AS EXECUTE_FLAG 
      R_SERVER AS RPT_SERVER
BY INST_KEY
WHERE INST_KEY EQ 
'&&IN_PM.EVAL'
WHERE RECORDLIMIT EQ 1
ON TABLE HOLD AS HOLD1
ON TABLE SET ASNAMES ON
END
MODIFY FILE RPT_INST
FIXFORM FROM HOLD1
MATCH INST_KEY
  ON NOMATCH REJECT
  ON MATCH UPDATE EXECUTE_FLAG RPT_SERVER
DATA ON HOLD1
END
-RUN

? PATH
-RUN

EX WRUNINDV outfmt='&&OUTFMT.EVAL',
outto='&&OUTTO.EVAL',
rp_pm='&&RP_PM.EVAL',
rt_pm='&&RT_PM.EVAL',
se_pm='&&SE_PM.EVAL',
sk_pm='&&SK_PM.EVAL',
sn_pm='&&SN_PM.EVAL',
st_pm='&&ST_PM.EVAL',
ix_pm='&&IX_PM.EVAL',
fm_pm='&&FM_PM.EVAL',
to_pm='&&TO_PM.EVAL',
fx_pm='&&FX_PM.EVAL',
tx_pm='&&TX_PM.EVAL',
cmfm='&&CMFM.EVAL',
cmto='&&CMTO.EVAL',
pmfm='&&PMFM.EVAL',
pmto='&&PMTO.EVAL', 
cyfm='&&CYFM.EVAL',
cyto='&&CYTO.EVAL',
pyfm='&&PYFM.EVAL',
pyto='&&PYTO.EVAL', 
in_pm='&&IN_PM.EVAL'