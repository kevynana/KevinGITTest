-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492
-* S-08064 - 4/3/2015 - removed All Other from Top Market graph

-INCLUDE SETECHO
SET ASNAMES=ON

DEFINE FILE MR_50
FARE_PAID/D15.2 = SEG_AMT + SEG_TAX ;
MRK_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                     (FARE_PAID GE 0) AND
                     (SEG_COUNT EQ 1) AND
                     (CONJ_REL EQ 1) THEN 1 ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (EX_FLAG NE ' ') THEN (-1) ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (RF_FLAG NE ' ') THEN (-1) ELSE 0;
-INCLUDE TRANTYPE                     
END
-RUN

TABLE FILE MR_50
PRINT AIRLINE_NAME
      FARE_PAID
      MRK_CNT
      TKT_DATE
      TRN_DATE
WHERE VOID.VOID_DATE EQ 0
WHERE (TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL)
   OR (TRN_DATE GE &&FXDATE.EVAL AND TRN_DATE LE &&TXDATE.EVAL)
&&WHERE1
&&WHERE2
&&WHERE3


-INCLUDE RPTPARMS
ON TABLE HOLD AS OVMAHOLD
END
-RUN


TABLE FILE OVMAHOLD
PRINT AIRLINE_NAME
      FARE_PAID
      MRK_CNT
      TRN_DATE
BY TKT_DATE
ON TABLE HOLD AS OVMAHOLD1 FORMAT FOCUS INDEX TKT_DATE
END
-RUN


-* Global Reporting JOIN
JOIN OVMAHOLD1.TKT_DATE IN OVMAHOLD1 TO GLOBAL.DATE IN GLOBAL AS J1

DEFINE FILE OVMAHOLD1
CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;
CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
CMAMT/P15.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
PMAMT/P15.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CMCNT/D8CS = IF CURRENT EQ 'Y' THEN MRK_CNT ELSE 0;
PMCNT/D8CS = IF PRIOR EQ 'Y' THEN MRK_CNT ELSE 0;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
  CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
  CAMT/D20.6 = CMAMT * CURR1X;
  PAMT/D20.6 = PMAMT * CURR1X;

-* DEFINES ON PREVIOUSLY DEFINES DEFINES

  CAMTX/P15.2 = CAMT;
  PAMTX/P15.2 = PAMT;

  CURR1/P12.2C = CURR1X;      
END
-RUN


TABLE FILE OVMAHOLD1
PRINT AIRLINE_NAME
    CAMTX
    CMCNT
    PAMTX
    PMCNT
    MRK_CNT
    FARE_PAID
    NAMEC
BY AIRLINE_NAME
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS OVGCCR
END
-RUN

TABLE FILE OVGCCR
SUM CAMTX CMCNT NAMEC
BY AIRLINE_NAME
ON TABLE HOLD AS CGCCRHLD
END
-RUN


TABLE FILE OVGCCR
SUM PAMTX PMCNT NAMEC
BY AIRLINE_NAME
ON TABLE HOLD AS PGCCRHLD
END
-RUN

-* JOIN CLEAR *
-RUN

MATCH FILE CGCCRHLD
PRINT CAMTX
      CMCNT
      NAMEC
BY AIRLINE_NAME
ON TABLE HOLD
RUN
FILE PGCCRHLD
PRINT PAMTX
      PMCNT
      NAMEC
BY AIRLINE_NAME
AFTER MATCH HOLD AS APHOLD3 OLD-OR-NEW
END

TABLE FILE APHOLD3
SUM CAMTX
    CMCNT
    PAMTX
    PMCNT 
    NAMEC
BY AIRLINE_NAME
ON TABLE HOLD AS APHOLD4 
END
-RUN

TABLE FILE APHOLD4
PRINT *
RANKED BY HIGHEST CMCNT NOPRINT
ON TABLE HOLD AS APHOLD5 
END
-RUN


-SET &RECFOUND = &RECORDS;



DEFINE FILE APHOLD5
  LIST/D9 = IF AIRLINE_NAME NE LAST AIRLINE_NAME THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
-*************************************************************************
-*DATE          NAME               DESCRIPTION
-*10/11/00    IBISTL-SB         CHANGED THE ARANK FIELD TO 'A11'
-*10/11/00    IBISTL-SB         CHANGED RANK_VALUE FIELD
-* S-08064 - 4/3/2015 - removed All Other from Top Market graph

-*************************************************************************
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 3 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL1/A30 = IF RANK_VALUE NE 'OTHER' THEN AIRLINE_NAME
                      ELSE 'ALL OTHER';
  RANK_LABEL/A30 = LCWORD(30, RANK_LABEL1, RANK_LABEL);
END
-RUN

TABLE FILE APHOLD5
SUM CMCNT
    CAMTX
    PMCNT
    PAMTX
    NAMEC
BY RANK_VALUE NOPRINT
BY RANK_LABEL
ON TABLE HOLD AS APHOLD6 
END
-RUN


TABLE FILE APHOLD6
SUM CMCNT
    CAMTX
    PMCNT
    PAMTX
    NAMEC
BY RANK_LABEL
WHERE RANK_LABEL NE 'All Other'
ON TABLE HOLD AS APHOLD7
END
-RUN



GRAPH FILE APHOLD7
SUM CMCNT AS 'Current'
    PMCNT AS 'Prior'
BY RANK_LABEL AS ' '
ON TABLE SET GRAPHSTYLE *
setGraphType(24);
setLegendDisplay(true);
setLegendTextAutofit(true);
setLegendPosition(2);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),false);
setFontSize(getLegendText(),15);
setFontStyle(getLegendText(),0);
setLegendMarkerPosition(1);
setO1LabelDisplay(true);
setFontSizeAbsolute(getO1Label(),true); 
setAutofit(getO1Label(),false);
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),15);
setO1LabelRotate(0);
setO1MajorGridDisplay(true);
setO1MinorGridDisplay(false);
setFontSizeAbsolute(getY1Label(),true);
setAutofit(getY1Label(),false);
setY1LabelDisplay(true);
setY1LabelFormat(1);
setFontSize(getY1Label(),15);
setFontStyle(getY1Label(),0);
setY1AxisSide(0);
setFontSizeAbsolute(getTitle(),true); 
setAutofit(getTitle(),false);
setTitleString("Top Carriers");
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),0);
setFontSize(getTitle(),15);
setSubtitleString(" ");
setDataTextDisplay(false);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 115 160 247));
ENDSTYLE
ON GRAPH SAVE AS CARRIER FORMAT SVG
END
-RUN

-IF &&OUTFMT NE 'PDF' GOTO NOGRAPH1 ELSE GOTO GRAPH1;
-NOGRAPH1;
-SET &&C_GIF = 'N';
-GOTO GRAPHN1;
-GRAPH1;
-SET &&C_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';
-GRAPHN1;


 

DEFINE FILE APHOLD6
LABEL1/A30 = 'Airline Usage';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL1 ELSE LABEL1||'-'||NAMEC;
CAT_ORDER/I2 = 4;
END
-RUN

TABLE FILE APHOLD6
PRINT CATEGORY/A61 AND RANK_LABEL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CAMTX/P15C  AS 'VOL_CURR' AND 
CMCNT/P9CS AS 'TKT_CURR' AND PAMTX/P15C  AS 'VOL_PRIOR' 
AND PMCNT/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS APHOLD7 
END
-RUN

TABLE FILE APHOLD7
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS APHOLD8 
END
MODIFY FILE OV1_GC
FIXFORM FROM APHOLD8
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON APHOLD8
END
-RUN


-**************************************************************************
-* Start of New Code added to add total line
-**************************************************************************

DEFINE FILE APHOLD5
LABEL1/A30 = 'Airline Usage';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL1 ELSE LABEL1||'-'||NAMEC;
CAT_ORDER/I2 = 4;
RANK_LABEL/A30 = 'Total';
END
-RUN

TABLE FILE APHOLD5
SUM CMCNT
    CAMTX
    PMCNT
    PAMTX
BY CATEGORY
BY CAT_ORDER
BY RANK_LABEL
ON TABLE HOLD AS NEWTOT
END
-RUN


TABLE FILE NEWTOT
PRINT CATEGORY/A61 AND RANK_LABEL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CAMTX/P15C  AS 'VOL_CURR' AND 
CMCNT/P9CS AS 'TKT_CURR' AND PAMTX/P15C  AS 'VOL_PRIOR' 
AND PMCNT/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = 5;    
ON TABLE HOLD AS NEWTOT1 
END
-RUN

TABLE FILE NEWTOT1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS NEWTOT2
END
-RUN

MODIFY FILE OV1_GC
FIXFORM FROM NEWTOT2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON NEWTOT2
END
-RUN

