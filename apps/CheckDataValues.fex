-**********************************************************************************************************
-*  CheckDataValues.fex - nightly job runs through ReportCaster
-*  The program is used to check some values in certain database fields, which may cause report to error 
-*  out if the value is missing.
-*
-*  The following values will be validated:  SELECT_LEV and BREAK_LEVELS in RPT_INST database and those two
-*  are used in codegen to combine AROLL_LEV# AND AROLL_DSC#; PAIR_DB in RPT_INST.  
-*  CURRENCY_CODE in RPT_FLDS database, which may be used to join GLOBAL database, can not be empty.
-*
-*  Date: 03/01/2010
-**********************************************************************************************************
-INCLUDE SETECHO
TABLE FILE RPT_INST
PRINT SELECT_LEV BREAK_LEVELS
BY INST_KEY
BY ID
WHERE ROLLUP_CODE NE 'TOTAL-R' AND SELECT_LEV EQ 0 OR BREAK_LEVELS EQ ''
ON TABLE HOLD
END
-RUN
-IF &LINES EQ 0 THEN GOTO ChkPairDB;
-*default to level 1, total company, if missing.
MODIFY FILE RPT_INST
FIXFORM FROM HOLD
COMPUTE SELECT_LEV = 1;
COMPUTE BREAK_LEVELS = '01';
MATCH INST_KEY 
  ON MATCH CONTINUE
  MATCH ID
    ON MATCH UPDATE SELECT_LEV BREAK_LEVELS
DATA ON HOLD
END
-RUN

-ChkPairDB
-*default to S if it is empty for AIR,ADD,DVR,OTH,OTM,QTR
TABLE FILE RPT_INST
PRINT RPT_GROUP PAIR_DB 
BY INST_KEY
WHERE PAIR_DB EQ '' AND RPT_GROUP EQ 'AIR' OR 'ADD' OR 'DVR' OR 'OTH' OR 'OTM' OR 'QTR'
ON TABLE HOLD
END
-RUN
-IF &LINES EQ 0 THEN GOTO ChkCurrency;

MODIFY FILE RPT_INST
FIXFORM FROM HOLD
COMPUTE PAIR_DB = 'S';
MATCH INST_KEY 
  ON MATCH UPDATE PAIR_DB
DATA ON HOLD
END
-RUN

-ChkCurrency
-*default currency code to USD
TABLE FILE RPT_FLDS
PRINT CURRENCY_CODE
BY INST_KEY
WHERE CURRENCY_CODE EQ ''
ON TABLE HOLD
END
-RUN
-IF &LINES EQ 0 THEN GOTO ChkRollset;
MODIFY FILE RPT_FLDS
FIXFORM FROM HOLD
COMPUTE CURRENCY_CODE = 'USD';
MATCH INST_KEY
  ON MATCH UPDATE CURRENCY_CODE
DATA ON HOLD
END
-RUN

-ChkRollset;
TABLE FILE ROLLSET_MASTER
PRINT PAIR_DB
BY GLOBAL_PARM
WHERE PAIR_DB EQ '' AND RPT_GROUP EQ 'AIR' OR 'ADD' OR 'DVR' OR 'OTH' OR 'OTM' OR 'QTR'
ON TABLE HOLD
END
-RUN

-IF &LINES EQ 0 THEN GOTO ChkRollset2;
MODIFY FILE ROLLSET_MASTER
FIXFORM FROM HOLD
COMPUTE PAIR_DB = 'S';
MATCH GLOBAL_PARM 
  ON MATCH UPDATE PAIR_DB
DATA ON HOLD
END
-RUN

-ChkRollset2
-*Check for CAR and Hotel groups
TABLE FILE ROLLSET_MASTER
PRINT PAIR_DB
BY GLOBAL_PARM
WHERE RPT_GROUP EQ 'CAR' OR 'HTL'
ON TABLE HOLD
END
-RUN

-IF &LINES EQ 0 THEN GOTO ChkDBSync;
MODIFY FILE ROLLSET_MASTER
FIXFORM FROM HOLD
COMPUTE PAIR_DB = '';
MATCH GLOBAL_PARM 
  ON MATCH UPDATE PAIR_DB
DATA ON HOLD
END
-RUN

-ChkDBSync
-*if the INST_KEY is in RPT_FLDS, but Not in RPT_INST, it should be deleted from RPT_FLDS
MATCH FILE RPT_FLDS
BY INST_KEY
RUN
FILE RPT_INST
BY INST_KEY
AFTER MATCH HOLD OLD-NOT-NEW
END
-RUN
-IF &LINES EQ 0 THEN GOTO ChkDBSync2;

TABLE FILE HOLD
PRINT INST_KEY
ON TABLE HOLD
END
-RUN

MODIFY FILE RPT_FLDS
FIXFORM FROM HOLD
MATCH INST_KEY 
ON MATCH DELETE
DATA ON HOLD
END
-RUN

-ChkDBSync2
-*if the INST_KEY is in RPT_INST, but Not in RPT_FLDS, it should be added the INST_KEY Segment to RPT_FLDS
-*with the default values currency _code USD and all booking types selected. 

MATCH FILE RPT_INST
BY INST_KEY
RUN
FILE RPT_FLDS
BY INST_KEY
AFTER MATCH HOLD OLD-NOT-NEW
END
-RUN
-IF &LINES EQ 0 THEN GOTO DoneChkData;

DEFINE FILE HOLD
  CURRENCY_CODE/A3 = 'USD';
  BOOKTYPE_A/I1 = 1;
  BOOKTYPE_AE/I1 = 1;
  BOOKTYPE_O/I1 = 1;
  BOOKTYPE_OE/I1 = 1;
  BOOKTYPE_W/I1 = 1;
  BOOKTYPE_WE/I1 = 1;
  BOOKTYPE_AW/I1 = 1;
  BOOKTYPE_AWE/I1 = 1;
  BOOKTYPE_E/I1 = 1;
  BOOKTYPE_EE/I1 = 1;
  BOOKTYPE_C/I1 = 1;
  BOOKTYPE_CE/I1 = 1;
END
TABLE FILE HOLD
PRINT CURRENCY_CODE BOOKTYPE_A BOOKTYPE_AE
	BOOKTYPE_O BOOKTYPE_OE
	BOOKTYPE_W BOOKTYPE_WE
	BOOKTYPE_AW BOOKTYPE_AWE
	BOOKTYPE_E BOOKTYPE_EE
	BOOKTYPE_C BOOKTYPE_CE
BY INST_KEY
ON TABLE HOLD
END
-RUN

MODIFY FILE RPT_FLDS
FIXFORM FROM HOLD
MATCH INST_KEY
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN




-DoneChkData
-RUN