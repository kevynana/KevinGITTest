-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* JK Added Level2, Level3 9/16/04
-* JK Changed Level2, Level3 to carry through instead of a sort prior to the
-*    match
-* DV ADDED DOD_TYPE 'J' TO IBOOK DEFINE - 12/21/04
-* DV  ADDED CODE FOR GLOBAL CURRENCY 1/5/05
-* DV  CARRIED THROUGH LEVEL1 7/25/05
-* DV ADDED DOD_TYPE H TO IBOOK DEFINE 10/11/05
-* DV ADDED SORT BY LEVEL1 FOR CONG 4/7/08
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-* ADDED DEFINE FOR LPNR_CNT - JK 7/23/14
-* ADDED LEVEL4  DEB - 04/23/15  S-S-08549 

-* FILE TOPCARPX.FEX
-INCLUDE SETECHO

-* -*SET &ECHO=ALL;
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';

DEFINE FILE &&EXTRACT

BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
  
  LEVEL_DESC/A60 = &&LDESC;
  
  DAY_OF_WK/W = PICKUP_DATE;	
  DROP_OFF_DT/MDYY = DROP_DATE;	
  IBOOK/A13 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'INTERNET' ELSE
              'NON-INTERNET';
  INV_DATE/MDYY = INVOICE_DATE;
  INV_MONTH/MT = INVOICE_DATE;	
  NBR_DAYS/D12 = NUM_DAYS;
  PICK_UP_DT/MDYY = PICKUP_DATE;	
  XPICK_DOW/W   = PICKUP_DATE;
  XDROP_DOW/W   = DROP_DATE;
  NEW_RENTAL_AMT/D12.2CS = RENTAL_AMT;
  RENTAL_DAYS/D8S = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
  
  RES_ST/A1 = EDIT(RESV_STATUS, '9');
  LPNR_CNT/P8CS = IF RES_ST NE 'X' AND TKT_NUM EQ '0000000000' AND PNR_LOCATOR NE LAST PNR_LOCATOR THEN 1 ELSE 0;
  
END
-RUN

TABLE FILE &&EXTRACT
PRINT 
  CURR_DATE 
  INVOICE_DATE
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  NEW_RENTAL_AMT AS 'LRNTL_AMT'
  PNR_LOCATOR
  NBR_DAYS AS 'LNBR_DAYS'
  NUM_CARS AS 'NUM_LCARS'
  RENTAL_DAYS AS 'LRENTAL_DAYS'
  PASSNGR_NAME
  VENDOR_NAME AS 'LVDR_NAME'
  BOOKINGS AS 'LBOOK'
  LPNR_CNT
-INCLUDE &CARDATES   
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************

&&WHERE8



-INCLUDE RPTPARMS

ON TABLE HOLD AS TOPLIMH
END
-RUN


TABLE FILE TOPLIMH
PRINT LBOOK 
  INVOICE_DATE
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  LRNTL_AMT
  PNR_LOCATOR
  LNBR_DAYS
  NUM_LCARS
  LRENTAL_DAYS
  PASSNGR_NAME
  LVDR_NAME
  LPNR_CNT
BY CURR_DATE
ON TABLE HOLD AS TPLIMHLD FORMAT FOCUS INDEX CURR_DATE
END
-RUN

-* Global Reporting JOIN
JOIN TPLIMHLD.CURR_DATE IN TPLIMHLD TO GLOBAL.DATE IN GLOBAL AS J1
END

DEFINE FILE TPLIMHLD

 NAMEC/A30 = CNAME;


-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
LRNTL_AMTX/D20.6 = LRNTL_AMT * CURR1X;


END 

TABLE FILE TPLIMHLD
PRINT LBOOK
  CURR_DATE
  INVOICE_DATE
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  LRNTL_AMT
  PNR_LOCATOR
  LNBR_DAYS
  NUM_LCARS
  LRENTAL_DAYS
  PASSNGR_NAME
  LVDR_NAME
  LRNTL_AMTX/D16.2CS
  NAMEC
  LPNR_CNT
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS TOPLIMH2
END
-RUN


TABLE FILE TOPLIMH2
SUM LEVEL1
  LEVEL2
  LEVEL3
  LEVEL4
  LRNTL_AMT
  LRENTAL_DAYS
  LNBR_DAYS
  NUM_LCARS
  LBOOK
  LPNR_CNT
-*   LEVEL_DESC
BY PASSNGR_NAME
BY LEVEL_DESC
ON TABLE HOLD AS TPLIMHLD2
END
-RUN


