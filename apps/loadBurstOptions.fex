-******************************************************************************************
-* loadBurstOptions.fex 
-*    This is a similar program to loadbins, except the followings:
-*
-*    Update RPT_FLDS.HIEORG2.MEMBER field with REPORT_BREAK_HIERS.ROLL_LEV 
-*    Update RPT_FLDS.HIEORG1.SEL_LEVEL field with REPORT_BREAK_OWNERS.BREAK_LEVEL
-*    Update BRPTINST.SELECT_LEV with REPORT_BREAK_OWNERS.BREAK_LEVEL if neccessary
-*    (BRPTINST.SELECT_LEV GE REPORT_BREAK_OWNERS.BREAK_LEVEL)
-*
-******************************************************************************************
-INCLUDE SETECHO

-SET &LBWH1=IF '&&WRRUN.EVAL' EQ 'Y' THEN ' ' ELSE
- 'WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTINST.RPT_INST.INTIME';

CREATE FILE BRPTINST
-RUN

-* Load The BRPTINST file from HRPTINST OR RPT_INST
-* JOIN CLEAR *
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO JOIN_WR1;
JOIN INST_KEY IN BRPT_QUE TO ALL INST_KEY IN HRPTINST AS J1.
-GOTO JOIN_DONE1
-JOIN_WR1
JOIN INST_KEY IN BRPT_QUE TO INST_KEY IN RPT_INST AS J1.
-JOIN_DONE1
-RUN
 
TABLE FILE BRPT_QUE
PRINT
  RPT_INST.INST_KEY
  RPT_INST.ROLLUP_CODE
  RPT_INST.SET_KEY
  RPT_INST.RPT_GROUP
  RPT_INST.RPT_TYPE
  RPT_INST.RPT_STREAM
  RPT_INST.GLOBAL_PARM
  RPT_INST.USE_PARM
  RPT_INST.PAIR_DB
  RPT_INST.RPT_NAME
 
  SETTINGS.ID
  SETTINGS.RPT_TITLE1
  SETTINGS.RPT_TITLE2
  SETTINGS.RPT_FOOT1
  SETTINGS.RPT_FOOT2
  SETTINGS.STYLE1
  SETTINGS.STYLE2
  SETTINGS.STYLE3
  SETTINGS.SELECT_LEV
  SETTINGS.EXECUTE_FLAG
  SETTINGS.RUN_SEQ
  SETTINGS.CONTROL_ID
  SETTINGS.OUTPUT_FORMAT
  SETTINGS.SECURE_FLAG
  SETTINGS.DISTRB_FLAG
  SETTINGS.LOGO_1
  SETTINGS.LOGO_2
  SETTINGS.RPT_SERVER
 
  SETTINGS.FM_DATE
  SETTINGS.TO_DATE
  SETTINGS.FXDATE
  SETTINGS.TXDATE
  SETTINGS.CMFM_DATE
  SETTINGS.CMTO_DATE
  SETTINGS.PMFM_DATE
  SETTINGS.PMTO_DATE
  SETTINGS.CYFM_DATE
  SETTINGS.CYTO_DATE
  SETTINGS.PYFM_DATE
  SETTINGS.PYTO_DATE
  SETTINGS.BREAKBY
  SETTINGS.DATETYPE
  SETTINGS.CONNECTION
&LBWH1.EVAL
ON TABLE HOLD AS HOLDI1
END
-RUN
 
MODIFY FILE BRPTINST
FIXFORM FROM HOLDI1
MATCH INST_KEY
 ON MATCH REJECT
 ON NOMATCH INCLUDE
MATCH ID
 ON MATCH REJECT
 ON NOMATCH INCLUDE
DATA ON HOLDI1
END
-RUN

-*update select_lev if neccessary
DEFINE FILE BRPTINST
  LEVEL/I2 WITH SELECT_LEV = &&LEVEL;
END
TABLE FILE BRPTINST
PRINT
  RPT_INST.INST_KEY
  SETTINGS.ID
  LEVEL AS 'SELECT_LEV'
WHERE INST_KEY EQ &&IKEY
WHERE SELECT_LEV LT LEVEL
ON TABLE HOLD AS HOLDI1
END
-RUN
-IF &LINES EQ 0 THEN GOTO NoBurstInst1;
 
MODIFY FILE BRPTINST
FIXFORM FROM HOLDI1
MATCH INST_KEY
 ON MATCH CONTINUE
 ON NOMATCH REJECT
MATCH ID
 ON MATCH UPDATE SELECT_LEV
 ON NOMATCH REJECT
DATA ON HOLDI1
END
-RUN
 

-NoBurstInst1 
-***********************
CREATE FILE BRPT_SET
-RUN
-***********************
-* JOIN CLEAR *
 
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO JOIN_WR2;
-*JOIN INST_KEY IN BRPT_QUE TO ALL INST_KEY IN HRPTINST AS J1.
JOIN SET_KEY IN BRPT_QUE TO ALL SET_KEY IN HRPT_SET AS J2.
-GOTO JOIN_DONE2
-JOIN_WR2
-*JOIN INST_KEY IN BRPT_QUE TO INST_KEY IN RPT_INST AS J1.
JOIN SET_KEY IN BRPT_QUE TO SET_KEY IN RPT_SET AS J2.
-JOIN_DONE2
-RUN
 
TABLE FILE BRPT_QUE
PRINT
  RPT_SET.SET_KEY
  RPT_SET.ROLLUP_CODE
  RPT_SET.SET_NAME
  RPT_SET.USER_NAME
  RPT_SET.USER_SECURITY
  RPT_SET.SET_TYPE
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO SKIP_WH4;
-*WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTINST.RPT_INST.INTIME
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPT_SET.RPT_SET.INTIME
-SKIP_WH4
ON TABLE HOLD AS HOLDS1
WHERE RECORDLIMIT EQ 1
END
-RUN
MODIFY FILE BRPT_SET
FIXFORM FROM HOLDS1
MATCH SET_KEY
 ON NOMATCH INCLUDE
 ON MATCH REJECT
DATA ON HOLDS1
END
-RUN
 
-***********************
CREATE FILE BROLLUP
-RUN
-***********************
 
-* JOIN CLEAR *
JOIN ROLLUP_CODE IN BRPT_QUE TO ROLLUP_CODE IN ROLLUP AS J1.
-RUN
 
TABLE FILE BRPT_QUE
PRINT SEG.ROLL_STATUS
WHERE RECORDLIMIT EQ 1
ON TABLE HOLD AS HOLDR1
END
-RUN
 
MODIFY FILE BROLLUP
FIXFORM FROM HOLDR1
MATCH ROLLUP_CODE
 ON NOMATCH INCLUDE
 ON MATCH REJECT
DATA ON HOLDR1
END
-RUN
 
-**************************************************
-*Added on Jan 21, 2003 TO move data to BRPTFLDS
-**************************************************
CREATE FILE BRPTFLDS
-RUN
 
-* JOIN CLEAR *
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO JOIN_4;
JOIN INST_KEY IN BRPT_QUE TO ALL INST_KEY IN HRPTFLDS
-GOTO JOIN_DONE4
-JOIN_4
JOIN INST_KEY IN BRPT_QUE TO INST_KEY IN RPT_FLDS
-JOIN_DONE4
-RUN
 
TABLE FILE BRPT_QUE
PRINT INST_KEY CURRENCY_CODE
BOOKTYPE_A BOOKTYPE_AE
BOOKTYPE_O
BOOKTYPE_OE
BOOKTYPE_W
BOOKTYPE_WE
BOOKTYPE_AW
BOOKTYPE_AWE
BOOKTYPE_E
BOOKTYPE_EE
BOOKTYPE_C BOOKTYPE_CE
 
BY RPT_INST.INST_KEY NOPRINT
 
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE0;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
 
-NO_WHERE0
ON TABLE HOLD AS HOLDRPT0
END
-RUN
-IF &LINES EQ 0 THEN GOTO HIEORG;
 
-*MODIFY FILE BRPTFLDS
-*FIXFORM FROM HOLDRPT0
-*DATA ON HOLDRPT0
-*END
-*-RUN
MODIFY FILE BRPTFLDS
FIXFORM FROM HOLDRPT0
MATCH INST_KEY
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT0
END
-RUN
 
TABLE FILE BRPT_QUE
PRINT  SEG COMMON1.INCL1
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE1;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE1
ON TABLE HOLD AS HOLDRPT1
END
-RUN
 
-IF &LINES EQ 0 THEN GOTO :COMMON2;
 
-*MODIFY FILE BRPTFLDS
-*FIXFORM FROM HOLDRPT1
-*DATA ON HOLDRPT1
-*END
-*-RUN
MODIFY FILE BRPTFLDS
FIXFORM FROM HOLDRPT1
  MATCH INST_KEY INCL1
   ON NOMATCH INCLUDE
   ON MATCH REJECT
DATA ON HOLDRPT1
END
-RUN

-:COMMON2 
TABLE FILE BRPT_QUE
PRINT SEG.COMMON2.SEQ_NO
BY RPT_INST.INST_KEY
 
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE2;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
 
-NO_WHERE2
ON TABLE HOLD AS HOLDRPT2
END
-RUN
-IF &LINES EQ 0 THEN GOTO CHKVIEW; 
 
MODIFY FILE BRPTFLDS
FIXFORM FROM HOLDRPT2
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH REJECT
MATCH * KEYS SEG COMMON2
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT2
END
-RUN
 
-CHKVIEW
TABLE FILE BRPTINST
PRINT RPT_GROUP USE_PARM
ON TABLE SAVE
END
-RUN
 
-SET &VIEWTYPE = 'N';
-READ SAVE &RPTGRP.A3. &VIEWTYPE.A1. &XX.A7.
-SET &&RPTGRP = IF '&RPTGRP.EVAL' EQ 'AIR' OR '&RPTGRP.EVAL' EQ 'ADD' THEN 'AIR'
- ELSE IF '&RPTGRP.EVAL' EQ 'CAR' OR '&RPTGRP.EVAL' EQ 'CDD' THEN 'CAR'
- ELSE IF '&RPTGRP.EVAL' EQ 'HTL' OR '&RPTGRP.EVAL' EQ 'HDD' THEN 'HTL'
- ELSE '&RPTGRP.EVAL';
 
-IF '&RPTGRP.EVAL' EQ 'AIR' OR '&RPTGRP.EVAL' EQ 'ADD' OR
-  '&VIEWTYPE.EVAL' EQ 'A' THEN GOTO AIRPRT
- ELSE IF '&RPTGRP.EVAL' EQ 'CAR' OR '&RPTGRP.EVAL' EQ 'CDD' OR
-  '&VIEWTYPE.EVAL' EQ 'C' THEN GOTO CARRPT
- ELSE IF '&RPTGRP.EVAL' EQ 'HTL' OR '&RPTGRP.EVAL' EQ 'HDD' OR
-  '&VIEWTYPE.EVAL' EQ 'H' THEN GOTO HTLRPT
- ELSE IF '&RPTGRP.EVAL' EQ 'OTH' THEN GOTO TRVLSMRY
-  ELSE GOTO AIRPRT;
 
-****************************
-AIRPRT
-****************************
TABLE FILE BRPT_QUE
PRINT SEG.AIR.ID
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE3;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE3
ON TABLE SAVE AS HOLDRPT3
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 AIR.ID/1 AIR.INCL1/1 AIR.INCL2/1 AIR.INCL3/1
FIXFORM AIR.INCL4/1 AIR.INCL5/1 AIR.INCL6/1 AIR.INCL7/1 AIR.INCL8/1
FIXFORM AIR.INCL9/1 AIR.INCL10/1  AIR.INCL11/1 AIR.INCL12/1
FIXFORM INTDOMT/1 INTDOMS/1 AIR.RANKBYTOP1/5 AIR.RANKMETHOD/1
FIXFORM CLASSCAT_A/1 CLASSCAT_F/1 CLASSCAT_B/1 CLASSCAT_C/1 CLASSCAT_D/1
FIXFORM TRANS_TYPE_S/1 TRANS_TYPE_E/1 TRANS_TYPE_R/1 TRANS_TYPE_V/1
FIXFORM ADV_PUR_A/1 ADV_PUR_0/1 ADV_PUR_7/1 ADV_PUR_14/1 ADV_PUR_21/1
FIXFORM AIR.RANKBYTOP2/5 AIR.CITYPAIRFLD/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG AIR
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT3
END
-RUN
 
TABLE FILE BRPT_QUE
PRINT SEG.AIRFLDS.SEQ_NO
BY RPT_INST.INST_KEY
BY AIRFLDS.SEQ_NO NOPRINT
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE4;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE4
ON TABLE SAVE AS HOLDRPT4
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 AIRFLDS.SEQ_NO/2 AIRFLDS.PASSENGER/A33 TICKET_NO/A10
FIXFORM DPT_AIRPORT/A3 RTE_AIRPORT/A3 VAL_AIR/A3 FLOWN_AIR/A3
FIXFORM AIRFLDS.REFUSE_CODE/A2 CP_AIRPORT/A6
FIXFORM AIRFLDS.TRIP_CODE/A2 NHTL_CODE/A2 ORG_APC/A3 DST_APC/A3
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG AIRFLDS
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT4
END
-RUN
 
-GOTO HIEORG
-****************************
-CARRPT
-****************************
TABLE FILE BRPT_QUE
PRINT SEG CAR.ID
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE5;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE5
ON TABLE SAVE AS HOLDRPT5
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 CAR.ID/1 CAR.INCL1/1 CAR.INCL2/1 CAR.INCL3/1
FIXFORM CAR.INCL4/1 CAR.INCL5/1 CAR.INCL6/1
FIXFORM CAR.RANKBYTOP/5 CAR.RANKMETHOD/1 RATEALL/1
FIXFORM RATE1/1 RATE2/1 RATE3/1 RATE4/1 RATE5/1 RATE6/1
REPOSTION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG CAR
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT5
END
-RUN
 
TABLE FILE BRPT_QUE
PRINT SEG CARFLDS.SEQ_NO
BY RPT_INST.INST_KEY
BY CARFLDS.SEQ_NO NOPRINT
 
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE6;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
 
-NO_WHERE6
ON TABLE SAVE AS HOLDRPT6
END
 
-RUN
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 CARFLDS.SEQ_NO/2 CARFLDS.PASSENGER/A33
FIXFORM VENDOR_CODE/A2  CARFLDS.REFUSE_CODE/A2
FIXFORM PICKUP_CITY/A3  DROPOFF_CITY/A3 CAR_TYPE/A7
REPOSTION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG CARFLDS
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT6
END
-RUN
 
-GOTO HIEORG
-****************************
-HTLRPT
-****************************
TABLE FILE BRPT_QUE
PRINT SEG.HTL.ID
BY RPT_INST.INST_KEY
 
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE7;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
 
-NO_WHERE7
ON TABLE SAVE AS HOLDRPT7
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HTL.ID/1 HTL.INCL1/1 HTL.INCL2/1 HTL.INCL3/1
FIXFORM HTL.INCL4/1 HTL.RANKBYTOP1/5 HTL.RANKMETHOD/1 HTL.RANKBYTOP2/5
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH HTL.ID
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT7
END
-RUN
 
TABLE FILE BRPT_QUE
PRINT SEG.HTLFLDS.SEQ_NO
BY RPT_INST.INST_KEY
BY HTLFLDS.SEQ_NO NOPRINT
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE8;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE8
ON TABLE SAVE AS HOLDRPT8
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HTLFLDS.SEQ_NO/2 HTLFLDS.PASSENGER/A33
FIXFORM CHAIN_CODE/A2 HTL_CITY/A3 HTLFLDS.REFUSE_CODE/A2
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HTLFLDS
  ON NOMATCH INCLUDE
  ON MATCH INCLUDE
DATA ON HOLDRPT8
END
-RUN
 
-GOTO HIEORG;
-*************************************************
-* The START of new change 2005/06/09
-*************************************************
-TRVLSMRY
TABLE FILE BRPT_QUE
PRINT SEG.TRVLSMRY.ID
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE20;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE20
ON TABLE SAVE AS HOLDRPT20
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 TRVLSMRY.ID/2 TRVLSMRY.ORDER/2 TRVLSMRY.CHECKED/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG TRVLSMRY
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT20
END
-RUN
-*************************************************
-* The END of change 2005/06/09
-*************************************************
-HIEORG 

-*************************************************
-* Get level from REPORT_BREAK_OWNERS
-*************************************************
DEFINE FILE BRPT_QUE
  INCL/I1 WITH HIEORG1.ID = 1;
  LEVEL/A2 WITH HIEORG1.ID = '&&LEVEL.EVAL';  
END

TABLE FILE BRPT_QUE
-*PRINT SEG.HIEORG1.ID
PRINT INCL AS 'ID' HIEORG1.INCL1 HIEORG1.INCL2 INCL AS 'INCL3' LEVEL AS 'SEL_LEVEL'
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE9;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE9
ON TABLE SAVE AS HOLDRPT9
END
-RUN

-IF &LINES EQ 0 THEN GOTO HSCheck;
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HIEORG1.ID/1 HIEORG1.INCL1/1 HIEORG1.INCL2/1
FIXFORM HIEORG1.INCL3/1 HIEORG1.SEL_LEVEL/A2
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG1
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT9
END
-RUN

-HSCheck
-*************************************************
-*Start to Check User's Hierarchy Security
-*************************************************
-SET &DONEHS = 'N';
-INCLUDE CHKUSERHS
-IF '&DONEHS.EVAL' EQ 'Y' THEN GOTO TOTALCOMP;
-*************************************************
-*End to Check User's Hierarchy Security
-*************************************************

TABLE FILE BRPT_QUE
PRINT HIEORG2.SEQ_NO CLIENT CATG
WHERE CLIENT NE ' ' OR CATG NE ' '  
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE10;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE10
ON TABLE SAVE AS HOLDRPT0
END
-RUN
-IF &LINES EQ 0 THEN GOTO GETBURSTMEM;

MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.CLIENT/A8 HIEORG2.CATG/A15
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG2
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HOLDRPT0
END
-RUN

-GETBURSTMEM
DEFINE FILE XMAPKEYS
  SEQ_NO/I2 WITH MEMBER = 1;
END
TABLE FILE XMAPKEYS
PRINT SEQ_NO MEMBER
BY INST_KEY 
WHERE NINSTKEY EQ &&NIKEY
ON TABLE SAVE AS HLDRMEMB
END
-RUN

MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.MEMBER/A15
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG2
  ON NOMATCH INCLUDE
  ON MATCH UPDATE MEMBER
DATA ON HLDRMEMB
END
-RUN

-*************************************************
-* Change End - new SU database structure
-*************************************************
 
-TOTALCOMP
-*************************************************
-* Added total company reporting 02/02/04
-*************************************************
TABLE FILE BRPT_QUE
PRINT SEG.ROLL1.ID
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE11;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE11
ON TABLE SAVE AS HLDRPT11
END
-RUN
 
-IF &LINES EQ 0 THEN GOTO ENDRPT;
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 ROLL1.ID/1 ROLL1.INCL1/1 ROLL1.INCL2/1 ROLL1.INCL3/1
FIXFORM ROLL1.INCL4/1 ROLL1.INCL5/1 ROLL1.INCL6/1 ROLL1.OF_ACCT/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG ROLL1
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HLDRPT11
END
-RUN
 
TABLE FILE BRPT_QUE
PRINT SEG.ROLL2.SEQ_NO
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE12;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE12
ON TABLE SAVE AS HLDRPT12
END
-RUN
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 ROLL2.SEQ_NO/4 ROLL2.ROLLUP/A8 ROLL2.BRANCH_CODE/A2
FIXFORM ROLL2.RESV_BRANCH/A2 ROLL2.TKT_BRANCH/A2
FIXFORM ROLL2.CATG2/A15 ROLL2.BR_CL_IDX/A8 ROLL2.ROLLUP2/A8
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG ROLL2
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HLDRPT12
END
-RUN
-*************************************************
-* End of total company reporting 02/02/04
-*************************************************
 
-ENDRPT
-*************************************************
-* Enhancement - QUARTERLY REVIEW
-*************************************************

TABLE FILE BRPT_QUE
PRINT SEG.QREVIEW.RPT_ID
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE15;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE15
ON TABLE SAVE AS HLDRPT15
END
-RUN
-IF &LINES EQ 0 THEN GOTO QREVIEW2;
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 RPT_ID/3 RPT_NBR/3 RPT_ABBR/10 RPT_MISC/7
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG QREVIEW
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HLDRPT15
END
-RUN
 
-QREVIEW2
 
TABLE FILE BRPT_QUE
PRINT SEG.QREVIEW2.LSO_AMT
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE15A;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE15A
ON TABLE SAVE AS HLDRPT15
END
-RUN
-IF &LINES EQ 0 THEN GOTO UDIDINFO;
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 QREVIEW2.SEQ_NO/4 QREVIEW2.PEERGRP/1 QREVIEW2.PEERMBR/8
FIXFORM QREVIEW2.CPEERGRP/1 QREVIEW2.CPEERMBR/8 QREVIEW2.HPEERGRP/1
FIXFORM QREVIEW2.HPEERMBR/8 QREVIEW2.PERCENT/3 QREVIEW2.AL_MFC/A2
FIXFORM QREVIEW2.AL_CSA/A2 QREVIEW2.AL_MSM/A2 QREVIEW2.AL_CA/A2
FIXFORM QREVIEW2.LSO_AMT/7 QREVIEW2.OA_PCT/3 QREVIEW2.CP_LOGO/20
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG QREVIEW2
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HLDRPT15
END
-RUN
 
-UDIDINFO
-*************************************************
-* Enhancement - UDID
-*************************************************
TABLE FILE BRPT_QUE
PRINT SEG.UDINFO.UID
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE16;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE16
ON TABLE SAVE AS HLDRPT16
END
-RUN
-IF &LINES EQ 0 THEN GOTO DVRSection;
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 UID/3 UINCL/1 UDID/A3 UDDATA/A64 FROMVAL/16 TOVAL/16
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG UDINFO
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HLDRPT16
END
-RUN

-DVRSection
-*************************************************
-* Enhancement - UDID
-*************************************************
TABLE FILE BRPT_QUE
PRINT SEG.DVR.DVRID
BY RPT_INST.INST_KEY
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE17;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE17
ON TABLE SAVE AS HLDRPT17
END
-RUN
-IF &LINES EQ 0 THEN GOTO RPTFLDSDONE;
 
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 DVRID/1 TRAN1/1 TRAN2/1 TRAN3/1 TRAN4/1 TRAN5/1 
FIXFORM LSONLY/1 UNUSEDT1/1 UNUSEDT2/1 UNUSEDT3/1 UNUSEDT4/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH DVRID
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HLDRPT17
END
-RUN

  
-RPTFLDSDONE
 
-***** RESOLVE CONTROL FILE PROBLEM   *************
DEFINE FILE BRPTINST
CONTROL_ID/A3 = 'PGM';
END
TABLE FILE BRPTINST
PRINT CONTROL_ID ROLLUP_CODE INST_KEY
ON TABLE HOLD AS HOLDX
END
-RUN
 
-IF &LINES GT 0 THEN GOTO MODCONTROL;
-MODCONTROL
 
MODIFY FILE CONTROL
FIXFORM FROM HOLDX
MATCH CONTROL_ID
  ON MATCH UPDATE INST_KEY ROLLUP_CODE
  ON NOMATCH INCLUDE
DATA ON HOLDX
END
-RUN
 
TABLE FILE CONTROL
PRINT ROLLUP_CODE
ON TABLE SAVE AS SAVE1
END
-RUN
-READ SAVE1 &&ROLLUP.A8.
-RUN