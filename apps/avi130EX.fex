-* Extract for IRC listing of tickets booked with return booked out 6 months from return
-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-*SET TEMPERASE = OFF
-RUN

-SET &RPARM = &&WEB_PATH || 'RPTPARMS.fex';

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC/A60 = &&LDESC;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;    
  XARR_DOW/W           = ARR_DATE;
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XDPT_DATE/MDY        = DPT_DATE;
  XFST_DPT/MDYY = FST_DPT_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;


-* ****DEFINES ON PREVIOUS DEFINES**** 
  ARR_DAY/A3           = DECODE XARR_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');  
        
-INCLUDE TRANTYPE 
             
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  AIR_KEY   
  AIRLINE   
  ARR_TIME
  DPT_DATE
  DPT_TIME  
  DST_APC
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'  
  FARE_PAID 
  FST_DPT_DATE
  LEVEL_DESC    
  NET_TKT_CNT   
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'  
  RTE_CTY.COUNTRY_CODE AS 'RTE_CNTRY_CODE'  
  SEG_COUNT 
  SEG_NBR   
  TKT_NUM   
  TKT_SORT  
  TRN_DATE
  XARR_DT   
  XDPT_DT   
  XDPT_DATE
  XFST_DPT
  XPSNGR_NM
  XTRAN_DT  
  
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
WHERE AIR_MAIN.INTL_DOM EQ 'I'

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10 

-INCLUDE &RPARM

-* -INCLUDE RPTPARMS

ON TABLE HOLD AS RPTHLD
END
-RUN


DEFINE FILE RPTHLD
FDATE/YYMD = DPT_DATE;
TDATE/YYMD = DPT_DATE;
T_MONTH/YYMD = DATEADD(FDATE, 'M', (+6));
END

TABLE FILE RPTHLD
PRINT DPT_DATE FDATE T_MONTH
BY XPSNGR_NM
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS 1HLD
END
-RUN

TABLE FILE 1HLD
SUM FST.DPT_DATE AS 'DPT' LST.DPT_DATE AS 'RTN' FST.T_MONTH AS 'RTN2'
BY XPSNGR_NM
BY TKT_SORT
ON TABLE HOLD AS 2HLD
END
-RUN

MATCH FILE 1HLD
PRINT DPT_DATE FDATE T_MONTH
BY XPSNGR_NM
BY TKT_SORT
ON TABLE HOLD
RUN 
FILE 2HLD
SUM DPT RTN RTN2
BY XPSNGR_NM
BY TKT_SORT
AFTER MATCH HOLD AS 3HLD OLD-AND-NEW
END
-RUN

TABLE FILE 3HLD
PRINT DPT RTN FDATE T_MONTH RTN RTN2
BY XPSNGR_NM
BY TKT_SORT
WHERE RTN GE RTN2
ON TABLE HOLD AS 4HLD
END
-RUN


 




