-* 1/30/13 - DEB changed due to expansion of car database to
-* include trip purpose
-* CHANGED CODE DUE TO DRILL DOWN NOT WORKING - DEB 11/18/13
-* Updated CCOM flag to look at National and Enterprise per Andy - JK 4/7/14
-*02/12/16 - DLV - S-13286  Updated to allow reports to sort/subtotal by id-levels
-*06/30/16 - RSB - S-20630 New Employee ID database field
-*                         Added EMP_ID
-*
SET ASNAMES=ON

-INCLUDE SETECHO

FILEDEF RPTPARMS CLEAR
-RUN

EX CARUSE
-RUN

-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN

DEFINE FILE BRPTINST
  RT/A3 = 'CAR';
END
TABLE FILE BRPTINST
PRINT RT AS 'RPT_GROUP'
BY INST_KEY
ON TABLE SAVE AS AIRACH00
END
-RUN
MODIFY FILE BRPTINST
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 RPT_GROUP/3
MATCH INST_KEY
  ON MATCH UPDATE RPT_GROUP
DATA ON AIRACH00
END
-RUN

MODIFY FILE BRPTFLDS
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 CAR.ID/1 CAR.INCL1/1 
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG CAR
  ON NOMATCH INCLUDE
  ON MATCH UPDATE CAR.INCL1
DATA ON AIRACH00
END
-RUN

EX CODEGEN
-RUN

-SET &&IDLEVELBREAK = '';
-RUN

-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
LEVEL_DESC1/A60      = &&LDESC;
CBOOK/P8CS = IF DAILY_RATE GE 0 THEN 1 ELSE (-1);
CAR_AMT/D12.2CS = RENTAL_AMT;

NBR_DAYS/D12 = NUM_DAYS;
CAR_DAYS/P12CS = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
CAR_DT/MDYY = PICKUP_DATE;                   
TRN_DATE/YYMD = INVOICE_DATE;
CC_ITIN/A67 = PICK_CTY.CITY_NAME;
XPSNGR_NM/A15 = EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
TRIP_KEY/A2   = EDIT(TRP_KEY, '$$$$$$$$99');
TRIPRSN/A10 = IF TRIP_KEY NE ' ' THEN TRIP_KEY ELSE 'ZZZZZZZZZZ';
TRIP_RSN/A10 = IF TRIPRSN EQ 'ZZZZZZZZZZ' THEN 'NONE GIVEN' ELSE TRIPRSN;   
XCINV_DT/MDYY  = INVOICE_DATE;
FLAG/A1 = 'C';
REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
CRSNCDE/A48=REFUSE_CD|| '-' ||REFUSAL_DESC;
-*CCOM/A3 = IF REFUSE_CD EQ 'P' THEN 'NO' ELSE 'YES';
CCOM/A3 = IF VENDOR_NAME EQ 'NATIONAL' OR 'ENTERPRISE' THEN 'YES' ELSE 'NO';
-INCLUDE IDLEVDEFINE  
END                
TABLE FILE &&EXTRACT
PRINT AROLL_DSC3 AROLL_DSC4 CBOOK CAR_DAYS CAR_AMT LEVEL_DESC CC_ITIN XPSNGR_NM TKT_NUM CAR_DT VENDOR_NAME LEVEL4 LEVEL1 TRIP_KEY TRIP_RSN XCINV_DT TRP_REF.TRP_DESC 
      INVOICE_NUM AS 'CINV_NUM' FLAG CCOM CRSNCDE EMP_ID
BY CAR_KEY
-INCLUDE &CARDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHLDC
END
-RUN

-IF '&&SETF.EVAL' EQ 'SDACHC' GOTO STAND;

JOIN CAR_KEY IN RPTHLDC TO CAR_KEY IN CARUDID
-RUN

-STAND;

DEFINE FILE RPTHLDC     
TKTN1/A10 = IF TKT_NUM NE '0000000000' THEN TKT_NUM ELSE 'CAR ONLY';
END
TABLE FILE RPTHLDC
PRINT CBOOK CAR_DAYS CAR_AMT CAR_DT VENDOR_NAME CCOM CRSNCDE EMP_ID
BY LEVEL_DESC
BY AROLL_DSC3
BY AROLL_DSC4
BY FLAG
BY XPSNGR_NM
BY TKTN1
BY XCINV_DT
BY CAR_DT
BY CC_ITIN
BY VENDOR_NAME
BY TRIP_KEY
BY TRIP_RSN
BY TRP_DESC
BY LEVEL4
BY LEVEL1
BY CINV_NUM
-*WHERE UDID EQ 'TP-' OR 'TPO'
-*WHERE UDID EQ 'TPO' OR TRIP_RSN NE ' '
ON TABLE HOLD AS CARHLD
END
-RUN




