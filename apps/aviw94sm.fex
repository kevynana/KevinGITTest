-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the Country Report to be Drillable JK 12/9/03
-* Added subroutine to extract the commas from the country_name 
-* ADDED PASSENGER COUNT TO SUMMARY REPORT - DV 1/6/05
-* field JK 8/19/04
-* ADDED CODE FOR GLOBAL CURRENCY - DEB - 5/2/05
-* CHANGED RANK_LIMIT TO 100 - DEB 10/27/05
-*11/25/15  Joy    S-07987  Corrected for running Currency Enabled Version
-*12/17/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-* 07/15/16   DLV  S-20630 - Carried through EMP_ID
-*09/28/2017 - JEM - S-35422 Place the footer date/time stamp text in a separate file to be -INCLUDED in the stylesheets

SET ASNAMES=ON

-INCLUDE SETECHO

-IF &&CURRENCY EQ 'USD' THEN GOTO SKIPTHIS;
-SET &&SUBHEAD = <NAMEC;
-SET &FSEG_CUR = 'TOTAL,SEGMENT,AMOUNT, (' | &&CURRENCY || ')';
-SET &&S_SUBJ9 = 'KILO AS ''KILOS''';
-SET &&S_SUBJ10='PAID_FARE1/D16.2C AS ' | '''&FSEG_CUR.EVAL''';
-SET &&SUMM_ON2='ON TABLE COLUMN-TOTAL MRK_TKT_CNT PCNT NEW_STAY PAID_FARE1';


-SKIPTHIS;


TABLE FILE CNTRY2
SUM MRK_TKT_CNT
    PCT.MRK_TKT_CNT/D9.1 AS 'PCT_SEG'
    NEW_SEG_MILES
    NEW_STAY
    PCT.NEW_STAY AS 'PCT_STAY'
    MAX.NEW_STAY AS 'MAX_STAY'
    AVE.NEW_STAY AS 'AVG_STAY'
    FARE_PAID
    PAID_FARE1/D16.2CS
    NAMEC
    TRIPS
    KILO
BY RTE_CNTRY_CODE 
ON TABLE HOLD AS PRIOR1 
END
-RUN

TABLE FILE CNTRY2
SUM MRK_TKT_CNT/D8CS AS 'PCNT'
BY RTE_CNTRY_CODE
BY PASSNGR_NAME
ON TABLE HOLD AS HOLD2
END
-RUN

TABLE FILE HOLD2
COUNT PCNT
BY RTE_CNTRY_CODE
BY PASSNGR_NAME 
IF PCNT NE ' '
ON TABLE HOLD AS HOLD3
END
-RUN

TABLE FILE HOLD3
SUM PCNT
BY RTE_CNTRY_CODE
ON TABLE HOLD AS HOLD4
END
-RUN

MATCH FILE PRIOR1
PRINT MRK_TKT_CNT
      PCT_SEG
      NEW_SEG_MILES
      NEW_STAY
      PCT_STAY
      MAX_STAY
      AVG_STAY
      FARE_PAID
      PAID_FARE1
      NAMEC
      TRIPS
      KILO
BY RTE_CNTRY_CODE
ON TABLE HOLD
RUN
FILE HOLD4
SUM PCNT
BY RTE_CNTRY_CODE
AFTER MATCH HOLD AS XONE OLD-OR-NEW
END
-RUN



-* JOIN TO COUNTRY FILE
-SET &CNTRY_PATH = &&COM_PATH || COUNTRY.FOC;
-RUN

USE ADD
&CNTRY_PATH AS COUNTRY
END
-RUN

JOIN 
XONE.RTE_CNTRY_CODE IN XONE TO ALL COUNTRY.COUNTRY_CODE IN COUNTRY 
 AS JOINCT
 END
-RUN

DEFINE FILE XONE
COUNTRY_N/A25 = CTRAN (25, COUNTRY_NAME, 44, 32, COUNTRY_N);
END
TABLE FILE XONE
PRINT FARE_PAID 
      MRK_TKT_CNT
      NEW_SEG_MILES
      PCT_SEG
      NEW_STAY
      PCT_STAY
      MAX_STAY
      AVG_STAY
      COUNTRY_N
      RTE_CNTRY_CODE
      TRIPS
      PCNT
      PAID_FARE1
      NAMEC
      KILO
RANKED BY HIGHEST MRK_TKT_CNT NOPRINT
ON TABLE HOLD AS PRIOR2 
END
-RUN

-SET &&RANK_LIMIT = '100';


DEFINE FILE PRIOR2
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = (LAST LIST + 1);
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE 
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE 
                  CRANK;
  RANK_VAL/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                IF RANK GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                ARANK;
  RANK_LAB1/A80 = IF RANK_VAL NE 'ALL OTHER' THEN &&RANK_LABEL1
                  ELSE 'ALL OTHER';
  RANK_LABEL1/A80 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL1
                      ELSE ' ';
  RANK_LABEL2/A80 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL2
                      ELSE ' ';
  RANK_LABEL3/A80 = IF RANK_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL3
                      ELSE ' ';
  NOWTOD/A8 WITH RANK = HHMMSS (NOWTOD);
END
-RUN

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN

TABLE FILE PRIOR2
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10

-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS

-IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;
-INCLUDE &&DSTY

-SKIP_DRILL

ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;

EX REPORT_3

-SKIP_R3

JOIN CLEAR JOINCT
END
-RUN


