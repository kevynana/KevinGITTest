-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*8/28/02    JOY 	        CHANGED MRK_CNT DEFINE TO EX_FLAG/RF_FLAG
-*                              NE ' ' FOR BALANCING ACCURACY


DEFINE FILE CURMATCH1
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX ;
MRK_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                     (FARE_PAID GE 0) AND
                     (SEG_COUNT EQ 1) AND
                     (CONJ_REL EQ 1) THEN 1 ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (EX_FLAG NE ' ') THEN (-1) ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (RF_FLAG NE ' ') THEN (-1) ELSE 0;

CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
CMAMT/D12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
PMAMT/D12.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CMCNT/D8CS = IF CURRENT EQ 'Y' THEN MRK_CNT ELSE 0;
PMCNT/D8CS = IF PRIOR EQ 'Y' THEN MRK_CNT ELSE 0;

END
-RUN

DEFINE FILE PRIORMATCH1
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX ;
MRK_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                     (FARE_PAID GE 0) AND
                     (SEG_COUNT EQ 1) AND
                     (CONJ_REL EQ 1) THEN 1 ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (EX_FLAG NE ' ') THEN (-1) ELSE
                  IF (TICKET_CODE EQ '1') AND
                     (RF_FLAG NE ' ') THEN (-1) ELSE 0;

CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
CMAMT/D12.2 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
PMAMT/D12.2 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
CMCNT/D8CS = IF CURRENT EQ 'Y' THEN MRK_CNT ELSE 0;
PMCNT/D8CS = IF PRIOR EQ 'Y' THEN MRK_CNT ELSE 0;

END
-RUN

TABLE FILE CURMATCH1
SUM CMAMT CMCNT 
BY AIRLINE_NAME
WHERE CURRENT EQ 'Y'
ON TABLE HOLD AS HOLD1 
END
-RUN

TABLE FILE PRIORMATCH1
SUM PMAMT PMCNT 
BY AIRLINE_NAME
WHERE PRIOR EQ 'Y'
ON TABLE HOLD AS HOLD2 
END
-RUN


MATCH FILE HOLD1
PRINT CMAMT
      CMCNT
BY AIRLINE_NAME
ON TABLE HOLD
RUN
FILE HOLD2
PRINT PMAMT
      PMCNT
BY AIRLINE_NAME
AFTER MATCH HOLD AS HOLD3 OLD-OR-NEW
END

TABLE FILE HOLD3
SUM CMAMT
    CMCNT
    PMAMT
    PMCNT
BY AIRLINE_NAME
ON TABLE HOLD AS HOLD4 
END
-RUN

TABLE FILE HOLD4
PRINT *
RANKED BY HIGHEST CMCNT NOPRINT
ON TABLE HOLD AS HOLD5 
END
-RUN


-SET &RECFOUND = &RECORDS;



DEFINE FILE HOLD5
  LIST/D9 = IF AIRLINE_NAME NE LAST AIRLINE_NAME THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 3 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL/A30 = IF RANK_VALUE NE 'OTHER' THEN AIRLINE_NAME
                      ELSE 'ALL OTHER';
END
-RUN

TABLE FILE HOLD5
SUM CMCNT
    PCT.CMCNT AS 'CMPCT'
    CMAMT
    PMCNT
    PCT.PMCNT AS 'PMPCT'
    PMAMT
BY RANK_VALUE NOPRINT
BY RANK_LABEL
ON TABLE HOLD AS HOLD6 
END
-RUN

DEFINE FILE HOLD6
CATEGORY/A30 = 'CARRIERS';
CAT_ORDER/I2 = 1;
END
-RUN

TABLE FILE HOLD6
PRINT CATEGORY/A30 AND RANK_LABEL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CMCNT/D15 AS 'VOL_CURR' AND 
CMPCT/D15 AS 'TKT_CURR' AND PMCNT/D15 AS 'VOL_PRIOR' 
AND PMPCT/D15 AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS HOLD7 
END
-RUN

TABLE FILE HOLD7
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS HOLD8 
END
MODIFY FILE OV1_MAIN
FIXFORM FROM HOLD8
MATCH ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HOLD8
END
-RUN


-*-EXIT




