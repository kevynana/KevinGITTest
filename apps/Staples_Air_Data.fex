-* STEVE - 08/18/2010 - Changed to show voids as a negative fare and lost savings - being backed out  
-*                      Each void will show up two times - once positive and once negative
-* STEVE - 08/24/2010 - Corrected RADV_PURCH define to account for $.00 first segment
-*1/30/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14   DEB  - ADDED EX5 TO BE EXCLUDED FROM EXTRACT
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492

-INCLUDE SETECHO
SET ASNAMES=ON
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
AL_TKTNUM/A13 = AIRLINE_NUM | TKT_NUM;

FARE/D12.2 = SEG_AMT + SEG_TAX;
LOST/D12.2 = FARE - SEG_LOWEST;
ND_CP_CD/A50  = NDO_APT.ND_ORG ||NDD_APT.ND_DST;
NEW_ADV_PURCH/D9    = ADV_PURCH;
-* TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
-*                           (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
-*                         IF (SEG_COUNT LE 0) AND
-*                           (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
-*                         IF (SEG_COUNT LE 0) AND
-*                           (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
-*                         IF (SEG_COUNT LE 0) AND
-*                           (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';

FARE_SEG/D12.2CS = SEG_AMT;
FARE_TAX/D12.2CS = SEG_TAX;
FARE_OT1/D12.2CS = IF SEG_NBR EQ '01' THEN TAXOTH ELSE 0;
FARE_OT/D12.2CS = IF FARE_SEG LT 0 THEN FARE_OT1 * -1 ELSE FARE_OT1;
POSD/A2 = 'US';  
-* TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE GE 0) AND (SEG_COUNT EQ 1) THEN 1 ELSE 0;	
XTRAN_DT/MDYY = TRN_DATE;
CCAT/A10 = IF CL_CAT EQ 'F' THEN 'First' ELSE
           IF CL_CAT EQ 'B' THEN 'Business' ELSE 'Economy';
OW_RT/A10 = 'One-Way';       
IDOM/A15 = IF CITYPAIR.INTL_DOM EQ 'I' THEN 'International' ELSE 'Domestic';
NR_FLAG/A3 = IF NONREF_FLAG EQ 'R' OR ' '  THEN 'Y' ELSE 'N';
OTFLAG/A15 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Online' ELSE 'Offline';
CURR/A3 = 'USD';
ORIG_SALE/A10 = IF (FARE GE 0) AND (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 'Original' ELSE
                IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND (RF_FLAG EQ ' ') THEN 'Original' ELSE ' ';
               
EXCHGS/A10 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR 'RF1' OR 'FP3') THEN 'Exchange' ELSE 
             IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN ' ' ELSE 
             IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN 'Exchange' ELSE ' ';

REFUNDS/A10 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (DOC_TYPE NE '1') AND (SEG_COUNT LT 0)) THEN 'Refund' ELSE 
              IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (SEG_COUNT LE 0)) THEN 'Refund' ELSE ' ';

-* PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;

-* REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;	


TKT_TYPE/A25 = IF ORIG_SALE EQ 'Original' THEN 'Original' ELSE 
               IF EXCHGS EQ 'Exchange' AND FARE LT 0 THEN 'Exchanged' ELSE 
               IF EXCHGS EQ 'Exchange' AND FARE GE 0 THEN 'Exchange Sale' ELSE
               IF REFUNDS EQ 'Refund' THEN 'Refund' ELSE ' ';
               
-INCLUDE TRANTYPE
END
TABLEF FILE &&EXTRACT
PRINT ADV_PURCH 
      AIR_KEY   
      AL_TKTNUM
      CDIR_ALL
      C_ITIN
      CLASS
      CCAT
      CL_CAT
      CURR
      DST_APC
      EMBARK
      EX_FLAG
      FARE
      FARE_SEG
      FARE_TAX
      FARE_OT
      FARE_BAS
      AIR_MAIN.INTL_DOM AS 'INTDOM'
      IDOM
      LEVEL1
      LEVEL2
      LEVEL3
      LEVEL4
      LOST
      ND_CP_CD
      NEW_ADV_PURCH
      NR_FLAG
      ORG_CNTRY_CODE
      OTFLAG
      OW_RT
      PASSNGR_NAME
      POS
      REFUSE_CODE
      REFUSAL_DESC
      RF_FLAG
      ROUTE
      RNDT_FLAG
      SEG_COUNT
      SEG_NBR
      STOP_CON
      TKT_MAIN.SEG_MILES AS 'MILES'
      TKT_NUM
      TKT_SORT
      TKT_TYPE
      TRN_DATE
      VAL_AIRLINE
      VOID_DATE
      XTRAN_DT
      ORIG_SALE
      EXCHGS
      REFUNDS
      
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
WHERE EMBARK NE 'FP2' OR 'FP3' OR 'DT1' OR 'EX1' OR 'EX5'
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPT_HOLD
END
-RUN 

-SET &&RPTSUF = 'STAIRFILE';

-INCLUDE FDEFRPTS
-RUN


DEFINE FILE RPT_HOLD
NOWTOD/A8 WITH TKT_NUM = HHMMSS (NOWTOD);
END

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
-RUN

TABLE FILE RPT_HOLD
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
PRINT POSD AS 'Point of,Sale' ORG_CNTRY_CODE AS 'Point of,Origin' XTRAN_DT AS 'Date of Booking' VAL_AIRLINE AS 'Validating,Carrier' ND_CP_CD AS 'Non-,Directional,City Pair'
EMBARK AS 'Origin Airport,Code' ROUTE AS 'Destination,Airport Code' C_ITIN AS 'Ticket,Routing' CCAT AS 'Cabin,(First,Business,or Economy)'
FARE_BAS AS 'Fare Basis Code' CLASS AS 'Booking,Inventory' FARE_SEG AS 'Base Fare,Paid' FARE_TAX AS 'Tax' FARE_OT AS 'Other Taxes,Fuel Surchages,Fees'  FARE AS 'Total Fare Paid'
OW_RT AS 'One Way,or,Roundtrip' IDOM AS 'Trip Type,(eg. Dom,Intl)' ADV_PURCH AS 'Advanced Purchase' NR_FLAG AS 'Refundable' OTFLAG AS 'Online or,Offline'
AL_TKTNUM AS 'Ticket #'  TKT_TYPE AS 'Transaction Type,(Original,Exchange,Refund)' CURR AS 'Currency'
BY TKT_NUM NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
-INCLUDE FOOTERDT
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE GLOBAL_LOGO

UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.030000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
    
TYPE=REPORT, FONT='ARIAL', SIZE=10, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=15, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=15, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=3, JUSTIFY=LEFT, $

TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(51 51 153),
     STYLE=BOLD,$
     
TYPE=TITLE, JUSTIFY=LEFT, $
TYPE=DATA, JUSTIFY=LEFT, $
     
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
     
TYPE=FOOTING, FONT=ARIAL, COLOR=GRAY,$

TYPE=SUBTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $
ENDSTYLE 
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN


