-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-* 9/9/14 Updated Pref_non define to use exclude_flags - jk

-INCLUDE SETECHO

FILEDEF RPTPARMS CLEAR
-RUN

EX HTLUSE
-RUN

-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN

DEFINE FILE BRPTINST
  RT/A3 = 'HTL';
END
TABLE FILE BRPTINST
PRINT RT AS 'RPT_GROUP'
BY INST_KEY
ON TABLE SAVE AS AIRACH00
END
-RUN
MODIFY FILE BRPTINST
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 RPT_GROUP/3
MATCH INST_KEY
  ON MATCH UPDATE RPT_GROUP
DATA ON AIRACH00
END
-RUN

MODIFY FILE BRPTFLDS
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 HTL.ID/1 HTL.INCL1/1 
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HTL
  ON NOMATCH INCLUDE
  ON MATCH UPDATE HTL.INCL1
DATA ON AIRACH00
END
-RUN

EX CODEGEN
-RUN

-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT
  CC/A2 = EDIT(CREDIT_CARD, '99');
  FLAG/A1='H';
  LEVEL_DESC/A60  = &&LDESC;
  PREF_CC/A1 = IF CC EQ 'AX' THEN 'Y' ELSE 'N';
  NBR_DAYS/P12CS=NUM_DAYS;
  NBR_ROOMS/P12CS = NUM_ROOMS;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  NNROOMS/P12CS = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN 
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
 PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
 PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');                
-* HPREF/A1 = IF CLIENT_PREF EQ 'C' OR 'P' OR 'N' OR 'M' OR 'L' THEN 'Y' ELSE 'N';
 HPREF/A1 = IF PNP_FLAG EQ 'I' THEN 'Y' ELSE 'N';
 ONLH/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';

 HTL_AMT/D12.2CSM = TOTAL_AMT;
                  
  XTRAN_DTH/MDYY = INVOICE_DATE;
  TVL_DTH/MDYY = CHKIN_DATE;

  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');   
TKTN/A10 = IF TKT_NUM EQ '0000000000' THEN 'HTL ONLY' ELSE TKT_NUM;   
FLAG/A1 = 'H';
RES_ST/A1 = EDIT(RESV_STATUS, '9');
MIR_SEQ1/A1 = EDIT(MIR_SEQ, '9$$$$$');

RES_SYS/A1 = IF MIR_SEQ1 EQ 'X' THEN 'S' ELSE 'A';

PROP_CODE1/A7 = IF RES_SYS EQ 'S' THEN PROP_SABRE_CODE ELSE PROP_APOLLO_CODE;

END
-RUN

TABLE FILE &&EXTRACT
PRINT CHAIN_CODE
      ONLH
      PNR_LOCATOR
      PASSNGR_NAME
      PREF_CC
      HPREF
      HTL_AMT
      TKTN
      XTRAN_DTH
      TVL_DTH
      FLAG
      RES_ST
      ROLLUP_CODE AS 'ROLLCDH'
      PROP_CODE1 AS 'PROP_CODE'
BY HTL_KEY      
-* WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') 
      
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE &HTLDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHLDH1
END
-RUN

TABLE FILE RPTHLDH1
PRINT CHAIN_CODE
    ONLH
    PREF_CC
    HPREF
    XTRAN_DTH
    PASSNGR_NAME
    PNR_LOCATOR
    TKTN
    FLAG
    TVL_DTH
    HTL_AMT
    ROLLCDH
    PROP_CODE
BY HTL_KEY
ON TABLE HOLD AS RPTHLDH FORMAT FOCUS INDEX HTL_KEY
END
-RUN

TABLE FILE HTLUDID
PRINT AUD_DATA AS 'UID'
BY HTL_KEY
WHERE UDID EQ 'UID'
ON TABLE HOLD AS HTLUDID1
END
-RUN

JOIN HTL_KEY IN RPTHLDH TO HTL_KEY IN HTLUDID1 AS J3
-RUN

DEFINE FILE RPTHLDH
UID_DSC/A64 = IF UID EQ ' ' THEN 'NONE GIVEN' ELSE UID;
END
-RUN
TABLE FILE RPTHLDH
PRINT CHAIN_CODE
    ONLH
    XTRAN_DTH
    TVL_DTH
    HPREF
    HTL_AMT
    PREF_CC
    ROLLCDH
    PROP_CODE
BY UID_DSC    
BY PASSNGR_NAME      
BY PNR_LOCATOR
BY TKTN
BY FLAG
ON TABLE HOLD AS HTLHLD
END
-RUN

-IF &RECORDS NE 0 THEN GOTO SETA ELSE GOTO SETA1;

-SETA
-SET &&HTLDATA='Y';
-GOTO XXIT;

-SETA1
-SET &&HTLDATA='N';
-GOTO XXIT;


-XXIT;

