-*2/22/05        JK                 ADDED TNT_COMPANY WHERE STATEMENT
-*5/17/13        JK                 ADDED DEFINES FOR WN/FL REPORT
-*1/10/14        JK                 ADDING LOGIC FOR TMOBIL REPORT

-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
CLASS1/A25 = IF TKT_CL_CAT CONTAINS 'F' THEN 'FIRST' ELSE
               IF TKT_CL_CAT CONTAINS 'U' THEN 'UPGRADE' ELSE
               IF TKT_CL_CAT CONTAINS 'B' THEN 'BUSINESS' ELSE
               IF TKT_CL_CAT CONTAINS 'P' THEN 'PREMIUM ECONOMY' ELSE               
               IF TKT_CL_CAT CONTAINS 'E' THEN 'ECONOMY' ELSE 'DEFAULT';
FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
MIR_SEQ1/A1 = EDIT(MIR_SEQ, '9$$$$$');
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
EXG_ISS/D8CS = IF (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1) AND 
                  (SEG_NBR EQ '01') THEN 1 ELSE 0; 
NEW_SEG_MILES/D12 = CITYPAIR.SEG_MILES;
NEW_SEG_DISC/D12.2CS = SEG_DISCOUNT;
MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
TMCTRY/A15 = IF CTRY_COD CONTAINS 'CA' OR 'MX' THEN 'CANADA/MEXICO' ELSE 
             IF CTRY_COD OMITS 'CA' OR 'MX' THEN 'INTERNATIONAL';
                           
-* Defines on Previous
RES_SYS/A1 = IF MIR_SEQ1 EQ 'X' THEN 'S' ELSE 'A';
VASAVINGS/D12.2CS    = NEW_SEG_DISC - FARE_PAID;
ORGDST/A6 = IF ORG_APC LT DST_APC THEN ORG_APC|DST_APC ELSE DST_APC|ORG_APC;
WN_TKTS/P8CS = IF VAL_AIRLINE EQ 'WN' THEN NET_TKT_CNT ELSE 0;
WN_FARE/D12.2CS = IF VAL_AIRLINE EQ 'WN' THEN FARE_PAID ELSE 0;
FL_TKTS/P8CS = IF VAL_AIRLINE EQ 'FL' THEN NET_TKT_CNT ELSE 0;
FL_FARE/D12.2CS = IF VAL_AIRLINE EQ 'FL' THEN FARE_PAID ELSE 0;
FARE/P12.2CS = SEG_AMT + SEG_TAX;
NET_TKTS/P8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TRP_LEN/D8CS = TRIP_LENGTH * NET_TKT_CNT;

-* AA DL Upfront TAT report defines

TAT_SEG_DISC/D12.2CS = NEW_SEG_DISC;                     

-IF &&SETF EQ 'TCATATEK' THEN GOTO EK;

TAT_FLAG/A1 = IF (VAL_AIRLINE EQ 'AA') AND (TOUR_CODE EQ 'C535250') AND (ORGDST EQ 'BOSLGA') THEN 'Y' ELSE 
              IF (VAL_AIRLINE EQ 'DL') AND (SEG_DESIG EQ 'ANHR') AND (ORGDST EQ 'BOSLGA') THEN 'Y' ELSE
              IF (VAL_AIRLINE EQ 'DL') AND (SEG_DESIG EQ 'APAG') AND (ORGDST EQ 'BOSDCA') THEN 'Y' ELSE
              IF (VAL_AIRLINE EQ 'DL') AND (SEG_DESIG EQ 'ANHTT') AND (ORGDST EQ 'LGAORD') THEN 'Y' ELSE 'N';
                 
TAT_AD/D12.2CS = IF TAT_FLAG EQ 'Y' THEN VASAVINGS ELSE 0;
TAT_CS/D12.2CS = IF TAT_FLAG EQ 'N' THEN VASAVINGS ELSE 0;
                                      
-GOTO NOEK;

-EK;                      

TAT_FLAG/A1= IF (VAL_AIRLINE EQ 'EK') AND (TOUR_CODE EQ 'ZZTVT322') THEN 'Y' ELSE
             IF (VAL_AIRLINE EQ 'EK') AND (SEG_DESIG EQ 'TVT3') THEN 'Y' ELSE 'N';

TAT_AD/D12.2CS = IF TAT_FLAG EQ 'Y' THEN VASAVINGS ELSE 0;
TAT_CS/D12.2CS = IF TAT_FLAG EQ 'N' THEN VASAVINGS ELSE 0;

-NOEK;                      
END

TABLEF FILE &&EXTRACT
PRINT CLASS1
  EXG_ISS
  FARE_PAID
  NET_TKT_CNT
  MRK_TKT_CNT
  REF_EXCH_CNT
  TKT_PURCH  
  VAL_AIRLINE 
  AIRLINE
  NEW_SEG_MILES
  PNR_LOCATOR
  TRP_LEN
  TMCTRY
-* FIELDS ADDED FOR AA DL TAT REPORT
TAT_SEG_DISC
TAT_CS
TAT_AD
ORGDST
WN_TKTS
WN_FARE
FL_TKTS
FL_FARE
FARE
NET_TKTS
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCASUM 
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;



-IF &&SETF EQ 'TCAIRTAT' THEN GOTO AIRTAT;
-IF &&SETF.EVAL EQ 'TCATATEK' THEN GOTO EKTAT;
-IF &&SETF EQ 'TCAPOLFL' THEN GOTO AirTran;
-IF &&SETF EQ 'TCTRPCNT' THEN GOTO TCTRPCNT;
-IF &&SETF EQ 'TCHICAT' THEN GOTO HIGHCAT;

-*========================================================================
-*   PROCESS EXTRACTED DATA FROM ABOVE
-*========================================================================
DEFINE FILE TCASUM
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCASUM
SUM EXG_ISS
    FARE_PAID/D14.2CS
    NET_TKT_CNT    
    REF_EXCH_CNT
    TKT_PURCH 
    NEW_SEG_MILES AS 'MILES'
    MRK_TKT_CNT
    WN_TKTS
    WN_FARE
    FL_TKTS
    FL_FARE
    FARE
    NET_TKTS
BY ROLLCD
BY VAL_AIRLINE
BY AIRLINE
ON TABLE HOLD AS TCAS
END
-RUN


DEFINE FILE TCAS
 NMB/I5 = NMB + 1;
END

TABLE FILE TCAS
PRINT EXG_ISS
      FARE_PAID
      NET_TKT_CNT
      MRK_TKT_CNT
      REF_EXCH_CNT
      TKT_PURCH
      VAL_AIRLINE
      AIRLINE
      MILES
    WN_TKTS
    WN_FARE
    FL_TKTS
    FL_FARE
    FARE
    NET_TKTS      
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCA1
END
-RUN


MODIFY FILE TC_ASUM
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM TCA1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCA1
END
-RUN
-GOTO NEXTONE

-AIRTAT;

DEFINE FILE TCASUM
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCASUM
SUM TKT_PURCH
    REF_EXCH_CNT
    TAT_SEG_DISC
    FARE_PAID
    TAT_CS
    TAT_AD
BY ROLLCD
BY ORGDST
BY VAL_AIRLINE
ON TABLE HOLD AS TCAS
END
-RUN

DEFINE FILE TCAS
NMB/I2 = NMB + 1;
END
TABLE FILE TCAS
PRINT ORGDST
      VAL_AIRLINE
      TKT_PURCH
      REF_EXCH_CNT
      TAT_SEG_DISC
      FARE_PAID
      TAT_CS
      TAT_AD
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCA1
END
-RUN


MODIFY FILE TC_ASTAT
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM TCA1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCA1
END
-RUN
-GOTO NEXTONE

-EKTAT;
DEFINE FILE TCASUM
ROLLCD/A8 = '&&ROLL';
END

TABLE FILE TCASUM
SUM TKT_PURCH
    REF_EXCH_CNT
    TAT_SEG_DISC
    FARE_PAID
    TAT_CS
    TAT_AD
BY ROLLCD    
BY ORGDST
BY VAL_AIRLINE
ON TABLE HOLD AS TCAS
END
-RUN


DEFINE FILE TCAS
NMB/I2 = NMB + 1;
END
TABLE FILE TCAS
PRINT ORGDST
      VAL_AIRLINE
      TKT_PURCH
      REF_EXCH_CNT
      TAT_SEG_DISC
      FARE_PAID
      TAT_CS
      TAT_AD
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCA1
END
-RUN


MODIFY FILE TC_ASTAT
FIXFORM FROM TCA1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCA1
END
-RUN

TABLE FILE TC_ASTAT
PRINT ROLLCD
      VAL_AIRLINE
      TKT_PURCH
      REF_EXCH_CNT
      TAT_SEG_DISC
      FARE_PAID
      TAT_CS
      TAT_AD
BY ORGDST
WHERE VAL_AIRLINE EQ 'EK'
ON TABLE HOLD AS T1
END
-RUN

MODIFY FILE TC_ATAT
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM T1
MATCH ORGDST
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON T1
END
-RUN

TABLE FILE TC_ASTAT
PRINT ROLLCD
      VAL_AIRLINE
      TKT_PURCH
      REF_EXCH_CNT
      TAT_SEG_DISC
      FARE_PAID
      TAT_CS
      TAT_AD
BY ORGDST
WHERE VAL_AIRLINE NE 'EK'
ON TABLE HOLD AS T2
END
-RUN


MODIFY FILE TC_ATAT
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM T2
MATCH ORGDST
ON MATCH INCLUDE
ON NOMATCH REJECT
DATA ON T2
END
-RUN

-GOTO NEXTONE

-TCTRPCNT;

DEFINE FILE TCASUM
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCASUM
SUM NET_TKT_CNT
    TRP_LEN
BY ROLLCD
BY TMCTRY
ON TABLE HOLD AS TCAS
END
-RUN

DEFINE FILE TCAS
NMB/I2 = NMB + 1;
END
TABLE FILE TCAS
PRINT TMCTRY
      NET_TKT_CNT
      TRP_LEN
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCA1
END
-RUN


MODIFY FILE TC_TRP
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM TCA1
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCA1
END
-RUN
-GOTO NEXTONE

-HIGHCAT

DEFINE FILE TCASUM
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCASUM
SUM NET_TKT_CNT
 
BY ROLLCD
BY CLASS1
ON TABLE HOLD AS TCHCAT
END
-RUN

DEFINE FILE TCHCAT
NMB/I2 = NMB + 1;
END
TABLE FILE TCHCAT
PRINT 
      NET_TKT_CNT
BY ROLLCD
BY NMB
BY CLASS1
ON TABLE HOLD AS TCHCAT2
END
-RUN



MODIFY FILE TC_AHCAT
LOG DUPL MSG OFF
LOG NOMATCH MSG OFF
FIXFORM FROM TCHCAT2
MATCH ROLLCD NMB
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCHCAT2
END

-NEXTONE

