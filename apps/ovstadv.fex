-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

SET ASNAMES=ON

DEFINE FILE OVSTSHLD
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX ;

CY/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
           TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
MXADV/A8  = IF (ADV_PURCH LT 7) THEN '00 - 06' ELSE
            IF (ADV_PURCH GE 7) AND (ADV_PURCH LE 13) THEN '07 - 13' ELSE
            IF (ADV_PURCH GE 14) AND (ADV_PURCH LE 20) THEN '14 - 20' ELSE
               '21 +';

NET_TKT_CNT/D12      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

CYADV_TKT/D12 = IF CY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CYADV_FAR/D12 = IF CY EQ 'Y' THEN FARE_PAID ELSE 0;
PYADV_TKT/D12 = IF PY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PYADV_FAR/D12 = IF PY EQ 'Y' THEN FARE_PAID ELSE 0;
END
-RUN

TABLE FILE OVSTSHLD
 SUM CYADV_TKT 
     CYADV_FAR
     PYADV_TKT
     PYADV_FAR
     PCT.CYADV_TKT AS 'CYADV_PCT'
     PCT.PYADV_TKT AS 'PYADV_PCT'
BY MXADV
ON TABLE HOLD AS OVADVC 
END
-RUN


-SET &RECFOUND = &RECORDS;

-IF &RECFOUND EQ 0 GOTO SKIP_GRAPH;

GRAPH FILE OVADVC
SUM CYADV_TKT AS 'Current'
    PYADV_TKT AS 'Prior'
BY MXADV AS ' '
ON TABLE SET GRAPHSTYLE *
setGraphType(24);
setLegendDisplay(true);
setLegendTextAutofit(true);
setLegendPosition(2);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),false);
setFontSize(getLegendText(),15);
setFontStyle(getLegendText(),0);
setLegendMarkerPosition(1);
setO1LabelDisplay(true);
setFontSizeAbsolute(getO1Label(),true); 
setAutofit(getO1Label(),false);
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),15);
setO1LabelRotate(0);
setO1MajorGridDisplay(true);
setO1MinorGridDisplay(false);
setFontSizeAbsolute(getY1Label(),true);
setAutofit(getY1Label(),false);
setY1LabelDisplay(true);
setY1LabelFormat(1);
setFontSize(getY1Label(),15);
setFontStyle(getY1Label(),0);
setY1AxisSide(0);
setFontSizeAbsolute(getTitle(),true); 
setAutofit(getTitle(),false);
setTitleString("Advanced Purchase");
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),0);
setFontSize(getTitle(),15);
setSubtitleString(" ");
setDataTextDisplay(false);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 115 160 247));
ENDSTYLE
ON GRAPH SAVE AS ADVPUR FORMAT SVG
END
-RUN

-SKIP_GRAPH
-SET &&ADV_SVG=IF &LINES GT 0 THEN 'Y' ELSE 'N';


-INCLUDE DTRANGE
-RUN

DEFINE FILE OVADVC
CATEGORY/A30 = 'Advanced Purchase';
CAT_ORDER/I2 = 3;
END
-RUN

TABLE FILE OVADVC
PRINT CATEGORY/A30 AND MXADV/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CYADV_TKT/D15 AS 'TKT_CURR' AND 
CYADV_FAR/D15 AS 'VOL_CURR' AND CYADV_PCT AS 'PCT_CURR' 
AND PYADV_TKT/D15 AS 'TKT_PRIOR' PYADV_FAR/D15 AS 'VOL_PRIOR'
AND PYADV_PCT/D15 AS 'PCT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1; 
ON TABLE HOLD AS OVADVC1
END
-RUN

TABLE FILE OVADVC1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR PCT_CURR
      AND VOL_PRIOR AND TKT_PRIOR PCT_PRIOR
ON TABLE HOLD AS OVADVC2
END
MODIFY FILE OV_STAP
FIXFORM FROM OVADVC2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVADVC2
END
-RUN




