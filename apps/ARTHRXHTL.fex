
-*11/13/17-S-39877-JEM- Creation of new program for setfile ARTCNTRY


-INCLUDE SETECHO 

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN




-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

DEFINE FILE &&EXTRACT
  AIR_DSS/D12.2CSM = 0;
  FARE_PAID/D12.2CSM   = 0;
-*   LEVEL_DESC/A60 = &&LDESC;
  LEVEL_DESC/A60 = IF AROLL_LEV2 EQ 'ARTHRXB' OR 'ARTHRXP' OR 'ARTHRXG' OR 'ARTHRXP' THEN 'UNITED STATES' ELSE AROLL_LEV2;

  NBR_DAYS/D12CS =  NUM_DAYS; 
  NNROOMS/D12CS = IF (NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)
                THEN ((NBR_DAYS*NUM_ROOMS)*(-1))
                ELSE NBR_DAYS*NUM_ROOMS;
                
  NET_TKT_CNT/D8CS = 0; 
  NEW_SEG_COUNT/D12CS = 0;                          
  NEW_SEG_MILES/D12CS = 0;                
  NEW_TOTAL_AMT/D12.2CSM = TOTAL_AMT;
  RENTAL_DAYS/D12CS = 0;
  RES_ST/A1 = EDIT(RESV_STATUS, '$9');
  
  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT AIR_DSS
      CITY.COUNTRY_CODE AS 'CCODE'
      FARE_PAID
      HTL_KEY
      LEVEL_DESC
      INVOICE_DATE
      NNROOMS
      NET_TKT_CNT
      NEW_SEG_COUNT
      NEW_SEG_MILES
      NEW_TOTAL_AMT
      PNR_LOCATOR
      RENTAL_DAYS
      RES_ST
      TKT_NUM

-INCLUDE &HTLDATES 

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
-* &&WHERE1
-* &&WHERE2
-* &&WHERE3
-* &&WHERE4
-* &&WHERE5
-* &&WHERE6
-* &&WHERE7
-* &&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS
ON TABLE HOLD AS HTLHOLD
END
-RUN


TABLE FILE HTLHOLD
PRINT AIR_DSS
      FARE_PAID
      NNROOMS
      LEVEL_DESC
      NET_TKT_CNT
      NEW_SEG_COUNT
      NEW_SEG_MILES
      NEW_TOTAL_AMT
      PNR_LOCATOR
      RENTAL_DAYS
      RES_ST
      TKT_NUM
BY HTL_KEY
ON TABLE HOLD AS &&RPT_HOLD3 FORMAT FOCUS INDEX HTL_KEY
END
-RUN

-SET &&REPTTYPE = 'HTL';
-INCLUDE RETUDID2
-RUN

TABLE FILE UDIDHOLD
PRINT HSS
BY HTL_KEY
ON TABLE HOLD AS UHOLD1
END
-RUN


JOIN HTL_KEY IN &&RPT_HOLD3 TO HTL_KEY IN UHOLD1 AS JUDH1
-RUN

DEFINE FILE &&RPT_HOLD3
HTL_SCSAV/P12.2 = HSS;
HTL_SCSAVC/P12.2 = HSS * (-1);
HRS_AMT/D12.2CSM = IF HTL_SCSAV EQ 0 THEN 0 ELSE 
                   IF (NNROOMS LT 0) AND HSS LT 0 THEN (HTL_SCSAVC*NNROOMS) ELSE
                      (HTL_SCSAV*NNROOMS);
END
-RUN

TABLE FILE &&RPT_HOLD3
SUM AIR_DSS
    FARE_PAID
    HRS_AMT
    NET_TKT_CNT
    NEW_SEG_COUNT
    NEW_SEG_MILES
    NEW_TOTAL_AMT
    NNROOMS
    RENTAL_DAYS
BY LEVEL_DESC
ON TABLE HOLD AS HHOLD3
END
-RUN


TABLE FILE &&RPT_HOLD3
SUM PNR_LOCATOR 
BY LEVEL_DESC
BY PNR_LOCATOR
BY TKT_NUM
WHERE RES_ST NE 'X'
WHERE TKT_NUM EQ '0000000000'
ON TABLE HOLD AS PNRHH1
END
-RUN


DEFINE FILE PNRHH1
PNR_CNT/D12CS = IF PNR_LOCATOR NE LAST PNR_LOCATOR THEN 1 ELSE 0;
END
-RUN

TABLE FILE PNRHH1
SUM PNR_CNT 
BY LEVEL_DESC
BY PNR_LOCATOR
ON TABLE HOLD AS PNRHH2
END
-RUN



MATCH FILE HHOLD3
SUM AIR_DSS
    FARE_PAID
    HRS_AMT
    NET_TKT_CNT
    NEW_SEG_COUNT
    NEW_SEG_MILES
    NEW_TOTAL_AMT
    NNROOMS
    RENTAL_DAYS
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE PNRHH2
SUM PNR_CNT
BY LEVEL_DESC
AFTER MATCH HOLD AS HHOLD4 OLD-OR-NEW
END
-RUN


TABLE FILE HHOLD4
PRINT AIR_DSS
    FARE_PAID
    HRS_AMT
    NET_TKT_CNT
    NEW_SEG_COUNT
    NEW_SEG_MILES
    NEW_TOTAL_AMT
    NNROOMS
    PNR_CNT
    RENTAL_DAYS
BY LEVEL_DESC
ON TABLE HOLD AS HOLDH FORMAT FOCUS
END
-RUN


-SET &&HTLREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';

