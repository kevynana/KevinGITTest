-* File DELDATA.FEX
-* THIS PROGRAM DELETES DATA FOR A CERTAIN TIME PERIOD FROM
-* AIR, CAR, HOTEL, SEGMENT, MARKET, DESTINATION, TKT_MAIN
-*


-* The date range in this file should be the date range you want to delete

TABLE FILE S4GHSM
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR TRN_DATE
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE GE '20071201') AND (TRN_DATE LE '20071231')
-* WHERE TRN_DATE LT '20071001'
ON TABLE HOLD AS SHOLD
END


-* The date range in this file is all METX data outside of the data being deleted above

TABLE FILE S4GHSM
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE GE '20071001') AND (TRN_DATE LE '20071130') 
-*       (TRN_DATE GE '20061225') AND (TRN_DATE LE '20061231')
-*WHERE TRN_DATE LE '20060630'
ON TABLE HOLD AS SHOLD2
END
-RUN


-* Match these two files together getting everything in the original file that is not in the new file 

MATCH FILE SHOLD
PRINT AIR_KEY TRN_DATE
BY AIR_KEY NOPRINT
RUN
FILE SHOLD2
PRINT AIR_KEY
BY AIR_KEY NOPRINT
AFTER MATCH HOLD AS SHOLD3 OLD-NOT-NEW
END
-RUN

TABLE FILE SHOLD3
SUM TRN_DATE
BY AIR_KEY
ON TABLE HOLD AS SHOLD4
END
-RUN


TABLE FILE SHOLD4
PRINT AIR_KEY 
BY AIR_KEY NOPRINT
ON TABLE HOLD AS SHOLD5
END


-* AIR_MAIN
MODIFY FILE A4GHSM
FIXFORM FROM SHOLD5
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD5
END
-RUN

-* TKT_MAIN
MODIFY FILE T4GHSM
FIXFORM FROM SHOLD5
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD5
END

-* SEGMENT
MODIFY FILE S4GHSM
FIXFORM FROM SHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD
END

TABLE FILE M4GHSM
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE GE '20071201') AND (TRN_DATE LE '20071231')
-* WHERE TRN_DATE GT '20061231'
-* WHERE TRN_DATE LT '20071001'
ON TABLE HOLD AS MHOLD
END

-* MARKET
MODIFY FILE M4GHSM
FIXFORM FROM MHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON MHOLD
END

TABLE FILE D4GHSM
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE GE '20071201') AND (TRN_DATE LE '20071231')
-* WHERE TRN_DATE GT '20061231'
-* WHERE TRN_DATE LT '20071001'
ON TABLE HOLD AS DHOLD
END

-* DESTINATION
MODIFY  FILE D4GHSM
FIXFORM FROM DHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DHOLD
END

-CAR

-* CAR

TABLE FILE C4GHSM
PRINT CAR_KEY
BY CAR_KEY NOPRINT
WHERE (INVOICE_DATE GE '20071201') AND (INVOICE_DATE LE '20071231')
-* WHERE INVOICE_DATE EQ '20071231'
-* WHERE INVOICE_DATE LT '20071001'
ON TABLE HOLD AS CHOLD
END

MODIFY FILE C4GHSM
FIXFORM FROM CHOLD
MATCH CAR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON CHOLD
END

-* HOTEL

TABLE FILE H4GHSM
PRINT HTL_KEY
BY HTL_KEY NOPRINT
WHERE (INVOICE_DATE GE '20071201') AND (INVOICE_DATE LE '20071231')
-* WHERE INVOICE_DATE EQ '20061231'
-* WHERE INVOICE_DATE LT '20071001'
ON TABLE HOLD AS HHOLD
END

MODIFY FILE H4GHSM
FIXFORM FROM HHOLD
MATCH HTL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HHOLD
END
-EXIT
