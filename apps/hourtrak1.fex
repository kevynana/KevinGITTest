-INCLUDE SETECHO
SET ASNAMES=ON

-DEFAULT &FROMDT='2000/01/01';
-DEFAULT &TODT= '2000/01/31';
-DEFAULT &ROLLUP = ' ';
-DEFAULT &REQUESTER = ' ';
-DEFAULT &OUTFMT = ' ';
-DEFAULT &COMPBY = ' ';
-DEFAULT &ICFLAG = ' ';

-************************************************
-* Date Range Selection
-IF '&FROMDT.EVAL' EQ '2000/01/01' THEN GOTO NODTE;
-SET &DATE ='WHERE (DATE_COMPLETED GE ''&FROMDT.EVAL'') AND (DATE_COMPLETED LE ''&TODT.EVAL'')';
-GOTO ROLL;
-NODTE;
-SET &DATE = ' ';
-*************************************************

-************************************************
-* Rollup Selection
-ROLL
-IF '&ROLLUP.EVAL' EQ ' ' THEN GOTO NOROLL;
-SET &ROLLUP = 'WHERE AREA_REQ EQ ''&ROLLUP.EVAL''';
-GOTO REQUESTER;
-NOROLL
-SET &ROLLUP = ' ';
-RUN
-************************************************

-************************************************
-REQUESTER
-*Requester Selection
-IF '&REQUESTER.EVAL' EQ ' ' THEN GOTO NOREQUEST;
-SET &REQUESTER = 'WHERE REQUESTER1 EQ ''&REQUESTER.EVAL''';
-GOTO OUTPUT;
-NOREQUEST
-SET &REQUESTER = ' ';
-RUN
-*************************************************
-OUTPUT 
-* Output format
-IF '&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO EXCEL ELSE
-IF '&OUTFMT.EVAL' EQ 'PDF' THEN GOTO PDF ELSE GOTO DEFAULT;
-EXCEL
-SET &OUTFMT1 = '.xls';
-GOTO COMPBY;
-PDF
-SET &OUTFMT1 = '.pdf';
-GOTO COMPBY
-DEFAULT
-SET &OUTFMT = 'EXL2K';
-SET &OUTFMT1 = '.xls';
-RUN

-************************************************
-COMPBY
-* SPECIFIC PERSON COMPLETING
-IF '&COMPBY.EVAL' EQ ' ' THEN GOTO NOCOBY;
-SET &COMPBY = 'WHERE COMPBY1 EQ ''&COMPBY.EVAL''';
-GOTO BTYPE;
-NOCOBY
-SET &COMPBY = ' ';
-RUN

-************************************************
-BTYPE
-* Client/Internal Billing Type
-IF '&ICFLAG.EVAL' EQ ' ' THEN GOTO NOIC;
-SET &ICFLAG = 'WHERE ICFLAG EQ ''&ICFLAG.EVAL''';
-GOTO BEGIN;
-NOIC
-SET &ICFLAG = 'WHERE ICFLAG EQ ''C'' OR ''I'' OR '' ''';
-RUN
-************************************************


-BEGIN;
DEFINE FILE HOURTRAK
REQUESTER1/A20 = UPCASE(20, REQUESTOR, 'A20');
COMPBY1/A15 = UPCASE(15, COMPLETED_BY, 'A15');
END
-RUN
TABLE FILE HOURTRAK
PRINT COMPBY1 DATE_COMPLETED COMPLETED_BY REQUESTOR SHORT_DESC BILLED_HOURS
      TECHNOLOGY_CREDITS TOTAL_HOURS KEY ICFLAG
BY AREA_REQ 
&DATE
&ROLLUP
&REQUESTER
&COMPBY
&ICFLAG
ON TABLE HOLD AS H1 FORMAT FOCUS INDEX AREA_REQ
END
-RUN


JOIN AREA_REQ IN H1 TO ROLLUP_CODE IN ROLLUP 
-RUN

DEFINE FILE H1
CODE/A1 = EDIT(AREA_REQ, '$$9$$$$$');
BRANCH_CODE/A2 = IF AREA_REQ CONTAINS '-R' THEN ' ' ELSE 
                 IF CODE EQ ' ' THEN EDIT (AREA_REQ, '99$$$$$$') ELSE ' ';
END
-RUN

TABLE FILE H1
PRINT AREA_REQ ROLL_NAME COMPLETED_BY REQUESTOR DATE_COMPLETED SHORT_DESC
      BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS KEY 
BY BRANCH_CODE
ON TABLE HOLD AS H2 FORMAT FOCUS INDEX BRANCH_CODE
END
-RUN


JOIN BRANCH_CODE IN H2 TO BRANCH_CODE IN BRANCH
-RUN

TABLE FILE H2
PRINT AREA_REQ ROLL_NAME COMPLETED_BY REQUESTOR DATE_COMPLETED SHORT_DESC BRANCH_CODE ROLL_NAME
      BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS BRANCH_NAME KEY 
BY COMPLETED_BY
ON TABLE HOLD AS H3
END
-RUN

DEFINE FILE H3
BNAME/A25 = IF BRANCH_CODE EQ ' ' AND ROLL_NAME NE ' ' THEN ROLL_NAME ELSE 
            IF BRANCH_CODE EQ ' ' AND ROLL_NAME EQ ' ' THEN AREA_REQ ELSE BRANCH_NAME;
END
-RUN
TABLE FILE H3
SUM BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS 
BY BNAME
BY DATE_COMPLETED
BY REQUESTOR
BY COMPLETED_BY
BY SHORT_DESC
BY KEY
ON TABLE HOLD AS H4
END
-RUN


-SET &RPTSUF = 'HOURTRAK1';
-SET &NOWTM = HHMMSS ('A8');
-SET &NOWTM = EDIT(&NOWTM,'99$99$99');
-SET &NOWDT = &YYMD;
-SET &NOWDTTM = &NOWDT || '_' || &NOWTM;
-SET &DTTMIID = &RPTSUF || '_' || &NOWDTTM;
-SET &RPTPATH = 'D:/HOURTRAK/';
-SET &SAVTRAK = &RPTPATH||&DTTMIID||&OUTFMT1;
-RUN

FILEDEF SAVREP DISK -
&SAVTRAK

DEFINE FILE H4
SPACE/A1 = ' ';
BILLABLE/A2 = IF BILLED_HOURS GT 0 THEN 'B' ELSE
              IF TECHNOLOGY_CREDITS GT 0 THEN 'C' ELSE 'NO';
BILLABLE1/A20 = IF BILLABLE EQ 'B' THEN 'BILLABLE' ELSE
                IF BILLABLE EQ 'C' THEN 'TECHNOLOGY CREDITS' ELSE 'NO';
TOTAL_HOURS1/D7.2 = TOTAL_HOURS;
COMPLETED/A10 = ' ';
END
-RUN


TABLE FILE H4
HEADING CENTER
"DP WORK ORDER LOG"
" "
PRINT SPACE AS '' BNAME AS 'CLIENT/,BRANCH NAME' DATE_COMPLETED AS 'REQ DATE' REQUESTOR AS 'REQUESTER/,CSM'
      SPACE AS '' COMPLETED_BY AS 'ITS,CONTACT' SHORT_DESC AS 'PROJECT,DESCRIPTION' KEY AS 'PROJECT #'
      SPACE AS '' TOTAL_HOURS AS 'EST.,HOURS' TOTAL_HOURS1 AS 'ACT.,HOURS' BILLABLE1 AS 'BILLABLE'
      COMPLETED AS 'COMPLETED' 
BY BNAME NOPRINT
BY DATE_COMPLETED NOPRINT
BY REQUESTOR NOPRINT
BY COMPLETED_BY NOPRINT
BY SHORT_DESC NOPRINT
BY KEY NOPRINT
ON TABLE SUMMARIZE TOTAL_HOURS TOTAL_HOURS1 AS 'TOTAL HOURS'
ON TABLE SAVE AS SAVREP FORMAT &OUTFMT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.500000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL,$
TYPE=REPORT, GRID=ON, $
TYPE=HEADING, OBJECT=TEXT, SIZE=12, COLOR=BLACK, BACKCOLOR=RGB(255 255 128), STYLE=BOLD, $
TYPE=TITLE, STYLE=BOLD, SIZE=8,BACKCOLOR=GREY, $
TYPE=SUBFOOT, SIZE=9, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
ENDSTYLE
END
-RUN



-XXIT;
-TYPE "No Records for that time period"
