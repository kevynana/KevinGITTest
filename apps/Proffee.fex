-* Proffee.fex
-* Create professional fee database and reload data



SET ASNAME=ON
SET TEMPERASE = OFF

-*-GOTO FILEEXISTS;

-SET &LOGFILE = 'D:\TNT\TTRACKER\SYSTEM\LOGFILE.TXT';
FILEDEF LOGFILE DISK &LOGFILE
-RUN

FILEDEF PROF_FEE DISK D:\AS400\CH2M-Rttpfeea5.txt

TABLE FILE PROF_FEE
BY ROLLUP_CODE
ON TABLE SAVE AS PROROLL
END
-RUN

-IF &LINES EQ 0 THEN GOTO XXIT;
-SET &TOTROLLS = &LINES;
-REPEAT TOEND1 &TOTROLLS TIMES
-READ PROROLL NOCLOSE &XROLL.8. 


TABLE FILE ROLLUP
PRINT COMP
BY ROLLUP_CODE 
WHERE ROLLUP_CODE EQ '&XROLL.EVAL'
WHERE QTR_ENABLE EQ 'X' 
ON TABLE SAVE AS THISROLL
END
-RUN
-IF &LINES EQ 0 THEN GOTO TOEND1;

-READ THISROLL &ROLL.8. &COMP.6. 

-*check database file
-SET &DBFILE = 'D:\TNT\TTRACKER\DATA\PF' | '&COMP.EVAL' || '.FOC';
-DOS STATE &DBFILE
-RUN
-IF &RETCODE.EVAL EQ 0 THEN  GOTO FILEEXISTS;

-*COPY master file 
-SET &SRCMAS = 'D:\TNT\TTRACKER\DATA\PROFFEE.MAS';
-SET &DSTMAS = 'D:\TNT\TTRACKER\DATA\PF' | '&COMP.EVAL' || '.MAS';
-SET &COPY = 'COPY &SRCMAS.EVAL &DSTMAS.EVAL';
&COPY
-RUN

-SET &SRCFOC = 'D:\TNT\TTRACKER\DATA\PROFFEE.FOC';
-SET &DSTFOC = 'D:\TNT\TTRACKER\DATA\PF' | '&COMP.EVAL' || '.FOC';
-SET &COPY = 'COPY &SRCFOC.EVAL &DSTFOC.EVAL';
&COPY
-RUN

-FILEEXISTS

-*Get Year and Month as a part of key SEQNBR
-SET &ThisMDYY = &MDYY;
-SET &CurrYM = EDIT(&ThisMDYY,'$$$$$$99') | EDIT(&ThisMDYY,'99$$$$$$');
-SET &BASENBR = &CurrYM | '00000';

-*Check how many records in the current month in the database.
-SET &DBFILE = 'PF' | '&COMP.EVAL' || '';

TABLE FILE &DBFILE
COUNT SEQNBR
WHERE SEQNBR GE &BASENBR
ON TABLE HOLD
END
-RUN
-SET &BASENBR = &BASENBR + &LINES;

-*Retrieve Records from AS400 download TTFEEA5.TXT
DEFINE FILE PROF_FEE
  CNT/I5 WITH TRANS_TYPE = CNT + 1;
  SEQNBR/I9 WITH TRANS_TYPE = &BASENBR + CNT;
END

TABLE FILE PROF_FEE
PRINT 	AIR_KEY 
	CAR_KEY 
	HTL_KEY
	MISC_KEY

	TRANS_TYPE
	TRANS_DATE
	FEE_SRC 
	FEE_AMT	
	PAY_METHOD
	CREDIT_CARD
	
	EXCPTN_CDE
 	FEE_STATUS
 	FEE_DSC
  	FEE_TYPE
 	FEE_CPN	
 	RPT_DATE
 	TASF_STATUS
BY SEQNBR
WHERE ROLLUP_CODE EQ '&ROLL.EVAL'
ON TABLE HOLD
END
-RUN	

-IF &LINES EQ 0 THEN GOTO TOEND1;

MODIFY FILE &DBFILE
FIXFORM FROM HOLD
MATCH SEQNBR
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN

-WRITELOG
-WRITE LOGFILE NOCLOSE  &ROLL
-TOEND1

-XXIT