-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-*                     FOCEXEC: LVIEW2DT
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-* 9/28/09      DEB   CREATED FROM CVIEW2DT FOR ABTOPLIM
-* 12/22/11     GSE   Removed parens from CLIMIT in DEFINEs and moved -* to
-*                    first two spaces for 7.7.03
-*10/02/2017 JEM S-35468 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets

-**************************************************************************
-INCLUDE SETECHO

-TYPE &&RPT_TTL2
-SET &&RANK_LIMIT = 25 ;
-SET &&RANK_METH = NMBR_DAYS;


SET ASNAMES=ON
-RUN
-IF &&CURRENCY EQ 'USD' THEN GOTO NEXT;

-SET &&SUBHEAD = <NAMEC;
-SET &TAMT_CUR = ' TOTAL,DOLLARS, (' | &&CURRENCY || ')';
-SET &CDRT_CUR = 'DAILY,RATE, (' | &&CURRENCY || ')';
-SET &&PRINT2 ='GCAR_AMT/D16.2CS AS ' | '''&TAMT_CUR.EVAL''';
-SET &&PRINT5='COMPUTE AVGDR/D12.2CS=IF (GCAR_AMT LT 0) AND (NMBR_DAYS LT 0)';
-SET &&PRINT6='THEN ((GCAR_AMT/NMBR_DAYS)*(-1)) ELSE IF ((NMBR_DAYS LT 1) AND';
-SET &&PRINT7='(NMBR_DAYS GT (-1))) THEN 0 ELSE GCAR_AMT/NMBR_DAYS;';
-SET &&PRINT8='AS ' | '''&CDRT_CUR.EVAL''';

-NEXT;


TABLE FILE LIMHLD2
PRINT NBR_DAYS/I8 AS 'NMBR_DAYS'
  RENTAL_AMT/D12.2 AS 'CAR_AMT'
  AMT_RENTALX/D16.2 AS 'GCAR_AMT'
  NBR_CARS/I8 AS 'NMBR_CARS'
  NAMEC
BY CITY_ST  
BY PICK_CTY_NAME
BY STATE_CODE
BY VENDOR_NAME
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
SUM CAR_AMT/D12.2
  GCAR_AMT/D16.2
  NMBR_DAYS
  NMBR_CARS
  NAMEC
BY CITY_ST  
BY PICK_CTY_NAME
BY STATE_CODE
RANKED BY HIGHEST &&RANK_METH NOPRINT
BY VENDOR_NAME
ON TABLE HOLD AS HOLD2
END
-RUN

DEFINE FILE HOLD2
  DRANK/D5      = RANK;
  ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
  ARANK/A5 = EDIT(ARANKX,'$99999');
  RANK_LIMIT/I5 = &&RANK_LIMIT;
  RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN

TABLE FILE HOLD2
SUM CAR_AMT
  GCAR_AMT
  NMBR_DAYS
  NMBR_CARS
  NAMEC
BY CITY_ST  
BY PICK_CTY_NAME
BY STATE_CODE
BY RANK_VALUE
BY VENDOR_NAME
ON TABLE HOLD AS HOLD3
END
-RUN

TABLE FILE HOLD3
SUM CAR_AMT
  GCAR_AMT
  NMBR_DAYS
  NMBR_CARS
  NAMEC
BY CITY_ST  
BY PICK_CTY_NAME
BY STATE_CODE
BY RANK_VALUE
BY VENDOR_NAME
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE HOLD1
SUM CAR_AMT
  GCAR_AMT
  NMBR_DAYS
  NMBR_CARS
  NAMEC
-* BY PICK_CTY_NAME
BY CITY_ST
ON TABLE HOLD AS RHOLD1
END
-RUN

TABLE FILE RHOLD1
SUM CAR_AMT
    GCAR_AMT
    NMBR_DAYS
    NMBR_CARS
    NAMEC
RANKED BY HIGHEST &&RANK_METH AS 'DAYS_RANK'
BY CITY_ST
ON TABLE HOLD AS RHOLD2
END
-RUN

MATCH FILE HOLD4
PRINT CAR_AMT
      GCAR_AMT
      NMBR_DAYS
      VENDOR_NAME
      RANK_VALUE
      NMBR_CARS
      NAMEC
      STATE_CODE
      PICK_CTY_NAME
BY CITY_ST
ON TABLE HOLD
RUN
FILE RHOLD2
SUM DAYS_RANK
BY CITY_ST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN

TABLE FILE HOLD5
SUM CAR_AMT
    GCAR_AMT
    NMBR_DAYS
    NMBR_CARS
    NAMEC
RANKED BY HIGHEST DAYS_RANK
BY CITY_ST
BY PICK_CTY_NAME
BY STATE_CODE
BY RANK_VALUE
BY VENDOR_NAME
ON TABLE HOLD AS HOLD6
END
-RUN

DEFINE FILE HOLD6
  CTY_LIMIT/I2 = &&RANK_LIMIT;
  CLIMIT/A2    = EDIT(CTY_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6      = IF PICK_CTY_NAME NE LAST PICK_CTY_NAME THEN 
                 (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999');
  CTY_VALUE/A6 = IF CTY_LIMIT EQ 0 THEN BRANK ELSE
               IF RANK GT CTY_LIMIT THEN 'OTHER' ELSE BRANK;
  CTYST_PRINT/A35 = IF CTY_VALUE LT CLIMIT THEN CITY_ST ELSE ' ';               
  CTY_PRINT/A35 = IF CTY_VALUE LT CLIMIT THEN PICK_CTY_NAME ELSE ' ';
  ST_PRINT/A3 = IF CTY_VALUE LT CLIMIT THEN STATE_CODE ELSE ' ';
  VENDOR_RANK/A5 = IF CTY_VALUE LT CLIMIT THEN RANK_VALUE ELSE ' ';
-* JN6KEY/A50 = PICK_CTY_NAME||VENDOR_NAME;
  JN6KEY/A50 = CITY_ST||VENDOR_NAME;
END
-RUN

TABLE FILE HOLD6
SUM CAR_AMT
  GCAR_AMT
  NMBR_DAYS
  NMBR_CARS
  NAMEC
BY JN6KEY
BY CTY_VALUE
BY CTYST_PRINT
BY CTY_PRINT
BY ST_PRINT
BY VENDOR_RANK
BY VENDOR_NAME
ON TABLE HOLD AS HOLD7 FORMAT FOCUS INDEX JN6KEY
END
-RUN

DEFINE FILE LIMHLD2
-* JN6KEY/A50 = PICK_CTY_NAME||VENDOR_NAME;
  JN6KEY/A50 = CITY_ST||VENDOR_NAME;
END
-RUN

TABLE FILE LIMHLD2
SUM RENTAL_AMT/D12.2 AS 'CAR_AMT'
    AMT_RENTALX/D16.2 AS 'GCAR_AMT'
    NBR_DAYS/I8 AS 'NMBR_DAYS'
    NBR_CARS/I8 AS 'NMBR_CARS'
    NAMEC
BY JN6KEY
BY VENDOR_NAME
BY PASSNGR_NAME
BY CAR_TYPE
BY CAR_DESC
BY PICK_UP_DT
BY DROP_OFF_DT
BY DROP_CTY_NAME
BY AIRPORT_CODE
BY AIRPORT_NAME
BY INVOICE_NUM
ON TABLE HOLD AS HOLD8 FORMAT FOCUS INDEX JN6KEY
END
-RUN


-CONTINUE;


-TYPE JOIN 7 AND 8
JOIN HOLD8.JN6KEY IN HOLD8 TO HOLD7.JN6KEY IN HOLD7 AS MYJOIN
-RUN

-GOTO ENDTEST;
TABLE FILE HOLD8
 BY CTY_VALUE AS 'RANK'
 BY CTY_PRINT AS 'CITY'
 BY VENDOR_RANK 
 BY VENDOR_NAME AS 'VENDOR'
 WHERE HOLD8.JN6KEY EQ HOLD7.JN6KEY
END
-RUN
-QUIT


-ENDTEST



-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_DRIL;
-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'HOLD8';
-INCLUDE DRILWHRS
-************************************************************
-SKIP_DRIL

-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-**************************************************************************

DEFINE FILE HOLD8
NOWTOD/A8 WITH CTY_PRINT = HHMMSS (NOWTOD);
END

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;

TABLE FILE HOLD8
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
-*
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*
&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-*
-**********************************************************************
-* Where statements for Drilled Report
-**********************************************************************
-*
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
-*
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10
-*
-**********************************************************************
-* Report specific footer(s)
-**********************************************************************
-*
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&PASTYD
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*

-GOTO XXIT

-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-XXIT