-* File DELDATA.FEX
-* THIS PROGRAM DELETES DATA FOR A CERTAIN TIME PERIOD FROM
-* AIR, CAR, HOTEL, SEGMENT, MARKET, DESTINATION, TKT_MAIN
-*

-* The date range in this file should be the date range you want to delete

TABLE FILE S9CROWN
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR TRN_DATE
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE GE '20060327') AND (TRN_DATE LE '20060331')
-* WHERE TRN_DATE GT '20051231'
ON TABLE HOLD AS SHOLD
END

-* The date range in this file is all other data outside of the data being deleted above

TABLE FILE S9CROWN
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE (TRN_DATE GE '20060101') AND (TRN_DATE LE '20060326')
ON TABLE HOLD AS SHOLD2
END

-* Match these two files together getting everything in the original file that is not in the new file 

MATCH FILE SHOLD
PRINT AIR_KEY TRN_DATE
BY AIR_KEY NOPRINT
RUN
FILE SHOLD2
PRINT AIR_KEY
BY AIR_KEY NOPRINT
AFTER MATCH HOLD AS SHOLD3 OLD-NOT-NEW
END
-RUN

TABLE FILE SHOLD3
SUM TRN_DATE
BY AIR_KEY
ON TABLE HOLD AS SHOLD4
END
-RUN


TABLE FILE SHOLD4
PRINT AIR_KEY 
BY AIR_KEY NOPRINT
ON TABLE HOLD AS SHOLD5
END

-* AIR_MAIN
MODIFY FILE A9CROWN
FIXFORM FROM SHOLD5
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD5
END

-* TKT_MAIN
MODIFY FILE T9CROWN
FIXFORM FROM SHOLD5
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD5
END

-* UDID AIR
MODIFY FILE UACROWN
FIXFORM FROM SHOLD5
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD5
END

-* SEGMENT
MODIFY FILE S9CROWN
FIXFORM FROM SHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SHOLD
END

TABLE FILE M9CROWN
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE TRN_DATE GE '20060327' AND TRN_DATE LE '20060331'
-* IF TRN_DATE GT '20051231'
ON TABLE HOLD AS MHOLD
END

-* MARKET
MODIFY FILE M9CROWN
FIXFORM FROM MHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON MHOLD
END

TABLE FILE D9CROWN
PRINT AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
BY AIR_KEY NOPRINT
BY EMBARK NOPRINT
BY ROUTE NOPRINT
BY TKT_SORT NOPRINT
BY SEG_NBR NOPRINT
WHERE TRN_DATE GE '20060327' AND TRN_DATE LE '20060331'
ON TABLE HOLD AS DHOLD
END

-* DESTINATION
MODIFY FILE D9CROWN
FIXFORM FROM DHOLD
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DHOLD
END

-* CAR

TABLE FILE C9CROWN
PRINT CAR_KEY
BY CAR_KEY NOPRINT
WHERE INVOICE_DATE GE '20060327' AND INVOICE_DATE LE '20060331'
ON TABLE HOLD AS CHOLD
END

MODIFY FILE C9CROWN
FIXFORM FROM CHOLD
MATCH CAR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON CHOLD
END


-* CAR UDID
MODIFY FILE UCCROWN
FIXFORM FROM CHOLD
MATCH CAR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON CHOLD
END
-* HOTEL

TABLE FILE H9CROWN
PRINT HTL_KEY
BY HTL_KEY NOPRINT
WHERE INVOICE_DATE GE '20060327' AND INVOICE_DATE LE '20060331'
ON TABLE HOLD AS HHOLD
END

MODIFY FILE H9CROWN
FIXFORM FROM HHOLD
MATCH HTL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HHOLD
END

-* HOTEL UDID 
MODIFY FILE UHCROWN
FIXFORM FROM HHOLD
MATCH HTL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HHOLD
END
-EXIT