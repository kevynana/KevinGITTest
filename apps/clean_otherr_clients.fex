-*======================================================================
-*  PROGRAM : CLEAN_OTHERR_CLIENTS.FEX                   DATE: 01/30/2014
-*======================================================================
-*  DESCRIPTION: This program finds OTHER-R Client Code values that are
-*  used by other Rollups and deltes them from OTHER-R files.
-*
-*  PROGRAMMER: GSE                               REQUESTOR: STEVE CORDS
-*  DATE        PROGRAMMER  CHANGE
-*  ----        ----------  ---------------------------------
-*  01/31/2014  GSE         Removed JOIN to ROLLUP as UPD_ENABLE WHERE
-*                          is not needed/removed.
-*======================================================================
-SET &LOOPSTART = 0;
-SET &LOOPEND = 11;
-SET &NOW = HHMMSS('A8');
-RUN
-TYPE BEGINNING &NOW
-RUN
-*
-SET &ERROR = '';
-SET &AMSTR = 0;
-SET &HMSTR = 0;
-SET &CMSTR = 0;
-SET &LMSTR = 0;
-*
-DOS STATE d:\data\focus_backups\other\cleanup
-*
-IF &RETCODE EQ 0 GOTO SKIPMD;
-DOS MD d:\data\focus_backups\other\cleanup
-SKIPMD
 
COPY D:\TNT\TTRACKER\DATA\*OTHER.FOC D:\DATA\FOCUS_BACKUPS\OTHER\CLEANUP\ > NUL
-*
SET HOLDLIST = PRINTONLY
SET ASNAMES = ON
SET MESSAGE = OFF
-*
TABLE FILE ROLLUP
PRINT COMP QTR_ENABLE
BY ROLLUP_CODE
WHERE ROLLUP_CODE EQ 'OTHER-R'
ON TABLE HOLD AS ROLLLIST
END
-RUN
-*
-READROLL
 
-READFILE ROLLLIST
-RUN
-IF &IORETURN NE 0 GOTO XXIT;
 
-QTRD
-SET &NOW = HHMMSS('A8');
-RUN
 
-SET &QTRCTR = &LOOPSTART.EVAL;
-SET &ENDCTR = IF &QTR_ENABLE NE 'X' THEN 1 ELSE (&LOOPEND.EVAL - &LOOPSTART.EVAL + 1);
-TYPE Looping &ENDCTR Quarters
-*
-REPEAT QTRMOD &ENDCTR TIMES;
-SET &QTR = IF &QTR_ENABLE NE 'X' THEN '_' ELSE
-           IF &QTRCTR EQ 10 THEN 'Y' ELSE
-           IF &QTRCTR EQ 11 THEN 'Z' ELSE &QTRCTR;
-SET &SMODFILE = 'S' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &SFOCFILE = '&SMODFILE.EVAL' || '.foc';
-SET &MMODFILE = 'M' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &MFOCFILE = '&MMODFILE.EVAL' || '.foc';
-SET &DMODFILE = 'D' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &DFOCFILE = '&DMODFILE.EVAL' || '.foc';
-SET &TMODFILE = 'T' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &AMODFILE = 'A' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &AFOCFILE = 'A' | '&QTR.EVAL' | '&COMP.EVAL' || '.foc';
-SET &HMODFILE = 'H' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &CMODFILE = 'C' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &LMODFILE = 'L' | '&QTR.EVAL' | '&COMP.EVAL';
-SET &AUDIDFIL = 'UA' | '&COMP.EVAL';
-SET &CUDIDFIL = 'UC' | '&COMP.EVAL';
-SET &HUDIDFIL = 'UH' | '&COMP.EVAL';
-SET &LUDIDFIL = 'UL' | '&COMP.EVAL';
-SET &PFEEFILE = 'PF' | '&COMP.EVAL';
-SET &QTRCTR = &QTRCTR + 1;
-*
-TYPE QTR&QTR.EVAL
TABLE FILE &AMODFILE
BY BR_CL_IDX
BY AIR_KEY
ON TABLE HOLD AS ACLKEYS FORMAT FOCUS INDEX BR_CL_IDX
END
-RUN
-*
JOIN BR_CL_IDX IN ACLKEYS TO MULTIPLE BR_CL_IDX IN CLIENT AS J0
-*
TABLE FILE ACLKEYS
PRINT
      AIR_KEY
BY BR_CL_IDX NOPRINT
BY ROLLUP_CODE NOPRINT
WHERE ROLLUP NE 'OTHER-R'
ON TABLE HOLD AS AIRDELS
END
-RUN
-SET &AKEYS = &RECORDS;
JOIN CLEAR J0
JOIN CLEAR J1
-RUN
-IF &AKEYS EQ 0 GOTO NOPA;
-SET &ADELS = &AKEYS * 4;
-SET &AMSTR = &AMSTR + &ADELS;
-*
-TYPE &AMODFILE DELETE &ADELS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &AMODFILE
FIXFORM FROM AIRDELS
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON AIRDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &ADELS NE &DELTD THEN '&AMODFILE.EVAL actual deletes of &ADELS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
JOIN AIR_KEY IN AIRDELS TO MULTIPLE AIR_KEY IN &SMODFILE AS J0
-*
TABLE FILE AIRDELS
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS SEGDELS
END
-RUN
-SET &SDELS = &RECORDS;
JOIN CLEAR J0
-RUN
-*
-TYPE &SMODFILE DELETE &SDELS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &SMODFILE
FIXFORM FROM SEGDELS
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON SEGDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &SDELS NE &DELTD THEN '&SMODFILE.EVAL actual deletes of &SDELS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
JOIN AIR_KEY IN AIRDELS TO MULTIPLE AIR_KEY IN &MMODFILE AS J0
-*
TABLE FILE AIRDELS
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS MKTDELS
END
-RUN
-SET &MDELS = &RECORDS;
JOIN CLEAR J0
-RUN
-*
-TYPE &MMODFILE DELETE &MDELS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &MMODFILE
FIXFORM FROM MKTDELS
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON MKTDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &MDELS NE &DELTD THEN '&MMODFILE.EVAL actual deletes of &MDELS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
JOIN AIR_KEY IN AIRDELS TO MULTIPLE AIR_KEY IN &DMODFILE AS J0
-*
TABLE FILE AIRDELS
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS DSTDELS
END
-RUN
-SET &DDELS = &RECORDS;
JOIN CLEAR J0
-RUN
-TYPE &DMODFILE DELETE &DDELS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &DMODFILE
FIXFORM FROM DSTDELS
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON DSTDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &DDELS NE &DELTD THEN '&DMODFILE.EVAL actual deletes of &DDELS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
-TYPE &TMODFILE DELETE &AKEYS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &TMODFILE
FIXFORM FROM AIRDELS
MATCH AIR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON AIRDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &AKEYS NE &DELTD THEN '&TMODFILE.EVAL actual deletes of &AKEYS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
-NOPA
-*
TABLE FILE &HMODFILE
BY BR_CL_IDX
BY HTL_KEY
ON TABLE HOLD AS HCLKEYS FORMAT FOCUS INDEX BR_CL_IDX
END
-RUN
-*
JOIN BR_CL_IDX IN HCLKEYS TO MULTIPLE BR_CL_IDX IN CLIENT AS J0
-*
TABLE FILE HCLKEYS
PRINT
      HTL_KEY
BY BR_CL_IDX NOPRINT
BY ROLLUP_CODE NOPRINT
WHERE ROLLUP NE 'OTHER-R'
ON TABLE HOLD AS HTLDELS
END
-RUN
-SET &HDELS = &RECORDS;
-SET &HMSTR = &HMSTR + &HDELS;
JOIN CLEAR J0
JOIN CLEAR J1
-RUN
-IF &HDELS EQ 0 GOTO NOPH;
-*
-TYPE &HMODFILE DELETE &HDELS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &HMODFILE
FIXFORM FROM HTLDELS
MATCH HTL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HTLDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &HDELS NE &DELTD THEN '&HMODFILE.EVAL actual deletes of &HDELS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
-NOPH
-*
TABLE FILE &CMODFILE
BY BR_CL_IDX
BY CAR_KEY
ON TABLE HOLD AS CCLKEYS FORMAT FOCUS INDEX BR_CL_IDX
END
-RUN
-*
JOIN BR_CL_IDX IN CCLKEYS TO MULTIPLE BR_CL_IDX IN CLIENT AS J0
-*
TABLE FILE CCLKEYS
PRINT
      CAR_KEY
BY BR_CL_IDX NOPRINT
BY ROLLUP_CODE NOPRINT
WHERE ROLLUP NE 'OTHER-R'
ON TABLE HOLD AS CARDELS
END
-RUN
-SET &CDELS = &RECORDS;
-SET &CMSTR = &CMSTR + &CDELS;
JOIN CLEAR J0
JOIN CLEAR J1
-RUN
-IF &CDELS EQ 0 GOTO NOPC;
-*
-TYPE &CMODFILE DELETE &CDELS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &CMODFILE
FIXFORM FROM CARDELS
MATCH CAR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON CARDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &CDELS NE &DELTD THEN '&CMODFILE.EVAL actual deletes of &CDELS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
-NOPC
-*
TABLE FILE &LMODFILE
BY BR_CL_IDX
BY CAR_KEY
ON TABLE HOLD AS LCLKEYS FORMAT FOCUS INDEX BR_CL_IDX
END
-RUN
-*
JOIN BR_CL_IDX IN LCLKEYS TO MULTIPLE BR_CL_IDX IN CLIENT AS J0
-*
TABLE FILE LCLKEYS
PRINT
      CAR_KEY
BY BR_CL_IDX NOPRINT
BY ROLLUP_CODE NOPRINT
WHERE ROLLUP NE 'OTHER-R'
ON TABLE HOLD AS LIMDELS
END
-RUN
-SET &LDELS = &RECORDS;
-SET &LMSTR = &LMSTR + &LDELS;
JOIN CLEAR J0
JOIN CLEAR J1
-RUN
-IF &LDELS EQ 0 GOTO NOPL;
-*
-TYPE &LMODFILE DELETE &LDELS RECORDS
-*
SET MESSAGE = ON
MODIFY FILE &LMODFILE
FIXFORM FROM LIMDELS
MATCH CAR_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON LIMDELS
END
SET MESSAGE = OFF
-RUN
-*
-SET &ERROR = IF &LDELS NE &DELTD THEN '&LMODFILE.EVAL actual deletes of &LDELS.EVAL does not match actual deletes of &DELTD.EVAL' ELSE '';
-IF &ERROR GT '' GOTO :ERROR;
-*
-NOPL
-QTRMOD
 
-GOTO READROLL
 
-XXIT
 
-SET &NOW = HHMMSS('A8');
-RUN
-TYPE ENDING &NOW
-RUN
 
-EXIT
 
-:ERROR
-TYPE An error occurred:
-TYPE &ERROR.EVAL
