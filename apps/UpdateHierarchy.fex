-************************************************************************************************************
-* UpdateHierarchy.fex
-*   This program is used to update RPT_FLDS database, BURST_REPORT_HIERS and PTSELHIER tables,
-*   when the client changes their hierarchy members.  
-*
-*   This program will also update booking type for those inst_key for kiwiet-r, one time only.
-*
-*   This hierarchy file - input file has to be saved to CSV Comma delimited file and 
-*   ended with ,$ - add a column to excel with value of ,$ - NEED TO EDIT CSV FILE as it adds ",$" to it
-*   when you save it.
-* 
-* Date: 10/05/2009
-* 07/27/15 - DEB - S-10299 COMMENTED OUT SET &ECHO=ALL

-************************************************************************************************************

-* 07/27/15 - DEB - S-10299 COMMENTED OUT SET &ECHO=ALL
-*-SET &ECHO=ALL;
-DEFAULT &ROLLUP = 'FUJI-R';
-DEFAULT &LEVEL = '03';
-DEFAULT &INPUTFILE = 'TLTECH';

-*SET TEMPERASE = OFF
SET ASNAME = ON

TABLE FILE &INPUTFILE
PRINT OLDVAL NEWVAL 
BY NEWVAL NOPRINT
ON TABLE HOLD AS NEWHIER FORMAT FOCUS INDEX OLDVAL
END
-RUN


DEFINE FILE &INPUTFILE
  MEMBER/A17 = '''' | OLDVAL | '''';
END
TABLE FILE &INPUTFILE
PRINT MEMBER
BY OLDVAL NOPRINT
ON TABLE SAVE AS OLDMEMB
END
-RUN

JOIN CLEAR *
JOIN INST_KEY IN RPT_INST TO INST_KEY IN RPT_FLDS AS J1
END

TABLE FILE RPT_INST
PRINT MEMBER LEVEL
BY SET_KEY
BY INST_KEY
BY HIEORG2.SEQ_NO
WHERE ROLLUP_CODE EQ '&ROLLUP.EVAL'
WHERE LEVEL EQ '&LEVEL.EVAL'
WHERE MEMBER IN FILE OLDMEMB
ON TABLE HOLD AS OLDHIER FORMAT FOCUS INDEX MEMBER
END
-RUN
-IF &LINES EQ 0 THEN GOTO BurstReports;

JOIN CLEAR *
JOIN OLDVAL IN NEWHIER TO ALL MEMBER IN OLDHIER AS J2
END
TABLE FILE NEWHIER
PRINT  LEVEL NEWVAL AS 'MEMBER'
BY INST_KEY
BY SEQ_NO
ON TABLE SAVE AS INDATA
END
-RUN

MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 LEVEL/A2 HIEORG2.MEMBER/A15
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG2
  ON NOMATCH REJECT
  ON MATCH UPDATE MEMBER
DATA ON INDATA
END
-RUN

-BurstReports
JOIN CLEAR *
JOIN BREAK_KEY IN BURST_GROUPS TO ALL BREAK_KEY IN BURST_REPORT_HIERS AS J3
END

TABLE FILE BURST_GROUPS
PRINT ROLL_LEV BREAK_LEVEL
BY BREAK_KEY
BY BREAK2NDKEY
WHERE ROLLUP_CODE EQ '&ROLLUP.EVAL'
WHERE BREAK_LEVEL EQ '&LEVEL.EVAL'
WHERE ROLL_LEV IN FILE OLDMEMB
ON TABLE HOLD AS BURST FORMAT FOCUS
END
-RUN

-IF &LINES EQ 0 THEN GOTO DashPush;

JOIN CLEAR *
JOIN ROLL_LEV IN BURST TO OLDVAL IN NEWHIER AS J3A
END
TABLE FILE BURST
PRINT NEWVAL AS 'ROLL_LEV'
BY BREAK_KEY
BY BREAK2NDKEY
ON TABLE SAVE AS INBURST
END
-RUN

MODIFY FILE BURST_REPORT_HIERS
FIXFORM BREAK_KEY/11 BREAK2NDKEY/11 ROLL_LEV/15
MATCH BURST_REPORT_HIERS.BURST_REPORT_HIERS.BREAK_KEY BURST_REPORT_HIERS.BURST_REPORT_HIERS.BREAK2NDKEY
  ON MATCH UPDATE ROLL_LEV
  ON NOMATCH REJECT
DATA ON INBURST
END
-RUN

-DashPush
JOIN CLEAR *
JOIN SELECTIONID IN PTREPORT TO ALL SELECTIONID IN PTSELHIER AS J4
END

TABLE FILE PTREPORT
PRINT ROLL_LEV HIER_LEVEL
BY SELECTIONID
BY HIERID
WHERE ROLLUP_CODE EQ '&ROLLUP.EVAL'
WHERE HIER_LEVEL EQ '&LEVEL.EVAL'
WHERE ROLL_LEV IN FILE OLDMEMB
ON TABLE HOLD AS DPUSH FORMAT FOCUS
END
-RUN
-IF &LINES EQ 0 THEN GOTO DoneUpdate;

JOIN CLEAR *
JOIN ROLL_LEV IN DPUSH TO OLDVAL IN NEWHIER AS J3A
END

TABLE FILE DPUSH
PRINT NEWVAL AS 'ROLL_LEV'
BY SELECTIONID
BY HIERID
ON TABLE SAVE AS INPUSH
END
-RUN

MODIFY FILE PTSELHIER
FIXFORM SELECTIONID/11 HIERID/11 ROLL_LEV/15
MATCH PTSELHIER.PTSELHIER.SELECTIONID PTSELHIER.PTSELHIER.HIERID
  ON MATCH UPDATE ROLL_LEV
  ON NOMATCH REJECT
DATA ON INPUSH
END
-RUN


-DoneUpdate