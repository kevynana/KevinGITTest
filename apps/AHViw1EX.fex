-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File AHEXTRT.FEX
-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-*03/18/13  CHANGED CLASS_CAT DEFINE DUE TO CLASS CATEGORY CHANGES - DV
-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT

-*  CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'
-*                               'D' 'DISCOUNT'  'B' 'BUSINESS');
  CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE' 
                                ' ' 'DEFAULT'); 
  FARE_PAID/P11.2CS   = SEG_AMT + SEG_TAX;
  TKT_PURCH/P8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
 
 END
-RUN

TABLEF FILE &&EXTRACT
-* Report column elements
PRINT 
  AIR_KEY   
  CLASS_CAT 
  CONJ_REL  
  EMB_CTY.CITY_NAME AS 'EMB_CTY_NM' 
  EMB_CTY.EMB_CITY  AS 'EMB_CTY_CD' 
  FARE_PAID 
  HOTEL_FLAG    
  INVOICE_NUM   
  LEVEL_DESC    
  LEVEL1    
  MIR_SEQ   
  PASSNGR_NAME  
  ROLL_KEY  
  ROLL_LEV  
  RTE_CTY.CITY_NAME AS 'RTE_CTY_NM' 
  SEG_COUNT 
  SEG_NBR   
  STAY          
  TICKET_CODE   
  TKT_NUM   
  TKT_PURCH 
  TKT_SORT  
  TRN_DATE  
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
WHERE STAY GT 0

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

ON TABLE HOLD AS &&RPT_HOLDA

END
-*-EXIT
        
