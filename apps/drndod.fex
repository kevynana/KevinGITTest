-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/26/04    JOY                Added/Changed Norecords logic
-*12/28/04   DEB     ADDED DOD_TYPE 'M' TO BE EXCLUDED FROM EXTRACT
-*                   ADDED ADDT'L DOD_TYPE TO INT_FLAG DEFINE
-* 10/11/05  DEB     ADDED DOD_TYPE H TO INT_FLAG DEFINE
-*1/29/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-* ADDED TRANTYPE INCLUDE - JK 12/19/14 STORY ID S-06492

-INCLUDE SETECHO
-*SET TEMPERASE = OFF

-DEFAULT &&CPFLAG ='XX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';

-SET &&DTSTY = 'DT';

-SET &&FIELD = &ROW_LABEL;
-SET &&CPFLAG = &PARM1; 

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 11/03/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT


-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX OVSETDB SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-* CY (CURRENT YEAR), PY (PRIOR YEAR)
-****
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKTS';
-SET &&TICOUNT = 'TKT_ISS AS TKT,ISS';
-SET &&FAREAMT = 'FARE_AMT AS FARE,PAID';

-IF &&CPFLAG EQ 'CY' THEN GOTO CURYEAR
-  ELSE IF &&CPFLAG EQ 'PY' THEN GOTO PRRYEAR
-  ELSE GOTO CURYEAR; 

-CURYEAR
-SET &&DCHK='WHERE TRN_DATE GE '&&FROMDT.EVAL' AND TRN_DATE LE '&&TODT.EVAL'';
-SET &&FROMDT = &&FROMDT;
-SET &&TODT = &&TODT;
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-GOTO ENDDATE

-PRRYEAR
-SET &&DCHK='WHERE TRN_DATE GE '&&FXDATE.EVAL' AND TRN_DATE LE '&&TXDATE.EVAL'';
-SET &&FROMDT = &&FXDATE;
-SET &&TODT = &&TXDATE;
-SET &FMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FXDATE,'$9999');
-SET &TOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TXDATE,'$9999');
-GOTO ENDDATE

-ENDDATE


-FIELDS
-IF &&FIELD EQ 'Traditional' THEN GOTO TRADITIONAL
-  ELSE GOTO ONLINE;


-TRADITIONAL
-SET &&RPT_TTL1 = '           TRADITIONAL BOOKED TICKET DETAIL   ';
-SET &&DODCHK = 'WHERE INT_FLAG EQ ''TRADITIONAL''';
-SET &&SUBHEAD = '';
-GOTO OUTSIDE

-ONLINE
-SET &&RPT_TTL1 = '               ONLINE BOOKED TICKET DETAIL   ';
-SET &&DODCHK = 'WHERE INT_FLAG EQ ''ONLINE''';
-SET &&SUBHEAD = '';
-GOTO OUTSIDE


-OUTSIDE

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN


-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

EX TSAIRUSE
-RUN

-TYPE Before Extract
SET ASNAMES = ON 
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  NON_FLAG/A15 = IF NONREF_FLAG EQ 'N' THEN 'NON-REFUNDABLE' ELSE 
                                            'REFUNDABLE';
  INT_FLAG/A15 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE' ELSE 'TRADITIONAL';
  SAVINGS/D12.2CS     = SEG_STANDARD - SEG_AMT - SEG_TAX;
  LSTSVNGS/D12.2CS    = SEG_AMT + SEG_TAX - SEG_LOWEST;
  VALUEADD/D12.2CS    = SEG_DISCOUNT - SEG_AMT - SEG_TAX;
  
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

  TKT_ISS/D8CS        = IF ((SEG_NBR EQ '01') 
                            AND (SEG_AMT+SEG_TAX GE 0) AND (SEG_COUNT EQ 1))
                            THEN 1 ELSE 0;

  TKT_TYPE/A4         = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  MONTH/MT = TRN_DATE;
  PAX1/A25 = EDIT(PASSNGR_NAME, '9999999999999999999999999');
   PAX/A25 = LCWORD(25, PAX1, PAX);
   EMBAP/A25 = LCWORD(25, EMB_APT.AIRPORT_NAME, EMBAP);
 RTEAP/A25 = LCWORD(25, RTE_APT.AIRPORT_NAME, RTEAP);  
-INCLUDE TRANTYPE 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  AIRLINE	
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  SEG_AMT
  SEG_TAX
  SEG_STANDARD
  SEG_LOWEST
  SEG_DISCOUNT
  FARE_PAID     
  SAVINGS
  LSTSVNGS
  VALUEADD
  NET_TKT_CNT
  TKT_ISS	
  PASSNGR_NAME        
  REFUSE_CODE	
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  TKT_NUM
  TKT_TYPE       
  TRN_DATE	
  VAL_AIRLINE	
  DPT_TIME
  XDPT_DT
  XTRAN_DT      
  MONTH
  TKT_SORT
  SEG_NBR
  PAX
  EMBAP
  RTEAP
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

&&DCHK
&&VOIDCHK
&&DODCHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN


-TYPE After Extract

-TYPE Before Report
TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT'
TKT_ISS AS 'TISS'
FARE_PAID AS 'TFARE'
SEG_AMT AS 'TSEG'
SEG_TAX AS 'TSTAX'
SEG_STANDARD AS 'TSTD'
SEG_LOWEST AS 'TLOW'
SEG_DISCOUNT AS 'TDIS'
SAVINGS AS 'TSAV'
LSTSVNGS AS 'TLOST'
VALUEADD AS 'TVAL'
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE RPTHOLD
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_TAX
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID 
      SAVINGS
      LSTSVNGS
      VALUEADD  
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      XDPT_DT
      XTRAN_DT 
      PAX
      EMBAP
      RTEAP      
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_TAX
      SEG_LOWEST
      SEG_STANDARD
      SEG_DISCOUNT
      FARE_PAID   
      SAVINGS
      LSTSVNGS
      VALUEADD
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      XDPT_DT     
      XTRAN_DT
      PAX
      EMBAP
      RTEAP         
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TFARE
      TISS
      TTKT 
      TSEG
      TSTAX
      TSTD
      TLOW
      TSAV
      TLOST
      TDIS
      TVAL
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

TABLE FILE AIRHOLD2
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_TAX
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID 
      SAVINGS
      LSTSVNGS
      VALUEADD  
      PASSNGR_NAME
      REFUSE_CODE 
      RTE_APT_NM
      TFARE
      TISS
      TSEG
      TSTAX
      TSTD
      TLOW
      TSAV
      TLOST
      TDIS
      TVAL 
      TKT_NUM     
      TKT_TYPE	
      TRN_DATE
      TTKT
      XDPT_DT     
      DPT_TIME
      XTRAN_DT
      PAX
      EMBAP
      RTEAP         
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN

JOIN ROLL WITH TKT_SORT IN AIRHOLD3 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN
 
DEFINE FILE AIRHOLD3
NET_TKT_CNT/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
TKT_ISS/D9S = IF (SEG_NBR EQ '01') THEN TISS ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN PASSNGR_NAME ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/D12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
SEG_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSEG ELSE 0;
TAX_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSTAX ELSE 0;
SAV_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSAV ELSE 0;
LOST_TOT/D12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TLOST ELSE 0;
STD_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSTD ELSE 0;
LOW_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;
DIS_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TDIS ELSE 0;
VALUE_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TVAL ELSE 0;
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
ROLL/A8='&&ROLLUP.EVAL';
LVL/A60='&&LVL_DSC.EVAL';
RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
TDATE1/A8MDYY='&MDYY';
TDATE2/A10=EDIT(TDATE1,'99/99/9999');
TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
RD/A10 = 'Run Date:';
RT/A10 = 'Run Time:'; 
END
-RUN

TABLE FILE AIRHOLD3
HEADING
"Client: <6 <ROLL_NAME <37 <RN"
"Contact: <6 <ROLL_CONTACT <30 <LVL <85 &&DTDESC"
"Phone: <6 <RPHONE <85 &FMM/&FMD/&FMY - &TOM/&TOD/&TOY" 
" "
" "
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'RSN,CDE'
&&DEPARTD
EMBAP AS 'ORIGIN'
RTEAP AS 'DESTINATION'
AIRLINE AS 'CR'
&&FAREAMT
LOST_TOT AS 'LOST,SVGS'
&&NTCOUNT
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PAX AS 'PASSENGER,NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY TICKET_NMBR AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'

-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PAX SUMMARIZE MULTILINES AS 'TOTAL FOR'
ON XTKT_SRT SKIP-LINE
ON TABLE SET PAGE-NUM OFF
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
"</3"
"<15 &M1.EVAL <45 &M2.EVAL <75 &M3.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT

ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.050000, RIGHTMARGIN=0.050000,
    TOPMARGIN=0.010000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
    
TYPE=REPORT, FOCEXEC=NONE, FONT='TIMES NEW ROMAN', SIZE=8, COLOR=BLACK,
    BACKCOLOR=NONE, STYLE=NORMAL, $   
    
-IF '&&OUTFMT.EVAL' NE 'PDF' THEN GOTO DIFFFMT;    
TYPE=DATA,
     SIZE=8,
     GRID=OFF,
     BACKCOLOR=( RGB(239 239 239) 'WHITE' ),
$
-DIFFFMT;
TYPE=HEADING,
     LINE=1,
     OBJECT=TEXT,
     ITEM=1,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=1,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=1,
     OBJECT=FIELD,
     ITEM=2,
     SIZE=10,
     JUSTIFY=CENTER,
     COLOR='WHITE',
     FONT='ARIAL',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=3,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=1,
     OBJECT=FIELD,
     ITEM=4,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=2,
     OBJECT=TEXT,
     ITEM=1,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=2,
     OBJECT=FIELD,
     ITEM=1,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=2,
     OBJECT=FIELD,
     ITEM=2,
     SIZE=10,
     JUSTIFY=CENTER,
     COLOR='WHITE',
     FONT='ARIAL',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=2,
     OBJECT=FIELD,
     ITEM=3,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,   
     LINE=2,
     OBJECT=TEXT,
     ITEM=4,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$
TYPE=HEADING,
     LINE=3,
     SIZE=9,
     JUSTIFY=LEFT,
     COLOR='WHITE',
     FONT='TIMES NEW ROMAN',
     STYLE=BOLD,
$

TYPE=HEADING,
     LINE=5,
     SIZE=9,
     FONT='ARIAL',
     STYLE=BOLD,
     COLOR=RGB(0 0 138),
     JUSTIFY=LEFT,
$

TYPE=TITLE,
     SIZE=8,
     FONT='ARIAL',
     STYLE=BOLD,
     COLOR=RGB(0 0 138),
$     

TYPE=FOOTING,
     SIZE=7,
     COLOR='WHITE',
     STYLE=NORMAL,
$
TYPE=GRANDTOTAL,
     SIZE=8,
     STYLE=BOLD,
$     

TYPE=SUBTOTAL,
     SIZE=8,
     STYLE=BOLD,
$     

TYPE=DATA,
     COLUMN=N1,
     FONT='ARIAL',
$   

TYPE=DATA,
     COLUMN=N9,
     FONT='ARIAL',
$  


TYPE=DATA,
     COLUMN=N10,
     FONT='ARIAL',
$  


TYPE=DATA,
     COLUMN=N12,
     FONT='ARIAL',
$  

TYPE=DATA,
     COLUMN=N13,
     FONT='ARIAL',
$  

TYPE=REPORT, IMAGE=TSMHEAD.jpg, POSITION=(0 0), $
TYPE=REPORT, IMAGE=FOOTER.jpg, POSITION=(0 8.2), $ 
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END


-XXIT
