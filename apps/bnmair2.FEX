-***************************************************************************
-* REPORT NAME: QUARTERLY REVIEW (AIR EXTRACT)
-* TOTAL COMPANY REPORTING
-* Steve - temporary change to include Domestic only - 06/18/2004
-*2/22/05        JK                 ADDED TNT_COMPANY WHERE STATEMENT
-*04/20/10   DEB                CHANGED ORIG_SALE DEFINE
-* ADDED TRANTYPE INCLUDE - JK 12/16/14 STORY ID S-06492
-*12/29/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels

-***************************************************************************
-INCLUDE SETECHO


SET ASNAMES = ON
-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================

-SET &&IDLEVELBREAK = '';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT

FARE_PAID/D12 = SEG_AMT + SEG_TAX;
LEVEL_DESC1/A80 = &&LDESC;
  NET_TKT_CNT/D8CS  =   IF (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) THEN 1 ELSE 
                        IF (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1)
				   THEN (-1) ELSE 
                        IF (DOC_TYPE NE '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') AND
				   (SEG_COUNT EQ -1)
				   THEN (-1) ELSE 0;

TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                             (SEG_NBR EQ '01') AND
                             (FARE_PAID GE 0) AND
                             (SEG_COUNT EQ 1) AND 
                             (CONJ_REL EQ 1) THEN 1 ELSE 0;

-* Defines on Previous Defines
  DISSAVINGS/D12 = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  VASAVINGS/D12 = SEG_DISCOUNT - FARE_PAID;
-* Defines on Previously Defined Defines
-INCLUDE TRANTYPE
-INCLUDE IDLEVDEFINE 

END
-RUN

TABLEF FILE &&EXTRACT

PRINT AIR_MAIN.INTL_DOM AS 'INTL_DOM'
  AIR_KEY
  DISSAVINGS
  DOC_TYPE	
  DOD_TYPE
  DPT_DATE	
  EMBARK	
  EX_FLAG	
  EXCHG_SALE
  FARE_PAID
  LEVEL_DESC
  NET_TKT_CNT
  RF_FLAG
  SEG_AMT/D12	
  SEG_COM/D12	
  SEG_COUNT
  SEG_NBR 	
  SEG_TAX/D12
  TKT_DATE
  TKT_NUM
  TKT_PURCH	
  TRN_DATE
  VASAVINGS
  VOID_DATE 	
  VOID_STATUS 	

WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
-* &&WHERE7
-* &&WHERE8
-* &&WHERE9
-* &&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS BNMAIR2
END
-RUN


TABLE FILE BNMAIR2
PRINT DISSAVINGS
      AIR_KEY
      DOC_TYPE
      DOD_TYPE
      DPT_DATE
      EMBARK
      EX_FLAG
      EXCHG_SALE
      FARE_PAID
      INTL_DOM
      LEVEL_DESC
      NET_TKT_CNT
      RF_FLAG
      SEG_AMT
      SEG_COM
      SEG_NBR
      SEG_TAX
      TKT_NUM
      TKT_PURCH
      TRN_DATE 
      SEG_COUNT
      VASAVINGS
      VOID_DATE
      VOID_STATUS 
BY TKT_DATE
ON TABLE HOLD AS BNMA2 FORMAT FOCUS INDEX TKT_DATE
END
-RUN



-* Global Reporting JOIN
JOIN BNMA2.TKT_DATE IN BNMA2 TO GLOBAL.DATE IN GLOBAL AS J1


DEFINE FILE BNMA2
NAMEC/A30 = CNAME;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
PAID_FAREX/D20.6 = FARE_PAID * CURR1X;
VASAVEX/D20.6 = VASAVINGS * CURR1X;

DISSAVEX/D20.6 = DISSAVINGS * CURR1X;

END
-RUN

TABLE FILE BNMA2
PRINT DISSAVINGS
      AIR_KEY
      DOC_TYPE
      DOD_TYPE
      DPT_DATE
      EMBARK
      EX_FLAG
      EXCHG_SALE
      FARE_PAID
      INTL_DOM
      LEVEL_DESC
      NET_TKT_CNT
      RF_FLAG
      SEG_AMT
      SEG_COM
      SEG_NBR
      SEG_TAX
      TKT_NUM
      TKT_PURCH
      TRN_DATE 
      SEG_COUNT
      VASAVINGS
      VOID_DATE
      VOID_STATUS 
      NAMEC
      PAID_FAREX/D16.2CS
      VASAVEX/D16.2CS
      DISSAVEX/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS BNMAHLD2
END
-RUN  


-* -GOTO DEB;

TABLE FILE BNMAHLD2
PRINT DISSAVINGS
      AIR_KEY
      DOC_TYPE
      DOD_TYPE
      DPT_DATE
      EMBARK
      EX_FLAG
      EXCHG_SALE
      FARE_PAID
      INTL_DOM
      LEVEL_DESC
      NET_TKT_CNT
      RF_FLAG
      SEG_AMT
      SEG_COM
      SEG_NBR
      SEG_TAX
      TKT_NUM
      TKT_PURCH
      SEG_COUNT
      VASAVINGS
      VOID_DATE
      VOID_STATUS 
      NAMEC
      PAID_FAREX 
      VASAVEX 
      DISSAVEX 	
BY TRN_DATE
ON TABLE HOLD AS BNMAHLD3
END
-RUN

-* -DEB


DEFINE FILE BNMAHLD3
CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') THEN 1 ELSE 0;
FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;	
PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;

-*ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;	



ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND 
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;	
REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;
	
TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;	
	
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01')
  	     THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) 
	     THEN 1 ELSE 0;

-* Defines on previous DEFINEs

EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
  NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
  IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;	

TOT_ECNT/D9 = EXCHGS + EXMCOS;	
TOT_TRANS/D9 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;

-* DEFINES ON PREVIOUS DEFINES TO DETERMINE CURRENT

CTOT_TRANS/D9CS = IF CURRENT EQ 'Y' THEN TOT_TRANS ELSE 0;

CVOIDS/D9 = IF CURRENT EQ 'Y' THEN VOIDS ELSE 0;
CREFUNDS/D9 = IF CURRENT EQ 'Y' THEN REFUNDS ELSE 0;
CTOT_ECNT/D9 = IF CURRENT EQ 'Y' THEN TOT_ECNT ELSE 0;
TRAN_MY/MTYY = TRN_DATE;  
END




TABLE FILE BNMAHLD3
SUM CTOT_TRANS
      CVOIDS
      CREFUNDS
      CTOT_ECNT
BY TRAN_MY
BY LEVEL_DESC
ON TABLE HOLD AS BNMHLD4
END
-RUN




TABLE FILE BNMHLD4
SUM CTOT_TRANS
    CVOIDS
    CREFUNDS
    CTOT_ECNT
    
COMPUTE PCTVOID/D9.2 = IF ((CTOT_TRANS LT 1) AND (CTOT_TRANS GT (-1))) THEN 0 
                        ELSE ((CVOIDS/CTOT_TRANS)*100);
COMPUTE PCTREF/D9.2 = IF ((CTOT_TRANS LT 1) AND (CTOT_TRANS GT (-1))) THEN 0 
                        ELSE ((CREFUNDS/CTOT_TRANS)*100);                        
COMPUTE PCTEXCH/D9.2 = IF ((CTOT_TRANS LT 1) AND (CTOT_TRANS GT (-1))) THEN 0 
                        ELSE ((CTOT_ECNT/CTOT_TRANS)*100);                        
BY LEVEL_DESC
ON TABLE HOLD AS TRANTHREE FORMAT FOCUS INDEX LEVEL_DESC
END
-RUN

















