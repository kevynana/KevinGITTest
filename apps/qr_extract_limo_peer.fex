-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK


-* Get the selected Peer Groups
-*EX GETPGRP2 PEERTYPE='LIM'
-*-RUN
-INCLUDE SETECHO

TABLE FILE ROLLUP
PRINT ROLLUP
IF ROLLUP EQ 'BHCC-R' OR 'JOINT-R'  OR 'MOFO-R'
ON TABLE SAVE AS PEERMEM
END
-RUN
-SET &&TOTPEERS = &LINES;
-IF &LINES EQ 0 THEN GOTO NOPEERGROUP;

-SET &CNT = 1;
-REPEAT TOEND &LINES TIMES
-READ PEERMEM NOCLOSE &PEERMBR.8.
-SET &&PEERMBR.&CNT = '&PEERMBR.EVAL';
-SET &CNT = &CNT + 1;
-TOEND
-CLOSE PEERMEM;
-NOPEERGROUP

 

-* initialize the temp file to hold peer group data
 
-**************************************************************
-* LOOP THROUGH ALL THE PEERS SELECTED
-**************************************************************
-IF &&TOTPEERS EQ 0 THEN GOTO ALLPEERS;
-SET &CNT = 1;
-REPEAT ALLPEERS &&TOTPEERS TIMES
-SET &&ROLLUP = &&PEERMBR.&CNT;
-TYPE &&ROLLUP
 
EX QRSETBDB
-RUN
 
EX LIMUSE
-RUN
-********************************************************
-*Execute program to generate data from selected Peers
-********************************************************
-*EX NQCRPEX
-*-RUN
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
DEFINE FILE &&EXTRACT
 
BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
NBR_DAYS/D8 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
CCQ/A1 = IF INVOICE_DATE GE '&&CQFMDT.EVAL' AND
                INVOICE_DATE LE '&&CQTODT.EVAL' THEN 'Y' ELSE 'N';
TC_AMT/D12.2 = RENTAL_AMT;
 
 
-* Defines on Previous Defines
 
CLIENTCODE/A8 = '&&ROLLUP.EVAL';
-INCLUDE QRFTRANC
 
END
TABLE FILE &&EXTRACT
SUM NBR_DAYS
    TC_AMT
    BOOKINGS
    PICK_CTY.CITY_NAME/A20 AS 'PICK_CTY_NAME'
BY YEAR
BY TRAN
BY CLIENTCODE AS 'ROLLUP_CODE'
BY VENDOR_CODE
BY VENDOR_NAME
BY CAR_CAT
BY PICKUP_CITY
BY CURRENT
BY CCQ
BY PRIOR
BY PYTD
-IF &&INTDOM EQ 'T' THEN GOTO SKIP1;
WHERE PICK_CTY.INTL_DOM EQ '&&INTDOM.EVAL';
-SKIP1
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') OR
      (INVOICE_DATE GE '&&FXDATE.EVAL') AND (INVOICE_DATE LE '&&TXDATE.EVAL')
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
-INCLUDE PEERPARM
ON TABLE HOLD AS HOLDLIMP
END
-RUN
 

-IF &LINES EQ 0 THEN GOTO NEXTONE;
 
DEFINE FILE HOLDLIMP
CDAY/D8 = IF CURRENT EQ 'Y' THEN NBR_DAYS ELSE 0;
PDAY/D8 = IF PYTD EQ 'Y' THEN NBR_DAYS ELSE 0;
CAMT/P12.2 = IF CURRENT EQ 'Y' THEN TC_AMT ELSE 0;
PAMT/P12.2 = IF PYTD EQ 'Y' THEN TC_AMT ELSE 0;
CBOOKINGS/D8 = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
PBOOKINGS/D8 = IF PYTD EQ 'Y' THEN BOOKINGS ELSE 0;
END
 
TABLE FILE HOLDLIMP
SUM CDAY
    CAMT
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
    VENDOR_NAME
BY ROLLUP_CODE
BY VENDOR_CODE
ON TABLE HOLD AS PEERLIMO
END
-RUN
 
DEFINE FILE PEERLIMO
RANK/A9 = ' ';
END
TABLE FILE PEERLIMO
SUM CDAY
    CAMT
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
    VENDOR_NAME
    RANK
BY ROLLUP_CODE
BY VENDOR_CODE
WHERE VENDOR_CODE IN FILE SAVLVDR
ON TABLE HOLD AS PEERLIMO2
END
-RUN
 
TABLE FILE PEERLIMO2
PRINT ROLLUP_CODE VENDOR_NAME RANK CDAY CBOOKINGS CAMT PDAY PBOOKINGS PAMT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE &&QRLVDR
LOG DUPL MSG OFF
FIXFORM ROLLUP_CODE/8 VENDOR_NAME/12 RANK/9 CDAY/8 CBOOKINGS/8 CAMT/12 PDAY/8 PBOOKINGS/8 PAMT/12
MATCH ROLLUP_CODE VENDOR_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
DEFINE FILE PEERLIMO
VND_NAME/A12 = 'ALL OTHER';
RANK/A9 = 'OTHER';
END
TABLE FILE PEERLIMO
SUM CDAY
    CAMT
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
    VND_NAME AS 'VENDOR_NAME'
    RANK
BY ROLLUP_CODE
WHERE NOT VENDOR_CODE IN FILE SAVLVDR
ON TABLE HOLD AS PEERLIMO3
END
-RUN
 
TABLE FILE PEERLIMO3
PRINT ROLLUP_CODE VENDOR_NAME RANK CDAY CBOOKINGS CAMT PDAY PBOOKINGS PAMT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE &&QRLVDR
LOG DUPL MSG OFF
FIXFORM ROLLUP_CODE/8 VENDOR_NAME/12 RANK/9 CDAY/8 CBOOKINGS/8 CAMT/12 PDAY/8 PBOOKINGS/8 PAMT/12
MATCH ROLLUP_CODE VENDOR_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


-GOTO DEB2 
TABLE FILE HOLDLIMP
SUM CBOOKINGS
    CAMT
    CDAY
    PICK_CTY_NAME
BY ROLLUP_CODE
BY PICKUP_CITY
WHERE CURRENT EQ 'Y'
ON TABLE HOLD AS PEERLCTY
END
-RUN
 
DEFINE FILE PEERLCTY
RANK/A9 = ' ';
END
TABLE FILE PEERLCTY
SUM CBOOKINGS
    CAMT
    CDAY
    RANK
BY ROLLUP_CODE
BY PICK_CTY_NAME
WHERE PICKUP_CITY IN FILE LCARCTY
ON TABLE HOLD AS PEERLCTY2
END
-RUN
 
TABLE FILE PEERLCTY2
PRINT ROLLUP_CODE PICK_CTY_NAME RANK CBOOKINGS CDAY CAMT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE &&QRLCTY
LOG DUPL MSG OFF
FIXFORM ROLLUP_CODE/8 PICK_CTY_NAME/20 RANK/9 CBOOKINGS/8 CDAY/8 CAMT/12
MATCH ROLLUP_CODE PICK_CTY_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
DEFINE FILE PEERLCTY
PKCTY/A20 = 'ALL OTHER';
RANK/A9 = 'OTHER';
END
TABLE FILE PEERLCTY
SUM CBOOKINGS
    CAMT
    CDAY
    PKCTY AS 'PICK_CTY_NAME'
    RANK
BY ROLLUP_CODE
WHERE NOT PICKUP_CITY IN FILE LCARCTY
ON TABLE HOLD AS PEERLCTY3
END
-RUN
 
TABLE FILE PEERLCTY3
PRINT ROLLUP_CODE PICK_CTY_NAME RANK CBOOKINGS CDAY CAMT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE &&QRLCTY
LOG DUPL MSG OFF
FIXFORM ROLLUP_CODE/8 PICK_CTY_NAME/20 RANK/9 CBOOKINGS/8 CDAY/8 CAMT/12
MATCH ROLLUP_CODE PICK_CTY_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


-DEB2 
DEFINE FILE HOLDLIMP
RIGHT_TRAN/A7 = RJUST(7, TRAN, RIGHT_TRAN);
END
TABLE FILE HOLDLIMP
SUM BOOKINGS
    NBR_DAYS
    TC_AMT
BY ROLLUP
BY YEAR
BY RIGHT_TRAN AS 'TRAN'
ON TABLE HOLD AS PQLADR
END
-RUN
 
TABLE FILE PQLADR
PRINT ROLLUP_CODE YEAR TRAN BOOKINGS NBR_DAYS TC_AMT
ON TABLE SAVE
END
-RUN
 
MODIFY FILE &&QRLADR
LOG DUPL MSG OFF
FIXFORM ROLLUP_CODE/8 YEAR/4 TRAN/7 BOOKINGS/8 NBR_DAYS/8 TC_AMT/12
MATCH ROLLUP_CODE YEAR TRAN
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN
 
 
 
-NEXTONE
 
-SET &CNT = &CNT + 1;
-ALLPEERS
