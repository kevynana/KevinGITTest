-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**


-INCLUDE SETECHO
-* File DRDTLTVA.FEX
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*----           ----                 --------------------- 
-*8/31/00        SAC                   Initial Creation
-*****************************************************************  
-*
-*************************************************************************
-*DATE         NAME             DESCRIPTION
-*10/04/00    IBISTL-SB         ADDED SET STATEMENTS TO GET THE VALUE
-*                              INTO THIS DRILLED FEX 
-*10/5/00     IBISTL-SB         ADDED THE LOGIC FOR LOCATING THE SET FILE
-*                              IN THE WEBPATH AND THEN EXECUTING THE SET
-*                              FILE
-*10/04/00    IBISTL-SB         CHANGED THE PARAMETER IN THE &&FIELD
-*                              STATEMENT TO MIXED CASE.
-*10/04/00    IBISTL-SB         REMOVED THE ';' FROM THE SET ASNAMES 
-*                              STATEMENT
-* 3/07/01   TANDT-SS           CHANGE ALL NUMERIC FORMATS FROM P (PACKED) TO
-*                              D (FLOATING-POINT DOUBLE PRECISION) TO PROVIDE
-*                              CONSISTENCY IN ROUNDING AND PERCENTAGES.
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*7/26/04    JOY                Added/Changed Norecords logic
-*10/2/15 - S-11863 - Updated to correct drilldown error - JM
-* 07/27/16 - DLV - S-21094 - Added Default &&ORIENTATION
-* 02/24/17 DLV   S-22486 - Updated footing to show Post-trip
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox
-*************************************************************************

-DEFAULT &&CPFLAG ='XX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&ORIENTATION =  'P%';
-SET &&DTSTY = 'DT';

-SET &&FIELD = &ROW_LABEL;
-SET &&CPFLAG = &PARM1; 

-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
-***************************************************
-* CHECK FOR NON-DRILLABLE FIELD
-***************************************************
-IF (&&FIELD EQ 'Average Ticket Cost') 
- OR (&&FIELD EQ 'Savings %') 
- OR (&&FIELD EQ 'Lost Savings %') 
- THEN GOTO FIRST 
- ELSE GOTO SECOND;

-SECOND

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 11/03/2003 ****
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';


-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT


-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX OVSETDB SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
-INCLUDE OTHPARMS

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-* CY (CURRENT YEAR), PY (PRIOR YEAR)
-****
-* &&DIFLAG WILL EQUAL ONE OF THE FOLLOWING:\
-* D (DOMESTIC), I (INTERNATIONAL), B (INTERNATIONAL AND DOMESTIC)
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK = ' ';
-SET &&DIFLAGCHK = ' ';
-SET &&AIRFEECHK = ' ';
-SET &&SUBHEAD = 'DOMESTIC AND INTERNATIONAL';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKT,CNT';
-SET &&TICOUNT = 'TKT_ISS AS TKT,ISS';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&NETVOLUME = '';
-SET &&TAXES = '';
-SET &&STANDARD = ' ';
-SET &&SAVINGS = ' ';
-SET &&LOWEST = ' ';
-SET &&LSTSVGS = ' ';
-SET &&DISCOUNT = ' ';
-SET &&VALUEADD = ' ';

-IF &&CPFLAG EQ 'CY' THEN GOTO CURYEAR
-  ELSE IF &&CPFLAG EQ 'PY' THEN GOTO PRRYEAR
-  ELSE GOTO CURYEAR; 

-CURYEAR
-SET &&DCHK='WHERE TRN_DATE GE '&&FROMDT.EVAL' AND TRN_DATE LE '&&TODT.EVAL'';
-SET &&FROMDT = &&FROMDT;
-SET &&TODT = &&TODT;
-GOTO ENDDATE

-PRRYEAR
-SET &&DCHK='WHERE TRN_DATE GE '&&FXDATE.EVAL' AND TRN_DATE LE '&&TXDATE.EVAL'';
-SET &&FROMDT = &&FXDATE;
-SET &&TODT = &&TXDATE;
-GOTO ENDDATE

-ENDDATE

-GOTO SKIP

-IF &&DIFLAG EQ 'D' THEN GOTO DOMESTIC  
-  ELSE IF &&DIFLAG EQ 'I' THEN GOTO INTERNTL
-  ELSE IF &&DIFLAG EQ 'B'
-  ELSE GOTO FIELDS;

-DOMESTIC
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&SUBHEAD = 'DOMESTIC ONLY';
-GOTO FIELDS

-INTERNTL
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&SUBHEAD = 'INTERNATIONAL ONLY';

-SKIP

-FIELDS
-IF &&FIELD EQ 'DOMESTIC' THEN GOTO DOMESTIC
-  ELSE IF &&FIELD EQ 'INTERNATIONAL' THEN GOTO INTL
-  ELSE IF &&FIELD EQ 'TOTAL' THEN GOTO TOTAL
-  ELSE GOTO DOMESTIC;

-GOTO OUTSIDE

-DOMESTIC
-SET &&RPT_TTL1 = '                     AIR VOLUME DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&SUBHEAD = 'DOMESTIC ONLY';
-GOTO OUTSIDE

-INTL
-SET &&RPT_TTL1 = '                     AIR VOLUME DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&DIFLAGCHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&SUBHEAD = 'INTERNATIONAL ONLY';
-GOTO OUTSIDE

-TOTAL
-SET &&RPT_TTL1 = '                     AIR VOLUME DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&DIFLAGCHK = '';
-SET &&SUBHEAD = 'DOMESTIC AND INTERNATIONAL';
-GOTO OUTSIDE


-OUTSIDE

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

EX AIRUSE
-RUN

-TYPE Before Extract
SET ASNAMES = ON 
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT

  DEPT/A1 = IF LEVEL1 EQ '02410' OR '02412' OR '02425' OR '02433' OR '02451'
           OR '02455' OR '02456' OR '02457' OR '02458' OR '02463' OR '02467' 
           OR '02470' OR '02471' OR '02472' OR '02473' OR '02474' OR '02475'
           OR '02476' OR '02477' OR '02479' OR '02482' OR '02490' OR '02498'
           OR '02501' OR '02519' OR '02522' OR '02540' OR '02541' OR '02542'
           OR '02544' OR '02545' OR '02546' OR '02547' OR '02548' OR '02604'
           OR '02638' OR '02640' OR '02660' OR '02662' OR '02664' OR '02665'
           OR '02684' OR '02686' OR '02688' OR '05715' OR '05719' OR '05723'
           OR '08056' OR '02638' THEN 'Y' ELSE 'N';
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  SAVINGS/D12.2CS     = SEG_STANDARD - SEG_AMT - SEG_TAX;
  LSTSVNGS/D12.2CS    = SEG_AMT + SEG_TAX - SEG_LOWEST;
  VALUEADD/D12.2CS    = SEG_DISCOUNT - SEG_AMT - SEG_TAX;
  
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

  TKT_ISS/D8CS        = IF ((SEG_NBR EQ '01') 
                            AND (SEG_AMT+SEG_TAX GE 0) AND (SEG_COUNT EQ 1))
                            THEN 1 ELSE 0;

  TKT_TYPE/A4         = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  MONTH/MT = TRN_DATE;
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  AIRLINE	
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
  SEG_AMT
  SEG_TAX
  SEG_STANDARD
  SEG_LOWEST
  SEG_DISCOUNT
  FARE_PAID     
  SAVINGS
  LSTSVNGS
  VALUEADD
  NET_TKT_CNT
  TKT_ISS	
  PASSNGR_NAME        
  REFUSE_CODE	
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  TKT_NUM
  TKT_TYPE       
  TRN_DATE	
  VAL_AIRLINE	
  DPT_TIME
  XDPT_DT
  XTRAN_DT      
  MONTH
  TKT_SORT
  SEG_NBR
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

&&DCHK
&&VOIDCHK
&&REFUNDCHK
&&EXCHANGECHK
&&DIFLAGCHK
&&AIRFEECHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD

END
-RUN

-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN


-TYPE After Extract

-TYPE Before Report
TABLE FILE RPTHOLD
SUM NET_TKT_CNT AS 'TTKT'
TKT_ISS AS 'TISS'
FARE_PAID AS 'TFARE'
SEG_AMT AS 'TSEG'
SEG_TAX AS 'TSTAX'
SEG_STANDARD AS 'TSTD'
SEG_LOWEST AS 'TLOW'
SEG_DISCOUNT AS 'TDIS'
SAVINGS AS 'TSAV'
LSTSVNGS AS 'TLOST'
VALUEADD AS 'TVAL'
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE RPTHOLD
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_TAX
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID 
      SAVINGS
      LSTSVNGS
      VALUEADD  
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      XDPT_DT
      XTRAN_DT    
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_TAX
      SEG_LOWEST
      SEG_STANDARD
      SEG_DISCOUNT
      FARE_PAID   
      SAVINGS
      LSTSVNGS
      VALUEADD
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      XDPT_DT     
      XTRAN_DT
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TFARE
      TISS
      TTKT 
      TSEG
      TSTAX
      TSTD
      TLOW
      TSAV
      TLOST
      TDIS
      TVAL
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

TABLE FILE AIRHOLD2
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_TAX
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID 
      SAVINGS
      LSTSVNGS
      VALUEADD  
      PASSNGR_NAME
      REFUSE_CODE 
      RTE_APT_NM
      TFARE
      TISS
      TSEG
      TSTAX
      TSTD
      TLOW
      TSAV
      TLOST
      TDIS
      TVAL 
      TKT_NUM     
      TKT_TYPE	
      TRN_DATE
      TTKT
      XDPT_DT     
      DPT_TIME
      XTRAN_DT
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
DEFINE FILE AIRHOLD3
NET_TKT_CNT/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
TKT_ISS/D9S = IF (SEG_NBR EQ '01') THEN TISS ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN PASSNGR_NAME ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
SEG_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSEG ELSE 0;
TAX_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSTAX ELSE 0;
SAV_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSAV ELSE 0;
LOST_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOST ELSE 0;
STD_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSTD ELSE 0;
LOW_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;
DIS_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TDIS ELSE 0;
VALUE_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TVAL ELSE 0;
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END
-RUN

TABLE FILE AIRHOLD3
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'RSN,CDE'
&&DEPARTD
EMB_APT_NM AS 'ORG'
RTE_APT_NM AS 'DST'
AIRLINE AS 'CR'
&&FAREAMT
-* SAV_TOT AS 'SAVINGS'
LOST_TOT AS 'LOST,SAVINGS'
&&NTCOUNT
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY TICKET_NMBR AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'

-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
ON XTKT_SRT SKIP-LINE
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
"&&FOOTER"
"Post-trip Reporting &DATE AT <NOWTOD <90 &&FOOTR"
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END



-XXIT
