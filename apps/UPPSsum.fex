-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* CHANGED TITLE OF 0-6 AND 7-13 TO INCLUDE ADV PURCHASE DAYS PER PATTI   
-* DEB 1/13/05
-*03/13/17 - DLV - S-22486 - updated ettek Review to Post-trip Reporting


-SET &TPANR1 = ' ';
-SET &NRATP1 = ' ';
-SET &TMIDADR1 = ' ';
-SET &TADVATP1 = ' ';

-INCLUDE SETECHO
SET ASNAMES=ON

-SET &&RPTSUF = 'POTENTIALSVGS';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');


-INCLUDE FDEFRPTS
-RUN

MATCH FILE UPPSA
PRINT ADVATP
      DISSAVINGS       
      LOWTKT
      NONLTKT
      NRATP
      RATP
      TADVATP
      TLSV
      UPADV_TKT
      UPADV_TKT1
      UPNREF_TKT
      UPREF_TKT
BY LEVEL_DESC
BY EMP_ID
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE UPPSH
SUM 
    PREF_NGT
    NPREF_NGT
    NPANR
    TPANR
BY LEVEL_DESC
BY EMP_ID
BY PASSNGR_NAME
AFTER MATCH HOLD AS UPPSA1 OLD-OR-NEW
END
-RUN


MATCH FILE UPPSA1
PRINT ADVATP
      DISSAVINGS    
      LOWTKT
      NONLTKT
      NRATP
      RATP
      TADVATP
      TLSV
      UPADV_TKT
      UPADV_TKT1
      UPNREF_TKT
      UPREF_TKT
      PREF_NGT
      NPREF_NGT
      NPANR
      TPANR
BY LEVEL_DESC
BY EMP_ID
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE UPPSC
SUM 
    MIDBOOK
    NMIDADR
    NOMIDBK
    RENTAL_DAYS
    TMIDADR
    TMIDBK
BY LEVEL_DESC
BY EMP_ID
BY PASSNGR_NAME
AFTER MATCH HOLD AS UPPSAHC OLD-OR-NEW
END
-RUN



TABLE FILE UPPSAHC
PRINT TPANR NRATP TMIDADR TADVATP
WHERE TPANR NE 0
WHERE NRATP NE 0
WHERE TMIDADR NE 0
WHERE TADVATP NE 0
WHERE RECORDLIMIT EQ 1
ON TABLE SAVE AS TAVGR
END
-RUN


- IF &LINES EQ 0 THEN GOTO NoAVGR;

-READ TAVGR NOCLOSE &TPANR.9. &NRATP.9. &TMIDADR.9. &TADVATP.9.

EX FMTNUMBER VALUE=&TPANR ,DOLLAR='Y',LEN = 6
-RUN
-SET &TPANR1 = '&&RETVAL.EVAL'; 
-RUN
EX FMTNUMBER VALUE=&NRATP ,DOLLAR='Y',LEN = 6
-RUN
-SET &NRATP1 = '&&RETVAL.EVAL';
-RUN
EX FMTNUMBER VALUE=&TMIDADR ,DOLLAR='Y',LEN = 6
-RUN
-SET &TMIDADR1 = '&&RETVAL.EVAL';
-RUN
EX FMTNUMBER VALUE=&TADVATP ,DOLLAR='Y',LEN = 6
-RUN
-SET &TADVATP1 = '&&RETVAL.EVAL';
-RUN

-CLOSE TAVGR
-NoAVGR




TABLE FILE UPPSAHC
PRINT DISSAVINGS 
  LOWTKT
  NONLTKT
  UPADV_TKT 
  UPADV_TKT1
  UPREF_TKT 
  UPNREF_TKT
  TLSV
  RATP
  NRATP
  ADVATP
  TADVATP
  NPREF_NGT
  PREF_NGT
  NPANR
  TPANR
  RENTAL_DAYS
  MIDBOOK
  NOMIDBK
  TMIDBK
  NMIDADR
  TMIDADR
  LEVEL_DESC
  PASSNGR_NAME
BY EMP_ID
ON TABLE HOLD AS UPPSAV
END
-RUN




DEFINE FILE UPPSAV
PSVREF/D12.2CS = (RATP - NRATP) * UPREF_TKT;  
PSVADV/D12.2CS = (ADVATP - TADVATP) * UPADV_TKT;
PSVHTL/D12.2CS = (NPANR - TPANR) * NPREF_NGT;
PSVCAR/D12.2CS = (NMIDADR - TMIDADR) * NOMIDBK;
PSVREF1/D12.2CS = IF PSVREF GT 0 THEN PSVREF ELSE 0;
PSVADV1/D12.2CS = IF PSVADV GT 0 THEN PSVADV ELSE 0;
PSVHTL1/D12.2CS = IF PSVHTL GT 0 THEN PSVHTL ELSE 0;
PSVCAR1/D12.2CS = IF PSVCAR GT 0 THEN PSVCAR ELSE 0;
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
BLANK/A1 = ' ';
END
-RUN

TABLE FILE UPPSAV
SUM LEVEL_DESC AS 'Department,Number'
    UPADV_TKT1 AS 'Number of Tickets,Purchased 14 +,Days in Advance'
    UPADV_TKT AS 'Number of Tickets,Purchased Less Than 14,Days in Advance'
    PSVADV AS 'Potential Savings,if 100% of tickets,were purchased 14+,days in advance'
    UPREF_TKT AS 'Number of,Refundable,Tickets,Purchased'
    UPNREF_TKT AS 'Number of,Non Refundable,Tickets,Purchased'
    PSVREF AS 'Potential Savings,if 100% of tickets,purchased were,Non Refundable'
    LOWTKT AS 'Number of Tickets,where Lowest,Airfare Chosen'
    NONLTKT AS 'Number of Tickets,with Lost Savings'
    DISSAVINGS AS 'Potential Savings,if 100% of tickets,were purchased at,lowest airfare'
    PREF_NGT AS 'Number of,Nights at,CLC Hotels'
    NPREF_NGT AS 'Number of,Nights at,Non CLC Hotels'
    PSVHTL AS 'Potential Savings,if 100% of all,nights were at,CLC Hotels'
    MIDBOOK AS 'Number of,Midsize Car,Rental Days'
    NOMIDBK AS 'Number of,Larger than,Midsize Car,Rental Days'
    PSVCAR AS 'Potential Savings,if 100% of Car,Rentals were,Midsize'
BY LEVEL_DESC NOPRINT    
BY PASSNGR_NAME AS 'Individual,Traveler,Name'
BY EMP_ID AS 'Employee,Number'


-*ON TABLE SET ONLINE-FMT EXL2K

HEADING

-INCLUDE HEADAIR

FOOTING BOTTOM
"</2"
"&&FOOTER1"
"&&FOOTER2"
"Average ticket price for OP000 for 14+ days advance purchase - &TADVATP1.EVAL "
"Average ticket price for OP000 for non refundable tickets - &NRATP1.EVAL "
"Average nightly rate for OP000 for CLC hotels - &TPANR1.EVAL "
"Average daily rate for OP000 for midsize cars - &TMIDADR1.EVAL "
"Post-trip Reporting &DATE AT <NOWTOD <75 &&FOOTR"

ON TABLE SET PAGE-NUM OFF
&&BREAKOPTION
ON LEVEL_DESC SUMMARIZE AS 'Total For'
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=7, BACKCOLOR=NONE, STYLE=NORMAL, $
-* TYPE=REPORT, WRAP=1,$
TYPE=REPORT, COLUMN=N2, WRAP=1.8,$
TYPE=REPORT, COLUMN=N3, WRAP=1.2,$
TYPE=REPORT, COLUMN=N4, WRAP=2.3,$
TYPE=REPORT, COLUMN=N5, WRAP=1.2,$
TYPE=REPORT, COLUMN=N6, WRAP=1.2,$
TYPE=REPORT, COLUMN=N7, WRAP=1.2,$
TYPE=REPORT, COLUMN=N8, WRAP=1.2,$
TYPE=REPORT, COLUMN=N9, WRAP=1.2,$
TYPE=REPORT, COLUMN=N10, WRAP=1.2,$
TYPE=REPORT, COLUMN=N11, WRAP=1.2,$
TYPE=REPORT, COLUMN=N12, WRAP=1.2,$
TYPE=REPORT, COLUMN=N13, WRAP=1.2,$
TYPE=REPORT, COLUMN=N14, WRAP=1.2,$
TYPE=REPORT, COLUMN=N15, WRAP=1.2,$
TYPE=REPORT, COLUMN=N16, WRAP=1.2,$
TYPE=REPORT, COLUMN=N17, WRAP=1.2,$
TYPE=REPORT, COLUMN=N18, WRAP=1.2,$
TYPE=REPORT, COLUMN=N19, WRAP=1.2,$
TYPE=REPORT, COLUMN=N20, WRAP=1.2,$
TYPE=REPORT, COLUMN=N21, WRAP=1.2,$
TYPE=REPORT, COLUMN=N22, WRAP=1.2,$




-* TYPE=FOOTING, SIZE=9, LINE=17, OBJECT=FIELD, ITEM=2,COLOR=BLUE, FONT=TIMES NEW ROMAN,$

-* TYPE=DATA, FONT=TIMES NEW ROMAN, SIZE=9, BACKCOLOR=RGB(128 255 255), $
-* TYPE=DATA, SIZE=9, OBJECT=FIELD, ITEM=2,COLOR=BLUE, FONT=TIMES NEW ROMAN,$
TYPE=DATA, COLUMN=N7, BACKCOLOR=RGB(128 255 255),$
TYPE=DATA, COLUMN=N10, BACKCOLOR=RGB(128 255 255),$
TYPE=DATA, COLUMN=N13, BACKCOLOR=RGB(128 255 255),$
TYPE=DATA, COLUMN=N16, BACKCOLOR=RGB(128 255 255),$
TYPE=DATA, COLUMN=N19, BACKCOLOR=RGB(128 255 255),$

-* TYPE=HEADING, OBJECT=TEXT, SIZE=8, COLOR=BLACK, $
TYPE=HEADING, LINE=4, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=5, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=6, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=7, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=8, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=10, SIZE=9, STYLE=BOLD, COLOR=GREY, $
TYPE=HEADING, LINE=11, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=12, SIZE=12, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=HEADING, LINE=13, SIZE=10, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=TITLE, STYLE=BOLD, COLUMN=N7, BACKCOLOR=RGB(128 255 255), $
TYPE=TITLE, STYLE=BOLD, COLUMN=N10, BACKCOLOR=RGB(128 255 255), $
TYPE=TITLE, STYLE=BOLD, COLUMN=N13, BACKCOLOR=RGB(128 255 255), $
TYPE=TITLE, STYLE=BOLD, COLUMN=N16, BACKCOLOR=RGB(128 255 255), $
TYPE=TITLE, STYLE=BOLD, COLUMN=N19, BACKCOLOR=RGB(128 255 255), $

TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=8, STYLE=BOLD,$
-*TYPE=SUBTOTAL, SIZE=8, STYLE=BOLD, N=10, BACKCOLOR=RGB(128 255 255),$
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD,$
TYPE=REPORT, IMAGE=&&TNTPIC,  POSITION=(&&TNTDTPOS), SIZE=(&&TNTDTSIZ), $
TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLDTPOS), SIZE=(&&ROLDTSIZ), $

ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN
