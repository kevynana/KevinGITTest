-***************************************************************************
-* REPORT NAME: QUARTERLY REVIEW (CAR EXTRACT)
-* TOTAL COMPANY REPORTING
-*2/22/05        JK                 ADDED TNT_COMPANY WHERE STATEMENT
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK

-***************************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON

DEFINE FILE CR_50
BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
  NBR_DAYS/P12S = NUM_DAYS;
  RENTAL_DAYS/P12S = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
 
-* ****DEFINES ON PREVIOUS DEFINES****
  MIDBOOK/P12S = IF (DAILY_RATE GE 0) AND 
                  (CAR_CAT EQ 'INTERMEDIATE' OR 'ECONOMY' OR 'COMPACT') THEN 1 
                  ELSE IF (DAILY_RATE LT 0) AND 
                  (CAR_CAT EQ 'INTERMEDIATE' OR 'ECONOMY' OR 
                  'COMPACT') THEN (-1) ELSE 0;
    
  MID_DAYS/P12S = IF (CAR_CAT EQ 'INTERMEDIATE' OR 'ECONOMY' 
                   OR 'COMPACT') THEN RENTAL_DAYS ELSE 0;
    
END
-RUN

TABLEF FILE CR_50
PRINT BOOKINGS
  CAR_CAT	
  CAR_DESC
  CAR_KEY	
  CAR_RATE 	
  CAR_TYPE 	
  DAILY_RATE
  INVOICE_DATE 	
  MIDBOOK	
  MID_DAYS
  NBR_DAYS 
  RENTAL_AMT
  RENTAL_DAYS
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL)
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS

ON TABLE HOLD AS CARHOLD
END
-RUN
-IF &LINES EQ 0 THEN GOTO NEXTONE;

DEFINE FILE CARHOLD
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE CARHOLD
PRINT BOOKINGS
  CAR_CAT	
  CAR_DESC	
  CAR_RATE/D9.2S 	
  CAR_TYPE 	
  DAILY_RATE/D9.2S 
  INVOICE_DATE 	
  MIDBOOK	
  MID_DAYS
  NBR_DAYS 
  RENTAL_AMT/D9.2S
  RENTAL_DAYS
BY ROLLCD
BY CAR_KEY
ON TABLE HOLD AS CARHOLD1 
END
-RUN


DEFINE FILE CARHOLD1
  NMB/I5 = NMB + 1;
END
TABLE FILE CARHOLD1
PRINT BOOKINGS
  CAR_CAT	
  CAR_DESC	
  CAR_RATE 	
  CAR_TYPE 	
  DAILY_RATE
  INVOICE_DATE 	
  MIDBOOK	
  MID_DAYS
  NBR_DAYS 
  RENTAL_AMT
  RENTAL_DAYS
BY ROLLCD
BY NMB
BY CAR_KEY
ON TABLE HOLD AS TCC1
END
-RUN

MODIFY FILE TCCREV
FIXFORM FROM TCC1
DATA ON TCC1
END
-RUN

-NEXTONE
