 
-* 12/28/04 - DEB - ADDED ADDT'L DOD_TYPE TO INTERNET_FLAG DEFINE
-* 10/11/05 - DV - ADDED DOD_TYPE H TO INTERNET_FLAG DEFINE
-* 3/27/06 - JK - Added Agent_num and Ticket_branch
-*04/20/10   DEB                CHANGED ORIG_SALE DEFINE
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*5/23/14  - DEB  ADDED EX5 TO NUMEROUS DEFINES
-*3/11/15 - DEB - S-07697 ADDED ONL_TRANS FOR TCTRNNG
-* 4/27/15 - DEB - S-08598 - ADDED REPORT STYLING
-*10/08/15 - JM - S-12023 Updated for new report TCTRNCI shows agent and home branch (joins to agent file)
-* 12/7/15 - DLV  S-13556 Added additional code to GOTO FinRpt2 due
-*                        to reports not returning correctly
-*2/10/16 JEM S-15448 commented out set temperase=off
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
-* SET TEMPERASE = OFF
-INCLUDE SETECHO
 
CREATE FILE TCTRAN
-RUN
 
 
-IF '&&SETF.EVAL' NE 'TCTRNDI' THEN GOTO NoMore;
 
CREATE FILE TCTRNYTD
-RUN
 
CREATE FILE TCHTRNYD
-RUN
 
CREATE FILE TCCTRNYD
-RUN
 
-INCLUDE TCGETROLL2
-RUN
 
-GOTO NextStep
 
 
-NoMore;
-INCLUDE TCGETROLL
-RUN
 
-NextStep
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
 
 
-IF '&&SETF.EVAL' EQ 'TCTRNDI' THEN GOTO STEP2;
 
-TYPE TCAIRDB
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCTRNEX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-GOTO SKIPstep;
 
-STEP2;
 
-TYPE TCAIRDB
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCTRNEX
-RUN
 
 
 
EX TCTRNYTD
-RUN
 
 
 
EX TCCARDB
-RUN
 
 
EX TCTRNCEX
-RUN
 
 
EX TCHTLDB
-RUN
 
EX TCTRNHEX
-RUN
 
 
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
 
-SKIPstep;
 
 
 
-*-INCLUDE SetParmtime
-*-RUN
 
 
 
-*add hierarchy member as a part of &&DTTMID
-*-GOTO DONTADDMEM;
-IF '&&BURST.EVAL' NE 'Y' THEN GOTO DONTADDMEM;
TABLE FILE BRPTFLDS
PRINT MEMBER
WHERE INST_KEY EQ &&IKEY
WHERE MEMBER NE ' '
ON TABLE SAVE AS HORGMEMX
END
-RUN
-IF &LINES EQ 0 THEN GOTO DONTADDMEM;
 
-READ HORGMEMX NOCLOSE &MEM.15.
 
-SET &LEN=ARGLEN(15, '&MEM.EVAL', 'I2');
-SET &MEM = SUBSTR(15,'&MEM.EVAL',1,&LEN, &LEN, 'A&LEN.EVAL');
 
-SET &RNDNUM = '&RNDNUM.EVAL' || '&MEM.EVAL';
 
-DONTADDMEM
 
-SET &&INSTID = TRIM('L', &&INSTID, 11, ' ', 1, &&INSTID);
-SET &IID = &&INSTID;
-SET &NOWTM = HHMMSS ('A8');
 
-SET &NOWTM = EDIT(&NOWTM,'99$99$99');
-SET &NOWDT = &YYMD;
 
-SET &NOWDTTM = &NOWDT || '_' || &NOWTM | &&SCHED;
-SET &STRMID = &&STREAM || '_' || &IID;
-SET &&DTTMSTRM = &NOWDTTM || '_' || &STRMID;
-SET &&DTTMID = &NOWDTTM || '_' || &IID;
-SET &&NOWDTTM = &NOWDTTM;
 
 
 
 
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
 
TABLE FILE TCTRAN
PRINT AGENT_NUM
   AGENT_NAME
   CLIENT_NUM
   CLIENT_NAME
   DOC_TYPE
   DOD_TYPE
   DOD_DESC
   ONLINE_TRADITIONAL
   DOM_INT
   EMBARK
   EX_FLAG
   EXCHG_SALE
   FARE
   HOME_BRANCH
   ROLLCD
   RESV_BRANCH
   RF_FLAG
   SEG_AMT
   SEG_COM
-*    SEG_COUNT
   SEG_NBR
   SEG_TAX
-*   TKT_NUM
   TICKET_BRANCH
   TRN_DATE
   VOID_DATE
   VOID_STATUS
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS TRANONE
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
 
DEFINE FILE TRANONE
EMCO_TAX/D12.2 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_TAX
  ELSE 0;
EXCH_AMT/D12.2 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_AMT ELSE 0;
EXCH_MCO/D12.2 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN SEG_AMT
  ELSE 0;
EXCH_TAX/D12.2 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE IF
  ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK NE 'DT1' OR
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ')) OR ((SEG_COUNT GE 0) AND
  (EXCHG_SALE NE ' ')) OR (EMBARK EQ 'EX1' OR 'EX5') THEN SEG_TAX ELSE 0;
EXMCOS/D9 = IF (EX_FLAG NE ' ') AND (DOC_TYPE EQ '1') AND (EXCHG_SALE
	NE ' ') THEN 1 ELSE 0;
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (TKT_NUM
  NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
INTERNET_FLAG/A33 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE BOOKINGS' ELSE
            IF DOD_DESC CONTAINS 'EXCHANGE' THEN 'EXCHANGE FROM AN INTERNET BOOKING' ELSE
            IF DOD_DESC CONTAINS 'WEB' THEN 'WEB BOOKINGS' ELSE 'TRADITIONAL BOOKINGS';
NEW_SALES/D12.2 = IF (SEG_AMT GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE
  EQ ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5') THEN SEG_AMT ELSE 0;
NEW_SEG_AMT/D12.2 = IF VOID_STATUS EQ '0' THEN SEG_AMT ELSE 0;
NEW_SEG_COM/D12.2 = IF VOID_STATUS EQ '0' THEN SEG_COM ELSE 0;
NEW_SEG_TAX/D12.2 = IF VOID_STATUS EQ '0' THEN SEG_TAX ELSE 0;
NEW_TAX/D12.2 = IF ((SEG_TAX GT 0) AND (SEG_COUNT GE 0) AND (EXCHG_SALE EQ
  ' ') AND (EMBARK NE 'EX1' OR 'RF1' OR 'EX5')) THEN SEG_TAX ELSE 0;
NEW_TOTAL/D12.2 = NEW_SALES + NEW_TAX;
-*ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT
-* EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE IF (DOC_TYPE EQ '1') AND
-* (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') THEN 1 ELSE 0;
 
 
ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND
     (SEG_COUNT EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE
     IF (DOC_TYPE EQ '1') AND (EX_FLAG EQ ' ') AND (EXCHG_SALE NE ' ') AND
     (RF_FLAG EQ ' ') THEN 1 ELSE 0;
 
OTH_SALES/D9 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN ORIG_SALE
  ELSE 0;
OTHR_AMT/D12.2 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_SALES
  ELSE 0;
OTHR_TAX/D12.2 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_TAX ELSE
  0;
OTHR_TOTAL/D12.2 = IF DOC_TYPE EQ '5' OR '6' OR '7' OR '8' THEN NEW_TOTAL
  ELSE 0;
PAID_FARE/D12.2 = IF VOID_DATE EQ 0 THEN FARE_PAID ELSE 0;
PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0;
PTA_AMT/D12.2 = IF DOC_TYPE EQ '4' THEN NEW_SALES ELSE 0;
PTA_CNT/D9 = IF DOC_TYPE EQ '4' THEN ORIG_SALE ELSE 0;
PTA_TAX/D12.2 = IF DOC_TYPE EQ '4' THEN NEW_TAX ELSE 0;
PTA_TOTAL/D12.2 = IF DOC_TYPE EQ '4' THEN NEW_TOTAL ELSE 0;
REFUND_AMT/D12.2 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_AMT ELSE
  0;
REFUND_TAX/D12.2 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN SEG_TAX ELSE
  0;
REFUND_TOTAL/D12.2 = IF ((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR
  'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1') THEN FARE_PAID ELSE
  0;
REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;
TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT
  EQ 1) THEN 1 ELSE 0;
TKTLESS/D9 = IF DOC_TYPE EQ '9' THEN ORIG_SALE ELSE 0;
TKTLS_AMT/D12 = IF DOC_TYPE EQ '9' THEN NEW_SALES ELSE 0;
TKTLS_TAX/D12 = IF DOC_TYPE EQ '9' THEN NEW_TAX ELSE 0;
TKTLS_TOTAL/D12 = IF DOC_TYPE EQ '9' THEN NEW_TOTAL ELSE 0;
TOT_ETAX/D12.2 = EXCH_TAX + EMCO_TAX;
TOT_EXCH/D12.2 = EXCH_AMT + EXCH_MCO;
TOUR_AMT/D12.2 = IF DOC_TYPE EQ '2' THEN NEW_SALES ELSE 0;
TOUR_ORDERS/D9 = IF DOC_TYPE EQ '2' THEN ORIG_SALE ELSE 0;
TOUR_TAX/D12 = IF DOC_TYPE EQ '2' THEN NEW_TAX ELSE 0;
TOUR_TOTAL/D12 = IF DOC_TYPE EQ '2' THEN NEW_TOTAL ELSE 0;
TRAN_DT/MDYY = TRN_DATE;
VOID_AMT/D12.2 = IF VOID_DATE NE 0 THEN SEG_AMT ELSE 0;
VOID_TAX/D12.2 = IF VOID_DATE NE 0 THEN SEG_TAX ELSE 0;
VOID_TOTAL/D12.2 = IF VOID_DATE NE 0 THEN FARE_PAID ELSE 0;
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01')
 THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) THEN 1 ELSE 0;
 
-* Defines on previous DEFINEs
EMCO_TOTAL/D12.2 = IF DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E' THEN FARE_PAID
  ELSE 0;
EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK
 NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE IF (DOC_TYPE EQ '1') AND
(EXCHG_SALE EQ 'E') THEN 0 ELSE IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE
  ' ') THEN TKT_CNT ELSE 0;
EXCH_TOTAL/D12.2 = IF (DOC_TYPE EQ '1' AND EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (((EXCHG_SALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EMBARK NE 'DT1' OR
  'RF1' OR 'FP3') AND (RF_FLAG EQ ' ') AND (EX_FLAG EQ 'F' OR 'P') AND
  (SEG_COUNT LE 0)) OR (EMBARK EQ 'EX1' OR 'EX5')) THEN FARE_PAID ELSE 0;
FRFND_AMT/D12 = IF RF_FLAG EQ 'F' THEN REFUND_AMT ELSE 0;
FRFND_TAX/D12 = IF RF_FLAG EQ 'F' THEN REFUND_TAX ELSE 0;
FRFND_TOTAL/D12 = IF RF_FLAG EQ 'F' THEN REFUND_TOTAL ELSE 0;
HNDWRT_AMT/D12 = IF DOC_TYPE EQ '3' THEN NEW_SALES ELSE 0;
HNDWRT_TAX/D12 = IF DOC_TYPE EQ '3' THEN NEW_TAX ELSE 0;
HNDWRT_TOTAL/D12 = IF DOC_TYPE EQ '3' THEN NEW_TOTAL ELSE 0;
HNDWRTS/D9 = IF DOC_TYPE EQ '3' THEN ORIG_SALE ELSE 0;
MCO_AMT/D12.2 = IF DOC_TYPE EQ '1' THEN NEW_SALES ELSE 0;
MCO_CNT/D9 = IF DOC_TYPE EQ '1' THEN ORIG_SALE ELSE 0;
MCO_TAX/D12.2 = IF DOC_TYPE EQ '1' THEN NEW_TAX ELSE 0;
MCO_TOTAL/D12.2 = IF DOC_TYPE EQ '1' THEN NEW_TOTAL ELSE 0;
MCOS/D9CS           = IF (DOC_TYPE EQ '1') AND
                           (EX_FLAG EQ ' ')  AND
                           (EXCHG_SALE NE ' ') THEN 1 ELSE 0;
NET_AMT/D12.2 = NEW_SALES + REFUND_AMT + TOT_EXCH - VOID_AMT;
NET_TAX/D12.2 = NEW_TAX + REFUND_TAX + TOT_ETAX - VOID_TAX;
PRFND_AMT/D12 = IF RF_FLAG EQ 'P' THEN REFUND_AMT ELSE 0;
PRFND_TAX/D12 = IF RF_FLAG EQ 'P' THEN REFUND_TAX ELSE 0;
PRFND_TOTAL/D12 = IF RF_FLAG EQ 'P' THEN REFUND_TOTAL ELSE 0;
TOTAL_EXCHG/D12.2 = EXCH_TOTAL + EMCO_TOTAL;
TOT_ECNT/D9 = EXCHGS + EXMCOS;
TOT_TRANS/D9 = ORIG_SALE + VOIDS + TOT_ECNT + REFUNDS;
CHARGE_TRN/D9 = ORIG_SALE + VOIDS + EXCHGS + REFUNDS;
KW_TRN/D9 = ORIG_SALE + EXCHGS;
TRN_TYPE/A4 = IF (VOIDS EQ 1) THEN 'VOID' ELSE IF (ORIG_SALE EQ 1) THEN
  'SALE' ELSE IF (TOT_ECNT EQ 1) THEN 'EXCH' ELSE IF (REFUNDS EQ 1) THEN
  'RFND' ELSE ' ';
FEE1/D12.2CS = 35.25;
NOWTOD/A8 WITH ROLLCD = HHMMSS (NOWTOD);
DFARE/D12.2M = IF DOM_INT EQ 'DOMESTIC' THEN PAID_FARE ELSE 0;
DORIG_SALE/D12CS = IF DOM_INT EQ 'DOMESTIC' THEN ORIG_SALE ELSE 0;
DEXCHGS/D12CS = IF DOM_INT EQ 'DOMESTIC' THEN EXCHGS ELSE 0;
IFARE/D12.2M = IF DOM_INT EQ 'INTERNATIONAL' THEN PAID_FARE ELSE 0;
IORIG_SALE/D12CS = IF DOM_INT EQ 'INTERNATIONAL' THEN ORIG_SALE ELSE 0;
IEXCHGS/D12CS = IF DOM_INT EQ 'INTERNATIONAL' THEN EXCHGS ELSE 0;
ONL_TOT_TRANS/D9 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN TOT_TRANS ELSE 0;
 
END
-RUN
 
 
-IF '&&SETF.EVAL' EQ 'TCTRBTDI' THEN GOTO RPT2;
-IF '&&SETF.EVAL' EQ 'TCTRNDI' THEN GOTO RPT3;
 
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
 
TABLE FILE TRANONE
-INCLUDE &&PAHDR
-IF '&&OUTFMT.EVAL' NE 'PPT' OR 'PDF' OR 'COM' THEN GOTO NOSPC;
"</2"
-NOSPC;
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
 
-* -INCLUDE FOOTERSM
FOOTING BOTTOM
"NOTE:"
" ** TOTAL TRANSACTIONS EXCLUDES MCOS ** "
" ** CHARGED TRANSACTIONS EXCLUDES EXCHANGE MCOS AND MCOS ** "
"&&FOOTER1"
"&&FOOTER2"
"eTTek Review &DATE AT <NOWTOD <75 &&FOOTR"
-*-GOTO FinRpt2
-IF '&&OUTFMT.EVAL' NE 'PDF' THEN GOTO FinRpt2;
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
 
 
EX TCEMPTY
-RUN
 
-RPT2
 
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE TRANONE
-INCLUDE &&PAHDR
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-GOTO FinRpt;
 
 
-RPT3
 
TABLE FILE TRANONE
SUM DFARE/D12.2CSM
    IFARE/D12.2CSM
    PAID_FARE/D12.2CSM
COMPUTE DCNT/D12CS = DORIG_SALE + DEXCHGS;
COMPUTE ICNT/D12CS = IORIG_SALE + IEXCHGS;
COMPUTE TCNT/D12CS = ORIG_SALE + EXCHGS;
BY ROLLCD
ON TABLE HOLD AS TRANHOLD1
END
-RUN
 
 
MATCH FILE TRANHOLD1
PRINT DFARE IFARE PAID_FARE DCNT ICNT TCNT
BY ROLLCD
ON TABLE HOLD
RUN
FILE TCTRNYTD
SUM  YNET_AMT
 YDNET_AMT
 YINET_AMT
 YGTI
 YDGTI
 PYNET_AMT
 PYDNET_AMT
 PYINET_AMT
 PYGTI
 PYDGTI
 PYIGTI
 PROSP1
 PROSP1D
 PROSP1I
 PROGTI
 PRODGTI
 PROIGTI
BY ROLLCD
AFTER MATCH HOLD AS XTWO OLD-OR-NEW
END
-RUN
 
MATCH FILE XTWO
PRINT DFARE IFARE PAID_FARE DCNT ICNT TCNT
      YNET_AMT YDNET_AMT YINET_AMT YGTI YDGTI YDGTI YDGTI
      PYNET_AMT PYDNET_AMT PYINET_AMT PYGTI PYDGTI PYIGTI PROSP1 PROSP1D PROSP1I PROGTI PRODGTI PROIGTI
BY ROLLCD
ON TABLE HOLD
RUN
FILE TCHTRNYD
SUM YHAMT PYHAMT HPROSP1
BY ROLLCD
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW
END
-RUN
 
 
MATCH FILE XTHREE
PRINT DFARE IFARE PAID_FARE DCNT ICNT TCNT
      YNET_AMT YDNET_AMT YINET_AMT YGTI YDGTI YDGTI YDGTI
      PYNET_AMT PYDNET_AMT PYINET_AMT PYGTI PYDGTI PYIGTI PROSP1 PROSP1D PROSP1I PROGTI PRODGTI PROIGTI
      YHAMT PYHAMT HPROSP1
BY ROLLCD
ON TABLE HOLD
RUN
FILE TCCTRNYD
SUM YCAMT PYCAMT CPROSP1
BY ROLLCD
AFTER MATCH HOLD AS TRANTWO OLD-OR-NEW
END
-RUN
 
 
 
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
DEFINE FILE TRANTWO
NOWTOD/A8 WITH ROLLCD = HHMMSS (NOWTOD);
END
TABLE FILE TRANTWO
-INCLUDE &&PAHDR
 SUM DFARE/D12.2CSM AS 'DOMESTIC,FARE,PAID'
     DCNT/D12C AS 'DOMESTIC,GROSS,TICKETS,ISSUED'
  IFARE/D12.2CSM AS 'INTERNATIONAL,FARE,PAID'
     ICNT/D12C AS  'INTERNATIONAL,GROSS,TICKETS,ISSUED'
  PAID_FARE/D12.2CSM AS 'TOTAL,AIR,VOLUME'
     TCNT AS 'TOTAL,GROSS,TICKETS,ISSUED'
  PROSP1D/D12.2CSM AS 'ANNUALIZED,DOMESTIC,AIR,VOLUME'
  PRODGTI/D12C AS 'ANNUALIZED,DOMESTIC, GTI'
  PROSP1I/D12.2CSM AS 'ANNUALIZED,INTERNATIONAL,AIR VOLUME'
  PROIGTI/D12C AS 'ANNUALIZED,INTERNATIONAL,GTI'
  PROSP1/D12.2CSM AS 'ANNUALIZED,AIR VOLUME'
  PROGTI/D12C AS 'ANNUALIZED,TOTAL GTI'
  YHAMT/D12.2CSM AS 'TOTAL,HOTEL,VOLUME'
  HPROSP1/D12.2CSM AS 'ANNUALIZED,HOTEL,VOLUME'
  YCAMT/D12.2CSM AS 'TOTAL,CAR,VOLUME'
  CPROSP1/D12.2CSM AS 'ANNUALIZED,CAR,VOLUME'
 
  PYDNET_AMT/D12.2CSM AS 'PRIOR YEAR,DOMESTIC,AIR VOLUME'
  PYDGTI/D12C AS 'PRIOR YEAR,DOMESTIC,GTI'
  PYINET_AMT/D12.2CSM  AS 'PRIOR YEAR,INTERNATIONAL,AIR VOLUME'
  PYIGTI/D12C  AS 'PRIOR YEAR,INTERNATIONAL,GTI'
  PYNET_AMT/D12.2CSM AS 'PRIOR YEAR,TOTAL AIR,VOLUME'
  PYGTI/D12C     AS 'PRIOR YEAR,TOTAL GTI'
  PYHAMT/D12.2CSM AS 'PRIOR YEAR,TOTAL,HOTEL,VOLUME'
  PYCAMT/D12.2CSM AS 'PRIOR YEAR,TOTAL,CAR VOLUME'
 
 
 
 
BY ROLLCD AS 'ROLLUP'
ON ROLLCD SKIP-LINE
ON TABLE NOTOTAL
 
 
 
-FinRpt
 
-INCLUDE FOOTERSM1
 
-FinRpt2
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.300000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=REPORT, WRAP=1.5,$
 
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=3, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=9, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=9, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=3, JUSTIFY=LEFT, $
 
 
 
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,  $
 
 
 
 
 
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$
 
TYPE=TITLE, JUSTIFY=LEFT, $
 
 
TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=FOOTING, FONT=ARIAL, COLOR=GRAY,$
 
TYPE=SUBHEAD,
     STYLE=BOLD,
$
 
TYPE=SUBTOTAL,
     STYLE=BOLD,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $
 
TYPE=GRANDTOTAL, STYLE=BOLD, $
 
 
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
 
-IF &LINES GT 0 THEN GOTO XXIT;
 
EX TCEMPTY
-RUN
-GOTO XXIT;
 
 
 
 
 
 
EX TCEMPTY
-RUN
-XXIT
