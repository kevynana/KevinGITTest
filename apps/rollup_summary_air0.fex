-*  05/12/2015  REJ  S-07121 Updated the Advance Purchase categories.
 
-SET &GWHERE=IF '&&RUP.EVAL' EQ 'KIEWIT-R' THEN '-*' ELSE 'WHERE CATEGORY NE ' | '''' | 'GLOBAL' | '''';
 
DEFINE FILE SR_50
 DV_ADV_PUR/A14 = IF (ADV_PURCH LT 3) THEN '0-2 days' ELSE
                  IF (ADV_PURCH LT 7) THEN '3-6 days' ELSE
                  IF (ADV_PURCH LT 14) THEN '7-13 days' ELSE
				  IF (ADV_PURCH LT 21) THEN '14-20 days' ELSE '21+ days';
 FARE_PAID/P15.2 = SEG_AMT + SEG_TAX;
 NET_TKT_CNT/I5 = IF (TICKET_CODE EQ '1') AND (SEG_NBR EQ '01') AND
                     (FARE_PAID GE 0) AND (SEG_COUNT EQ 1) AND (CONJ_REL EQ 1) THEN 1 ELSE
                  IF (TICKET_CODE EQ '1') AND (SEG_NBR EQ '01') AND (EX_FLAG EQ 'F') THEN (-1) ELSE
                  IF (TICKET_CODE EQ '1') AND (SEG_NBR EQ '01') AND (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
 T_MONTH/YYM=TRN_DATE;
END
TABLE FILE SR_50
SUM NET_TKT_CNT AS NET_TICKETS
    FARE_PAID
BY ROLLUP_CODE
BY T_MONTH AS MONTH
BY CATEGORY
BY AIR_MAIN.INTL_DOM AS DOM_INTL_CODE
WHERE VOID_DATE EQ 0
&GWHERE.EVAL
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS HLDAIR01
END
 
 
MODIFY FILE ROLLUP_SUMMARY2
FIXFORM FROM HLDAIR01
MATCH ROLLUP_CODE MONTH CATEGORY DOM_INTL_CODE
 ON NOMATCH INCLUDE
 ON MATCH UPDATE NET_TICKETS FARE_PAID
DATA ON HLDAIR01
END
 
TABLE FILE SR_50
SUM NET_TKT_CNT AS ADV_TICKETS
    FARE_PAID AS ADV_FARE_PAID
BY ROLLUP_CODE
BY T_MONTH AS MONTH
BY CATEGORY
BY DV_ADV_PUR AS ADV_PURCH_CATEGORY
BY AIR_MAIN.INTL_DOM AS ADV_DOM_INTL_CODE
WHERE VOID_DATE EQ 0
&GWHERE.EVAL
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS HLDAIR02
END
 
MODIFY FILE ROLLUP_SUMMARY2
FIXFORM FROM HLDAIR02
MATCH ROLLUP_CODE MONTH CATEGORY ADV_PURCH_CATEGORY ADV_DOM_INTL_CODE
 ON NOMATCH INCLUDE
 ON MATCH UPDATE ADV_TICKETS ADV_FARE_PAID
DATA ON HLDAIR02
END
 
DEFINE FILE MR_50
 FARE_PAID/P15.2 = SEG_AMT;
 NET_TKT_CNT/I5 = IF (TICKET_CODE EQ '1') AND (FARE_PAID GE 0) AND (SEG_COUNT EQ 1) AND (CONJ_REL EQ 1) THEN 1 ELSE
                  IF (TICKET_CODE EQ '1') AND (EX_FLAG NE ' ') THEN (-1) ELSE
                  IF (TICKET_CODE EQ '1') AND (RF_FLAG NE ' ') THEN (-1) ELSE 0;
 T_MONTH/YYM=TRN_DATE;
END
TABLE FILE MR_50
SUM NET_TKT_CNT AS SEGMENTS
    FARE_PAID AS CP_FARE_PAID
BY ROLLUP_CODE
BY T_MONTH AS MONTH
BY CATEGORY
BY ORG_DST
WHERE ORG_DST NE ''
WHERE VOID_DATE EQ 0
&GWHERE.EVAL
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS HLDAIR03
END
 
MODIFY FILE ROLLUP_SUMMARY2
FIXFORM FROM HLDAIR03
MATCH ROLLUP_CODE MONTH CATEGORY ORG_DST
 ON NOMATCH INCLUDE
 ON MATCH UPDATE SEGMENTS CP_FARE_PAID
DATA ON HLDAIR03
END
-RUN
