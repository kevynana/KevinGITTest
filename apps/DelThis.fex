-**********************************************************************
-* Program Name: DelThis.fex
-* Delete a particular report in RPT_QUE
-* CMD FORMAT: EX DELTHIS INSTKEY=#######, INTIME='19990101  00.00.00'
-* NOTE: 2 Spaces between Date and Time.
-**********************************************************************
-INCLUDE SETECHO
-DEFAULT &INSTKEY = 0;
-DEFAULT &INTIME = '19990101  00.00.00';

-IF &INSTKEY NE 0 THEN STARTJOB;
-TYPE You must provide INSTKEY to run this program
-TYPE CMD FORMAT EX DELTHIS INSTKEY=####### INTIME='19990101  00.00.00'
-TYPE INTIME is optional.  If you don't enter INTIME, it will del all 
-TYPE report instances with that instkey.
-GOTO XXEXIT;

-STARTJOB
-SET &COND1 = 'WHERE INST_KEY EQ ''&INSTKEY.EVAL''';
-SET &COND2 = IF '&INTIME.EVAL' EQ '19990101  00.00.00' THEN ' '
- ELSE 'WHERE INTIME CONTAINS ''&INTIME.EVAL''';

TABLE FILE RPT_QUE
PRINT INST_KEY INTIME SET_KEY
&COND1
&COND2
ON TABLE SAVE 
END
-RUN
-IF &LINES EQ 0 THEN GOTO CHKHRPTINST;
-*-READ SAVE &X.25. &SETKEY.7.
-READ SAVE &IKEY.7. &ITIME.18. &SETKEY.7.
-CLOSE SAVE
-TYPE &IKEY &ITIME &SETKEY

MODIFY FILE RPT_QUE
FIXFORM INST_KEY/7 INTIME/18 X7
MATCH * KEYS SEG RPT_QUE
  ON MATCH DELETE
DATA ON SAVE
END
-RUN

-CHKHRPTINST
TABLE FILE HRPTINST
PRINT INST_KEY INTIME SET_KEY
&COND1
&COND2
ON TABLE SAVE 
END
-RUN
-IF &LINES EQ 0 THEN GOTO XXEXIT;
-READ SAVE &IKEY.7. &ITIME.18. &SETKEY.7.


MODIFY FILE HRPTINST
FIXFORM INST_KEY/7 INTIME/18 X7
MATCH * KEYS SEG RPT_INST
  ON MATCH DELETE
DATA ON SAVE
END
-RUN
MODIFY FILE HRPTFLDS
FIXFORM INST_KEY/7 INTIME/18 X7
MATCH * KEYS SEG RPT_INST
  ON MATCH DELETE
DATA ON SAVE
END
-RUN

-*Check remaining instances within the set
TABLE FILE HRPT_SET
PRINT SET_KEY INTIME
WHERE SET_KEY EQ &SETKEY
ON TABLE SAVE AS SAVE2
END

MODIFY FILE HRPT_SET
FIXFORM SET_KEY/7 INTIME/18
MATCH * KEYS SEG RPT_SET
  ON MATCH DELETE
DATA ON SAVE2
END
-RUN

-XXEXIT