-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File TKTLIST.FEX
-********************************************************************
-**************************************************************************
-*DATE           NAME               DESCRIPTION
-*10/22/01      TANDT-DB            COPIED FROM AVIW62
-*                                  CREATED FOR ISU FREQUENT FLYER REPORT
                                    STYLE SHEET PORTRAIT
-*4/29/02       TANDT SC DB         CHANGED MRK_TKT_CNT DEFINE TO CORRECTLY 
-*                                  BACK OUT PARTIAL REFUNDS AND EXCHANGES
-* ADDED TRANTYPE INCLUDE - JK 12/15/14 STORY ID S-06492
-*12/09/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels

-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT

  COMM_SEG/D12.2S = SEG_COM;
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC1/A80      = &&LDESC;
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT - SEG_COM ELSE 0;
  NEW_SEG_DISC/D12.2CS = SEG_DISC;
  NEW_SEG_MILES/D9 = SEG_MILES;
  REF_EXCH_CNT/D8.2CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  SEG_RSN/D12.2CS = SEG_REASON;
  TAX/D12.2S = SEG_TAX;
  TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  VASAVINGS/D12.2CS    = NEW_SEG_DISC - FARE_PAID;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
-* DEFINES ON PREVIOUS DEFINES
MISC_SAV/D12.2CS = SEG_RSN - FARE_PAID;

-INCLUDE TRANTYPE
-INCLUDE IDLEVDEFINE 
END
-RUN

TABLEF FILE &&EXTRACT
PRINT   
    AIR_KEY	
    AIR_MAIN.INTL_DOM
    AIRLINE    
    ARR_TIME
    BR_CL_IDX
    CITYPAIR.EMBARK AS 'EMB_APT_CD'
    CITYPAIR.ROUTE  AS 'RTE_APT_CD'
    CLASS	
    COMM_SEG	
    DOC_TYPE	
    DPT_TIME
    EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
    EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
    EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'	
    FARE_PAID	
    GSA_GTR
    INT_CODE	
    LEVEL_DESC
    MISC_SAV
    MRK_TKT_CNT
    NET_FARE
    NET_TKT_CNT	
    NEW_SEG_DISC	
    NEW_SEG_MILES
    REF_EXCH_CNT	
    REFUSE_KEY        	
    REFUSE_CODE        	
    RESV_BRANCH	
    RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
    RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
    RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'
    SEG_DESIG
    SEG_NBR	
    SEG_REASON
    SEG_RSN
    TAX
    TKT_NUM
    TKT_PURCH
    TKT_SORT
    TKT_TYPE
    TRN_DATE
    AIR_MAIN.VAL_AIRLINE AS 'VAL_AIR'	
    VASAVINGS	
    XDPT_DT	
    XTRAN_DT	
    XPSNGR_NM

-INCLUDE &AIRDATES

WHERE VOID_DATE EQ '0'

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD
END
-RUN


-*-EXIT
