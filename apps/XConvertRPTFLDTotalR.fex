-**************************************************************************************************
-* file name: ConvertRPTFLDTotalR.fex
-*   convert fields from ROLL1 segment in RPT_FLDS into SQL rpt_flds_total_r table.
-*   convert fields from ROLL2 segment in RPT_FLDS into SQL rpt_flds_total_r2 table.

-* SQL tables: rpt_flds_total_r and rpt_flds_total_r2
-*         
-**************************************************************************************************
-SET &ECHO = ALL;
SET ASNAME = ON

-* retrieve from roll1 segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT 
	ROLL1.INCL1 AS 'ROLLUP_OPERATOR'
	ROLL1.INCL2 AS 'BRANCH_OPERATOR'
	ROLL1.INCL3 AS 'RESV_BRANCH_OPERATOR'
	ROLL1.INCL4 AS 'TKT_BRANCH_OPERATOR'
	ROLL1.INCL5 AS 'CATEGORY_OPERATOR'
	ROLL1.INCL6 AS 'CLIENT_OPERATOR'
	OF_ACCT CTD
BY INST_KEY
WHERE ROLL1.ID GT 0 
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE RPT_FLDS_TOTALR
FIXFORM FROM HOLD
MATCH INST_KEY
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN

-* retrieve from roll2 segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT SEG ROLL2.SEQ_NO
BY INST_KEY
WHERE ROLL2.SEQ_NO GT 0
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE RPT_FLDS_TOTALR2
FIXFORM FROM HOLD
MATCH INST_KEY SEQ_NO
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN
