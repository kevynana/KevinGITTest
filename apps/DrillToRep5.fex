-* File DrillToRep.fex
SET ASNAMES=ON
-*SET TEMPERASE = OFF
JOIN AIR_KEY IN AYMETX TO ALL AIR_KEY IN SYMETX AS J1
END
DEFINE FILE AYMETX
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
MX_ADV_PUR/A8       = IF (ADV_PURCH LT 7) THEN '0 - 6' ELSE
                        IF (ADV_PURCH LT 14) THEN '07 - 13' ELSE
                        IF (ADV_PURCH LT 21) THEN '14 - 20' ELSE '21 +';
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
XTRAN_DT/MDYY        = TRN_DATE;
XAP/A8 = EDIT(ADV_PURCH);
MX_DAYS/A8 =  IF ADV_PURCH LT 21 THEN XAP ELSE MX_ADV_PUR;
END

TABLE FILE AYMETX
HEADING CENTER
"Advanced Purchase Detail"
"</3"
SUM FARE_PAID AS 'Fare,Paid' NET_TKT_CNT AS 'Ticket,Count'
BY MX_ADV_PUR NOPRINT
BY PASSNGR_NAME AS 'Passenger,Name'
BY TKT_SORT NOPRINT
BY TKT_NUM AS 'Ticket,Number'
BY XTRAN_DT AS 'Tran,Date'
BY MX_DAYS AS 'Days'
BY REFUSE_CODE AS 'Reason,Code'
WHERE MX_ADV_PUR EQ '&MX_ADV_PUR';
ON TABLE COLUMN-TOTAL
ON TABLE NOTOTAL
ON MX_ADV_PUR SUMMARIZE AS 'TOTAL FOR'
ON TABLE PCHOLD FORMAT PDF
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.500000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=HEADING, FONT=TIMES NEW ROMAN, SIZE=12, BACKCOLOR=NONE, STYLE=BOLD, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=TITLE, SIZE=9, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $

$
TYPE=DATA,
     GRID=OFF,
     FONT='TIMES NEW ROMAN',
     SIZE=10,
     COLOR='BLACK',
     BACKCOLOR='NONE',
     STYLE=NORMAL,
$

ENDSTYLE
END
