-**************************************************************************
-* DVRHTLDT.FEX
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-* 5/10/10 - changed Level1 to be handled like all other Levels - Deb
-*7/12/16 -DLV -  S-20630   Added EMP_ID to report  
-*03/09/17 - DLV - S-22486 - Updated IMAGE define to use server report is being ran from

-**************************************************************************
SET ASNAMES=ON
-RUN

-INCLUDE SETECHO
-SET &&FROMDT = EDIT(&&FROMDT,'9999999999');
-SET &&TODT = EDIT(&&TODT,'9999999999');
-SET &RPTTITLE = IF '&&ROLLUP.EVAL' NE 'OTHER-R' THEN   'Travel and Transport Hotel Detail Report for &&ROLLNAME.EVAL Account'
- ELSE 'Travel and Transport Hotel Detail Report for Client &&CLIENT.EVAL Account';

-IF &&DVRHTLRECS EQ 0 THEN GOTO NoHotelRec;

DEFINE FILE HTLHLD
NOWTOD/A8 WITH AIRPORT_CODE = HHMMSS (NOWTOD);
-*IMAGE/A80 = '<img src=HTTPS://www.ettekreview.com/asp/NEW_HDR.jpg>';
IMAGE/A80 = '<img src=' || '&&SRVR.EVAL' || '/asp/NEW_HDR.PNG>';
END
TABLE FILE HTLHLD
-*"&&SUBHEAD"
HEADING 
"<IMAGE>"
"</5"
"&RPTTITLE.EVAL"
"&&DTDESC.EVAL &&FROMDT - &&TODT " 

SUM BR_CL_IDX AS 'ACCT'
    LEVEL1 &&LV1
    PASSNGR_NAME AS 'TRAVELER'
    EMP_ID &EMPID
    INVOICE_DATE AS 'INVOICED'
    IN_DATE AS 'CHECK-IN'
    OUT_DATE AS 'CHECK-OUT'
    INVOICE_NUM AS 'INVOICE'
    DV_NUM AS 'GUARANTEED TO'
    PROP_NAME AS 'PROPERTY'
    PROP_CITY AS 'CITY'
    ROOM_TYPE AS 'ROOM TYPE'
    NUM_ROOMS AS 'UNITS'
    NNROOMS AS 'NIGHTS'
    ROOM_RATE AS 'RATE'
    NEW_TOTAL_AMT AS 'AMOUNT'
    LEVEL2 &&LV2 
    LEVEL3 &&LV3 
    LEVEL4 &&LV4  
    LEVEL5 &&LV5
    LEVEL6 &&LV6
    LEVEL7 &&LV7
BY BR_CL_IDX NOPRINT
BY PASSNGR_NAME NOPRINT 
BY INVOICE_DATE NOPRINT
BY IN_DATE NOPRINT
BY OUT_DATE NOPRINT
BY INVOICE_NUM NOPRINT
BY DV_NUM NOPRINT
BY PROP_NAME NOPRINT
ON TABLE COLUMN-TOTAL NUM_ROOMS NNROOMS NEW_TOTAL_AMT

-INCLUDE FOOTERDT

-*ON TABLE SET STYLE &&DTSTY
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=1.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=1.000000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=8, COLOR=BLACK, GRID=ON, STYLE=NORMAL,WRAP= OFF, $
TYPE=TITLE, FONT=TIMES NEW ROMAN, SIZE=9, BACKCOLOR=RGB(170 170 170), $
TYPE=FOOTING, SIZE=6, $
TYPE=TITLE, SIZE=9, STYLE=BOLD+UNDERLINE, $
TYPE=HEADING, SIZE=10, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=14, LINE=8, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=10, LINE=9, STYLE=BOLD, FONT=TIMES NEW ROMAN,$

-*TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(&&TNTTRPOS), SIZE=(&&TNTTRSIZ), $
-*TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLTRPOS), SIZE=(&&ROLTRSIZ), $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, COLOR=BLACK, BACKCOLOR=RGB(170 170 170),$
TYPE=REPORT, TITLETEXT=&DSC,$

ENDSTYLE
&&OUTLINE1
-*&&OUTLINE2
END
-RUN

-IF &FOCERRNUM EQ 205 OR &LINES EQ 0 THEN GOTO NoHotelRec;

-GOTO XXITHotel2;
-NoHotelRec
-SET &TITLETEXT = 'Hotel Detail  ';
-INCLUDE DVRNoRecord
-XXITHotel2