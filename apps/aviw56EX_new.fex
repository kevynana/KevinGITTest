-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File AIREXTRT.FEX
-********************************************************************
-* MODIFICAIONS:
-* 5/22/1998 ADDED TRN_DATE CRITERIA TO WHERE STATEMENTS - LP
-* 7/09/1998 ADDED A DEFINE FOR TICKETLESS MIR TRANSACTIONS - LP
-* 4/29/2002 CHANGED MRK_TKT_CNT DEFINE TO CORRECTLY BACK OUT PARTIAL
-*           REFUNDS AND EXCHANGES SC DB
-* 7/10/02   Changed Ibook define to reflect new standards
-*12/21/04   CHANGED BOOKING TYPE DEFINES TO INCLUDE ADDTL
-*           DOD_TYPE
-*10/11/05  ADDED DOD_TYPE H TO BOOKING TYPE DEFINES - DEB
-*1/23/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*11/4/14 Testing with hold files - JK
-*12/09/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*11/21/2016 RSB - S-23625 Create new standard hotel attachment report - Summary/Detail by Ticket
-*                          Added: HOTEL_FLAG
-*                          Created section for extracts: -HTL_ATTACH
-*12/27/2016 RSB - S-24104 Create new standard hotel attachment report - Summary/Detail by City
-*                          Added: SEG_HTL_FLAG
-*                          Created section for 'by city': -ABHTLCTY
-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-********************************************************************
-* -*SET &ECHO=ALL;


SET ASNAMES = ON
-SET HOLDATTR = ON;
-INCLUDE SETECHO

-SET &&IDLEVELBREAK = '';

-* -SET &RPARM = &&WEB_PATH || 'RPTPARMS.fex';

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN


DEFINE FILE &&EXTRACT

 BZNYCEMP/A1=IF LEVEL4 EQ '548716' OR '202929' OR '548524' OR '550015' OR '520237' OR '549046' OR '546467' OR
 '546770' OR '549289' OR '549625' OR '548247' OR '203303' OR '548509' OR '530064' OR '200235' OR '548492' OR 
 '525115' OR '516696' OR '509751' OR '548280' OR '550028' OR '549959' OR '549224' OR '549126' OR '530071' OR
 '549205' OR '550042' OR '531039' OR '530909' OR '529068' OR '549692' OR '546772' OR '549242' OR '529474' OR 
 '550000' OR '538382' OR '549754' OR '548327' OR '550031' OR '531012' OR '549900' OR '511092' OR '549647' OR 
 '529097' OR '549766' OR '531741' OR '549687' OR '550160' OR '548474' OR '524764' OR '549657' OR '544341' OR
 '549633' OR '550029' OR '548350' OR '539458' OR '528280' OR '548113' OR '550021' OR '549706' OR '549195' OR
 '301385' OR '206258' OR '206126' OR '550233' OR '546516' OR '538384' OR '549475' OR '506309' OR '546431' OR
 '547064' OR '546502' OR '530433' OR '549986' OR '549801' OR '528765' OR '549314' OR '546416' OR '535248' OR
 '546397' OR '549174' OR '548242' OR '302396' OR '303885' OR '549154' OR '549550' OR '550032' OR '548833' OR
 '546489' OR '549960' OR '538381' OR '548506' OR '503730' OR '549461' OR '548204' OR '531630' OR '517313' OR
 '546039' OR '549898' OR '549254' OR '544631' OR '528150' OR '549339' OR '548556' OR '549843' OR '511427' OR
 '204539' OR '546454' OR '548295' OR '550152' OR '200455' OR '538738' OR '548423' OR '550016' OR '521212' OR
 '201253' OR '548621' OR '517337' OR '525434' OR '205885' OR '538351' OR '540971' OR '504784' OR '201274' OR
 '546512' OR '201298' OR '549939' OR '549761' OR '549282' OR '550033' OR '549183' OR '549344' OR '522202' OR
 '548892' OR '546384' OR '549686' OR '549823' OR '300969' OR '527729' OR '205991' OR '543370' OR '548495' OR
 '548753' OR '531278' OR '302265' OR '549719' OR '538353' OR '549246' OR '525228' OR '526670' OR '002100' OR
 '542921' OR '526078' OR '548383' OR '523350' OR '548447' OR '547250' OR '549897' OR '526202' OR '548708' OR
 '548687' OR '549301' OR '521607' OR '528453' OR '546378' OR '550170' OR '525463' OR '530046' OR '549906' OR
 '548517' OR '549894' OR '549165' OR '546455' OR '541068' OR '548675' OR '548146' OR '549993' OR '549173' OR
 '548916' OR '530136' OR '541883' OR '550020' OR '550167' OR '303033' OR '549768' OR '530137' OR '512232' OR
 '539629' OR '543786' OR '544681' OR '548748' OR '502811' OR '304619' OR '521274' OR '526700' OR '546392' OR
 '549232' OR '549311' OR '550166' OR '532203' OR '544632' OR '549975' OR '544682' OR '550154' OR '538364' OR
 '207420' OR '542922' OR '548298' OR '546510' OR '205371' OR '548148' OR '549767' OR '546769' OR '548290' OR
 '501005' OR '548244' OR '531284' OR '515723' OR '538369' OR '305075' OR '546492' OR '548224' OR '548229' OR
 '549150' OR '026657' OR '548154' OR '028833' OR '549646' OR '548153' OR '527904' OR '549318' OR '521615' OR
 '549194' OR '544357' OR '548514' OR '532274' OR '548346' OR '301365' OR '549535' OR '207502' OR '548236' OR
 '541060' OR '543369' OR '546440' OR '548493' OR '548155' OR '525419' OR '538357' OR '548794' OR '549044' OR 
 '549278' OR '549901' OR '548067' OR '201026' OR '207094' OR '530569' OR '206835' OR '548342' OR '521673' OR
 '546511' OR '207641' OR '549775' OR '549284' OR '548301' OR '513489' OR '548674' OR '530371' OR '548525' OR
 '548140' OR '538354' OR '549995' OR '522109' OR '550041' OR '521392' OR '548256' OR '548141' OR '549296' OR
 '549309' OR '546443'  THEN 'Y' ELSE 'N';
 
 BZSTAY/D12 = IF RNDT_FLAG EQ 'N' THEN 0 ELSE STAY;
  
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  GROSS_FARE/D12.2CS  = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
  IBOOK/A42 = DOD_DESC;

  LEVEL_DESC1/A80      = &&LDESC;
  MIR_TYPE/A24 = DECODE AUX_NON_MIR ('X' 'X-DUAL MIR'
                                    'N' 'N-NON-TICKETING MIR'
                                    'J' 'J-NON-FARING MIR'
                                    'A' 'A-NON-ACCOUNTING MIR'
                                    'B' 'B-ACCOUNTING MIR'
                                    'H' 'H-REGULAR TICKET MIR'
                                    'V' 'V-VOID'
                                    'E' 'E-ELECTRONIC TKT NETWORK'
                                    'L' 'L-ELECTRONIC TKT MIR'
                                    ' ' ' -MANUALLY ENTERED TRANS');
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
-* NEW_STAY/D8 = STAY;                           
  NOHR_KEY/A2          = EDIT(NHR_KEY, '$$$$$$$$99');
-*  NOHTL_KEY/A10        = IF NOHR_KEY NE ' ' THEN NOHR_KEY ELSE 'NONE GIVEN'; 
  NOHTL_KEY/A40        = IF NOHR_KEY NE ' ' THEN NHR_DESC ELSE 'NONE GIVEN'; 
  TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' '; 
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  XARR_DOW/W           = ARR_DATE;
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
  XFST_DPT/MDYY = FST_DPT_DATE;
  XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');


-* ****DEFINES ON PREVIOUS DEFINES****

  ABOOK_CNT/D8CS = IF ONLINE_TRADITIONAL EQ 'TRADITIONAL' THEN NET_TKT_CNT ELSE 0;
ABOOK_FARE/D12.2CS = IF ONLINE_TRADITIONAL EQ 'TRADITIONAL' THEN FARE_PAID ELSE 0;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' 
     THEN FARE_PAID ELSE 0;
  ARR_DAY/A3           = DECODE XARR_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  DPT_DAY/A3           = DECODE XDPT_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  IBOOK_CNT/D8CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN NET_TKT_CNT ELSE 0;
  IBOOK_FARE/D12.2CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN FARE_PAID ELSE 0;
  TRIPS/P8CS = IF STAY GT 0 AND NET_TKT_CNT GT 0 THEN 1 ELSE 0;
-*               IF NET_TKT_CNT LT 0 THEN 0 ELSE 0;
-*               IF STAY LT 0 THEN (-1) ELSE 0;
               
-INCLUDE TRANTYPE               
-INCLUDE IDLEVDEFINE 


END
-RUN


TABLE FILE &&EXTRACT
PRINT 
  ABOOK_CNT
  ABOOK_FARE
  AGENT_NUM 
  AIR_MAIN.INTL_DOM 
  AIRLINE
  ARR_DAY   
  ARR_TIME
  BZSTAY
  DOD_PAX_CAT   
  DPT_DAY   
  DPT_TIME  
  DST_APC
  EMBARK
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  FARE_PAID  
  FLIGHT
  HOTEL_FLAG
  IBOOK
  IBOOK_CNT
  IBOOK_FARE
  INVOICE_NUM
  LEVEL_DESC    
  LEVEL1    
  LEVEL2    
  LEVEL3   
  LEVEL4
  NET_TKT_CNT
  NOHR_KEY
  NOHTL_KEY
  PASSNGR_NAME
  PNR_LOCATOR
  RNDT_FLAG
  ROUTE
  ROUTEX
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'  
  RTE_CTY.RTE_CITY AS 'CITY_CODE'
  RTE_CTY.STATE_CODE AS 'STATE_CODE'
  RTE_CTY.COUNTRY_CODE AS 'COUNTRY_CODE'
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM' 
  SEG_HTL_FLAG
  SEG_NBR   
  SEG_STANDARD  
  STAY  
  STAY AS 'NEW_STAY'
  STOP_CON
  TICKET_NMBR
  TKT_NUM   
  TKT_SORT  
  TKT_TYPE  
  TRN_DATE  
  TRIPS
  XARR_DT   
  XDPT_DT   
  XPSNGR_NM 
  XFST_DPT
  XTKT_SRT
  XTRAN_DT
-INCLUDE BYHIERARCHY
-INCLUDE &AIRDATES

WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS AIRHLD
END
-RUN
-*
-IF &&SETF EQ 'ABHTLTKT' OR 'ABHTLCTY' GOTO HTL_ATTACH;
-IF &&SETF NE 'PHYNH'    GOTO NOPHY;

TABLE FILE &&EXTRACT
SUM NET_TKT_CNT STAY/D8 AS 'NEW_STAY'
    HOTEL_FLAG
BY PASSNGR_NAME
BY TKT_NUM
WHERE VOID_DATE EQ 0
-INCLUDE &AIRDATES
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS AIRHLD1A
END
-RUN

TABLE FILE AIRHLD1A
SUM NET_TKT_CNT NEW_STAY
    HOTEL_FLAG
BY PASSNGR_NAME
BY TKT_NUM
WHERE NEW_STAY NE 0
ON TABLE HOLD AS AIRHLD1AA
END
-RUN

-GOTO XXIT;

-**********
-HTL_ATTACH
-**********
-*
-* Get tickets that have overnight stay
TABLE FILE AIRHLD
SUM STAY        NOPRINT
BY LEVEL_DESC   NOPRINT 
BY PASSNGR_NAME NOPRINT
BY TKT_NUM
BY TRN_DATE     NOPRINT
WHERE TOTAL STAY NE 0;
WHERE EMBARK OMITS 'DT1';
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS TKT_HOLD
END
-RUN
-*
-IF &&SETF EQ 'ABHTLCTY' GOTO ABHTLCTY;
-*********
-ABHTLTKT
-*********
-*
-* Get data for summary report
DEFINE FILE AIRHLD
 HTL_ATTCH  /I9 = IF (NET_TKT_CNT GT 0) AND (HOTEL_FLAG EQ 'Y') THEN 1 ELSE 0;
 HTL_NOATTCH/I9 = IF (NET_TKT_CNT GT 0) AND (HOTEL_FLAG EQ 'N') THEN 1 ELSE 0;
END
-*
TABLE FILE AIRHLD
PRINT NET_TKT_CNT
      HOTEL_FLAG
      STAY
      HTL_ATTCH
      HTL_NOATTCH
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_NUM
BY TRN_DATE
WHERE TKT_NUM IN FILE TKT_HOLD;
ON TABLE HOLD AS AIRHLDH1
END
-RUN
-*
-* Get data for detail report
TABLE FILE AIRHLD
PRINT AIRLINE  
      DPT_TIME  
      EMB_APT_NM
      FLIGHT
      HOTEL_FLAG
      INVOICE_NUM
      LEVEL_DESC    
      NET_TKT_CNT
      NOHR_KEY
      NOHTL_KEY
      PASSNGR_NAME
      RTE_APT_NM
      STAY  
      TKT_NUM   
      TKT_SORT  
      TKT_TYPE  
      TRN_DATE  
      XARR_DT   
      XDPT_DT   
      XPSNGR_NM 
      XFST_DPT
      XTKT_SRT
      XTRAN_DT
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_NUM
BY TRN_DATE
WHERE TKT_NUM IN FILE TKT_HOLD;
ON TABLE HOLD AS AIRHLDH
END
-RUN
-*
-GOTO XXIT;
-*********
-ABHTLCTY
-*********
-*
-* Get data for summary report
TABLE FILE AIRHLD
PRINT SEG_HTL_FLAG
      STOP_CON
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_NUM
BY RTE_CTY_NM
WHERE TKT_NUM IN FILE TKT_HOLD;
WHERE STAY NE 0;
ON TABLE HOLD AS AIRHLDCTYS
END
-RUN
-*
-* Get data for detail report
TABLE FILE AIRHLD
PRINT AIRLINE
      ARR_TIME
      DPT_TIME  
      EMB_APT_NM
      FLIGHT
      INVOICE_NUM
      LEVEL_DESC    
      NOHR_KEY
      NOHTL_KEY
      PASSNGR_NAME
      RTE_CTY_NM
      SEG_HTL_FLAG
      STAY  
      STOP_CON
      TKT_NUM   
      TRN_DATE  
      XDPT_DT   
      XPSNGR_NM 
      XTRAN_DT
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_NUM
BY TRN_DATE
WHERE TKT_NUM IN FILE TKT_HOLD;
WHERE STAY NE 0;
ON TABLE HOLD AS AIRHLDCTYD 
END
-RUN
-*
-GOTO XXIT;

-NOPHY;
TABLE FILE AIRHLD
SUM STAY XPSNGR_NM 
BY LEVEL_DESC
BY LEVEL1
BY TKT_NUM
BY NOHTL_KEY
BY NOHR_KEY
BY RTE_CTY_NM
WHERE STAY NE 0
ON TABLE HOLD AS AIRHLD2
END
-RUN




-TYPE "AFTER EXTRACT"
-XXIT;