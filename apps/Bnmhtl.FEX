-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 1/24/05 - DEB - ADDED CODE FOR GLOBAL CURRENCY
-* 3/31/14 - JOY - ADDED LOGIC FOR BV_LSV
-*  03/31/2015  REJ  S-05681 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-*12/29/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*4/20/2017-JEM-S-33318-updated logic for no records report
 
 
SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';

-INCLUDE IDLEVROLLDEF
-RUN
 
DEFINE FILE &&EXTRACT
 
  LEVEL_DESC1/A80  = &&LDESC;
  NBR_DAYS/D12=NUM_DAYS;
  NBR_ROOMS/D12 = NUM_ROOMS;
  NEW_STD_RATE/D12.2C = STD_RATE;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
 
 TRAN_MY/MTYY = INVOICE_DATE;
 -* Defines on previous
 TOTAL_STD/D12.2C = IF (NNROOMS LT 0) AND (NEW_STD_RATE LT 0)
                      THEN ((NNROOMS * NEW_STD_RATE)*(-1))
                     ELSE NNROOMS * NEW_STD_RATE;
 
TOTAL_SLSAV/D12.2C = TOTAL_STD - NEW_TOTAL_AMT;
 
BV_SAV/D12.2C = IF REFUSE_CODE EQ 'A' OR 'P' OR 'R' OR 'T' THEN TOTAL_SLSAV ELSE 0;
 
 
-INCLUDE IDLEVDEFINE 
 
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT AROLL_DSC3
  BV_SAV
  CURR_DATE
  INVOICE_DATE
  LEVEL_DESC
  NBR_DAYS
  NNROOMS
  NEW_TOTAL_AMT
  TRAN_MY
-IF &&FROMAVRP EQ 'Y' GOTO :BH1;
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') OR
      (INVOICE_DATE GE '&&FXDATE.EVAL') AND (INVOICE_DATE LE '&&TXDATE.EVAL')
-GOTO :BH1_CONT;
-:BH1
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL) AND (INVOICE_DATE LE &&TXDATE.EVAL)
-:BH1_CONT
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
-* &&WHERE1
-* &&WHERE2
-* &&WHERE3
-* &&WHERE4
-* &&WHERE5
-* &&WHERE6
-* &&WHERE7
-* &&WHERE8
&&WHERE9
&&WHERE10
 
-INCLUDE RPTPARMS
ON TABLE HOLD AS BNMHHOLD
END
-RUN
 
 
 
TABLE FILE BNMHHOLD
PRINT AROLL_DSC3
   BV_SAV
   INVOICE_DATE
  LEVEL_DESC
  NBR_DAYS
  NNROOMS
  NEW_TOTAL_AMT
  TRAN_MY
BY CURR_DATE
ON TABLE HOLD AS BNMHHLD FORMAT FOCUS INDEX CURR_DATE
END
-RUN
 
-* Global Reporting JOIN
JOIN BNMHHLD.CURR_DATE IN BNMHHLD TO GLOBAL.DATE IN GLOBAL AS J1
END
 
DEFINE FILE BNMHHLD
NAMEC/A30 = CNAME;
 
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;
 
-* DEFINES ON PREVIOUS DEFINES
AMT_TOTALX/D20.6 = NEW_TOTAL_AMT * CURR1X;
 
END
 
TABLE FILE BNMHHLD
PRINT AROLL_DSC3
 BV_SAV
 CURR_DATE
 INVOICE_DATE
  LEVEL_DESC
  NBR_DAYS
  NNROOMS
  NEW_TOTAL_AMT
  TRAN_MY
  NAMEC
  AMT_TOTALX/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HTLHLD
END
-RUN
 
 
DEFINE FILE HTLHLD
-IF &&FROMAVRP EQ 'Y' GOTO :BH2;
CURRENT/A1 = IF INVOICE_DATE GE '&&FROMDT.EVAL' AND
                INVOICE_DATE LE '&&TODT.EVAL' THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE '&&FXDATE.EVAL' AND
               INVOICE_DATE LE '&&TXDATE.EVAL' THEN 'Y' ELSE 'N';
-GOTO :BH2_CONT;
-:BH2
CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE &&FXDATE.EVAL AND
               INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
-:BH2_CONT
END
 
TABLE FILE HTLHLD
PRINT AROLL_DSC3
  BV_SAV
  CURRENT
  NAMEC
  NBR_DAYS
  NNROOMS
  NEW_TOTAL_AMT
  PRIOR
  AMT_TOTALX/D16.2CS
-* BY TRAN_MY
BY LEVEL_DESC
ON TABLE HOLD AS HTLHLD1
END
-RUN
 
DEFINE FILE HTLHLD1
CHTTLA/D12.2CS = IF CURRENT EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
GCHTTLA/D16.2CS = IF CURRENT EQ 'Y' THEN AMT_TOTALX ELSE 0;
CHTTLN/P8CS = IF CURRENT EQ 'Y' THEN NNROOMS ELSE 0;
CBVSV/D12.2CS = IF CURRENT EQ 'Y' THEN BV_SAV ELSE 0;
END
 
TABLE FILE HTLHLD1
SUM AROLL_DSC3
    CBVSV
    CHTTLA
    CHTTLN
    GCHTTLA
-* BY TRAN_MY
BY LEVEL_DESC
ON TABLE HOLD AS HTLHLD2
END
-RUN
 
TABLE FILE HTLHLD2
SUM CHTTLA
    CBVSV
    GCHTTLA
COMPUTE AVGRATE/D12.2CS = IF ((CHTTLN LT 1) AND (CHTTLN GT (-1))) THEN 0 ELSE
                             (CHTTLA/CHTTLN);
        GAVGRATE/D16.2CS = IF ((CHTTLN LT 1) AND (CHTTLN GT (-1))) THEN 0 ELSE
                             (GCHTTLA/CHTTLN);
-* BY TRAN_MY
BY LEVEL_DESC
-* ON TABLE HOLD AS HTLHLD3 FORMAT FOCUS INDEX TRAN_MY
ON TABLE HOLD AS HTLHLD3 FORMAT FOCUS INDEX LEVEL_DESC
END
-RUN
 
-SET &&HTLREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';
 
 
 
 
