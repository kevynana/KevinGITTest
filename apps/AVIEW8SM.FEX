-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File APSMRY.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR
-*                                  DRILLABLE REPORTS.
-*1/22/02      TANDT-DB             CARRIED THROUGH VAL_AIRLINE
-*6/17/02      STEVE                ADDED TTLBDAYS - ADVANCE BOOKING DAYS
-*6/19/02      STEVE                ADDED REC_CNT TO CORRECT AVERAGE
-*                                  BOOKING DAYS AND PURCHASE DAYS
-*6/25/02      STEVE                REC_CNT DOES NOT INCLUDE REFUNDS/EXCHANGES
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*07/31/02     DEB                  CARRIED THROUGH IFLAG
-*4/16/04    Joy                Carried Through KW_ADV_PURCH Define
-*8/18/04    Joy                Carried through trip_purpose
-*06/23/06   STEVE              Added code for BWTRIP (employees >= 50 days away)
-*06/26/06   STEVE              Added code for BWTRIP2 (employes with < 50 days away) 
-*7/18/13    Joy                Added code for BVTRIP1
-*06/02/14   Deb                Carried through STOP_CON added routine for 
-*                              RFSTOPSM
-*12/02/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*07/20/16   RSB   S-20630 New Employee ID database field
-*                          Added EMP_ID
-* 10/17/17 RSB S-41055 Added Emp ID to Summary only.
-**********************************************************************
SET ASNAMES = ON

-INCLUDE SETECHO

TABLE FILE &&RPT_HOLD

SUM
    DISSAVINGS AS 'DISSAV'
    FARE_PAID/D12.2CS AS 'TFARE'
    NET_TKT_CNT AS 'NTTKT'
    REF_EXCH_CNT AS 'TRFEX'
    SAVINGS AS 'TSAVE'
    TKT_PURCH AS 'TTKT'
BY LEVEL_DESC
ON TABLE HOLD AS HOLDONE
END
-RUN


TABLE FILE &&RPT_HOLD
PRINT
  BKD_ONLINE
  C_ITIN
  CLIENT_NAME
  DISSAVINGS
  FARE_PAID
  FLAG
  GRP_ADV_PUR
  IFLAG
  INTL_DOM
  EMP_ID
  LEVEL1
  LEVEL2
  LEVEL3
  LVSEL
  KM_DAYS
  KM_ADV_PUR
  KW_ADV_PUR
  MC_ADV_BOOK
  MRK_TKT_CNT
  MX_ADV_PUR
  MC_DAYS
  MX_DAYS
  NET_TKT_CNT
  NEW_ADV_PUR
  NEW_ADV_BOOK
  NEW_TRPLEN
  NONREF_FLAG
  NX_RANGE
  REF_EXCH_CNT
  RTE_APT_NM
  SEG_AMT
  SAVINGS
  STOP_CON
  TKT_NUM
  TKT_PURCH
  TRP_LEN
  TRIP_LENGTH
  TRIP_PURPOSE
  TRP_RANGE
  UP_ADV_PUR
  UP_DAYS
  VAL_AIRLINE
  XPSNGR_NM
BY LEVEL_DESC
BY TRP_RANGE
ON TABLE HOLD AS XTWO
END
-RUN

-IF '&&SETF.EVAL' EQ 'RFSTOPSM' THEN GOTO STOPSUM;

MATCH FILE XTWO
PRINT
  BKD_ONLINE
  C_ITIN
  CLIENT_NAME
  DISSAVINGS
  FARE_PAID
  FLAG
  GRP_ADV_PUR
  IFLAG
  INTL_DOM
  KM_DAYS
  KM_ADV_PUR
  KW_ADV_PUR
  EMP_ID
  LEVEL1
  LEVEL2
  LEVEL3
  LVSEL
  NONREF_FLAG
  MC_ADV_BOOK
  MRK_TKT_CNT
  MX_ADV_PUR
  MC_DAYS
  MX_DAYS
  NET_TKT_CNT
  NEW_ADV_BOOK
  NEW_ADV_PURCH
  NEW_TRPLEN
  NX_RANGE
  REF_EXCH_CNT
  RTE_APT_NM
  SAVINGS
  SEG_AMT
  STOP_CON
  TKT_NUM
  TKT_PURCH
  TRP_LEN
  TRIP_LENGTH
  TRIP_PURPOSE
  TRP_RANGE
  UP_ADV_PUR
  UP_DAYS
  VAL_AIRLINE
  XPSNGR_NM
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE HOLDONE
SUM TTKT
    TRFEX
    NTTKT
    DISSAV
    TSAVE
    TFARE
BY LEVEL_DESC
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW
END
-RUN

-GOTO CONTrpt;

-STOPSUM
DEFINE FILE XTWO
 
FLAG_NON/I3 = IF STOP_CON EQ 'O' THEN 1 ELSE 0;
FLAG_CONX/I3 = IF STOP_CON EQ 'X' THEN 1 ELSE 0;

END                           
TABLE FILE XTWO
SUM STOP_CON FLAG_NON FLAG_CONX C_ITIN NET_TKT_CNT
BY LEVEL_DESC
BY TKT_NUM
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 

DEFINE FILE AIRHOLD3
NON_FLAG/A1 = IF FLAG_NON GT 0 AND FLAG_CONX EQ 0 THEN 'Y' ELSE 'N';
NSFLAG/A13 = IF NON_FLAG EQ 'Y' THEN 'NON-STOP' ELSE 'CONNECTION';
END
TABLE FILE AIRHOLD3
PRINT NON_FLAG NSFLAG C_ITIN
BY LEVEL_DESC
BY TKT_NUM
ON TABLE HOLD AS AIRHOLD4
END
-RUN


MATCH FILE XTWO
PRINT
  BKD_ONLINE
  C_ITIN
  CLIENT_NAME
  DISSAVINGS
  FARE_PAID
  FLAG
  GRP_ADV_PUR
  IFLAG
  INTL_DOM
  KM_DAYS
  KM_ADV_PUR
  KW_ADV_PUR
  EMP_ID
  LEVEL1
  LEVEL2
  LEVEL3
  LVSEL
  NONREF_FLAG
  MC_ADV_BOOK
  MRK_TKT_CNT
  MX_ADV_PUR
  MC_DAYS
  MX_DAYS
  NET_TKT_CNT
  NEW_ADV_BOOK
  NEW_ADV_PURCH
  NEW_TRPLEN
  NX_RANGE
  REF_EXCH_CNT
  RTE_APT_NM
  SAVINGS
  SEG_AMT
  STOP_CON
  TKT_NUM
  TKT_PURCH
  TRP_LEN
  TRIP_LENGTH
  TRIP_PURPOSE
  TRP_RANGE
  UP_ADV_PUR
  UP_DAYS
  VAL_AIRLINE
  XPSNGR_NM
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE HOLDONE
SUM TTKT
    TRFEX
    NTTKT
    DISSAV
    TSAVE
    TFARE
BY LEVEL_DESC
AFTER MATCH HOLD AS XONE OLD-OR-NEW
END
-RUN

MATCH FILE XONE
PRINT
  BKD_ONLINE
  C_ITIN
  CLIENT_NAME
  DISSAVINGS
  FARE_PAID
  FLAG
  GRP_ADV_PUR
  IFLAG
  INTL_DOM
  KM_DAYS
  KM_ADV_PUR
  KW_ADV_PUR
  EMP_ID
  LEVEL1
  LEVEL2
  LEVEL3
  LVSEL
  NONREF_FLAG
  MC_ADV_BOOK
  MRK_TKT_CNT
  MX_ADV_PUR
  MC_DAYS
  MX_DAYS
  NET_TKT_CNT
  NEW_ADV_BOOK
  NEW_ADV_PURCH
  NEW_TRPLEN
  NX_RANGE
  REF_EXCH_CNT
  RTE_APT_NM
  SAVINGS
  SEG_AMT
  STOP_CON
  TKT_NUM
  TKT_PURCH
  TRP_LEN
  TRIP_LENGTH
  TRIP_PURPOSE
  TRP_RANGE
  UP_ADV_PUR
  UP_DAYS
  VAL_AIRLINE
  XPSNGR_NM
  TTKT
  TRFEX
  NTTKT
  DISSAV
  TSAVE
  TFARE
BY LEVEL_DESC
BY TKT_NUM
ON TABLE HOLD
RUN
FILE AIRHOLD4
SUM  NON_FLAG NSFLAG
BY LEVEL_DESC
BY TKT_NUM
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW
END
-RUN



-CONTrpt


DEFINE FILE XTHREE
  PCTDSAVE/D8.1 = IF ((DISSAV LT 1) AND (DISSAV GT (-1))) THEN 0
                  ELSE ((DISSAVINGS/DISSAV)*100);
  PCTFARE/D8.1S = IF ((TFARE LT 1) AND (TFARE GT (-1))) THEN 0
                  ELSE ((FARE_PAID/TFARE)*100);
  PCTNETTKT/D8.2 = IF ((NTTKT LT 1) AND (NTTKT GT (-1))) THEN 0
                  ELSE ((NET_TKT_CNT/NTTKT)*100);
  PCTRFEX/D8.1S = IF ((TRFEX LT 1) AND (TRFEX GT (-1))) THEN 0
                  ELSE ((REF_EXCH_CNT/TRFEX)*100);
  PCTSAVE/D8.1 = IF ((TSAVE LT 1) AND (TSAVE GT (-1))) THEN 0
                  ELSE ((SAVINGS/TSAVE)*100);
  PCTTKT/D8.1S = IF ((TTKT LT 1) AND (TTKT GT (-1))) THEN 0
                 ELSE ((TKT_PURCH/TTKT)*100);
 
                 
-*   REC_CNT/D8CS = IF NET_TKT_CNT NE 0 THEN 1 ELSE 0;
  REC_CNT/D8CS = IF NET_TKT_CNT EQ 1 THEN 1 ELSE 0;
  TTLDAYS/D8 = NEW_ADV_PURCH*REC_CNT;
  TTLBDAYS/D8 = NEW_ADV_BOOK*REC_CNT;
  NEW_TRPLEN = TRIP_LENGTH*NET_TKT_CNT;
  NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END

-IF &&SETF NE 'BWTRIP' OR 'BWTRIP2' OR 'BVTRIP50' OR 'BVTRP50U' THEN GOTO AROUNDD;

TABLE FILE XTHREE
SUM
  NET_TKT_CNT
  NEW_TRPLEN
  EMP_ID
  LEVEL1
  LVSEL
BY LEVEL_DESC
BY XPSNGR_NM
 
ON TABLE HOLD AS XTHREE1
END

-IF &&SETF EQ 'BWTRIP' OR 'BVTRIP50' THEN GOTO AROUNDB;

DEFINE FILE XTHREE1
PASOVR50/A15 = IF NEW_TRPLEN LT 50 THEN XPSNGR_NM ELSE 'ALL OTHER';
END

-GOTO AROUNDC

-AROUNDB

DEFINE FILE XTHREE1
PASOVR50/A15 = IF NEW_TRPLEN GE 50 THEN XPSNGR_NM ELSE 'ALL OTHER';
END

-AROUNDC

TABLE FILE XTHREE1
PRINT NET_TKT_CNT
    NEW_TRPLEN
    EMP_ID
    LEVEL1
    LVSEL
BY LEVEL_DESC
BY PASOVR50
ON TABLE HOLD AS XTHREE2
END
-RUN

DEFINE FILE XTHREE2
FLAG/A1 = IF PASOVR50 EQ 'ALL OTHER' THEN 'B' ELSE 'A';
END

TABLE FILE XTHREE2
PRINT NET_TKT_CNT
      NEW_TRPLEN
      PASOVR50 AS 'XPSNGR_NM'
      LEVEL_DESC
      LVSEL
      EMP_ID
      LEVEL1
BY FLAG 
ON TABLE HOLD AS XTHREE
END
-RUN

DEFINE FILE XTHREE
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
XPSNGR_ID/A15 = IF XPSNGR_NM EQ 'ALL OTHER' THEN ' ' ELSE EMP_ID;
END

-AROUNDD


-IF '&&SETF.EVAL' NE 'BVTRIP50' OR 'BVTRP50U' OR 'BVTRIP1' THEN GOTO Finrpt;

-SET &&BREAKOPTION = 'ON LVSEL RECOMPUTE MULTILINES AS ''TOTAL FOR''';


-Finrpt
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN

TABLE FILE XTHREE
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10

-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
&&BREAKOPTION
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10

-INCLUDE FOOTERSM
ON TABLE COLUMN-TOTAL
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**********************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS

-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR
-*                                  DRILLABLE REPORTS.
-**********************************************************************
-IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;

-IF &&SETF EQ 'STTRIP' THEN GOTO SKIP_DRILL;

-INCLUDE &&DSTY

-SKIP_DRILL

ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN


-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;

EX REPORT_3

-SKIP_R3

