-* File push_weekly.fex
-INCLUDE SETECHO
SET BASEURL='&&SRVR.EVAL'
SET EMPTYREPORT=ON
SET BYDISPLAY=ON
 
-INCLUDE CLASSPATH
 
-SET &&WPATH1=TEMPPATH(50,'A50');
-SET &&JPATH=STRREP(50,'&&WPATH1.EVAL',1,'\',2,'\\',150,'A150');
 
-*-SET &TDATE=&YYMD;
-SET &TDATE=20080410;
 
-* Get Reports where the Total Set was scheduled
-*
JOIN CLEAR *
JOIN SET_KEY IN PUSH_REPORTS TO ALL SET_KEY IN RPT_INST AS J1
JOIN ROLLUP_CODE IN PUSH_REPORTS TO ROLLUP_CODE IN ROLLUP AS J2
-RUN
 
DEFINE FILE PUSH_REPORTS CLEAR
OUTTO/A2='TC';
-* Today's Date
TDATE1/YYMD=&TDATE;
P_MONTH/MDYY=TDATE1;
P_BOW/MDYY=DATEMOV(P_MONTH, 'BOW');
P_EOW/MDYY=DATEMOV(P_MONTH, 'EOW');
 
AFM_DATE/MDYY=DATEADD(P_BOW, 'D', (-14));
ATO_DATE/MDYY=DATEADD((P_EOW + 2), 'D', (-14));
AFX_DATE/MDYY=DATEADD(AFM_DATE, 'D', (-7));
ATX_DATE0/MDYY=DATEADD(ATO_DATE, 'D', (-7));
ATX_DATE/MDYY=ATX_DATE0;
 
ACMFM_DATE/MDYY=AFM_DATE;
ACMTO_DATE/MDYY=ATO_DATE;
APMFM_DATE/MDYY=AFX_DATE;
APMTO_DATE/MDYY=ATX_DATE;
ACYFM_DATE/MDYY=AFM_DATE;
ACYTO_DATE/MDYY=ATO_DATE;
APYFM_DATE/MDYY=AFM_DATE;
APYTO_DATE/MDYY=ATO_DATE;
 
EXFEX/A10=IF RPT_TYPE EQ 'QTR' THEN 'RUN_QTR' ELSE 'WRUNINDV';
RPT_NAMEX/A60='Weekly-' || RPT_NAME;
END
TABLE FILE PUSH_REPORTS
PRINT SET_KEY
      RPT_INST.INST_KEY
      OUTTO
      OUTPUT_FORMAT
      DATETYPE
      RPT_NAMEX AS RPT_NAME
      AFM_DATE
      ATO_DATE
      AFX_DATE
      ATX_DATE
      ACMFM_DATE
      ACMTO_DATE
      APMFM_DATE
      APMTO_DATE
      ACYFM_DATE
      ACYTO_DATE
      APYFM_DATE
      APYTO_DATE
      EXFEX
      ROLLUP_CODE
      PUSH_EADDRS
BY SCHED_OWNER
WHERE SCHED_W EQ '1'
WHERE PUSH_REPORTS.INST_KEY EQ 0
 
WHERE ROLLUP.ROLLUP.END_DATE GE ATO_DATE AND
      PUSH_REPORTS.LAST_WEEKLY_PROCESS_DATE LT ATO_DATE
 
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS HLDWS FORMAT FOCUS
END
-RUN
 
JOIN CLEAR *
JOIN INST_KEY IN PUSH_REPORTS TO INST_KEY IN RPT_INST AS J1
JOIN ROLLUP_CODE IN PUSH_REPORTS TO ROLLUP_CODE IN ROLLUP AS J2
-RUN
 
DEFINE FILE PUSH_REPORTS CLEAR
OUTTO/A2='TC';
-* Today's Date
TDATE1/YYMD=&TDATE;
P_MONTH/MDYY=TDATE1;
P_BOW/MDYY=DATEMOV(P_MONTH, 'BOW');
P_EOW/MDYY=DATEMOV(P_MONTH, 'EOW');
 
AFM_DATE/MDYY=DATEADD(P_BOW, 'D', (-14));
ATO_DATE/MDYY=DATEADD((P_EOW + 2), 'D', (-14));
AFX_DATE/MDYY=DATEADD(AFM_DATE, 'D', (-7));
ATX_DATE0/MDYY=DATEADD(ATO_DATE, 'D', (-7));
ATX_DATE/MDYY=ATX_DATE0;
 
ACMFM_DATE/MDYY=AFM_DATE;
ACMTO_DATE/MDYY=ATO_DATE;
APMFM_DATE/MDYY=AFX_DATE;
APMTO_DATE/MDYY=ATX_DATE;
ACYFM_DATE/MDYY=AFM_DATE;
ACYTO_DATE/MDYY=ATO_DATE;
APYFM_DATE/MDYY=AFM_DATE;
APYTO_DATE/MDYY=ATO_DATE;
 
EXFEX/A10=IF RPT_TYPE EQ 'QTR' THEN 'RUN_QTR' ELSE 'WRUNINDV';
RPT_NAMEX/A60='Weekly-' || RPT_NAME;
END
-* Get list of Weekly Scheduled Reports
TABLE FILE PUSH_REPORTS
PRINT SET_KEY
      INST_KEY
      OUTTO
      OUTPUT_FORMAT
      DATETYPE
      RPT_NAMEX AS RPT_NAME
      AFM_DATE
      ATO_DATE
      AFX_DATE
      ATX_DATE
      ACMFM_DATE
      ACMTO_DATE
      APMFM_DATE
      APMTO_DATE
      ACYFM_DATE
      ACYTO_DATE
      APYFM_DATE
      APYTO_DATE
      EXFEX
      ROLLUP_CODE
      PUSH_EADDRS
BY SCHED_OWNER
WHERE SCHED_W EQ '1'
 
WHERE ROLLUP.ROLLUP.END_DATE GE ATO_DATE AND
      PUSH_REPORTS.LAST_WEEKLY_PROCESS_DATE LT ATO_DATE
 
ON TABLE HOLD AS HLDW FORMAT FOCUS
END
 
-* Put the files together and eliminate duplicates based on the
-* date ranges the reports are running for.
-*
USE CLEAR *
USE
HLDW.FOC AS HLDW
HLDWS.FOC AS HLDW
END
-RUN
 
TABLE FILE HLDW
SUM
      OUTTO
      OUTPUT_FORMAT
      DATETYPE
      RPT_NAME
      AFM_DATE
      ATO_DATE
      AFX_DATE
      ATX_DATE
      ACMFM_DATE
      ACMTO_DATE
      APMFM_DATE
      APMTO_DATE
      ACYFM_DATE
      ACYTO_DATE
      APYFM_DATE
      APYTO_DATE
      EXFEX
      ROLLUP_CODE
      PUSH_EADDRS
BY SCHED_OWNER
BY SET_KEY
BY INST_KEY
BY AFM_DATE NOPRINT
BY ATO_DATE NOPRINT
BY AFX_DATE NOPRINT
BY ATX_DATE NOPRINT
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS RCPARMS1
END
-RUN
 
TABLE FILE RCPARMS1
BY SCHED_OWNER
BY ROLLUP_CODE
BY PUSH_EADDRS
ON TABLE HOLD AS RCPARMS2 FORMAT ALPHA
END
-RUN
 
-SET &NSCHEDS = &LINES;
 
-REPEAT SLOOP &NSCHEDS TIMES
 
-READ RCPARMS2 NOCLOSE &SOWN.A10, &RCODE.A8, &EADDRS.A512
 
-TYPE &RCODE
-TYPE &EADDRS
TABLE FILE RCPARMS1
SUM
      OUTTO
      OUTPUT_FORMAT
      DATETYPE
      RPT_NAME
      AFM_DATE
      ATO_DATE
      AFX_DATE
      ATX_DATE
      ACMFM_DATE
      ACMTO_DATE
      APMFM_DATE
      APMTO_DATE
      ACYFM_DATE
      ACYTO_DATE
      APYFM_DATE
      APYTO_DATE
      EXFEX
      ROLLUP_CODE
      PUSH_EADDRS
BY SCHED_OWNER
BY SET_KEY
BY INST_KEY
BY ATO_DATE NOPRINT
BY AFX_DATE NOPRINT
BY ATX_DATE NOPRINT
BY ACMFM_DATE NOPRINT
BY ACMTO_DATE NOPRINT
BY APMFM_DATE NOPRINT
BY APMTO_DATE NOPRINT
BY ACYFM_DATE NOPRINT
BY ACYTO_DATE NOPRINT
BY APYFM_DATE NOPRINT
BY APYTO_DATE NOPRINT
WHERE SCHED_OWNER EQ '&SOWN';
WHERE ROLLUP_CODE EQ '&RCODE';
WHERE PUSH_EADDRS EQ '&EADDRS';
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS RCPARMS FORMAT ALPHA
END
-RUN
 
-SET &IFILE='&&JPATH.EVAL' || 'rcparms.ftm';
-RUN
 
-DOS java -cp "&CP.EVAL" com.tandt.rcaster.schedule.EtekPush "&IFILE.EVAL"
-RUN
-SLOOP
 
-SET &TDAY=EDIT(&MDYY,'99/99/9999');
 
TABLE FILE RCPARMS1
" "
"Weekly Push Reports that were Scheduled"
"for &TDAY"
BY SCHED_OWNER AS 'Schedule,Owner'
BY ROLLUP_CODE AS 'Rollup'
BY PUSH_EADDRS NOPRINT UNDER-LINE
PRINT
      SET_KEY AS 'Set,Key'
   INST_KEY AS 'Inst,Key'
   RPT_NAME AS 'Report Name'
      OUTPUT_FORMAT AS 'Report,Format'
   DATETYPE AS 'Date,Type'
   AFM_DATE AS 'FM_Date'
   ATO_DATE AS 'TO_Date'
   AFX_DATE AS 'FX_Date'
   ATX_DATE AS 'TX_Date'
   PUSH_EADDRS AS 'Push To EMail Addresses'
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT HTML
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     UNITS=IN,
     PAGESIZE='Letter',
     LEFTMARGIN=0.250000,
     RIGHTMARGIN=0.250000,
     TOPMARGIN=0.250000,
     BOTTOMMARGIN=0.250000,
     SQUEEZE=ON,
     ORIENTATION=PORTRAIT,
$
TYPE=REPORT,
     FONT='ARIAL',
     SIZE=10,
     COLOR='BLACK',
     BACKCOLOR='NONE',
     STYLE=NORMAL,
$
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8,
     BACKCOLOR=( RGB(242 242 242) 'WHITE' ),
$
TYPE=DATA,
     COLUMN=N10,
     JUSTIFY=CENTER,
$
TYPE=DATA,
     COLUMN=N9,
     JUSTIFY=CENTER,
$
TYPE=DATA,
     COLUMN=N8,
     JUSTIFY=CENTER,
$
TYPE=DATA,
     COLUMN=N7,
     JUSTIFY=CENTER,
$
TYPE=DATA,
     COLUMN=N6,
     JUSTIFY=CENTER,
$
TYPE=DATA,
     COLUMN=N5,
     JUSTIFY=CENTER,
$
TYPE=TITLE,
     COLUMN=N9,
     JUSTIFY=CENTER,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
$
TYPE=TITLE,
     COLUMN=N7,
     JUSTIFY=CENTER,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
$
TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=9,
     COLOR='WHITE',
     BACKCOLOR=RGB(154 175 212),
     STYLE=BOLD,
$
 
TYPE=HEADING,
     SIZE=12,
     COLOR=RGB(36 43 114),
     STYLE=BOLD,
     JUSTIFY=CENTER,
$
TYPE=FOOTING,
     STYLE=BOLD,
$
TYPE=SUBHEAD,
     SIZE=9,
     STYLE=BOLD,
$
TYPE=REPORT,
     IMAGE=/image/TNTLogo.gif,
     POSITION=(0.027778 0.027778),
     SIZE=(1.902778 0.652778),
$
TYPE=REPORT,
     COLUMN=N13,
     WRAP=8.000000,
$
ENDSTYLE
END
-RUN
 
JOIN CLEAR *
-RUN
USE CLEAR *
 
DEFINE FILE HLDWS
IKEY/I11=0;
END
TABLE FILE HLDWS
BY SCHED_OWNER
BY SET_KEY
BY IKEY AS INST_KEY
ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS HLDSETS
END
-RUN
 
TABLE FILE HLDW
BY SCHED_OWNER
BY SET_KEY
BY INST_KEY
ON TABLE HOLD AS HLDINST
END
-RUN
 
MODIFY FILE PUSH_REPORTS
FIXFORM FROM HLDSETS
MATCH SCHED_OWNER SET_KEY INST_KEY
 ON MATCH COMPUTE LAST_WEEKLY_PROCESS_DATE=&TDATE;
 ON MATCH UPDATE LAST_WEEKLY_PROCESS_DATE
DATA ON HLDSETS
END
-RUN
 
MODIFY FILE PUSH_REPORTS
FIXFORM FROM HLDINST
MATCH SCHED_OWNER SET_KEY INST_KEY
 ON MATCH COMPUTE LAST_WEEKLY_PROCESS_DATE=&TDATE;
 ON MATCH UPDATE LAST_WEEKLY_PROCESS_DATE
DATA ON HLDINST
END
-RUN
