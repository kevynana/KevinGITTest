-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File AIREXTRT.FEX
-********************************************************************
-* MODIFICAIONS:

-********************************************************************
-*
-* This program was created to join to the taf file
-*
-*5/23/14   DEB              ADDED EX5 TO EX_RF_FLAG
-********************************************************************
-* -*SET &ECHO=ALL;
-*SET TEMPERASE = OFF
-INCLUDE SETECHO


SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC/A60 = &&LDESC;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_AMT/D12.2CS = SEG_AMT;
  NEW_SEG_COUNT/D8CS = SEG_COUNT;
  NEW_SEG_DISC/D12.2CS = SEG_DISC;
  NEW_SEG_MILES/D9= SEG_MILES;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;


-* ****DEFINES ON PREVIOUS DEFINES****

  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' 
     THEN FARE_PAID ELSE 0;
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST; 
EX_RF_FLAG/A1 = IF (((RF_FLAG EQ 'F' OR 'P') AND
    (EMBARK NE 'EX1' OR 'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1'))
  THEN 'R'
  ELSE IF (((EXSALE NE ' ') AND (SEG_COUNT GE 0)) OR
    ((EMBARK NE 'DT1') AND (EX_FLAG EQ 'F' OR 'P') AND 
    (SEG_COUNT LE 0) AND (RF_FLAG EQ ' ')))
  THEN 'E'
  ELSE ' ';
SALES_FLAG/A1 = IF RF_FLAG EQ ' ' AND EX_FLAG EQ ' '  AND VOID_DATE EQ 0
  THEN 'S' 
  ELSE IF RF_FLAG EQ ' ' AND EX_FLAG EQ ' '  AND VOID_DATE NE 0
  THEN 'V' ELSE ' ';
 
-INCLUDE TRANTYPE  
               
END
TABLEF FILE &&EXTRACT
PRINT 
  AIR_KEY	
  AIR_MAIN.INTL_DOM	
  AIRLINE	
  AIRLINE_FEE	
  ARR_TIME
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
  CLASS	
  CREDIT_CARD	
  DISSAVINGS	
  DPT_TIME	
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
  FARE_PAID	
  LEVEL_DESC	
  LEVEL1	
  LEVEL2	
  LEVEL3	
  NET_TKT_CNT	
  RF_FLAG
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'
  SEG_COUNT
  SEG_NBR	
  TKT_NUM	
  TKT_SORT	
  TKT_TYPE	
  TRN_DATE
  VOID_DATE	
  XARR_DT	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT
WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
WHERE (SALES_FLAG EQ 'S' OR 'V') OR (EX_RF_FLAG EQ 'E' OR 'R')
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-* -INCLUDE RPTPARMS
ON TABLE HOLD AS FIRSTHOLD
END
-RUN

-SET &RECORDS=0;

-GOTO XXIT;

TABLE FILE TAFFILE
PRINT TAF_AMT TAF_TAX TAF_COM TAF_NET
BY TAF_TKT
BY TAF_NBR
BY TAF_DAT
BY TAF_PNR
BY TAF_SRC
BY TAF_TBR
BY TAF_CMP
BY TAF_ATK
BY TAF_PAX
BY TAF_CSH
BY TAF_STA
BY TAF_VOD
BY TAF_RFD
ON TABLE HOLD AS TAFHOLD FORMAT FOCUS INDEX TAF_TKT
END
-RUN
 
-IF &LINES EQ 0 THEN GOTO XXIT;


JOIN TKT_NUM IN FIRSTHOLD TO TAF_TKT IN TAFHOLD AS J1
-RUN

TABLE FILE FIRSTHOLD
PRINT AIR_KEY	
  AIRLINE	
  AIRLINE_FEE	
  ARR_TIME
  DPT_TIME	
  EMB_CTY_NM
  FARE_PAID	
  LEVEL_DESC	
  LEVEL1	
  LEVEL2	
  LEVEL3	
  NET_TKT_CNT	
  RF_FLAG
  RTE_CTY_NM
  SEG_COUNT
  SEG_NBR
  TAF_AMT
  TAF_CMP
  TAF_COM
  TAF_CSH
  TAF_NBR
  TAF_NET
  TAF_TKT
  TAF_PNR
  TAF_VOD
  TAF_RFD	
  TKT_NUM	
  TKT_SORT	
  TKT_TYPE	
  TRN_DATE
  VOID_DATE	
  XARR_DT	
  XDPT_DT
  XPSNGR_NM	
  XTRAN_DT
BY LEVEL_DESC
WHERE TAF_VOD NE 'V'
ON TABLE HOLD AS &&RPT_HOLD
END
-RUN
 

-XXIT; 






