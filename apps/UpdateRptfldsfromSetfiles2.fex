-**********************************************************************************************
-* UpdateRptfldsfromSetfiles2.fex
-* Use this procedure if it is a single record for the report instance.
-*
-* Remove WHERE statements in setfiles, and put in RPT_FLDS
-* 
-* retrieve all the report instances to be updated, omit those instances 
-*           which has values in those fields already.
-*  
-* LS - 07/10/2014
-**********************************************************************************************
-SET &ECHO = ALL;
SET TEMPERASE = OFF
SET ASNAME = ON
SET HOLDLIST=PRINTONLY

-*****************************************************************************
-* STEP 0. retrieve the set of report instances to updated 
-*****************************************************************************
-SET &SETFILE = 'ABACHDC';
-SET &WHERE1 = 'WHERE INCLGEO GT 0 AND INCL_CN GT 0';
-INCLUDE getTheInstances
-RUN

-*****************************************************************************
-* STEP 1. set the records in COMMON1 segment for step 3
-*****************************************************************************
DEFINE FILE RPT_INST
  AID/I1 = 1;
  AINCL1/I1 = 1;
  AINCL2/I1 = 1;
END

TABLE FILE RPT_INST
PRINT AID AS 'COMMON1.INCL1' AINCL1 AS 'INCLGEO'  AINCL2 AS 'INCL_CN' 
BY INST_KEY
WHERE INST_KEY IN FILE INSTKEY
ON TABLE SAVE AS HOLDAIR1
END
-RUN

-*****************************************************************************
-* STEP 2. set the records in COMMON2 segment for step 4
-*****************************************************************************
-*FILEDEF HOLD CLEAR
APP FI HOLDREC DISK HOLDREC.FTM (APPEND

-SET &CNT = 1;
-REPEAT REPEATEND 1 TIMES

DEFINE FILE RPT_INST
  SEQNO/I2 = &CNT;
  DATAFIELD/A3 = 'CAN';
  DVALUE2/A3 = 'CA';
END

TABLE FILE RPT_INST
PRINT SEQNO AS 'SEQ_NO' DATAFIELD AS 'REGION_CODE'  DVALUE2 AS 'COUNTRY_CODE'
BY INST_KEY
WHERE INST_KEY IN FILE INSTKEY
ON TABLE HOLD AS HOLDREC 
END
-RUN

-SET &CNT = &CNT + 1;
-REPEATEND

TABLE FILE HOLDREC
PRINT SEQ_NO REGION_CODE COUNTRY_CODE
BY INST_KEY
ON TABLE SAVE AS HOLDAIR2
END
-RUN

-*****************************************************************************
-* STEP 3. UPDATE AIR SEGMENT - Using data saved in step 1
-*****************************************************************************
MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 COMMON1.INCL1/1 INCLGEO/1 INCL_CN/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG COMMON1
  ON NOMATCH INCLUDE
  ON MATCH UPDATE INCLGEO INCL_CN
DATA ON HOLDAIR1
END
-RUN


-*****************************************************************************
-* STEP 4. UPDATE AIRFLDS SEGMENT - Using data saved in step 2
-*****************************************************************************
MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 COMMON2.SEQ_NO/2 COMMON2.REGION_CODE/3 COMMON2.COUNTRY_CODE/3
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG COMMON2
  ON NOMATCH INCLUDE
  ON MATCH UPDATE  REGION_CODE COUNTRY_CODE
DATA ON HOLDAIR2
END
-RUN

