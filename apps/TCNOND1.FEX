-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO
SET TEMPERASE = OFF
CREATE FILE TCARNK
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCND1EX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-INCLUDE SetParmtime
-RUN
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
TABLE FILE TCARNK
BY ROLLCD
SUM NET_TKT_CNT AS 'TTMRK'
    FARE        AS 'TTFARE'
ON TABLE HOLD AS PASS1 FORMAT FOCUS INDEX ROLLCD
END
 
TABLE FILE PASS1
RANKED BY HIGHEST TTMRK
       BY TTFARE
       BY ROLLCD
ON TABLE HOLD AS PASS2 FORMAT FOCUS INDEX ROLLCD
END
 
JOIN ROLLCD IN TCARNK TO ROLLCD IN PASS2
 
DEFINE FILE TCARNK
XL01/P9.2 = IF ((TTMRK LT 1) AND (TTMRK GT (-1))) THEN 0 ELSE
            (NET_TKT_CNT/TTMRK) * 100 ;
XL02/P9.2 = IF ((TTFARE LT 1) AND (TTFARE GT (-1))) THEN 0 ELSE
            (FARE/TTFARE) * 100 ;
-* NOWTOD/A8 WITH ROLLCD = HHMMSS (NOWTOD);
END
TABLE FILE TCARNK
 
SUM NET_TKT_CNT   AS 'TMARKETS'
    XL01          AS 'PERMRK'
    FARE          AS 'BASE'
    XL02          AS 'PERFARE'
    AVG_FLT       AS 'AVG'
    MILES         AS 'TMILES'
    COST_PER_MILE AS 'COST'
BY RANK           AS 'RANK'
BY ROLLCD         AS 'CLIENT'
BY RANK_VALUE     AS 'RNK'
BY ORGDST_PRINT   AS 'OD'
ON TABLE HOLD FORMAT FOCUS
END
-RUN
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
DEFINE FILE HOLD
NOWTOD/A8 WITH CLIENT = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE HOLD
-INCLUDE &&PAHDR
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET COMPOUND CLOSE
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
 
