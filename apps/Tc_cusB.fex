-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO
 
CREATE FILE TC_ACUS
CREATE FILE TC_CCUS
CREATE FILE TC_HCUS
CREATE FILE TCCUSBR
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCAIRDB
-RUN
 
-IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCACUSEX
-RUN
 
 
EX TCCARDB
-RUN
 
EX TCCCUSEX
-RUN
 
EX TCHTLDB
-RUN
 
EX TCHCUSEX
-RUN
 
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-******************************************************************
-* GET THE FINAL REPORT
-******************************************************************
TABLE FILE TC_ACUS
SUM FARE_PAID
  NET_TKT_CNT
  TKT_PURCH/D8CS
  REF_EXCH_CNT/D8CS
BY AR_BRANCH
ON TABLE HOLD AS AIRHOLD
END
-RUN
 
 
 
TABLE FILE AIRHOLD
PRINT FARE_PAID/D14.2CS AND NET_TKT_CNT/D8CS AND TKT_PURCH/D8CS
      AND REF_EXCH_CNT/D8CS AND AR_BRANCH/A2
ON TABLE HOLD AS CUSAIR
END
-RUN
 
 
MODIFY FILE TCCUSBR
FIXFORM FROM CUSAIR
MATCH AR_BRANCH
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON CUSAIR
END
-RUN
 
 
 
TABLE FILE TC_CCUS
SUM BOOKINGS AS 'CBOOK'
  NEW_RENTAL_AMT AS 'CAR_AMT'
  RENTAL_DAYS AS 'CNBR_DAYS'
BY AR_BRANCH
ON TABLE HOLD AS CARHOLD
END
-RUN
 
 
TABLE FILE CARHOLD
PRINT CBOOK/D8 AND CNBR_DAYS/P12CS AND CAR_AMT/D14.2CS
      AND AR_BRANCH/A2
ON TABLE HOLD AS CUSCAR
END
-RUN
 
MODIFY FILE TCCUSBR
FIXFORM FROM CUSCAR
MATCH AR_BRANCH
ON MATCH UPDATE CBOOK CNBR_DAYS CAR_AMT
ON NOMATCH INCLUDE
DATA ON CUSCAR
END
-RUN
 
TABLE FILE TC_HCUS
SUM BOOKINGS AS 'HBOOK'
      NNROOMS AS 'HNBR_DAYS'
      NEW_TOTAL_AMT AS 'HTL_AMT'
BY AR_BRANCH
ON TABLE HOLD AS HTLHOLD
END
-RUN
 
 
TABLE FILE HTLHOLD
PRINT HBOOK/D8 AND HNBR_DAYS/P12CS AND HTL_AMT/D14.2CS
      AND AR_BRANCH/A2
ON TABLE HOLD AS CUSHTL
END
-RUN
 
 
MODIFY FILE TCCUSBR
FIXFORM FROM CUSHTL
MATCH AR_BRANCH
ON MATCH UPDATE HBOOK HNBR_DAYS HTL_AMT
ON NOMATCH INCLUDE
DATA ON CUSHTL
END
-RUN
 
-SET &&RPTSUF = 'SMY';
 
-INCLUDE FDEFRPTS
-RUN
 
-TYPE BEFORE DEFINES
 
DEFINE FILE TCCUSBR
NOWTOD/A8 WITH AR_BRANCH = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE TCCUSBR
SUM NET_TKT_CNT AS 'NET TKT,COUNT'
    TKT_PURCH AS 'TKTS,PURCHASED'
    PCT.TKT_PURCH AS '% OF TKTS,PURCHASED'
    REF_EXCH_CNT AS 'REF/EXCHGS'
    FARE_PAID AS 'FARE PAID'
COMPUTE AVGTKT/D12.2CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1)))
                             THEN 0 ELSE (FARE_PAID/NET_TKT_CNT); AS
                             'AVERAGE,TKT PRICE'
    CBOOK AS 'NET CAR,BKGS'
    CNBR_DAYS AS 'CAR,DAYS'
    CAR_AMT AS 'TOTAL,RENTAL,AMOUNT'
COMPUTE AVGCR/D12.2CS = IF (CAR_AMT LT 0) AND (CNBR_DAYS LT 0) THEN
                            ((CAR_AMT/CNBR_DAYS) * (-1)) ELSE
                        IF ((CNBR_DAYS LT 1) AND (CNBR_DAYS GT (-1))) THEN 0
                             ELSE (CAR_AMT/CNBR_DAYS); AS 'AVG CAR,DAILY RATE'
   HBOOK AS 'NET HTL,BKGS'
   HNBR_DAYS AS 'HTL,NTS'
   HTL_AMT AS 'TOTAL,HOTEL,AMOUNT'
COMPUTE AVGHR/D12.2CS =  IF (HTL_AMT LT 0) AND (HNBR_DAYS LT 0) THEN
                            ((HTL_AMT/HNBR_DAYS) * (-1)) ELSE
                        IF ((HNBR_DAYS LT 1) AND (HNBR_DAYS GT (-1))) THEN 0
                             ELSE (HTL_AMT/HNBR_DAYS); AS 'AVG HTL,DAILY RATE'
BY AR_BRANCH AS 'CLIENT BRANCH'
ON AR_BRANCH RECOMPUTE MULTILINES AS 'TOTAL FOR'
 
 
 
 
-HEAD
HEADING
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
 
FOOTING BOTTOM
"eTTek Review &DATE AT <NOWTOD <60 &&FOOTR"
ON TABLE SET PAGE-NUM OFF
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
 
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
 
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN
 
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
