-* DelSetFile.FEX
-* This program is used to delete the old Travel Summary Reports in ROLLSET,
-* RPT_SET, RPT_INST, and RPT_FLDS, but also can be used to delete other 
-* set files and report streams.
-INCLUDE SETECHO

-*determine how sets affected
TABLE FILE RPT_INST
BY SET_KEY 
 WHERE GLOBAL_PARM EQ 'TLHRCD'
ON TABLE SAVE AS SETS
END
-RUN
-SET &TOTALSETS = &LINES;

-*loop though each set to see if it only contains those old travel summary
-REPEAT TOEND1 &TOTALSETS TIMES
-READ SETS NOCLOSE &SETKEY.7.

TABLE FILE RPT_INST
PRINT INST_KEY
WHERE SET_KEY EQ &SETKEY
ON TABLE SAVE
END
-RUN
-SET &LINE1 = &LINES;

TABLE FILE RPT_INST
PRINT INST_KEY
WHERE SET_KEY EQ &SETKEY
 WHERE GLOBAL_PARM EQ 'TLHRCD'
ON TABLE SAVE
END
-RUN
-SET &LINE2 = &LINES;

-IF &LINE1 NE &LINE2 THEN GOTO TOEND1;
-*Only contains old travel summary

MODIFY FILE RPT_SET
FIXFORM SET_KEY/7
MATCH SET_KEY
  ON MATCH DELETE
DATA 
&SETKEY
END
-RUN
-TOEND1


TABLE FILE RPT_INST
BY INST_KEY 
 WHERE GLOBAL_PARM EQ 'TLHRCD'
ON TABLE HOLD
END
-RUN

MODIFY FILE RPT_FLDS
FIXFORM FROM HOLD
MATCH INST_KEY 
  ON MATCH DELETE
DATA ON HOLD
END
-RUN

MODIFY FILE RPT_INST
FIXFORM FROM HOLD
MATCH INST_KEY 
  ON MATCH DELETE
DATA ON HOLD
END
-RUN


-*-GOTO XXIT;
-ROLLSET
TABLE FILE ROLLSET
PRINT GLOBAL_PARM
BY ROLLUP_CODE
 WHERE GLOBAL_PARM EQ 'TLHRCD'
ON TABLE HOLD
END
-RUN
MODIFY FILE ROLLSET
FIXFORM FROM HOLD
MATCH ROLLUP GLOBAL_PARM
  ON MATCH DELETE
DATA ON HOLD
END
-RUN


-XXIT