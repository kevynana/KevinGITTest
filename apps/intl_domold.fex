-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-*SET &ECHO=ALL;
-* File INTL_DOM.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-*1/15/01      TANDT-SS             ADDED ADDITIONAL HOLD FILES TO ALLOW
-*                                  RANKING TO WORK PROPERTY (HOLD4R, HOLD4,
-*                                  HOLD4S and HOLD4S2)
-*5/21/01      TANDT-SS               ADDED DEFINE TO NORECORDS SO MESSAGE
-*                                    "NO RECORDS FOUNDS FOR THIS SELECTION" 
-*                                    WILL APPEAR IN EXCEL 97
-*10/26/01     TANDT-DB             ADDED DEFINE TO SORT BY CP_EMB AND CP_RTE
-**********************************************************************

EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS

-TYPE &&OUTPUTDEST
-IF &&OUTLINE2 EQ 'OFFLINE' THEN GOTO XOFFLINE ELSE GOTO XONLINE;

-XONLINE
-TYPE Before ONLINE
ONLINE
-TYPE After ONLINE
-RUN
-GOTO CONT

-XOFFLINE
-SET &&OUTLINE2 = '';
-TYPE Before OFFLINE
OFFLINE
-TYPE After OFFLINE
-RUN
-GOTO CONT

-CONT
-SET &&RPT_HOLD = 'RPTHOLD';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN

EX AIRUSE
-RUN

EX &&EXPARM
-RUN

-IF &RECORDS GT 0 THEN GOTO DOIT;

-NORECORDS
-SET &&RPTSUF = 'MSG';
-INCLUDE FDEFRPTS
-RUN
DEFINE FILE &&XHIER
EMPTY/A35 = 'NO RECORDS FOUND FOR THIS SELECTION';
END
TABLE FILE &&XHIER
ON TABLE SET PAGE-NUM OFF
-* HEADING CENTER
-* "NO RECORDS FOUND FOR THIS SELECTION"
PRINT EMPTY AS '' ROLL_KEY NOPRINT
WHERE RECORDLIMIT EQ 1
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
-GOTO XXIT

-DOIT

EX CPCOVPG
-RUN

EX AIRLEVT
-RUN

DEFINE FILE &&RPT_HOLD ADD
  AIRSELECT/A3 = IF ('&&AIR1' EQ AIR_LINE) THEN AIR_LINE ELSE '0';
END
-RUN

TABLE FILE &&RPT_HOLD
SUM COMM_SEG AS 'TCOMM'
    CP_FARE_PD AS 'FARE'
    MILES
    NET_COMM AS 'NCOMM'
    NET_TKT_CNT AS 'TICK_SEG'
    TOTAL_FARE AS 'TFARE'
BY AIRSELECT
BY AIRLINE_NAME
BY ORGDST
BY CP_EMB
BY CP_RTE
BY EMB_APT
BY RTE_APT
WHERE CP_AIRLINE EQ '&&AIR1'
ON TABLE HOLD AS CPHOLD1
END
-RUN

TABLE FILE CPHOLD1
PRINT AIRLINE_NAME
      AIRSELECT
      CP_EMB
      CP_RTE
      FARE
      MILES
      NCOMM
      TCOMM
      TFARE
      TICK_SEG
-*RANKED BY HIGHEST TICK_SEG
BY ORGDST
BY EMB_APT
BY RTE_APT
ON TABLE HOLD AS CPHOLD2
END
-RUN

TABLE FILE &&RPT_HOLD
SUM COMM_SEG AS 'ACOMM'
    CP_FARE_PD AS 'AFARE'
    NET_COMM AS 'ANET'
    NET_TKT_CNT AS 'ATICK_SEG'
    TOTAL_FARE AS 'ATFARE'
BY ORGDST
BY CP_EMB
BY CP_RTE
BY EMB_APT
BY RTE_APT
WHERE CP_AIRLINE NE '&&AIR1'
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
SUM AFARE
    ANET ACOMM
    ATFARE
    ATICK_SEG
BY ORGDST
BY CP_EMB
BY CP_RTE
BY EMB_APT
BY RTE_APT
ON TABLE HOLD AS HOLD2
END
-RUN

MATCH FILE CPHOLD2
PRINT *
BY ORGDST
BY CP_EMB
BY CP_RTE
BY EMB_APT
BY RTE_APT
ON TABLE HOLD
RUN
FILE HOLD2
PRINT *
BY ORGDST
BY CP_EMB
BY CP_RTE
BY EMB_APT
BY RTE_APT
AFTER MATCH HOLD AS HOLD3 OLD-OR-NEW
END
-RUN

DEFINE FILE HOLD3
  BASE_FARE/D12.2CS = AFARE + FARE;
  FARE_PAID/D12.2CS = ATFARE + TFARE;
  NET_FARE/D12.2CS = ANET + NCOMM;
  NET_MKTS/D8CS = ATICK_SEG + TICK_SEG;
  TOTAL_COMM/D12.2 = ACOMM + TCOMM;
  CPEMB_APT/A21 = IF '&&CTYPR' EQ 'ALL' THEN CP_EMB ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE CP_EMB;
  CPRTE_APT/A21 = IF '&&CTYPR' EQ 'ALL' THEN CP_RTE ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE CP_RTE;
  EMBARK_APT/A21 = IF '&&CTYPR' EQ 'ALL' THEN EMB_APT ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE EMB_APT;
  ROUTE_APT/A21 = IF '&&CTYPR' EQ 'ALL' THEN RTE_APT ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE RTE_APT;
  ODTEST/A50=IF '&&CTYPR' EQ 'ALL' THEN ORGDST ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE ORGDST;
  SUBTOT/A1=IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN '1' ELSE ' ';

END
-RUN

-* ************************************************
-* Start of new files to correct ranking problem.
-* ************************************************
TABLE FILE HOLD3
PRINT
  ACOMM
  AFARE
  ANET
  ATFARE
  ATICK_SEG
  BASE_FARE
  CP_EMB
  CP_RTE
  FARE
  FARE_PAID
  MILES
  NCOMM
  NET_FARE
  NET_MKTS
  TCOMM
  TFARE
  TICK_SEG
  TOTAL_COMM
RANKED BY &&RANK_ORDER &&RANK_METH
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
BY CPEMB_APT
BY CPRTE_APT
WHERE ODTEST NE 'ALL OTHER NOT IN MKT'
ON TABLE HOLD AS HOLD4R
END
-RUN

TABLE FILE HOLD4R
PRINT *
BY RANK
BY TICK_SEG
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
BY CPEMB_APT
BY CPRTE_APT
ON TABLE HOLD AS HOLD4 FORMAT ALPHA
END
-RUN

DEFINE FILE HOLD3    ADD
 RANK/I7 = 99999;
END
-RUN

TABLE FILE HOLD3
PRINT
  ACOMM	
  AFARE	
  ANET
  ATFARE	
  ATICK_SEG	
  BASE_FARE
  CP_EMB
  CP_RTE
  FARE
  FARE_PAID
  MILES
  NCOMM
  NET_FARE
  NET_MKTS	
  TCOMM
  TFARE	
  TICK_SEG
  TOTAL_COMM	
BY RANK
BY TICK_SEG
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
BY CPEMB_APT
BY CPRTE_APT
WHERE ODTEST EQ 'ALL OTHER NOT IN MKT'
ON TABLE HOLD AS HOLD4S
END
-RUN

TABLE FILE HOLD4S
PRINT *
BY RANK
BY TICK_SEG
BY SUBTOT
BY ODTEST
BY EMBARK_APT
BY ROUTE_APT
BY CPEMB_APT
BY CPRTE_APT
ON TABLE HOLD AS HOLD4S2 FORMAT ALPHA
END
-RUN

-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &HRANK=&HLDPATH || 'HOLD4.FTM';
-SET &HNORANK=&HLDPATH || 'HOLD4S2.FTM';
-RUN

USE ADD 
&HRANK AS HOLD4
&HNORANK AS HOLD4
END
-RUN

-* **********************************
-* End of new additions
-* **********************************

-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN

DEFINE FILE HOLD4
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = LAST LIST + 1;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A20 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN
                   'ALL OTHER NOT IN MKT' ELSE
                   IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                   IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER IN MKT'
                   ELSE CRANK;
  RANK_LABEL/A50 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   ODTEST ELSE ' ';
  EMB_LABEL/A21 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   EMBARK_APT ELSE ' ';
  RTE_LABEL/A21 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   ROUTE_APT ELSE ' ';
  CPEMB_LBL/A3  = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   CP_EMB ELSE ' ';
  CPRTE_LBL/A3  = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   CP_RTE ELSE ' ';




-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH SUBTOT = HHMMSS (NOWTOD);
END
-RUN

-* CHECK VARIABLES TO SEE IF A COVER PAGE IS NEEDED

-IF &&WHERE11 EQ ' ' GOTO CATEGORY ELSE GOTO COVER;
-CATEGORY
-RUN
-IF &&WHERE15 EQ ' ' GOTO NOCOVER ELSE GOTO COVER;
-RUN

-COVER
-RUN

-SET &&H0 = 'NO SELECTION';
-SET &&H1 = IF &&WHERE11 EQ ' ' THEN &&H0 ELSE &&WHERE11 ;
-SET &&H2 = IF &&WHERE15 EQ ' ' THEN &&H0 ELSE &&WHERE15 ;
-RUN


TABLE FILE HOLD4
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBHEAD
" "
" "
" <20 PAGE: <TABPAGENO"
" "
" COMPANY NAME:<20 &&ROLLNME"
"<20 &&ROLLCS"
"<20 &&ROLLCNCT"
"<20 &&ROLLPHN"
" "
" "
" " 
" REPORT TITLE:  <7 &&RPT_TTL1"
" REPORT LEVEL:  <7 &&RPT_TTL2"
" "
" "
" " 
"             *** THIS REPORT IS BASED ON THE FOLLOWING SELECTION CRITERIA ***"
" "
" "
" <10 TRANSACTION PERIOD:  <25 &FMM/&FMD/&FMY - <31 &TOM/&TOD/&TOY"           
" "
" "
"========================================================================"
" "                           
" "
" CLIENT SELECTION CRITERIA"
" -------------------------------------------"
" "
" &&H1"
" &&WHERE12"
" &&WHERE13"
" &&WHERE14"
" "
" CATEGORY SELECTION CRITERIA"
" -----------------------------------------------"
" "
" &&H2"
" &&WHERE16"
" &&WHERE17"
" &&WHERE18"
" "
" "
"eTTek Review &DATE AT <NOWTOD <65 &&FOOTR"
"&&CRIGHT"


ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-XXIT

EX RESTART
-EXIT

-************************************ NO COVER ********************************
-NOCOVER
-RUN

TABLE FILE HOLD4
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************

ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-XXIT

EX RESTART
-EXIT



