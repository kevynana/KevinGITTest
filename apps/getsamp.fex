-INCLUDE SETECHO
-* Fex is used to display the Sample eTTek Review Reports.  It
-*  will verify that the report exists and either display the report
-*  or a message indicating the report does not exist.
-*
-*  Input Variables = &RPTNM = name of the report without the PDF suffix.
-*
-*
-DEFAULT &RPTNM='sample';
-DEFAULT &ROLL='ABCD-R';
 
TABLE FILE CONTROL
PRINT ROLLUP_CODE
ON TABLE SAVE
END
-RUN
-READ SAVE &ROLL.A8
 
-SET &PATH1=IF '&ROLL.EVAL' EQ 'TOTAL-R' THEN '\TTWEB\TOTAL-R\SAMPLE\'
- ELSE '\TTWEB\ABCD-R\SAMPLE\';
-SET &WSPATH=IF '&ROLL.EVAL' EQ 'TOTAL-R' THEN '/sampletotal/' ELSE '/Sample/';
 
-SET &FILEXIST = '&&WEBSRV.EVAL' || '&PATH1.EVAL' | &RPTNM || '.PDF';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO XLSFILE;
 
-SET &RPT=&RPTNM || '.PDF';
-GOTO SHOWIT;
 
-XLSFILE
-SET &FILEXIST = '&&WEBSRV.EVAL' || '&PATH1.EVAL' | &RPTNM || '.XLS';
-SET &RPT=&RPTNM || '.XLS';
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
 
-SHOWIT
 
-HTMLFORM BEGIN
<HTML>
<HEAD>
<SCRIPT>
 function showpdf () {
 location.href='&WSPATH.EVAL&RPT';
 }
</SCRIPT>
</HEAD>
<BODY onload="showpdf();">
</BODY>
<HTML>
-HTMLFORM END
 
 
-GOTO XXIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>The requested Report Sample<br>
does not exist.</H2></CENTER>
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT
 
 
 
