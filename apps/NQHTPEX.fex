-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the new quarterly review - JK 8/2/05
-******************************************************************************

-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-SET &&HTL_LMT=3;
-SET &&CTY_LMT=25;
-RUN

DEFINE FILE &&EXTRACT 
  PROP_CITY1/A25 = LCWORD(25, PROP_CITY, PROP_CITY1);
  CTY_PRINT/A36 = PROP_CITY1||(' '| PROP_STATE); 
  INV_DATE/MDYY = INVOICE_DATE;
  NEW_ROOM_RATE/D12 = ROOM_RATE;
  NEW_TOTAL_AMT/D12 = TOTAL_AMT;
  NBR_DAYS/D12 =  NUM_DAYS; 
  NNROOMS/D12 = IF (NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)
                THEN ((NBR_DAYS*NUM_ROOMS)*(-1))
                ELSE NBR_DAYS*NUM_ROOMS;
-*   ROLL/A12 = IF ROLLUP_CODE EQ '&&BASEROLL.EVAL' THEN 'Base Company' 
-*              ELSE 'Peer Group'; 
ROLL/A12 = ROLLUP_CODE;
-* ***DEFINES ON PREVIOUS DEFINED DEFINES***
 BOOKINGS/D12 = IF NEW_TOTAL_AMT GE 0 THEN 1 ELSE (-1);
 HTL_CNT/D12 = IF PROP_NAME NE LAST PROP_NAME THEN 1 ELSE 0;
-INCLUDE QRFTRANC
END


TABLEF FILE &&EXTRACT
PRINT BOOKINGS
      CHAIN_CODE
      CTY_PRINT
      CURRENT
      PYTD
      PRIOR
      HTL_CNT
      INVOICE_DATE
      NEW_TOTAL_AMT
      NNROOMS
      ROLL
      TRAN
      YEAR
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') OR
      (INVOICE_DATE GE '&&FXDATE.EVAL') AND (INVOICE_DATE LE '&&TXDATE.EVAL')
WHERE ROLLUP_CODE NE '&&BASEROLL.EVAL'
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
ON TABLE HOLD AS HTLB1
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTPEER;
 

-***************************************************************************************
-* Hotel City Ranking
-***************************************************************************************
TABLE FILE HTLB1
SUM NNROOMS NEW_TOTAL_AMT
BY ROLL
BY CTY_PRINT
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') 
WHERE CTY_PRINT NE ' '
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
SUM NNROOMS NEW_TOTAL_AMT
COMPUTE PAVGRR/D12 = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                        NEW_TOTAL_AMT/NNROOMS;
BY ROLL
BY CTY_PRINT
ON TABLE HOLD AS HOLD2
END
-RUN


TABLE FILE HOLD2
PRINT ROLL CTY_PRINT PAVGRR NNROOMS NEW_TOTAL_AMT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRHPEER
FIXFORM ROLL/12 CTY_PRINT/36 PAVGRR/12 NNROOMS/12 NEW_TOTAL_AMT/12
MATCH ROLL CTY_PRINT
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN

TABLE FILE HTLB1
SUM NNROOMS NEW_TOTAL_AMT CURRENT PYTD PRIOR
BY ROLL
BY YEAR
BY TRAN
ON TABLE HOLD AS BSE1
END
-RUN

TABLE FILE BSE1
PRINT ROLL YEAR TRAN NNROOMS NEW_TOTAL_AMT CURRENT PYTD PRIOR
ON TABLE SAVE
END
-RUN

MODIFY FILE QRHBCTY
FIXFORM ROLL/12 YEAR/4 TRAN/7 NNROOMS/12 NEW_TOTAL_AMT/12 CURRENT/1 PYTD/1 PRIOR/1
MATCH ROLL TRAN YEAR
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN

-NEXTPEER
