-* File UpdrefC.fex

-***************************************************************************
-* This program will correct refusal code and key in car and hotel databases.
-* DATE           NAME
-* 03/11/05       Steve Cords - Initial Creation
-***************************************************************************
TABLE FILE ROLLUP
PRINT ROLLUP
BY ROLLUP NOPRINT
ON TABLE HOLD
END

MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH COMPUTE PR_STAMP=' ';
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END

USE CLEAR *
USE
    D:\TNT\TTRACKER\SUDATA\ROLLUP.FOC AS ROLLUP
END

FI CUSTS DISK D:\LOAD\FOCTEMP\CUSTS.DAT
SET MESSAGE=ON
-*=======================================================================
SET ASNAMES=ON
DEFINE FILE ROLLUP
XL01/A1 = 'W' ;
END
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP AS 'ROLLUP_CODE'
      XL01   AS 'PR_STAMP'
	  QTR_ENABLE
IF ROLLUP EQ 'ACTON-R' OR 'ASSUR' OR 'BDGA-R' OR 'BLV2-R' OR 'BLV-R' 
OR 'BWS-R' OR 'CARLI-R' OR 'CENT-R' OR 'CLINE-R' OR 'COMMSC-R' OR 'CRETE-R' 
OR 'CROWN-R' OR 'DSTM-R' OR 'DUNC-R' OR 'EMCVEN-R'
OR 'FESS-R' OR 'GALLUP-R' OR 'GETRON-R'
OR 'GLAZER-R' OR 'GWL-R' OR 'INTEGR-R' OR 'IRC-R' OR 'JHA-R' OR 'KENEXA-R' 
OR 'KIEWIT-R' OR 'KIMB-R' OR 'LINEL-R' OR 'LPC-R' OR 'MAINST-R' OR 'METRO-R'
OR 'MOSAIC-R' OR 'MUSA-R' OR 'NAVA-R' OR 'NEST-R' OR 'NISOUR-R' OR 'NIXON-R' 
OR 'PCT-R' OR 'PLUS-R' OR 'PRINC-R' OR 'RODALE-R' OR 'SANDH-R' OR 'SCHUD-R' 
OR 'SEARS-R' OR 'SNYDER-R' OR 'UNIVEN-R' OR 'UNVM-R' OR 'UPTTL-R' OR 'VIGR-R' 
OR 'WOLFE-R' OR 'WOODS-R' OR 'WRNR-R'
ON TABLE HOLD
END

MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-*======================================================================
USE CLEAR *
-RUN
SET TEMPDIR=D:\FOCTEMP
SET MESSAGE=ON
SET WARNING=OFF
SET YRTHRESH=30
SET DEFCENT=19
-RUN
-*=======================================================================
-TOP
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP COMP QTR_ENABLE
BY ROLLUP_CODE NOPRINT
IF RECORDLIMIT EQ 1
IF PR_STAMP EQ 'W'
ON TABLE SAVE AS CUSTS
END
-IF &RECORDS EQ 0 GOTO STOP;
-RUN
-READ CUSTS &&ROLLUP.A8 &&COMP.A6 &&QTR.A1.
-RUN

FILEDEF ASCDATA DISK D:\IBI\APPS\BASEAPP\ASCDATA.TXT (LRECL 100
-RUN

TABLE FILE ASCDATA
PRINT KEY_CAR REFUSE_CODE REFUSE_KEY
BY KEY_CAR NOPRINT
IF ROLLUP EQ '&&ROLLUP'
ON TABLE HOLD AS ASCHOLD
END
-RUN

-SET &&QD = IF &&QTR = ' ' THEN '_' ELSE '5';
-SET &&CARCOM = 'C'||&&QD||&&COMP;

-TYPE &&QD
-TYPE &&CARCOM

DEFINE FILE &&CARCOM
KEY_CAR/A80 = SUBSTR(82, CAR_KEY, 1, 80, 80, KEY_CAR);
END

TABLE FILE &&CARCOM
PRINT KEY_CAR CAR_KEY
BY KEY_CAR NOPRINT
IF INVOICE_DATE GE '20050201'
IF INVOICE_DATE LE '20050306'
ON TABLE HOLD AS CHOLD
END
-RUN

MATCH FILE CHOLD
PRINT CAR_KEY
BY KEY_CAR NOPRINT
RUN
FILE ASCHOLD
SUM
REFUSE_CODE REFUSE_KEY
BY KEY_CAR NOPRINT
AFTER MATCH HOLD AS FHOLD OLD-AND-NEW
END
-RUN

TABLE FILE FHOLD
PRINT CAR_KEY REFUSE_CODE REFUSE_KEY
ON TABLE HOLD
END
-RUN

MODIFY FILE &&CARCOM
FIXFORM FROM HOLD
MATCH CAR_KEY
ON MATCH UPDATE REFUSE_CODE REFUSE_KEY
ON NOMATCH REJECT
DATA ON HOLD
END

DEFINE FILE &&CARCOM
REF2/A2 = EDIT(REFUSE_CODE, '$9');
KEYR/A8 = EDIT(REFUSE_KEY, '99999999$$');
KEYREF/A10 = KEYR||'  ';
END

TABLE FILE &&CARCOM
PRINT CAR_KEY REFUSE_CODE KEYREF AS 'REFUSE_KEY'
IF REF2 NE ' ';
IF REFUSE_CODE NE 'CC';
IF REFUSE_CODE NE 'DL';
ON TABLE HOLD
END

MODIFY FILE &&CARCOM
FIXFORM FROM HOLD
MATCH CAR_KEY
ON MATCH COMPUTE REFUSE_CODE='  ';
ON MATCH COMPUTE REFUSE_KEY='          ';
ON MATCH UPDATE REFUSE_CODE
ON MATCH UPDATE REFUSE_KEY
ON NOMATCH REJECT
DATA ON HOLD
END

-*=======================================================================
SET ASNAMES=ON
DEFINE FILE ROLLUP
XL01/A1 = 'Z' ;
END
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP AS 'ROLLUP_CODE'
      XL01   AS 'PR_STAMP'
WHERE ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE HOLD
END
MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-GOTO TOP
-RUN
-*======================================================================
-STOP

-TYPE PROGRAM COMPLETED.......
-EXIT




