-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Steve / Deb - added &&NETVOLUME under NETTKTS - 04/06/2004
-* DEB 5/10/04  CHANGED VOD_AMT DEFINE TO BALANCE TRAVEL SUMMARY
-*6/3/04 JK Changed Exg_amt/Ref_amt to calculate correctly
-*7/26/04    JOY                Added/Changed Norecords logic
-* 2/8/10 - DV  CHANGED EXG_AMT DEFINE DUE TO
-*              DETAIL NOT BALANCING TO SUMMARY PAGE
-*1/29/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-*10/23/15 - S-11851 Added logic for ABACHLOD (DOMESTIC ONLY) ABACHLOI (INTERNATIONAL ONLY) - JM
-*12/31/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*  03/15/2016  REJ  S-16385 Changes to allow the report to work in ER5.
-*  05/31/2016  REJ  S-16833 ER5 changes so the hierarchy will show on the header line.
 
-INCLUDE SETECHO
 
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&LSEL = 'AROLL_DSC1';
-DEFAULT &&SETF = 'XX';
-* -DEFAULT &&FDATE = ' ';
-* -DEFAULT &&TDATE = ' ';
 
 
-* -SET &&FIELD = &ROW_LABEL;
-SET &&LSEL = &LSEL;
-SET &&IDLEVELBREAK = &PARM2;
 
-SET &&OTFLAG = &PARM1;
-* -SET &&FDATE = &FDATE;
-* -SET &&TDATE = &TDATE;
-* -SET &FMM=EDIT(&&FDATE,'$$$$$99');
-* -SET &FMD=EDIT(&&FDATE,'$$$$$$$$99');
-* -SET &FMY=EDIT(&&FDATE,'9999');
-* -SET &TOM=EDIT(&&TDATE,'$$$$$99');
-* -SET &TOD=EDIT(&&TDATE,'$$$$$$$$99');
-* -SET &TOY=EDIT(&&TDATE,'9999');
 
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-GOTO CONT;
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX SETDBLS SETFILE=&&SETF
-RUN
 
-IF &&FROMAVRP EQ '' GOTO :OTER5;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:OTER5
 
-INCLUDE GetForDrillDown
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-****
-IF &&OTFLAG EQ 'O' GOTO ONLINESET ELSE
-IF &&OTFLAG EQ 'T' GOTO TRADSET ELSE GOTO ALLSET;
-RUN
 
-ONLINESET
-IF &&SETF EQ 'ABACHLD' GOTO DOMO;
-IF &&SETF EQ 'ABACHLI' GOTO INTLO;
-SET &&RPT_TTL1 = '                  ONLINE BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''ONLINE''';
-SET &&INTDOM = '';
-RUN
-GOTO OUTSIDE
 
-DOMO;
-SET &&RPT_TTL1 = '         DOMESTIC ONLINE BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''ONLINE''';
-SET &&INTDOM = 'WHERE PICK_CTY.INTL_DOM EQ ''D''';
-RUN
-GOTO OUTSIDE
 
-INTLO;
-SET &&RPT_TTL1 = '    INTERNATIONAL ONLINE BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''ONLINE''';
-SET &&INTDOM = 'WHERE PICK_CTY.INTL_DOM EQ ''I''';
-RUN
-GOTO OUTSIDE
 
-TRADSET
-IF &&SETF EQ 'ABACHLD' GOTO DOMT;
-IF &&SETF EQ 'ABACHLI' GOTO INTLT;
-SET &&RPT_TTL1 = '            TRADITIONAL BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''TRADITIONAL''';
-SET &&INTDOM='';
-RUN
-GOTO OUTSIDE
 
-DOMT;
-SET &&RPT_TTL1 = '   DOMESTIC TRADITIONAL BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''TRADITIONAL''';
-SET &&INTDOM = 'WHERE PICK_CTY.INTL_DOM EQ ''D''';
-RUN
-GOTO OUTSIDE
 
-INTLT;
-SET &&RPT_TTL1 = ' INTERNATIONAL TRADITIONAL BOOKINGS DETAIL  ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''TRADITIONAL''';
-SET &&INTDOM = 'WHERE PICK_CTY.INTL_DOM EQ ''I''';
-RUN
-GOTO OUTSIDE
 
 
-ALLSET
-IF &&SETF EQ 'ABACHLD' GOTO DOMA;
-IF &&SETF EQ 'ABACHLI' GOTO INTLA;
-SET &&RPT_TTL1 = '                     ALL BOOKINGS DETAIL     ';
-SET &&ONLCHK = ' ';
-SET &&INTDOM='';
-RUN
-GOTO OUTSIDE
 
-DOMA;
-SET &&RPT_TTL1 = '            DOMESTIC ALL BOOKINGS DETAIL     ';
-SET &&ONLCHK = ' ';
-SET &&INTDOM='WHERE PICK_CTY.INTL_DOM EQ ''D''';
-RUN
-GOTO OUTSIDE
 
-INTLA;
-SET &&RPT_TTL1 = '       INTERNATIONAL ALL BOOKINGS DETAIL     ';
-SET &&ONLCHK = ' ';
-SET &&INTDOM='WHERE PICK_CTY.INTL_DOM EQ ''I''';
-RUN
-GOTO OUTSIDE
 
-OUTSIDE
 
-SET &&SUBHEAD=IF &&IDLEVELBREAK EQ ' ' THEN ' ' ELSE '<LEVEL_DESC';
 
 
TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN
 
 
-READ SAVE &LEVEL.2.
 
-IF &&IDLEVELBREAK EQ ' ' GOTO LEV1;
 
-SET &LCHK = 'WHERE LEVEL_DESC EQ ''&&LSEL.EVAL''';
-RUN
 
 
 
-GOTO ARNDLEV1;
 
-LEV1;
-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &LCHK='WHERE ' | &AROLL_DSC.EVAL | ' CONTAINS ' | '''&&LSEL.EVAL''';
 
-ARNDLEV1;
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
 
EX TS_JAMS
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DCA1;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-RUN
-:DCA1
 
-INCLUDE SELCATQT
-RUN
 
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX LIMUSE
-RUN
 
-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
 
-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';
 
-INCLUDE IDLEVROLLDEF
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-INCLUDE OTHPARMS
-:AVSF1
 
DEFINE FILE &&EXTRACT
BOOKINGS/P8CS = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
 
 
DROP_OFF_DT/MDYY = DROP_DATE;
LEVEL_DESC1/A80 = &&LDESC;
PICK_UP_DT/MDYY = PICKUP_DATE;
NBR_DAYS/P12C = IF NUM_DAYS LT 0 AND NUM_CARS LT 0
                THEN ((NUM_DAYS * NUM_CARS) * (-1)) ELSE
                NUM_DAYS * NUM_CARS;
INV_DT/YYMD = INVOICE_DATE;
REFUSE_CD/A2 = EDIT(REFUSE_KEY,'$$$$$$$$99');
-INCLUDE IDLEVDEFINE
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT
  BOOKINGS
  CAR_CAT
  CAR_TYPE
  DAILY_RATE
  DROP_CTY.CITY_NAME AS 'DROP_CTY_NAME'
  DROP_OFF_DT
  INVOICE_DATE
  LEVEL_DESC
  NBR_DAYS
  PASSNGR_NAME
  PICK_CTY.CITY_NAME AS 'PICK_CTY_NAME'
  PICK_UP_DT
  REFUSE_CD
  RENTAL_AMT
  VENDOR_NAME
-INCLUDE &CARDATES
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
&&ONLCHK
&LCHK
&&INTDOM
-INCLUDE RPTPARMS
 
ON TABLE HOLD AS CARHOLD
END
-RUN
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
DEFINE FILE CARHOLD
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS';
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
 
 
 
TABLE FILE CARHOLD
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
 
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM
NBR_DAYS AS 'DAYS'
BOOKINGS AS 'BKGS'
RENTAL_AMT AS 'RNTL,AMT'
-* COMPUTE AVGDR/D12.2CS = IF RENTAL_AMT LT 0 AND BOOKINGS LT 0
-*  THEN ((RENTAL_AMT/BOOKINGS) * (-1))
-*  ELSE RENTAL_AMT/BOOKINGS; AS 'AVERAGE,BOOKING,RATE'
COMPUTE AVGDR/D12.2CSM = IF ((BOOKINGS LT 1) AND (BOOKINGS GT (-1))) THEN 0 ELSE (RENTAL_AMT/BOOKINGS); AS 'AVG,BKG,RTE'
 
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY LEVEL_DESC NOPRINT
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY VENDOR_NAME AS 'VENDOR'
BY CAR_TYPE AS 'CAR,TYPE'
BY CAR_CAT AS 'CAR,CATEGORY'
BY PICK_UP_DT AS 'PICKUP,DATE'
BY PICK_CTY_NAME AS 'PICKUP,CITY'
BY DROP_OFF_DT AS 'DROP,DATE'
BY DROP_CTY_NAME AS 'DROP,CITY'
BY REFUSE_CD AS 'RSN'
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
 
ON LEVEL_DESC SUMMARIZE AS 'TOTAL FOR'
ON LEVEL_DESC PAGE-BREAK
 
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
-INCLUDE FOOTERDT
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYD
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-GOTO XXIT;
 
 
-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-GOTO XXIT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT;
