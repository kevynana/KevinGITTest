-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File AIREXTRT.FEX
-********************************************************************
-* This program extracts data for the Avg Ticket price Sat nite stay
-********************************************************************
-* MODIFICATIONS
-*
-* 5/4/05  DEB    ADDED CODE FOR GLOBAL CURRENCY
-* ADDED TRANTYPE INCLUDE - JK 12/15/14 STORY ID S-06492

-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
BASE_FARE/D12.2CS = SEG_AMT;
-* CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'   
-*                               'D' 'DISCOUNT'  'B' 'BUSINESS'
-*                               ' ' 'OTHER');

CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE' 
                                ' ' 'DEFAULT'); 

FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
LEVEL_DESC/A60 = &&LDESC;
MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
NEW_SEG_AMT/D12.2CS = SEG_AMT;
NET_COMM/D12.2S = (SEG_AMT - SEG_COM);
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
SEG_CNT/D8CS         = SEG_COUNT;
TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                           (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' '; 
TRAN_MONTH/MT        = TRN_DATE; 
XDPT_DT/MDYY         = DPT_DATE;
XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
XTRAN_DT/MDYY        = TRN_DATE;
XDPT_DOW/W           = DPT_DATE;
-* ****DEFINES ON PREVIOUS DEFINES****
DPT_DAY/A3           = DECODE XDPT_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
-* ****DEFINES ON PREVIOUS DEFINES****

SAT_STAY/A1 = IF DPT_DAY EQ 'SUN' AND TRIP_LENGTH GT 7 THEN 'Y' ELSE 
              IF DPT_DAY EQ 'MON' AND TRIP_LENGTH GT 6 THEN 'Y' ELSE
              IF DPT_DAY EQ 'TUE' AND TRIP_LENGTH GT 5 THEN 'Y' ELSE
              IF DPT_DAY EQ 'WED' AND TRIP_LENGTH GT 4 THEN 'Y' ELSE
              IF DPT_DAY EQ 'THU' AND TRIP_LENGTH GT 3 THEN 'Y' ELSE
              IF DPT_DAY EQ 'FRI' AND TRIP_LENGTH GT 2 THEN 'Y' ELSE
              IF DPT_DAY EQ 'SAT' AND TRIP_LENGTH GT 1 THEN 'Y' ELSE 'N';

-INCLUDE TRANTYPE

END
-RUN

TABLE FILE &&EXTRACT
SUM AIR_KEY 
      DPT_DAY
      SAT_STAY      
BY AIR_KEY NOPRINT

-INCLUDE &AIRDATES

WHERE VOID_DATE EQ 0
WHERE SEG_NBR EQ '01'
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS

ON TABLE HOLD AS HOLDONE 
END
-RUN


TABLEF FILE &&EXTRACT
PRINT
  AGENT_NUM
  AIR_KEY	
  AIRLINE	
  AIRLINE_NAME
  AIR_MAIN.INTL_DOM AS 'INTL_DOM'
  BASE_FARE
  CL_CAT	
  CLASS	
  CLASS_CAT
  DPT_DAY	
  DPT_TIME
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
  FARE_PAID
  FARE_BAS	
  LEVEL_DESC
  MRK_TKT_CNT
  NET_COMM
  NEW_SEG_AMT
  NET_TKT_CNT
  PASSNGR_NAME
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'
  SEG_CNT
  SEG_COUNT
  SEG_NBR
  TRAN_MONTH
  TKT_DATE
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT	
BY AIR_KEY NOPRINT
WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
WHERE VOID_DATE EQ 0

&&WHERE1
&&WHERE2


-INCLUDE RPTPARMS

ON TABLE HOLD AS HOLDTWO
END
-RUN


 

MATCH FILE HOLDTWO
PRINT 
  AGENT_NUM 	
  AIRLINE	
  AIRLINE_NAME
  BASE_FARE
  CL_CAT	
  CLASS	
  CLASS_CAT
  DPT_DAY	
  DPT_TIME
  EMB_APT_NM	
  EMB_CTY_NM	
  FARE_PAID
  FARE_BAS
  INTL_DOM
  LEVEL_DESC
  MRK_TKT_CNT
  NET_COMM
  NEW_SEG_AMT
  NET_TKT_CNT
  PASSNGR_NAME
  RTE_APT_NM	
  RTE_CTY_NM
  SEG_CNT
  SEG_COUNT
  SEG_NBR
  TRAN_MONTH
  TKT_DATE
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT	
BY AIR_KEY
ON TABLE HOLD
RUN
FILE HOLDONE
SUM 
  SAT_STAY
  DPT_DAY AS 'ORDPT_DAY'
BY AIR_KEY
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW
END
-RUN




DEFINE FILE XTHREE
SAT_STY/A13 = DECODE SAT_STAY ('Y' 'SAT STAY'   'N' 'NO SAT STAY'
                               ' ' 'MCO OTHER');
END
-RUN

TABLE FILE XTHREE
PRINT 
  AGENT_NUM
  AIR_KEY	
  AIRLINE	
  AIRLINE_NAME
  BASE_FARE
  CL_CAT	
  CLASS	
  CLASS_CAT
  DPT_DAY	
  DPT_TIME
  EMB_APT_NM	
  EMB_CTY_NM	
  FARE_PAID
  FARE_BAS
  INTL_DOM
  LEVEL_DESC
  MRK_TKT_CNT
  NET_COMM
  NEW_SEG_AMT
  NET_TKT_CNT
  PASSNGR_NAME
  RTE_APT_NM	
  RTE_CTY_NM
  SAT_STAY
  SAT_STY
  SEG_CNT
  SEG_COUNT
  SEG_NBR
  TRAN_MONTH	
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT	
BY TKT_DATE 
ON TABLE HOLD AS SATNIT FORMAT FOCUS INDEX TKT_DATE
END
-RUN
 
 
 
-* Global Reporting JOIN
JOIN SATNIT.TKT_DATE IN SATNIT TO GLOBAL.DATE IN GLOBAL AS J1


DEFINE FILE SATNIT
NAMEC/A30 = CNAME;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
PAID_FAREX/D20.6 = FARE_PAID * CURR1X;
FARE_BASEX/D20.6 = BASE_FARE * CURR1X;

-* DEFINES ON PREVIOUSLY DEFINES DEFINES

PAID_FARE1/D20.6C = PAID_FAREX;
FARE_BASE1/D20.6C = FARE_BASEX;
END
 
TABLE FILE SATNIT
PRINT AGENT_NUM
  AIR_KEY	
  AIRLINE	
  AIRLINE_NAME
  BASE_FARE
  CL_CAT	
  CLASS	
  CLASS_CAT
  DPT_DAY	
  DPT_TIME
  EMB_APT_NM	
  EMB_CTY_NM	
  FARE_PAID
  FARE_BAS
  INTL_DOM
  LEVEL_DESC
  MRK_TKT_CNT
  NET_COMM
  NEW_SEG_AMT
  NET_TKT_CNT
  PASSNGR_NAME
  RTE_APT_NM	
  RTE_CTY_NM
  SAT_STAY
  SAT_STY
  SEG_CNT
  SEG_COUNT
  SEG_NBR
  TRAN_MONTH	
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT
  NAMEC
  PAID_FARE1/D16.2CS
  FARE_BASE1/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS SATNIT1
END
-RUN

TABLE FILE SATNIT1
PRINT AGENT_NUM
  AIR_KEY	
  AIRLINE	
  AIRLINE_NAME
  BASE_FARE
  CL_CAT	
  CLASS	
  CLASS_CAT
  DPT_DAY	
  DPT_TIME
  EMB_APT_NM	
  EMB_CTY_NM	
  FARE_PAID
  FARE_BAS
  INTL_DOM
  LEVEL_DESC
  MRK_TKT_CNT
  NET_COMM
  NEW_SEG_AMT
  NET_TKT_CNT
  PASSNGR_NAME
  RTE_APT_NM	
  RTE_CTY_NM
  SAT_STAY
  SAT_STY
  SEG_CNT
  SEG_COUNT
  SEG_NBR
  TRAN_MONTH	
  TKT_NUM	
  TKT_SORT
  TKT_TYPE	
  TRN_DATE	
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT
  NAMEC
  PAID_FARE1 
  FARE_BASE1 
BY AIR_KEY NOPRINT
ON TABLE HOLD AS SATNIT2
END
-RUN
