-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File OVERMAI1.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            COMMENTED OUT CREATEMSG SINCE IT
-*                                  DOES NOT APPLY IN WEBFOCUS
-* 3/07/01   TANDT-SS           CHANGE ALL NUMERIC FORMATS FROM P (PACKED) TO
-*                              D (FLOATING-POINT DOUBLE PRECISION) TO PROVIDE
-*                              CONSISTENCY IN ROUNDING AND PERCENTAGES.
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492

-**********************************************************************
-*SET &ECHO=ALL;

-SET &&OV1_MAIN = &&WEB_PATH || 'OV1_MAIN.FOC';
-RUN
FILEDEF OV1_MAIN DISK &&OV1_MAIN
-RUN

CREATE FILE OV1_MAIN
-RUN

DEFINE FILE &&EXTRACT
 CY/A1 = IF (TRN_DATE GE &&FROMDT.EVAL) AND 
            (TRN_DATE LE &&TODT.EVAL) 
            THEN 'Y' ELSE 'N';

 PY/A1 = IF (TRN_DATE GE &&FXDATE.EVAL) AND 
            (TRN_DATE LE &&TXDATE.EVAL) 
            THEN 'Y' ELSE 'N';

 FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
 SAVINGS/D12.2 = SEG_STANDARD - FARE_PAID;
 LOST_SAV/D12.2 = FARE_PAID - SEG_LOWEST;

 NET_TKT_CNT/D12      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

-*DOMESTIC PERIOD
-* CURRENT FARE AMOUNT DOMESTIC CALCULATION
      CDM1NET_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                             (CY EQ 'Y') THEN FARE_PAID ELSE 0;

-* CURRENT NET TICKETS DOMESTIC CALCULATION
        CDM1TKT_ISS/D12 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                             (CY EQ 'Y') THEN NET_TKT_CNT ELSE 0;

-*INTERNATIONAL PERIOD
-* CURRENT FARE AMOUNT INTERNATIONAL CALCULATION
        CIM1NET_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (CY EQ 'Y') THEN FARE_PAID ELSE 0;

-* CURRENT NET TICKTS INTERNATIONAL CALCULATION
        CIM1TKT_ISS/D12 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (CY EQ 'Y') THEN NET_TKT_CNT ELSE 0;

-*PRIOR PERIOD
-* PRIOR FARE AMOUNT DOMESTIC CALCULATION
      PDM1NET_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                             (PY EQ 'Y') THEN FARE_PAID ELSE 0;
                      

-* PRIOR TICKETS ISSUED DOMESTIC CALCULATION
        PDM1TKT_ISS/D12 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                               (PY EQ 'Y') THEN NET_TKT_CNT ELSE 0;


-*INTERNATIONAL
-* PRIOR FARE AMOUNT INTERNATIONAL CALCULATION
        PIM1NET_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (PY EQ 'Y') THEN FARE_PAID ELSE 0;                      

-* PRIOR TICKETS ISSUED INTERNATIONAL CALCULATION
        PIM1TKT_ISS/D12 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                             (PY EQ 'Y') THEN NET_TKT_CNT ELSE 0;
-* DOMESTIC CURRENT SAVINGS CALCULATION
        CDM1SAV_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                               (CY EQ 'Y') THEN SAVINGS ELSE 0;

-* DOMESTIC PRIOR SAVINGS CALCULATION
        PDM1SAV_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                               (PY EQ 'Y') THEN SAVINGS ELSE 0;

-* INTERNATIONAL CURRENT SAVINGS CALCULATION
        CIM1SAV_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (CY EQ 'Y') THEN SAVINGS ELSE 0;

-* INTERNATIONAL PRIOR SAVINGS CALCULATION
        PIM1SAV_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                               (PY EQ 'Y') THEN SAVINGS ELSE 0;

-* DOMESTIC CURRENT LOST SAVINGS CALCULATION
        CDM1LOS_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                               (CY EQ 'Y') THEN LOST_SAV ELSE 0;

-* DOMESTIC PRIOR LOST SAVINGS CALCULATION
       PDM1LOS_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'D') AND
                              (PY EQ 'Y') THEN LOST_SAV ELSE 0;

-* INTERNATIONAL CURRENT LOST SAVINGS CALCULATION
       CIM1LOS_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                              (CY EQ 'Y') THEN LOST_SAV ELSE 0;

-* INTERNATIONAL PRIOR LOST SAVINGS CALCULATION
      PIM1LOS_AMT/D12.2 = IF (AIR_MAIN.INTL_DOM EQ 'I') AND
                             (PY EQ 'Y') THEN LOST_SAV ELSE 0;
       
-INCLUDE TRANTYPE
END
-RUN

TABLEF FILE &&EXTRACT
-* Generated by Report Assist
SUM
CDM1NET_AMT/D15 CDM1TKT_ISS/D15 CDM1SAV_AMT/D15 CDM1LOS_AMT/D15
CIM1NET_AMT/D15 CIM1TKT_ISS/D15 CIM1SAV_AMT/D15 CIM1LOS_AMT/D15

PDM1NET_AMT/D15 PDM1TKT_ISS/D15 PDM1SAV_AMT/D15 PDM1LOS_AMT/D15
PIM1NET_AMT/D15 PIM1TKT_ISS/D15 PIM1SAV_AMT/D15 PIM1LOS_AMT/D15



WHERE (AIR_MAIN.TICKET_CODE EQ '1' );
WHERE (AIR_MAIN.CONJ_REL EQ 1) ;
WHERE (TRN_DATE GE &&FROMDT.EVAL
      AND TRN_DATE LE &&TODT.EVAL)
   OR (TRN_DATE GE &&FXDATE.EVAL
      AND TRN_DATE LE &&TXDATE.EVAL);
WHERE VOID.VOID_DATE EQ 0;
&&WHERE1
&&WHERE2
&&WHERE3


-INCLUDE RPTPARMS

ON TABLE HOLD AS OVHOLD 
END

-RUN

TABLEF FILE OVHOLD
-* Generated by Report Assist
PRINT
CDM1NET_AMT CDM1TKT_ISS CDM1SAV_AMT CDM1LOS_AMT
CIM1NET_AMT CIM1TKT_ISS CIM1SAV_AMT CIM1LOS_AMT

PDM1NET_AMT PDM1TKT_ISS PDM1SAV_AMT PDM1LOS_AMT
PIM1NET_AMT PIM1TKT_ISS PIM1SAV_AMT PIM1LOS_AMT

ON TABLE HOLD AS TKT_TYPO 
END

-*-EXIT
