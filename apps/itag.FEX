-*==========================================================================
-* DATE   :  02/14/2001                           REQUESTOR : STEVE CORDS
-* ANALYST:  WILLIAM TORRES (402) 548-3212, X2430
-*--------------------------------------------------------------------------
-* DESCRIPTION
-* ===========
-* MANUAL PROCESS TO RETAG REPORT INSTANCES - NEED TO CHANGE VALUE
-* ON '&&ROLL' TO THE CLIENT FOR WHICH YOU WISH TO PROCESS
-*==========================================================================

-INCLUDE SETECHO
-SET &&ROLL = 'DSTM-R' ;

TABLE FILE ROLLUP
PRINT ROLLUP_CODE
      COMP AS COMPANY
IF ROLLUP_CODE EQ &&ROLL
ON TABLE HOLD AS HOLDF1
END
-RUN
-READ HOLDF1 &X.8 &&COMPNY.6
-RUN

-* -SET &HIER = 'D:\TNT\TTRACKER\DATA\HP' ||
-SET &HIER = 'K:\TNT\TTRACKER\DATA\HP' ||
- &&COMPNY || '.FOC';
-*  AS HIER50';
-* -SET &XHIER = 'D:\TNT\TTRACKER\DATA\HX' ||
-SET &XHIER = 'K:\TNT\TTRACKER\DATA\HX' ||
- &&COMPNY || '.FOC';
-*  AS XHIER50';
USE CLEAR
EX SUUSES
-RUN

USE ADD
&HIER  AS HIER50
&XHIER AS XHIER50
END
-RUN

TABLE FILE ROLLUP
PRINT  ROLLUP_CODE
IF ROLLUP_CODE EQ &&ROLL
ON TABLE HOLD AS HOLDF2
END
-RUN

CREATE FILE CONTROL
-RUN

-TYPE Before first Control MODIFY
MODIFY FILE CONTROL
FIXFORM FROM HOLDF2
COMPUTE CONTROL_ID/A3  =  'PGM';
COMPUTE USER_ID/A20 = 'ADMIN';
COMPUTE SECURITY_LEVEL/I1 = 9;
COMPUTE WEB_RUN/A1 = 'N';
COMPUTE INST_KEY/A72 = ' ';
MATCH CONTROL_ID
 ON MATCH REJECT
 ON NOMATCH INCLUDE
DATA ON HOLDF2
END
-RUN

EX INST_A50
-RUN

-* Table request added with pchold to prevent messages from server.
TABLE FILE ROLLUP
PRINT ROLLUP
WHERE RECORDLIMIT EQ 1
ON TABLE PCHOLD
END
-RUN
