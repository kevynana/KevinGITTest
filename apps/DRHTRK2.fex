SET ASNAMES=ON
-*SET TEMPERASE = OFF
-* -DEFAULT &&BNAME = ' ';

-SET &&BNAME = &BNAME;
-SET &&FROMDT = &FROM;
-SET &&TODT = &TO;
-SET &&REQUESTER=&REQUESTER;
-SET &&ICFLAG1=&ICFLAG1;
-SET &&COMPBY=&COMPBY1;
-SET &&OUTFMT = &OUTFMT;
-************************************************
-* Date Range Selection
-IF '&FROM.EVAL' EQ '2000/01/01' THEN GOTO NODTE;
-SET &DATE = 'WHERE DATE_COMPLETED GE ''&FROM.EVAL'' AND DATE_COMPLETED LE ''&TO.EVAL''';
-GOTO REQUESTER;
-NODTE;
-SET &DATE = ' ';

-*************************************************

-************************************************
-REQUESTER
-*Requester Selection
-IF '&REQUESTER.EVAL' EQ 'NONE' THEN GOTO NOREQUEST;
-SET &REQUESTER = 'WHERE REQUESTER1 EQ ''&REQUESTER.EVAL''';
-GOTO COMPBY;
-NOREQUEST
-SET &REQUESTER = ' ';
-RUN
-*************************************************
-************************************************
-COMPBY
-* SPECIFIC PERSON COMPLETING
-IF '&COMPBY1.EVAL' EQ ' ' THEN GOTO NOCOBY;
-SET &COMPBY = 'WHERE COMPBY1 EQ ''&COMPBY1.EVAL''';
-SET &COMPBY1 = '&COMPBY1.EVAL';
-GOTO BTYPE;
-NOCOBY
-SET &COMPBY = ' ';
-RUN

-************************************************

-BTYPE
-* Client/Internal Billing Type
-IF '&ICFLAG1.EVAL' EQ 'CI' THEN GOTO NOIC;
-SET &ICFLAG1 = 'WHERE ICFLAG EQ ''&ICFLAG.EVAL''';
-GOTO OUTPUT;
-NOIC
-SET &ICFLAG1 = 'WHERE ICFLAG EQ ''C'' OR ''I''';
-RUN
-***********************************************

-OUTPUT 
-* Output format
-IF '&OUTFMT.EVAL' EQ 'EXCEL' OR 'EXL2K' THEN GOTO EXCEL ELSE
-IF '&OUTFMT.EVAL' EQ 'PDF' THEN GOTO PDF ELSE GOTO DEFAULT;
-RUN
-EXCEL
-SET &OUTFMT1 = '.XLS';
-GOTO BEGIN;
-PDF
-SET &OUTFMT1 = '.PDF';
-GOTO BEGIN;
-DEFAULT
-SET &OUTFMT = 'PDF';
-SET &OUTFMT1 = '.PDF';
-RUN

-BEGIN;
DEFINE FILE HOURTRAK
REQUESTER1/A20 = UPCASE(20, REQUESTOR, 'A20');
COMPBY1/A15 = UPCASE(15, COMPLETED_BY, 'A15');
RQSTT/A25 = IF REQUEST_TYPE EQ 'S' THEN 'Scheduled Task' ELSE
            IF REQUEST_TYPE EQ 'P' THEN 'Programming' ELSE
            IF REQUEST_TYPE EQ 'M' THEN 'Maintenance' ELSE
            IF REQUEST_TYPE EQ 'D' THEN 'Disccusion' ELSE
            IF REQUEST_TYPE EQ 'R' THEN 'Requests' ELSE ' ';
END
-RUN
TABLE FILE HOURTRAK
PRINT AREA_REQ COMPLETED_BY REQUESTOR DATE_COMPLETED COMPLETED_BY SHORT_DESC
      BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS KEY AREA LONG_DESC1 LONG_DESC2 RQSTT
&DATE
&REQUESTER
&ICFLAG1
&COMPBY
BY AREA_REQ 
ON TABLE HOLD AS H1 FORMAT FOCUS INDEX AREA_REQ
END
-RUN


JOIN AREA_REQ IN H1 TO ROLLUP_CODE IN ROLLUP 
-RUN

DEFINE FILE H1
CODE/A1 = EDIT(AREA_REQ, '$$9$$$$$');
BRANCH_CODE/A2 = IF AREA_REQ CONTAINS '-R' THEN ' ' ELSE 
                 IF CODE EQ ' ' THEN EDIT (AREA_REQ, '99$$$$$$') ELSE ' ';
END
-RUN

TABLE FILE H1
PRINT AREA_REQ ROLL_NAME COMPLETED_BY REQUESTOR DATE_COMPLETED COMPLETED_BY SHORT_DESC
      BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS KEY AREA LONG_DESC1 LONG_DESC2 RQSTT
BY BRANCH_CODE
ON TABLE HOLD AS H2 FORMAT FOCUS INDEX BRANCH_CODE
END
-RUN


JOIN BRANCH_CODE IN H2 TO BRANCH_CODE IN BRANCH
-RUN

TABLE FILE H2
PRINT AREA_REQ ROLL_NAME COMPLETED_BY REQUESTOR DATE_COMPLETED SHORT_DESC BRANCH_CODE ROLL_NAME
      BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS BRANCH_NAME KEY AREA LONG_DESC1 LONG_DESC2 RQSTT
BY COMPLETED_BY
ON TABLE HOLD AS H3
END
-RUN

DEFINE FILE H3
BNAME/A25 = IF BRANCH_CODE EQ ' ' AND ROLL_NAME NE ' ' THEN ROLL_NAME ELSE 
            IF BRANCH_CODE EQ ' ' AND ROLL_NAME EQ ' ' THEN AREA_REQ ELSE BRANCH_NAME;
END
-RUN
TABLE FILE H3
SUM BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS
BY AREA
BY BNAME
BY DATE_COMPLETED
BY REQUESTOR
BY COMPLETED_BY
BY SHORT_DESC
BY KEY
BY LONG_DESC1
BY LONG_DESC2
BY RQSTT
WHERE BNAME EQ '&BNAME.EVAL'
ON TABLE HOLD AS H4
END
-RUN



DEFINE FILE H4
AREA_W/A15=IF AREA EQ 'E' THEN 'DP-eTTek Review' ELSE 'DP-AS400';
SPACE/A1 = ' ';
BILLABLE/A2 = IF BILLED_HOURS GT 0 THEN 'B' ELSE
              IF TECHNOLOGY_CREDITS GT 0 THEN 'C' ELSE 'NO';
END
-RUN

-SET &FMD=EDIT(&FROM, '$$$$$$$$99');
-SET &FMY=EDIT(&FROM, '9999$$$$$$');
-SET &FMM=EDIT(&FROM, '$$$$$99$$$');
-SET &TOD=EDIT(&TO, '$$$$$$$$99');
-SET &TOY=EDIT(&TO, '9999$$$$$$');
-SET &TOM=EDIT(&TO, '$$$$$99$$$');
-SET &DATE1 = IF &DATE EQ ' ' THEN 'No Period Selected - All Included' ELSE '&DATE';
-RUN



TABLE FILE H4
HEADING CENTER
"<25 Detailed Hour Tracking by Requesting Client/Area"
" "
-IF &DATE1 EQ 'No Period Selected - All Included' THEN GOTO NOHEAD;
"<25 Completion Period: &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
-GOTO RPT;
-NOHEAD
"<25 Completion Period: &DATE1"
-RPT;
"</2"
PRINT  REQUESTOR AS 'Requester' 
OVER AREA_W AS 'Deparment'
OVER DATE_COMPLETED AS 'Date Completed' 
OVER RQSTT AS 'Request Type'
OVER LONG_DESC1 AS 'Description' 
OVER LONG_DESC2 AS 'Description'
OVER BILLED_HOURS AS 'Hours Billed' 
OVER TECHNOLOGY_CREDITS AS 'Technology Credits Used' 
OVER TOTAL_HOURS AS 'Total Hours'
BY BNAME AS 'Area/,Branch,Requesting'
BY COMPLETED_BY AS 'Completed,by '
BY KEY NOPRINT
ON COMPLETED_BY SKIP-LINE
ON COMPLETED_BY SKIP-LINE
ON KEY SKIP-LINE
ON BNAME NOSPLIT
ON BNAME SUMMARIZE BILLED_HOURS TECHNOLOGY_CREDITS TOTAL_HOURS AS 'TOTAL'
ON TABLE SET ONLINE-FMT &OUTFMT
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.500000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=TITLE, FONT=TIMES NEW ROMAN, STYLE=BOLD, COLUMN=N1, SIZE=8,$
TYPE=TITLE, FONT=TIMES NEW ROMAN, STYLE=BOLD, COLUMN=N2, SIZE=8,$
TYPE=HEADING, FONT='TIMES NEW ROMAN', SIZE=10, BACKCOLOR='NONE', STYLE=BOLD,$
TYPE=SUBFOOT, SIZE=8, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=REPORT,IMAGE=REVIEW,POSITION=(.3 .3), SIZE=(1.3 .44),$
TYPE=REPORT,IMAGE=ISERIES,POSITION=(8.3 .2), SIZE=(1.0 1.0), $
ENDSTYLE
END
-RUN



