
-*  03/21/2017  JEM  S-31961 Created for new report TCATPMY

-DEFAULT &&FROMER5 = 'N'; 
 
-INCLUDE SETECHO

CREATE FILE TC_ATPMY
-RUN
 
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
 
-TYPE TCAIRDB
-SET &&PAIRDB = 'S';
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCATPMYEX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************
 
 
DEFINE FILE TC_ATPMY
ID_FLAG/A15 = IF ID_KEY EQ 'D' THEN 'Domestic' ELSE 'International';
END
TABLE FILE TC_ATPMY
SUM COMM_AMT
    FARE_PAID
    NET_TKT_CNT
BY ID_FLAG
BY TRNYY
BY TRNMY 
ON TABLE HOLD AS ATPHDI FORMAT FOCUS
END
-RUN


DEFINE FILE TC_ATPMY
ID_FLAG/A15 = 'Combined';
END
TABLE FILE TC_ATPMY
SUM COMM_AMT
    FARE_PAID
    NET_TKT_CNT
BY ID_FLAG
BY TRNYY
BY TRNMY
ON TABLE HOLD AS ATPHC FORMAT FOCUS
END
-RUN

 
 
-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &HLD1=&HLDPATH || 'ATPHDI.FOC';
-SET &HLD2=&HLDPATH || 'ATPHC.FOC';
-RUN
 
USE CLEAR *
-RUN
 
EX SUUSES
-RUN
 
USE ADD
&HLD1 AS ATPHDI
&HLD2 AS ATPHDI
END
-RUN 


 
-SET &&RPTSUF = 'AVGTKT';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');

 
-INCLUDE FDEFRPTS
-RUN
 
 
DEFINE FILE ATPHDI
NOWTOD/A8 WITH ID_FLAG = HHMMSS (NOWTOD);
END
-RUN
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
TABLE FILE ATPHDI
-INCLUDE &&PAHDR
SUM FARE_PAID AS 'Fare,Paid'
    NET_TKT_CNT AS 'Net,Tickets'
COMPUTE ATP/D12CSM = IF (NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1)) THEN 0 ELSE FARE_PAID/NET_TKT_CNT; AS 'Overall,Avg Tkt,Price'
    COMM_AMT AS 'Air,Commission'
COMPUTE ACA/D12CSM = IF (NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1)) THEN 0 ELSE COMM_AMT/NET_TKT_CNT; AS 'Average,Commission'
COMPUTE COMR/P8.1% = IF (FARE_PAID LT 1) AND (FARE_PAID GT (-1)) THEN 0 ELSE COMM_AMT/FARE_PAID*100; AS 'Commission,Rate'
BY ID_FLAG AS 'Type'
BY TRNYY NOPRINT
BY TRNMY AS 'Period'


FOOTING BOTTOM
"&&FOOTER1"
"&&FOOTER2"
"<60 Post-trip Reporting &DATE at <NOWTOD &&FOOTR"
 
ON TABLE SET PAGE-NUM OFF
ON ID_FLAG SKIP-LINE
ON ID_FLAG RECOMPUTE AS 'TOTAL FOR'
ON TRNYY RECOMPUTE AS 'TOTAL FOR'
ON TABLE NOTOTAL
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
 
ON TABLE SET STYLE *
-INCLUDE &&PASTYS

TYPE=REPORT, JUSTIFY=LEFT, $

ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN
 
 
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
 
