
-* 4/23/15 - DEB -S-08292 CREATED FROM TCH109EX FOR TCHRSSUM 

-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*=======================================================================

-SET &&EXTRACT = 'HR_50';

-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';



DEFINE FILE &&EXTRACT
  BOOKINGS/D8 = IF TOTAL_AMT GE 0 THEN 1 ELSE (-1);
  CHAIN/A35 = CHAIN_CODE||(' '| CHAIN_NAME);
  CITY_ST/A35 = PROP_CITY||(' '| PROP_STATE);

  LEVEL_DESC/A60  = AROLL_DSC2;
  NBR_ROOMS/D12 =  NUM_ROOMS; 
  NBR_DAYS/D12 = NUM_DAYS;
  NEW_TOTAL_AMT/D12.2CS = TOTAL_AMT;
  NNROOMS/D12 = IF (NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)
                THEN ((NBR_DAYS*NUM_ROOMS)*(-1))
                ELSE NBR_DAYS*NUM_ROOMS;
  PROP_ZIP5/A5=EDIT(PROP_ZIP, '99999$$$$$');
  STATE/A3 = HOTL_PRP.PROP_STATE_CD;  
  NEW_ROOM_RATE/D12.2C = ROOM_RATE;
END

TABLE FILE &&EXTRACT
PRINT CITY_ST
  CHAIN
  CHAIN_CODE
  NEW_TOTAL_AMT
  NBR_ROOMS
  NNROOMS
  PROP_ADDRESS
  PROP_CITY
  PROP_NAME
  HOTEL_PHONE
  PROP_ZIP5
  NEW_ROOM_RATE AS 'ROOM_RATE'
  STATE
  TOTAL_AMT 
BY  HTL_KEY
 -INCLUDE &HTLDATES
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS

ON TABLE HOLD AS TCHLD

END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;



-*========================================================================
-*   PROCESS EXTRACT DATA FROM STEP4 - HOLD TO FOCUS DATABASE TCTRNK       
-*========================================================================

DEFINE FILE TCHLD
ROLLCD/A8 = '&&ROLL.EVAL';
END
TABLE FILE TCHLD 
PRINT CITY_ST
  CHAIN
  CHAIN_CODE
  NEW_TOTAL_AMT
  NBR_ROOMS
  NNROOMS
  PROP_ADDRESS
  PROP_CITY
  PROP_NAME
  HOTEL_PHONE
  PROP_ZIP5
  ROOM_RATE 
  STATE
  ROLLCD
BY HTL_KEY
ON TABLE HOLD AS TCHLD2 FORMAT FOCUS INDEX HTL_KEY
END
-RUN


-INCLUDE TCUDHUSE
-RUN

TABLE FILE &&EXTRACT2
SUM NUD_DATA/D12.2CS AS 'HRS'
BY HTL_KEY
WHERE UDID EQ 'HRS'
WHERE NUD_DATA NE 0
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS TCUH1
END
-RUN

TABLE FILE &&EXTRACT2
SUM NUD_DATA/D12.2CS AS 'HSS' 
BY HTL_KEY
WHERE UDID EQ 'HSS'
WHERE NUD_DATA NE 0
ON TABLE COLUMN-TOTAL
ON TABLE HOLD AS TCUH2
END
-RUN

MATCH FILE TCUH1
PRINT HRS 
BY HTL_KEY
ON TABLE HOLD
RUN
FILE TCUH2
SUM HSS 
BY HTL_KEY
AFTER MATCH HOLD AS TCUH3 OLD-OR-NEW
END
-RUN

TABLE FILE TCUH3
PRINT HRS HSS 
BY HTL_KEY
ON TABLE HOLD AS TCUH4 FORMAT FOCUS INDEX HTL_KEY
END
-RUN



JOIN HTL_KEY IN TCHLD2 TO HTL_KEY IN TCUH4 AS J1
-RUN

DEFINE FILE TCHLD2
HTL_SCSAV/D12.2 = HSS;
HTL_SCSAVC/D12.2 = HSS * (-1);
HRS_RAT2/D12.2CS = HRS;
HRS_RAT2C/D12.2CS = HRS * (-1); 

RATE/P12 = ROOM_RATE;

HRS_AMT/D12.2CS = IF HTL_SCSAV EQ 0 THEN 0 ELSE 
                   IF (RATE LT 0) AND (NNROOMS LT 0) AND HSS LT 0 THEN (HTL_SCSAVC*NNROOMS) ELSE
                   IF ((RATE LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                      (HTL_SCSAV*NNROOMS);                  
HRS_AMT1/D12.2CS = IF (RATE LT 0) AND (HRS_AMT GT 0) THEN HRS_AMT*(-1) ELSE HRS_AMT;
HRS_RATE/D12.2CS = IF HTL_SCSAV EQ 0 THEN 0 ELSE 
                   IF (RATE LT 0) AND (NNROOMS LT 0) AND HSS LT 0 THEN HRS_RAT2C ELSE
                   IF ((RATE LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE HRS_RAT2;

HTL_LSAV/D12.2CS = 0;
HRS_DAYS/D12CS = IF HRS_AMT1 NE 0 THEN NNROOMS ELSE 0;

END
TABLE FILE TCHLD2
PRINT CITY_ST
  CHAIN
  CHAIN_CODE
  NEW_TOTAL_AMT
  NBR_ROOMS
  NNROOMS
  PROP_ADDRESS
  PROP_CITY
  PROP_NAME
  HOTEL_PHONE
  PROP_ZIP5
  ROOM_RATE 
  STATE
  HRS_AMT1
  HRS_DAYS
  HRS_RATE
BY ROLLCD
BY HTL_KEY
ON TABLE HOLD AS TCHLD3 
END
-RUN




JOIN ROLLCD IN TCHLD3 TO ROLLUP IN ROLLUP
END

DEFINE FILE TCHLD3
RM_RATE/D12.2CS = IF HOTEL_PHONE NE LAST HOTEL_PHONE THEN ROOM_RATE ELSE 0;
END

TABLE FILE TCHLD3
SUM  
    NBR_ROOMS
    NEW_TOTAL_AMT
    NNROOMS	
COMPUTE RM_RATE/D12.2CS = IF ((NNROOMS LT 0) AND (NEW_TOTAL_AMT LT 0)) THEN  
                     ((NEW_TOTAL_AMT/NBR_ROOMS)*(-1)) ELSE IF ((NNROOMS LT 1) AND 
                     (NNROOMS GT (-1))) THEN 0 ELSE NEW_TOTAL_AMT/NNROOMS; AS 'ROOM_RATE'
    HRS_DAYS
    HRS_AMT1
    HRS_RATE
BY ROLLCD
BY CHAIN_CODE
BY HOTEL_PHONE
BY PROP_NAME
BY PROP_ADDRESS
BY PROP_CITY
BY STATE
BY PROP_ZIP5
ON TABLE HOLD AS TCHLD4
END
-RUN



DEFINE FILE TCHLD4
NMB/I5    = NMB + 1;
END
TABLE FILE TCHLD4
SUM  
    NBR_ROOMS
    NEW_TOTAL_AMT
    NNROOMS	
    ROOM_RATE 	
    HRS_DAYS
    HRS_AMT1
    HRS_RATE
BY ROLLCD
BY NMB
BY CHAIN_CODE
BY HOTEL_PHONE
BY PROP_NAME
BY PROP_ADDRESS
BY PROP_CITY
BY STATE
BY PROP_ZIP5
ON TABLE HOLD AS TCHLD5
END
-RUN


MODIFY FILE TCHRSSUM
FIXFORM FROM TCHLD5
DATA ON TCHLD5
END
-RUN


-NEXTONE
