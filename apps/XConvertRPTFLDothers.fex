-**************************************************************************************************
-* file name: ConvertRPTFLDothers.fex
-*   convert fields from TRVLSMRY segment in RPT_FLDS into SQL rpt_flds_trvlsmry table.
-*   convert fields from UDINFO segment in RPT_FLDS into SQL rpt_flds_UDID table.
-*   convert fields from HIEORG1 and HIEORG2 segments in RPT_FLDS into SQL rpt_flds_hierarchy table.
-*        *** client and catg converted into parm and parm_values tables
-*
-*   convert fields from DVR segment in RPT_FLDS into SQL rpt_flds_DVR table.
-*   convert fields from OPTIONS segment in RPT_FLDS into SQL rpt_flds_hide_options table.
-*
-* SQL tables: rpt_flds_trvlsmry
-*             rpt_flds_UDID
-*             rpt_flds_hierarchy
-*             rpt_flds_DVR
-*			   rpt_flds_hide_options         
-*            
-**************************************************************************************************
-SET &ECHO = ALL;
SET ASNAME = ON

-* retrieve from TRVLSMRY segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT TRVLSMRY.ID AS 'ID'
	ORDER CHECKED
BY INST_KEY
WHERE TRVLSMRY.ID GT 0 
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE rpt_flds_trvlsmry
FIXFORM FROM HOLD
MATCH INST_KEY ID
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN


-* retrieve from UDINFO segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT UDINFO.UID AS 'UDID_SEQ' UINCL AS 'UDID_INCL' UDID UDDATA FROMVAL TOVAL
BY INST_KEY
WHERE UDINFO.UID GT 0
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE rpt_flds_UDID
FIXFORM FROM HOLD
MATCH INST_KEY UDID_SEQ
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN

-* retrieve hierarchy selections from hieorg1 and hieor2 
-* SEL_LEVEL may be different from LEVEL if alt_hierarchy EQ 'Y' in rollup database
-INCLUDE ConvertOldPath
-RUN

DEFINE FILE RPT_FLDS
  OPERATOR/A20 = IF HIEORG1.INCL3 EQ 1 THEN 'EQ' ELSE 'NE';
END
TABLE FILE RPT_FLDS
PRINT HIEORG2.SEQ_NO AS 'HIER_SEQ' LEVEL AS 'HIER_LEVEL'
MEMBER AS 'HIER_MEMBER' OPERATOR SEL_LEVEL
BY INST_KEY
WHERE HIEORG1.INCL3 GT 0 
&WHERE
ON TABLE HOLD
END
-RUN
      
-INCLUDE ConvertNewPath
-RUN

MODIFY FILE rpt_flds_hierarchy
FIXFORM FROM HOLD
MATCH INST_KEY HIER_SEQ
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN

-* retrieve from DVR segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT TRAN1 AS 'ALL_TRANSACTION' 
TRAN2 AS 'AIR_TRANSACTION'
TRAN3 AS 'CAR_TRANSACTION'
TRAN4 AS 'HOTEL_TRANSACTION'
TRAN5 AS 'FEE_TRANSACTION'
LSONLY AS 'LOST_SAVING_ONLY'
UNUSEDT1 AS 'ALL_UNUSEDTKT'
UNUSEDT2 AS 'APPLIED_UNUSEDTKT'
UNUSEDT3 AS 'EXPIRED_UNUSEDTKT'
UNUSEDT4 AS 'USED_UNUSEDTKT'

BY INST_KEY
WHERE TRAN1 NE 0 OR TRAN2 NE 0 OR TRAN3 NE 0 OR TRAN4 NE 0 OR LSONLY NE 0 OR
UNUSEDT1 NE 0 OR UNUSEDT2 NE 0 OR UNUSEDT3 NE 0 OR UNUSEDT4 NE 0
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE rpt_flds_DVR
FIXFORM FROM HOLD
MATCH INST_KEY
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN

-* retrieve from OPTIONS segment
-INCLUDE ConvertOldPath
-RUN

TABLE FILE RPT_FLDS
PRINT SEG OPTIONS.OPTION1
BY INST_KEY
WHERE OPTION1 EQ 1 OR OPTION2 EQ 1 OR OPTION3 EQ 1 OR OPTION4 EQ 1 OR OPTION5 EQ 1 OR OPTION6 EQ 1
&WHERE
ON TABLE HOLD
END
-RUN

-INCLUDE ConvertNewPath
-RUN

MODIFY FILE rpt_flds_hide_options
FIXFORM FROM HOLD
MATCH INST_KEY
  ON MATCH REJECT
  ON NOMATCH INCLUDE
DATA ON HOLD
END
-RUN