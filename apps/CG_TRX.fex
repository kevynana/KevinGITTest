-***** Steve - 09/21/2007 - Changed program to include more BY statements
-*****                      for multiple months being ran
-*5/23/14    - DEB  -       ADDED EX5EX5 TO BE EXCLUDED FROM EXTRACT
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
-******************************************************************************
 
-SET &&DRILLABLE = IF &&DEPLOY EQ 'L' AND EDIT(&&OUTTO,'$9') NE 'B'
- THEN 'N' ELSE 'Y';
 
-*SET TEMPERASE = OFF
SET ASNAMES=ON
 
EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS
 
-INCLUDE SetParmtime
-RUN
 
-**********************************************************************
-* DETERMINE IF WE HAVE WEB DEPLOYMENT OR LOCAL STAND ALONE
-* SET PATH FOR SAVING THE SETFILE
-**********************************************************************
-******LOCAL DEPLOYMENT
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&SETFNM= &&LOCPATH || &&DTTMSTRM ||'.FTM';
COPY &&SETF &&SETFNM
-RUN
-GOTO SETFILDEF;
 
-******WEB DEPLOYMENT
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL|| '\';
-SET &&SETFNM = &&WEBPATH || &&DTTMSTRM ||'.FTM';
COPY &&VARFWEB &&SETFNM
-INCLUDE SETFPARM
-RUN
-GOTO SETFILDEF;
 
-SETFILDEF
-INCLUDE APNDSETF
-RUN
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-INCLUDE SELCATQT
-RUN
 
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
-SET &&SMD = 'M';
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :TA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:TA1
 
EX AIRUSE
-RUN
 
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
-SET &WHATDATE = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'TRN_DATE' ELSE 'FST_DPT_DATE';
 
DEFINE FILE &&EXTRACT
POS/A3 = 'USA';
FARE_PAID/D12.2CS = SEG_AMT + SEG_TAX;
MRK_TKT_CNT/P8C    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
DIR_CP_CD/A6  = CITYPAIR.EMBARK||CITYPAIR.ROUTE;
TMNTH/MT = TRN_DATE;
TYEAR/YY = TRN_DATE;
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT TMNTH TYEAR DIR_CP_CD AIRLINE MRK_TKT_CNT POS CLASS TKT_NUM SEG_AMT/D12.2CS SEG_MILES
      SEG_NBR
 
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
WHERE DIR_CP_CD NE 'DT1DT1' OR 'RF1RF1' OR 'EX1EX1' OR 'FP2FP2' OR 'FP3FP3' OR 'EX5EX5'
-* WHERE TKT_NUM EQ '7619194546'
-* WHERE DIR_CP_CD EQ 'CVGRDU'
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
 
-INCLUDE RPTPARMS
 
ON TABLE HOLD AS 1HLD
END
-RUN
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
TABLE FILE 1HLD
PRINT POS SEG_AMT SEG_MILES SEG_NBR TKT_NUM MRK_TKT_CNT
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
ON TABLE HOLD AS 2HLD
END
-RUN
 
TABLE FILE 1HLD
SUM MRK_TKT_CNT AS 'TMRK'
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
ON TABLE HOLD AS 3HLD
END
-RUN
 
 
MATCH FILE 2HLD
PRINT POS TKT_NUM SEG_AMT SEG_MILES SEG_NBR MRK_TKT_CNT
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
ON TABLE HOLD
RUN
FILE 3HLD
SUM TMRK
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
AFTER MATCH HOLD AS 4HLD OLD-AND-NEW
END
-RUN
 
TABLE FILE 4HLD
PRINT POS TMNTH TYEAR DIR_CP_CD AIRLINE CLASS TMRK TKT_NUM SEG_AMT SEG_MILES SEG_NBR MRK_TKT_CNT
BY POS NOPRINT
ON TABLE HOLD AS 5HLD
END
-RUN
 
TABLE FILE 5HLD
SUM SEG_MILES AS 'TMILE' SEG_AMT AS 'TSEG'
BY TMNTH
BY TYEAR
BY TKT_NUM
ON TABLE HOLD AS 6HLD
END
-RUN
 
TABLE FILE 5HLD
PRINT DIR_CP_CD SEG_AMT SEG_MILES SEG_NBR POS AIRLINE CLASS TMRK MRK_TKT_CNT
BY TMNTH
BY TYEAR
BY TKT_NUM
ON TABLE HOLD AS 7HLD
END
-RUN
 
MATCH FILE 7HLD
PRINT DIR_CP_CD SEG_AMT SEG_MILES SEG_NBR POS AIRLINE CLASS TMRK MRK_TKT_CNT
BY TMNTH
BY TYEAR
BY TKT_NUM
ON TABLE HOLD
RUN
FILE 6HLD
SUM TMILE TSEG
BY TMNTH
BY TYEAR
BY TKT_NUM
AFTER MATCH HOLD AS 8HLD OLD-AND-NEW
END
-RUN
 
TABLE FILE 8HLD
SUM TSEG
COMPUTE X1/D8.2 = SEG_MILES/TMILE;
COMPUTE Y1/D12CM = X1 * TSEG;
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY TKT_NUM
BY SEG_NBR
ON TABLE HOLD AS 9HLD
END
-RUN
 
MATCH FILE 8HLD
PRINT DIR_CP_CD SEG_AMT SEG_MILES SEG_NBR POS AIRLINE CLASS TMRK TSEG MRK_TKT_CNT
BY TMNTH
BY TYEAR
BY TKT_NUM
BY SEG_NBR
ON TABLE HOLD
RUN
FILE 9HLD
SUM Y1
BY TMNTH
BY TYEAR
BY TKT_NUM
BY SEG_NBR
AFTER MATCH HOLD AS 11HLD OLD-AND-NEW
END
-RUN
 
TABLE FILE 11HLD
SUM Y1 POS MRK_TKT_CNT TMRK
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
ON TABLE HOLD AS 12HLD
END
-RUN
 
TABLE FILE 12HLD
PRINT POS AIRLINE CLASS Y1 MRK_TKT_CNT
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
ON TABLE HOLD AS 13HLD
END
-RUN
 
TABLE FILE 12HLD
SUM Y1 MRK_TKT_CNT
COMPUTE AVGF/D12CM = IF ((MRK_TKT_CNT LT 1) AND (MRK_TKT_CNT GT (-1))) THEN 0 ELSE
                        Y1/MRK_TKT_CNT;
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
ON TABLE HOLD AS 14HLD
END
-RUN
 
MATCH FILE 13HLD
PRINT POS Y1 MRK_TKT_CNT
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
ON TABLE HOLD
RUN
FILE 14HLD
SUM AVGF
BY TMNTH
BY TYEAR
BY DIR_CP_CD
BY AIRLINE
BY CLASS
AFTER MATCH HOLD AS 15HLD OLD-OR-NEW
END
-RUN
 
-SET &&RPTSUF = 'CONGTRX';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
 
 
-INCLUDE FDEFRPTS
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
DEFINE FILE 15HLD
NOWTOD/A8 WITH POS = HHMMSS (NOWTOD);
END
-RUN
 
TABLE FILE 15HLD
PRINT POS AS 'POINT,OF,SALE'
      TMNTH AS 'SALE,MONTH'
      TYEAR AS 'SALE,YEAR'
      DIR_CP_CD AS 'ORIGIN/,DESTINATION'
      AIRLINE AS 'CARRIER,CODE'
      CLASS AS 'BOOKING,CLASS'
      MRK_TKT_CNT AS 'NET,UNITS'
      Y1 AS 'NET,BOOKINGS'
      AVGF AS 'NET AVERAGE,FLIGHT PRICE'
BY POS NOPRINT
WHERE MRK_TKT_CNT NE 0
-* WHERE CLASS EQ ' '
&&OUTLINE1
&&OUTLINE2
FOOTING BOTTOM
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.100000,
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
TYPE=TITLE, FONT=TIMES NEW ROMAN, SIZE=9, BACKCOLOR=NONE, STYLE=BOLD, $
TYPE=HEADING, FONT=TIMES NEW ROMAN, SIZE=9, BACKCOLOR=NONE, STYLE=BOLD, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=7, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=FOOTING, FONT=TIMES NEW ROMAN, SIZE=7, STYLE=BOLD,$
TYPE=REPORT,GRID=OFF, $
ENDSTYLE
END
-RUN
 
 
-NEXT
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW=&&EXPARM;
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS
 
-IF &RECORDS GT 0 THEN GOTO DOIT;
 
-NORECORDS
-SET &&RPTSUF = 'MSG';
-INCLUDE FDEFRPTS
-RUN
JOIN SET_KEY IN BRPTINST TO SET_KEY IN BRPT_SET AS J1
DEFINE FILE BRPTINST
EMPTY/A35 = 'NO RECORDS FOUND FOR THIS SELECTION';
NOWTOD/A8 WITH INST_KEY = HHMMSS (NOWTOD);
END
TABLE FILE BRPTINST
-INCLUDE HEADAIR
PRINT EMPTY AS 'REPORT,MESSAGE'
BY ROLLUP_CODE AS 'ROLLUP,CODE'
BY RPT_NAME AS 'REPORT,NAME'
BY SELECT_LEV AS 'BREAK,LEVEL'
IF INST_KEY EQ
 '&&IKEY.EVAL'
IF RECORDLIMIT EQ 1
-INCLUDE FOOTERDT
ON TABLE SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-DOIT
 
-XXIT
 
 
