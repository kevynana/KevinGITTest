-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File RNKEXTRT.FEX
-********************************************************************
-********************************************************************
-*
-* This program extracts data for a drillable frequent flyer report
-* ADDED TRANTYPE INCLUDE - JK 12/15/14 STORY ID S-06492
-*12/17/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*07/12/16   RSB   S-20630 - New Employee ID database field
-*                           Added EMP_ID 
-********************************************************************
-* -*SET &ECHO=ALL;
-INCLUDE SETECHO
SET ASNAMES = ON

-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
  COMM_SEG/D12.2S   = SEG_COM;
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC1/A80      = &&LDESC;
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  NET_COMM/D12.2S     = (SEG_AMT - SEG_COM);
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT ELSE 0;
  NET_TKT_CNT/D8S    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_SEG_LOWEST/D9 = SEG_LOWEST;
  NEW_SEG_MILES/D9 = SEG_MILES;
  NEW_STAY/D9 = STAY;
  NEW_TRIP_LENGTH/D12 = TRIP_LENGTH;
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TOTAL_FARE/D12.2S = SEG_AMT + SEG_TAX;
  TRAN_MONTH/MT = TRN_DATE;
  VASAVINGS/D11.2CS = SEG_DISCOUNT - TOTAL_FARE;
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' THEN FARE_PAID ELSE 0;
  
-* ****DEFINES ON PREVIOUS DEFINES****
  CPMILE/D12.2CS = IF ((SEG_MILES LT 1) AND (SEG_MILES GT (-1))) THEN 0 ELSE
                      (FARE_PAID/SEG_MILES);
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  AIRFEE_CNT/D8CS = IF AIRLINE_FEE GT 0 THEN 1 ELSE 0;
  
-INCLUDE TRANTYPE  
-INCLUDE IDLEVDEFINE  
END
-RUN

TABLEF FILE &&EXTRACT
PRINT   AIR_KEY
        AIRLINE
        AIRLINE_FEE
        AIRFEE_CNT	
        AIR_MAIN.INTL_DOM	
        AR_BRANCH
        BR_CL_IDX
        CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
        CITYPAIR.ROUTE       AS 'RTE_APT_CD'	
        CLASS
        CLIENT_NUM	
        COMM_SEG
        EMB_CTY.COUNTRY_CODE AS 'EMB_CNTRY_CODE'
        CPMILE	
        DISSAVINGS
        DPT_TIME
        EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
        EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'
        FARE_BAS		
        FARE_PAID
        LEVEL_DESC	
        EMP_ID	
        LEVEL1
        LEVEL2	
        LEVEL3
        MRK_TKT_CNT
        NET_COMM	
        NET_FARE	
        NET_TKT_CNT         	
        NEW_SEG_LOWEST
        NEW_SEG_MILES
        NEW_STAY
        NEW_TRIP_LENGTH
        PASSNGR_NAME
        REFUSE_CODE
        REF_EXCH_CNT
        RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
        RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
        SAVINGS	
        SEG_AMT	
        SEG_COUNT	
        SEG_DISCOUNT	
        SEG_LOWEST
        SEG_NBR	
        SEG_STANDARD	
        SEG_TAX	
        STAY
        TICKET_BRANCH	
        TKT_AMOUNT	
        TKT_NUM	
        TKT_PURCH
        TKT_SORT
        TKT_TYPE	
        TOTAL_FARE	
        TRN_DATE	
        VAL_AIRLINE	
        VASAVINGS
        XDPT_DT	
        XPSNGR_NM	
        XTRAN_DT
         

-INCLUDE &AIRDATES

WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD FORMAT ALPHA 

END
-RUN



-*-EXIT


        
