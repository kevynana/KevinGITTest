-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Steve / Deb - added &&NETVOLUME under NETTKTS - 04/06/2004
-* DEB 5/10/04  CHANGED VOD_AMT DEFINE TO BALANCE TRAVEL SUMMARY
-*6/3/04 JK Changed Exg_amt/Ref_amt to calculate correctly
-*7/26/04    JOY                Added/Changed Norecords logic

-INCLUDE SETECHO

-DEFAULT &&MFLAG ='XXX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';

-SET &&DTSTY = 'DT';

-SET &&FIELD = &ROW_LABEL;
-SET &&MFLAG = &PARM1;


-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;

-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT

-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN

EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE GetForDrillDown
-RUN

-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   


-****
-* &&MFLAG WILL EQUAL ONE OF THE FOLLOWING: 
-* JAN, FEB, MAR, APR, MAY, JUN, JUL, AUG, SEP, OCT, NOV, DEC, YTD
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKT,CNT';
-SET &&TICOUNT = 'TTKT_ISS AS TKT,ISS';
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SAVINGS';
-SET &&LSAVECHK = ' ';
-SET &&DISCOUNT = ' ';
-SET &&DICHK = ' ';
-SET &&LOWEST = ' ';
-SET &&ATP = 'COMPUTE ATP/D12.2CS = IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0 ELSE FARE_AMT/NET_TKT_CNT; AS AVG,TKT,PRICE';


-IF &&MFLAG EQ 'Jan' THEN GOTO JAN
-  ELSE IF &&MFLAG EQ 'Feb' THEN GOTO FEB
-  ELSE IF &&MFLAG EQ 'Mar' THEN GOTO MAR
-  ELSE IF &&MFLAG EQ 'Apr' THEN GOTO APR
-  ELSE IF &&MFLAG EQ 'May' THEN GOTO MAY
-  ELSE IF &&MFLAG EQ 'Jun' THEN GOTO JUN
-  ELSE IF &&MFLAG EQ 'Jul' THEN GOTO JUL
-  ELSE IF &&MFLAG EQ 'Aug' THEN GOTO AUG
-  ELSE IF &&MFLAG EQ 'Sep' THEN GOTO SEP
-  ELSE IF &&MFLAG EQ 'Oct' THEN GOTO OCT
-  ELSE IF &&MFLAG EQ 'Nov' THEN GOTO NOV
-  ELSE IF &&MFLAG EQ 'Dec' THEN GOTO DEC
-  ELSE GOTO YTD; 

-JAN
-SET &&MCHK='WHERE TRNM EQ ''Jan''';
-SET &&SUBHEAD = 'January';
-GOTO ENDDATE

-FEB
-SET &&MCHK='WHERE TRNM EQ ''Feb''';
-SET &&SUBHEAD = 'February';
-GOTO ENDDATE

-MAR
-SET &&MCHK='WHERE TRNM EQ ''Mar''';
-SET &&SUBHEAD = 'March';
-GOTO ENDDATE

-APR
-SET &&MCHK='WHERE TRNM EQ ''Apr''';
-SET &&SUBHEAD = 'April';
-GOTO ENDDATE

-MAY
-SET &&MCHK='WHERE TRNM EQ ''May''';
-SET &&SUBHEAD = 'May';
-GOTO ENDDATE

-JUN
-SET &&MCHK='WHERE TRNM EQ ''Jun''';
-SET &&SUBHEAD = 'June';
-GOTO ENDDATE

-JUL
-SET &&MCHK='WHERE TRNM EQ ''Jul''';
-SET &&SUBHEAD = 'July';
-GOTO ENDDATE

-AUG
-SET &&MCHK='WHERE TRNM EQ ''Aug''';
-SET &&SUBHEAD = 'August';
-GOTO ENDDATE

-SEP
-SET &&MCHK='WHERE TRNM EQ ''Sep''';
-SET &&SUBHEAD = 'September';
-GOTO ENDDATE

-OCT
-SET &&MCHK='WHERE TRNM EQ ''Oct''';
-SET &&SUBHEAD = 'October';
-GOTO ENDDATE

-NOV
-SET &&MCHK='WHERE TRNM EQ ''Nov''';
-SET &&SUBHEAD = 'November';
-GOTO ENDDATE

-DEC
-SET &&MCHK='WHERE TRNM EQ ''Dec''';
-SET &&SUBHEAD = 'December';
-GOTO ENDDATE

-YTD
-SET &&MCHK='WHERE TRNY EQ ''Ytd''';
-SET &&SUBHEAD = 'Year-to-Date';
-GOTO ENDDATE


-ENDDATE


-FIELDS
-IF &&FIELD EQ 'Air Volume' OR 'Average Ticket Price' THEN GOTO AIRVOLUME ELSE
-IF &&FIELD EQ 'Net Volume' THEN GOTO NETVOLUME ELSE
-IF &&FIELD EQ 'Tickets Issued' THEN GOTO TKTISSD ELSE
-IF &&FIELD EQ 'Full Refunds' OR 'Full Refund Amounts' OR 'Full Refunds %' THEN GOTO REFUNDS ELSE
-IF &&FIELD EQ 'Full Exchanges' OR 'Full Exchange Amounts' OR 'Full Exchange %' THEN GOTO EXCHANGES ELSE
-IF &&FIELD EQ 'Voids' OR 'Void Amounts'  OR 'Void %' THEN GOTO VOIDS ELSE
-IF &&FIELD EQ 'Net Tickets' THEN GOTO NETTKTS ELSE
-IF &&FIELD EQ 'Domestic Air Volume' OR 'Domestic ATP' OR 'Domestic %' THEN GOTO DOMNVOL ELSE
-IF &&FIELD EQ 'Domestic Net Tickets' THEN GOTO DOMNTKT ELSE
-IF &&FIELD EQ 'International Air Volume' OR 'International ATP' OR 'International %' THEN GOTO INTNVOL ELSE
-IF &&FIELD EQ 'International Net Tickets' THEN GOTO INTNTKT ELSE
-IF &&FIELD EQ 'Lost Savings Amount' OR 'Lost Savings %' THEN GOTO LOSTSAVE ELSE
-IF &&FIELD EQ 'Contract Savings Amt' OR 'Contract Savings %' THEN GOTO VSAVE ELSE
-IF &&FIELD EQ 'Online Bookings %' THEN GOTO ONLINE ELSE GOTO AIRVOLUME;

-GOTO OUTSIDE

-AIRVOLUME
-SET &&RPT_TTL1 = '                     AIR VOLUME DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&NETVOLUME = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE


-NETVOLUME
-SET &&RPT_TTL1 = '                     NET VOLUME DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&FAREAMT = ' ';
-SET &&NETVOLUME = 'SEG_TOT AS NET,FARE';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ATP = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE


-TKTISSD
-SET &&RPT_TTL1 = '                   TICKETS ISSUED DETAIL   ';
-SET &&VOIDCHK = ' ';
-SET &&NTCOUNT = ' ';
-SET &&FAREAMT = ' ';
-SET &&NETVOLUME = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK=' ';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ATP = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-REFUNDS
-SET &&RPT_TTL1 = '                  TICKETS REFUNDED DETAIL  ';
-SET &&REFUNDCHK = 'WHERE RF_FLAG EQ ''F''';
-SET &&FAREAMT = 'FARE_AMT1 AS AIR,VOLUME';
-SET &&NETVOLUME = ' ';
-SET &&EXCHANGECHK = ' ';
-SET &&TICOUNT = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&VSVAMT=' ';
-SET &&ATP = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-EXCHANGES
-SET &&RPT_TTL1 = '                 TICKETS EXCHANGED DETAIL  ';
-SET &&EXCHANGECHK = 'WHERE EX_FLAG EQ ''F''';
-SET &&NETVOLUME = ' ';
-SET &&REFUNDCHK=' ';
-SET &&TICOUNT = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&FAREAMT = 'FARE_AMT2 AS AIR,VOLUME';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&VSVAMT=' ';
-SET &&ATP = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-VOIDS
-SET &&RPT_TTL1 = '                    TICKETS VOIDED DETAIL  ';
-SET &&REFUNDCHK=' ';
-SET &&EXCHANGECHK=' ';
-SET &&NETVOLUME = ' ';
-SET &&TICOUNT = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK='WHERE VOID_DATE NE 0';
-SET &&FAREAMT = 'FARE_AMT3 AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ATP = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-NETTKTS
-SET &&RPT_TTL1 = '                     NET TICKETS DETAIL    ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&NETVOLUME = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-DOMNVOL
-SET &&RPT_TTL1 = '             DOMESTIC AIR VOLUME DETAIL    ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&NETVOLUME = ' ';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-DOMNTKT
-SET &&RPT_TTL1 = '             DOMESTIC NET TICKETS DETAIL   ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&NETVOLUME = 'SEG_TOT AS NET,FARE';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&LSTSVGS= ' ';
-SET &&EXCHANGECHK=' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = ' ';
-SET &&VSVAMT=' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-INTNVOL
-SET &&RPT_TTL1 = '         INTERNATIONAL AIR VOLUME DETAIL    ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&NETVOLUME = ' ';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-INTNTKT
-SET &&RPT_TTL1 = '          INTERNATIONAL NET TICKETS DETAIL   ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&NETVOLUME = 'SEG_TOT AS NET,FARE';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = ' ';
-SET &&VSVAMT=' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE



-LOSTSAVE
-SET &&RPT_TTL1 = '                    LOST SAVINGS DETAIL    ';
-SET &&LOWEST = 'LOW_TOT AS LOWEST,AMOUNT';
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SAVINGS,AMOUNT';
-SET &&LSAVECHK = 'WHERE DISSAVINGS NE 0';
-SET &&NETVOLUME = ' ';
-SET &&NTCOUNT = ' ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ATP = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-VSAVE
-SET &&RPT_TTL1 = '             VALUE ADDED SAVINGS DETAIL    ';
-SET &&LOWEST = '';
-SET &&LSTSVGS = '';
-SET &&LSAVECHK = ' ';
-SET &&NETVOLUME = ' ';
-SET &&NTCOUNT = ' ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT = 'VSAVINGS AS CONTRACT,SAVINGS';
-SET &&ATP = ' ';
-SET &&ONLCHK = ' ';
-GOTO OUTSIDE

-ONLINE
-SET &&RPT_TTL1 = '                ONLINE BOOKINGS DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&NETVOLUME = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&EXCHANGECHK=' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&VSVAMT=' ';
-SET &&ONLCHK = 'WHERE DOD_TYPE EQ ''I'' OR ''J''';
-GOTO OUTSIDE

-OUTSIDE

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';

EX TS_JAMS2
-RUN
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN

-SET &&SMD = 'S';

EX AIRUSE
-RUN

-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN

-TYPE Before Extract
SET ASNAMES = ON 
-SET HOLDATTR = ON;
-RUN

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';


DEFINE FILE &&EXTRACT
  EXG_AMT/D12.2 = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'F') 
                            AND (SEG_COUNT EQ -1) 
                            THEN (SEG_AMT+SEG_TAX) ELSE 0;
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
 REF_AMT/D12.2 = IF (VOID_DATE EQ 0) AND (RF_FLAG EQ 'F')
                    AND (DOC_TYPE NE '1') AND (SEG_COUNT EQ -1)
                    THEN (SEG_AMT+SEG_TAX) ELSE 0;
TRNM/MT = IF '&&DATETYPE.EVAL' EQ 'I' THEN TRN_DATE ELSE FST_DPT_DATE;
TRNY/A3 = 'Ytd';

 TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
 TKT_ISS/D12.2 = IF (SEG_NBR EQ '01') AND (SEG_AMT+SEG_TAX GE 0)
                 AND (SEG_COUNT EQ 1) THEN 1 ELSE 0;
 VOD_AMT/D12.2 = IF (VOID_DATE NE 0) 
                 THEN (SEG_AMT+SEG_TAX) ELSE 0;

  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  VAS_AMT/D12.2 = IF (VOID_DATE EQ 0) THEN 
                     (SEG_DISCOUNT - SEG_AMT - SEG_TAX) ELSE 0;
-* DEFINES TO PREVIOUS DEFINES 
DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  AIRLINE.AIRLINE AS 'AIRLINE'
  DISSAVINGS
  DPT_TIME
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
  EXG_AMT
  FARE_PAID     
  NET_TKT_CNT
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  REF_AMT
  REFUSE_CODE
  REFUSAL_DESC
  TRN_DATE	
  TKT_ISS
  TKT_TYPE
  TRNM
  TRNY
  XDPT_DT
  XTRAN_DT    
  XPSNGR_NM
  TKT_NUM
  TKT_SORT
  SEG_AMT
  SEG_COUNT
  SEG_DISCOUNT
  SEG_LOWEST
  SEG_NBR
  SEG_STANDARD
  VAS_AMT
  VOD_AMT
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
-INCLUDE &AIRDATES
&&WHERE1
&&WHERE2
&&WHERE3

&&VOIDCHK
&&MCHK
&&REFUNDCHK
&&DICHK
&&EXCHANGECHK
&&ONLCHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN


-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

TABLE FILE RPTHOLD
SUM DISSAVINGS AS 'TDSV'
NET_TKT_CNT AS 'TTKT'
FARE_PAID AS 'TFARE'
SEG_AMT AS 'TSEG'
SEG_STANDARD AS 'TSTD'
SEG_LOWEST AS 'TLOW'
SEG_DISCOUNT AS 'TDIS'
TKT_ISS AS 'TISS'
REF_AMT AS 'TFARE1'
EXG_AMT AS 'TFARE2'
VOD_AMT AS 'TFARE3'
VAS_AMT AS 'VSAVE'
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE RPTHOLD
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID 
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      XDPT_DT
      XTRAN_DT    
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_LOWEST
      SEG_STANDARD
      SEG_DISCOUNT
      FARE_PAID   
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM     
      TKT_TYPE	
      DPT_TIME
      XDPT_DT     
      XTRAN_DT
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TDSV
      TFARE
      TFARE1
      TFARE2
      TFARE3
      TISS
      TTKT 
      TSEG
      TSTD
      TLOW
      VSAVE
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

TABLE FILE AIRHOLD2
PRINT AIRLINE     
      EMB_APT_NM
      SEG_AMT
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID 
      XPSNGR_NM
      REFUSE_CODE 
      RTE_APT_NM
      TDSV
      TFARE
      TFARE1
      TFARE2
      TFARE3
      TISS
      TSEG
      TSTD
      TLOW
      TKT_NUM     
      TKT_TYPE	
      TRN_DATE
      TTKT
      XDPT_DT     
      DPT_TIME
      VSAVE
      XTRAN_DT
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
 
DEFINE FILE AIRHOLD3
NET_TKT_CNT/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN XPSNGR_NM ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
TTKT_ISS/D9S = IF (SEG_NBR EQ '01') THEN TISS ELSE 0;
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||XPSNGR_NM;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
FARE_AMT1/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE1 ELSE 0;
FARE_AMT2/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE2 ELSE 0;
FARE_AMT3/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE3 ELSE 0;
VSAVINGS/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN VSAVE ELSE 0;
SEG_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSEG ELSE 0;
LOST_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TDSV ELSE 0;
LOW_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END
-RUN

TABLE FILE AIRHOLD3
-INCLUDE HEADSCH
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'REASON,CODE'
&&DEPARTD
&&DEPARTT
EMB_APT_NM AS 'ORG'
RTE_APT_NM AS 'DST'
AIRLINE AS 'CR'
&&NETVOLUME
&&FAREAMT
&&NTCOUNT
&&TICOUNT
&&LOWEST
&&LSTSVGS
&&VSVAMT
&&ATP

-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY XPSNGR_NM AS 'PASSENGER NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT BY TRN_DATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY TICKET_NMBR AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'

-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
-* ON TABLE COLUMN-TOTAL;
-* ON XPSNGR_NM RECOMPUTE MULTILINES AS 'TOTAL FOR'
ON TABLE RECOMPUTE AS 'TOTAL'
ON XTKT_SRT SKIP-LINE;


-INCLUDE FOOTERDT

-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-GOTO XXIT;

-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-XXIT;
