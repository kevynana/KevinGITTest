-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO

-* File AIREXTRT.FEX
-********************************************************************
-* MODIFICAIONS:
-* 5/22/1998 ADDED TRN_DATE CRITERIA TO WHERE STATEMENTS - LP
-* 7/09/1998 ADDED A DEFINE FOR TICKETLESS MIR TRANSACTIONS - LP
-* 10/17/01 ADDED A DEFINE (UPCOSTC) TO CAPTURE CTS BOOKINGS - JK
-* 10/24/01 ADDED A DEFINE (TRAN_MY) TO SHOW MONTH YEAR FORMAT - JK
-* 4/29/2002 CHANGED MRK_TKT_CNT DEFINE TO CORRECTLY BACK OUT PARTIAL
-*           REFUNDS AND EXCHANGES SC DB
-* 7/10/02  Changed ibook define to reflect new standards JK
-*12/21/04  CHANGED BOOKING TYPE DEFINES TO INCLUDE ADDTL DOD_TYPE -DV
-*10/11/05  ADDED DOD_TYPE H TO BOOKING TYPE DEFINES - DEB
-*02/28/13  CHANGED CLASS_CAT DEFINE DUE TO CLASS CATEGORY CHANGES - DV
-*1/23/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-* ADDED TRANTYPE INCLUDE - JK 12/15/14 STORY ID S-06492

1-********************************************************************
-*
-* This program extracts data for the series of basic ticket listing
-* reports.  Based on the user profile selected, one of many report
-* formats will be produced.  
-*
-********************************************************************

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
DEFINE FILE &&EXTRACT
  BASE_FARE/D12.2CS = SEG_AMT - SEG_COM;
  BOOK_DT/MDYY         = RESV_DATE;
  CARD_NUM/A12=EDIT(CREDIT_CARD, '$$999999999999$$$$');
  CARD1_NUM/A4=EDIT(CREDIT_CARD, '$$$$$$$$$$$$$$9999');
  CARD_NUM1/A16=EDIT(CREDIT_CARD,'$$9999999999999999');
  CCARD/A4 = EDIT(CREDIT_CARD, '$$9999$$$$$$$$$$$$');
  CCARD1/A4 =EDIT(CREDIT_CARD, '$$$$$$9999$$$$$$$$');
  CCARD2/A6 =EDIT(CREDIT_CARD, '$$999999$$$$$$$$$$');
  CCARD3/A6 =EDIT(CREDIT_CARD, '$$999999$$$$$$$$$$');
  CCARD5/A6 = EDIT(CREDIT_CARD, '$$$$$$$$$$$$999999');
  CCARD6/A6 = EDIT(CREDIT_CARD, '999999$$$$$$$$$$$$');
-*  CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'   
-*                              'D' 'DISCOUNT'  'B' 'BUSINESS');
  CLASS_CAT/A21 = DECODE CL_CAT ('F' 'FIRST CLASS'     'P' 'PREMIUM ECONOMY CLASS'   
                              'D' 'DEFAULT CLASS'  'B' 'BUSINESS CLASS'
                              'E' 'ECONOMY CLASS'   'U'  'UPGRADE' 
                              ' ' 'DEFAULT CLASS'); 
  COM_AMT/D12.2CS = SEG_COM;
  CRD_CRD/A2=EDIT(CREDIT_CARD, '99$$$$$$$$$$$$$$$');
  DEPT_MONTH/MT        = DPT_DATE;
  DEPT_YEAR/YY         = DPT_DATE;
  DIR_CP_CD/A50        = CITYPAIR.EMBARK||'-'||CITYPAIR.ROUTE;
-****************************************************************************
-* 10/3/00  IBISTL-RJ    FIELD WAS BEING TRUNCATED IN FOCUS 6, NEED TO RUN A 
-*                       SUBROUTINE IN WEBFOCUS TO TRUNCATE.
  DIR_CP_NMX/A72 = '(' || CITYPAIR.EMBARK || '/' 
           || CITYPAIR.ROUTE || ') ' | 
           EMB_APT.AIRPORT_NAME ||'---' || RTE_APT.AIRPORT_NAME;
  DIR_CP_NM/A50 = SUBSTR(72,DIR_CP_NMX,1,50,50,'A50');
-*
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  GROSS_FARE/D12.2CS  = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;
                        
  IBOOK/A42 = DOD_DESC;

  LEVEL_DESC/A60  = &&LDESC;
  MIR_TYPE/A24 = DECODE AUX_NON_MIR ('X' 'X-DUAL MIR'
                                    'N' 'N-NON-TICKETING MIR'
                                    'J' 'J-NON-FARING MIR'
                                    'A' 'A-NON-ACCOUNTING MIR'
                                    'B' 'B-ACCOUNTING MIR'
                                    'H' 'H-REGULAR TICKET MIR'
                                    'V' 'V-VOID'
                                    'E' 'E-ELECTRONIC TKT NETWORK'
                                    'L' 'L-ELECTRONIC TKT MIR'
                                    ' ' ' -MANUALLY ENTERED TRANS');
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
  MTG_NUM/A7 = EDIT(LEVEL3, '$$$$$$$$9999999');
  ND_CITY_NM/A50= NDO_CTY.CITY_NAME | NDD_CTY.CITY_NAME;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NET_TKT_CNT1/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_COUNT/D8CS = SEG_COUNT;
  NEW_SEG_DISC/D12.2CS = SEG_DISC;
  NEW_SEG_MILES/D9= SEG_MILES;
  NOREF_SEG_COUNT/D8CS = IF (RF_FLAG EQ ' ') AND
                            (EX_FLAG EQ ' ') THEN
                           NEW_SEG_COUNT ELSE 0;
  REF_E_CNT/D8CS      = IF (RF_FLAG NE ' ') OR
                           (EX_FLAG NE ' ') THEN
                            1 ELSE 0;  
  REF_EXCH_CNT/D8CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  REFEXCH/D12.2CS     = IF (RF_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 
                        IF (EX_FLAG NE ' ') THEN
                        SEG_AMT + SEG_TAX ELSE 0;                      
  SAVINGS/D11.2CS     = SEG_STANDARD - FARE_PAID;  
  TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;
  TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  TRAN_MY/MTYY = TRN_DATE;
  UPCOSTC/A1 = IF LEVEL1 EQ '0EM999' OR '0EM299' OR '0EM199' OR '0EM167' OR 
  '0EM166' OR '0EM165' OR '0EM163' OR '0EM161' OR '0EM112' OR '0EM093' OR
  '0EM092' OR '0EM089' OR '0EM085' OR '0EM079' OR '0EM072' OR '0EM069' OR
  '0EM061' OR '0EM057' OR '0EM056' OR '0EM055' OR '0EM054' OR '0EM053' OR
  '0EM053' OR '0EM051' OR '0EM033' OR '0EM032' OR '0EM027' OR '0EM026' OR
  '0EM022' THEN 'Y' ELSE 'N';
  XARR_DOW/W           = ARR_DATE;
  XARR_DT/MDYY         = ARR_DATE;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XDPT_DATE/MDY        = DPT_DATE;
  XPSNGR_NM/A15        = 
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTKT_DT/MDYY         = TKT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;


-* ****DEFINES ON PREVIOUS DEFINES****

  ABOOK_CNT/D8CS = IF ONLINE_TRADITIONAL EQ 'TRADITIONAL' THEN NET_TKT_CNT ELSE 0;
ABOOK_FARE/D12.2CS = IF ONLINE_TRADITIONAL EQ 'TRADITIONAL' THEN FARE_PAID ELSE 0;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' 
     THEN FARE_PAID ELSE 0;
  ARR_DAY/A3           = DECODE XARR_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
  DPT_DAY/A3           = DECODE XDPT_DOW (1 'MON' 2 'TUE' 3 'WED'
                                 4 'THU' 5 'FRI' 6 'SAT' 7 'SUN');
  GROSS_DSAVE/D12.2CS = IF (RF_FLAG EQ ' ') AND
                           (EX_FLAG EQ ' ') THEN
                           DISSAVINGS ELSE 0; 
  IBOOK_CNT/D8CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN NET_TKT_CNT ELSE 0;
IBOOK_FARE/D12.2CS = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN FARE_PAID ELSE 0;
  TRIPS/D8CS = IF STAY GT 0 THEN 1 ELSE
               IF STAY LT 0 THEN (-1) ELSE 0;
               
-INCLUDE TRANTYPE

END
-RUN

TABLEF FILE &&EXTRACT
PRINT 
  ABOOK_CNT
  ABOOK_FARE
  AGENT_NUM 
  AIR_KEY   
  AIR_MAIN.INTL_DOM 
  AIRLINE   
  AIRLINE_FEE   
  AIRLINE_NAME  
  AR_BRANCH
  ARR_DAY   
  ARR_TIME
  AUX_NON_MIR   
  BASE_FARE
  BOOK_DT   
  BR_CL_IDX 
  CARD_NUM  
  CARD_NUM1 
  CARD1_NUM 
  CCARD 
  CCARD1    
  CCARD2    
  CCARD3    
  CCARD5
  CCARD6
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'  
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'  
  CL_CAT    
  CLASS 
  CLASS_CAT 
  CLIENT_NAME
  CLIENT_NUM    
  COM_AMT   
  CRD_CRD   
  CREDIT_CARD   
  DEPT_MONTH
  DEPT_YEAR 
  DIR_CP_CD 
  DIR_CP_NM 
  DISSAVINGS    
  DOC_TYPE
  DOD_PAX_CAT   
  DPT_DAY   
  DPT_TIME  
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  EMB_APT.STATE_CODE AS 'EMB_STATE' 
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'  
  EMB_CTY.COUNTRY_CODE AS 'EMB_CNTRY_CODE'
  EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'  
  FARE_BAS
  FARE_PAID 
  FLIGHT    
  GROSS_DSAVE   
  GROSS_FARE    
  GROUP_CODE
  IBOOK
  IBOOK_CNT
  IBOOK_FARE
  INT_CODE  
  INVOICE_NUM   
  LEVEL_DESC    
  LEVEL1    
  LEVEL2    
  LEVEL3    
  MIR_TYPE  
  MRK_TKT_CNT   
  MTG_NUM
  ND_CITY_NM
  NET_TKT_CNT   
  NET_TKT_CNT1  
  NEW_ADV_PURCH 
  NEW_SEG_COUNT 
  NEW_SEG_DISC  
  NEW_SEG_MILES 
  NOREF_SEG_COUNT   
  ORG_DST
  PASSNGR_NAME  
  PNR_LOCATOR   
  REF_E_CNT 
  REF_EXCH_CNT  
  REFEXCH   
  REFUSAL_DESC  
  REFUSE_CODE   
  REFUSE_KEY            
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'  
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'  
  RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'  
  SAVINGS   
  SEG_AMT   
  SEG_COM
  SEG_COUNT 
  SEG_DESIG 
  SEG_DISCOUNT  
  SEG_LOWEST    
  SEG_MILES 
  SEG_NBR   
  SEG_STANDARD  
  SEG_TAX
  STAY  
  TICKET_BRANCH 
  TKT_AMOUNT
  TKT_DATE  
  TKT_NUM   
  TKT_PURCH 
  TKT_SORT  
  TKT_TYPE  
  TOUR_CODE 
  TRAN_MONTH    
  TRAN_MY
  TRIPS
  TRIP_LENGTH   
  TRN_DATE  
  UPCOSTC
  VAL_AIRLINE   
  VOID_DATE 
  XARR_DT   
  XDPT_DT   
  XDPT_DATE
  XPSNGR_NM
  XTKT_DT   
  XTRAN_DT  


-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
&&WHERE19
&&WHERE20
&&WHERE21
&&WHERE22
&&WHERE23


-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD

END
-RUN

