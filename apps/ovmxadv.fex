-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 5/28/03 CHANGED MXADV DEFINE TO ONLY HAVE 0-6 AND 7+ PER Teri JK

SET ASNAMES=ON

DEFINE FILE OVMXAHLD
FARE_PAID/D12.2 = SEG_AMT + SEG_TAX ;

CY/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
           TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
MXADV/A8  = IF (ADV_PURCH LT 7) THEN '00 - 06' ELSE
               '7+ DAYS'; 


NET_TKT_CNT/D12      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;

CYADV_TKT/D12 = IF CY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PYADV_TKT/D12 = IF PY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
END
-RUN

TABLE FILE OVMXAHLD
 SUM CYADV_TKT 
     PYADV_TKT
     PCT.CYADV_TKT AS 'CYADV_PCT'
     PCT.PYADV_TKT AS 'PYADV_PCT'
BY MXADV
ON TABLE HOLD AS OVADVC 
END
-RUN


DEFINE FILE OVADVC
CATEGORY/A30 = 'ADV PURCHASE';
CAT_ORDER/I2 = 2;
END
-RUN

TABLE FILE OVADVC
PRINT CATEGORY/A30 AND MXADV/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CYADV_TKT/D15 AS 'VOL_CURR' AND 
CYADV_PCT/D15 AS 'TKT_CURR' AND PYADV_TKT/D15 AS 'VOL_PRIOR' 
AND PYADV_PCT/D15 AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1; 
ON TABLE HOLD AS OVADVC1
END
-RUN

TABLE FILE OVADVC1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVADVC2
END
MODIFY FILE OV1_MAIN
FIXFORM FROM OVADVC2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVADVC2
END
-RUN


