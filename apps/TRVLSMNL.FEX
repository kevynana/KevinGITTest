-* File TRVLSUMA.FEX
-* This Travel Summary includes all the standard travel summary 
-* reports.
-**********************************************************************
-*DATE           NAME      DESCRIPTION
-*7/31/00      IBISTL-KR   CHANGED TEMPDISK TO TEMPDIR
-*6/14/05      LS          updated to use compound PDF.
-*9/9/05       DEB         COMMENTED OUT COMPANY GIF IMAGE DUE TO
-*                         DISTORTION
-**********************************************************************
-INCLUDE SETECHO
SET 3D=OFF
SET GRID=ON
SET VGRID=OFF
SET EMPTYREPORT=ON

-* Following set command determines of the reports created by this
-* Stream will be drillable
-SET &&DRILLABLE = IF &&DEPLOY EQ 'L' AND EDIT(&&OUTTO,'$9') NE 'B' 
- THEN 'N' ELSE 'Y'; 
-DEFAULT &&GLO_CHK = 'N';
-*

EX TSSETDB SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS
-INCLUDE SetParmtime
-RUN

-**********************************************************************
-* DETERMINE IF WE HAVE WEB DEPLOYMENT OR LOCAL STAND ALONE
-* SET PATH FOR SAVING THE SETFILE
-**********************************************************************
-******LOCAL DEPLOYMENT
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-*-SET &&LOCPATH = &&TNTDRV || '\TT\ ' ||&&ROLLUP || '\ '||&&UNAME.EVAL|| '\';
-SET &&SETFNM= &&LOCPATH || &&DTTMSTRM ||'.FTM';
COPY &&SETF &&SETFNM
-RUN
-GOTO SETFILDEF;

-******WEB DEPLOYMENT
-SETWEB

-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL|| '\';
-SET &&SETFNM = &&WEBPATH || &&DTTMSTRM ||'.FTM';
COPY &&VARFWEB &&SETFNM
-INCLUDE SETFPARM
-RUN
-GOTO SETFILDEF;

-SETFILDEF
-INCLUDE APNDSETF
-RUN

EX SELDESC2
-RUN

-**** start to implement compound PDF ***************************
-* TRAVEL SUMMARY TO BE SELECTIVE
DEFINE FILE BRPTFLDS
  FEXNAME/A10 WITH ORDER =DECODE TRVLSMRY.ID 
   (1 'EXECSUMN' 2 'EXECSUMN2' 3 'NETTKTN' 4 'AVGTKTN' 
    5 'AIRCLSD'  6 'YTDCLSD'   7 'AIRCLSI' 8 'YTDCLSI' 
    9 'LSTSAVD'  10 'LSTSAVI'   11 'ORGDSTN' 12 'YRORGDN'
   13 'CARRIERN' 14 'YRCARIERN' 15 'TSHTLSMN' 16 'TSCARSMN');
   
  DBUSED/A1 WITH ORDER = DECODE TRVLSMRY.ID
    (1 'S' 2 'S' 3 'S' 4 'S' 5 'S' 6 'S' 7 'S' 8 'S' 9 'S' 10 'S'
    11 'M' 12 'M' 13 'M' 14 'M' 15 'M' 16 'M');
END

TABLE FILE BRPTFLDS
PRINT FEXNAME DBUSED  
BY ORDER NOPRINT 
WHERE INST_KEY EQ &&IKEY
WHERE CHECKED EQ 1
ON TABLE SAVE AS PRGNAMES
END
-RUN

-SET &CNT = 1;
-SET &SDB = 'N';
-SET &MDB = 'N';
-SET &NOOFRPTS = &LINES;
-REPEAT TOEND &NOOFRPTS TIMES
-READ PRGNAMES NOCLOSE &FEXNAME.&CNT.10. &DBUSED.1.
-TYPE FEX NAME==>&FEXNAME.&CNT
-SET &SDB = IF (&SDB EQ 'Y') OR (&DBUSED EQ 'S') THEN 'Y' ELSE 'N';
-SET &MDB = IF (&MDB EQ 'Y') OR (&DBUSED EQ 'M') THEN 'Y' ELSE 'N';
-SET &REDEF.&CNT = IF (&CNT EQ 1) AND ('&&OUTFMT.EVAL' EQ 'PDF') THEN 'Y' 
- ELSE IF '&&OUTFMT.EVAL' NE 'PDF' THEN 'Y' ELSE 'N';

-SET &XOUTLINE1.&CNT = IF (&&OUTFMT EQ 'PDF') THEN 'ON TABLE SAVE AS SAVREP' 
- ELSE '&&OUTLINE1.EVAL';
-SET &XOUTLINE2.&CNT = IF (&&OUTFMT EQ 'PDF') AND (&NOOFRPTS GT 1 AND &CNT NE &NOOFRPTS)
- THEN 'FORMAT PDF OPEN' 
- ELSE IF (&&OUTFMT EQ 'PDF') AND (&NOOFRPTS EQ 1) THEN 'FORMAT PDF OPEN'
- ELSE '&&OUTLINE2.EVAL';

-SET &CNT = &CNT + 1;
-TOEND

-IF &SDB EQ 'N' THEN GOTO MDBRPT;
-**** To implement compound PDF ***************************

-*EXTRACT FROM SEGMENT DATABASES
EX TS_JAMS
-RUN

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN

EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

-SET &&SMD = 'S';
-RUN

EX TSAIRUSE
-RUN

-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN


-TYPE TSSHOLD
EX TSSHOLD
-RUN

-TYPE EXECMAI1
EX EXECMAI1
-RUN

-TYPE EXECMAI2
EX EXECMAI2
-RUN

USE CLEAR *
-RUN

EX CARUSE
-RUN

-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN


-TYPE EXECCARN
EX EXECCARN
-RUN

-TYPE EXECCARN1
EX EXECCARN1
-RUN


EX LIMUSE
-RUN

-SET &&RPTGRP = 'LIM';
EX UDIDGEN
-RUN

-TYPE EXECLIMN
EX EXECLIMN
-RUN

-TYPE EXECLIMN2
EX EXECLIMN2
-RUN

 


EX HTLUSE
-RUN
 

-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN


-TYPE EXECHTL1
EX EXECHTL1
-RUN

-TYPE EXECHTL3
EX EXECHTL3
-RUN


 

-TYPE EXECTOT1_L
EX EXECTOT1_L
-RUN

-TYPE EXECTOT2_L
EX EXECTOT2_L
-RUN


 

-*EXTRACT FROM MARKET DATABASES
-MDBRPT
-******** MOVED - START ****************
EX TS_JAMM
-RUN

FILEDEF AUSEINCL DISK &&AUSEPASS
FILEDEF CUSEINCL DISK &&CUSEPASS
FILEDEF HUSEINCL DISK &&HUSEPASS
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN

-INCLUDE SELCATQT
-RUN

EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
FILEDEF CUSEINCL CLEAR
FILEDEF HUSEINCL CLEAR
FILEDEF LUSEINCL CLEAR
-RUN

-SET &&SMD = 'M';
EX TSAIRUSE
-RUN

-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN

-TYPE TSMHOLD
EX TSMHOLD
-RUN
-******** MOVED - END ****************


-****START TO GENERATE REPORTS********
-SET &FEXNO = &CNT - 1;
-SET &CNT = 1;
-*-REPEAT TOEND2 &FEXNO TIMES
-REPEAT TOEND2 &NOOFRPTS TIMES

-SET &FEXNAME = &FEXNAME.&CNT;
-SET &&OUTLINE1 = &XOUTLINE1.&CNT;
-SET &&OUTLINE2 = &XOUTLINE2.&CNT;
-SET &&REDFPATH=&REDEF.&CNT;
-SET &&PGNUM = &CNT;

-TYPE FEX NAME==>&FEXNAME.&CNT
-TYPE &&OUTLINE1
-TYPE &&OUTLINE2
-TYPE &&REDFPATH

-IF '&FEXNAME.EVAL' EQ 'TSHTLSMN' THEN GOTO RUNHOTEL;
-IF '&FEXNAME.EVAL' EQ 'TSCARSMN' THEN GOTO RUNCAR;

EX &FEXNAME
-RUN
-GOTO INCREMENT2

-RUNHOTEL
EX HTLUSE
-RUN
-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN
-TYPE TSHTLSMN
EX TSHTLSMN
-RUN
-GOTO INCREMENT2

-RUNCAR
EX CARUSE
-RUN
-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN
-TYPE TSCARSMN
EX TSCARSMN
-RUN

-INCREMENT2
-SET &CNT = &CNT + 1;
-TOEND2
-RUN

-IF (&&OUTFMT NE 'PDF') THEN GOTO XXIT;

DEFINE FILE BRPTINST
   NOWTOD/A8 WITH INST_KEY = HHMMSS (NOWTOD);
END
TABLE FILE BRPTINST
BY INST_KEY NOPRINT
ON TABLE SAVE AS SAVREP
FORMAT PDF CLOSE
ON TABLE SET PAGE-NUM OFF
ON TABLE SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.50000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FOCEXEC=NONE, FONT=TIMES NEW ROMAN, SIZE=9, COLOR=BLACK, 
    BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=FOOTING, SIZE=6, $
TYPE=TITLE, SIZE=10, STYLE=BOLD, $
TYPE=HEADING, LINE=1, ITEM=1, SIZE=10, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=1, ITEM=2, SIZE=10, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=1, ITEM=3, SIZE=10, FONT=ARIAL,
    STYLE=BOLD, $
TYPE=HEADING, LINE=2, ITEM=1, SIZE=10, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=2, ITEM=2, SIZE=10, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=2, ITEM=3, SIZE=10, FONT=ARIAL,
    STYLE=BOLD, $
TYPE=HEADING, LINE=4, OBJECT=TEXT, STYLE=BOLD, $
TYPE=GRANDTOTAL, STYLE=BOLD, $ 

- IF &&ROLLUP EQ 'DSTM-R' THEN GOTO DSTY ELSE GOTO STDY;
-STDY
TYPE=REPORT, IMAGE=&&TNTPIC,  POSITION=(&&TNTSMPOS), SIZE=(&&TNTSMSIZ), $
-* TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(.5 .2), SIZE=(2.0 .4), $
-GOTO NEXT1

-DSTY
TYPE=REPORT, IMAGE=&&TNTPIC,  POSITION=(&&TNTSMPOS), SIZE=(&&TNTSMSIZ), $
-* TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(1.5 .15), SIZE=(.7 .5), $

-NEXT1
ENDSTYLE
END
-RUN

-XXIT
SET EMPTYREPORT=OFF
-RUN

