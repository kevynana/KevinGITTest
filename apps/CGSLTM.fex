-*1/28/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK

-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
-INCLUDE SETECHO
-RUN
 
-**********************************************************************
-* Conagra SLT reports (AROLL_DSC5)
-*-**********************************************************************
-**********************************************************************
-* Conagra SLT reports
-*-**********************************************************************
-TYPE "IN CGSLT"
 
-SET &&OUTFMT = 'EXL2K';
-SET &&XTN = '.XLS';
 
-INCLUDE SETECHO
SET ASNAMES=ON
 
 
-* -SET &&RPTSUF = 'SLTANALYSIS';
 
 
-* -INCLUDE FDEFRPTS
-* -RUN
 
 
-*********************************************************************************
-* Extract Air Data
-*********************************************************************************
 
DEFINE FILE SR_50
  ADV/A8 = IF (ADV_PURCH LT 7) THEN '0-6' ELSE 'OTHER';
  BRK_LVL/I2 = &&BRK_LVL;
  LEVEL_DESC/A60 = &&LDESC;
 
  FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D12    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
-* Defines on Previous
CURRENT/A1 = IF (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) THEN 'Y' ELSE 'N';
PRIOR/A1 = IF (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL) THEN 'Y' ELSE 'N';
-* Defines on Previous
CFARE/D12 = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
CNET_TKTS/D12 = IF CURRENT EQ 'Y' THEN NET_TKT_CNT ELSE 0;
CNET_TKTD/D12 = IF (INTDOM EQ 'D') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
CDOMFP/D12.2 = IF (INTDOM EQ 'D') AND (CURRENT EQ 'Y') THEN FARE_PAID ELSE 0;
CADVT/D12 = IF (ADV EQ '0-6') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
CONL/D12 = IF (ONLINE_TRADITIONAL EQ 'ONLINE') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
CTRAD/D12 = IF (ONLINE_TRADITIONAL EQ 'TRADITIONAL') AND (CURRENT EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PFARE/D12 = IF PRIOR EQ 'Y' THEN FARE_PAID ELSE 0;
PNET_TKTS/D12 = IF PRIOR EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PNET_TKTD/D12 = IF (INTDOM EQ 'D') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PDOMFP/D12.2 = IF (INTDOM EQ 'D') AND (PRIOR EQ 'Y') THEN FARE_PAID ELSE 0;
PADVT/D12 = IF (ADV EQ '0-6') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PONL/D12 = IF (ONLINE_TRADITIONAL EQ 'ONLINE') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
PTRAD/D12 = IF (ONLINE_TRADITIONAL EQ 'TRADITIONAL') AND (PRIOR EQ 'Y') THEN NET_TKT_CNT ELSE 0;
END
-RUN
 
TABLE FILE SR_50
PRINT CFARE
      CNET_TKTS
      CNET_TKTD
      CDOMFP
      CADVT
      CONL
      CTRAD
      LEVEL_DESC
      PFARE
      PNET_TKTS
      PNET_TKTD
      PDOMFP
      PADVT
      PONL
      PTRAD
BY AROLL_DSC5
BY AROLL_DSC6
WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)
WHERE VOID_DATE EQ 0
-IF &&SETF EQ 'CGSLTMSD' THEN GOTO DOM ELSE GOTO NODOM;
-DOM;
WHERE AIR_MAIN.INTL_DOM EQ 'D'
-NODOM;
-INCLUDE RPTPARMS
ON TABLE HOLD AS TRANONE
END
-RUN
 
 
TABLE FILE TRANONE
SUM CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
BY AROLL_DSC5
BY AROLL_DSC6
ON TABLE HOLD AS AIRHLD
END
-RUN
 
-*********************************************************************************
-* Extract Car Data
-*********************************************************************************
 
-IF &&FROMAVRP NE 'Y' GOTO :TA2;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:TA2
 
EX CARUSE
-RUN
 
DEFINE FILE CR_50
BOOKINGS/D12 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
 LEVEL_DESC/A60 = &&LDESC;
 RENTAL_DAY/D12 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
 NEW_RENTAL_AMT/D12.2 = RENTAL_AMT;
 CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
 PRIOR/A1 = IF INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
-* Defines on Previous
CBOOKINGS/D12 = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
CRENTDAYS/D12 =  IF CURRENT EQ 'Y' THEN RENTAL_DAY ELSE 0;
CRENTAMT/D12.2 = IF CURRENT EQ 'Y' THEN NEW_RENTAL_AMT ELSE 0;
PBOOKINGS/D12 = IF PRIOR EQ 'Y' THEN BOOKINGS ELSE 0;
PRENTDAYS/D12 =  IF PRIOR EQ 'Y' THEN RENTAL_DAY ELSE 0;
PRENTAMT/D12.2 = IF PRIOR EQ 'Y' THEN NEW_RENTAL_AMT ELSE 0;
 
END
 
-RUN
TABLE FILE CR_50
PRINT CBOOKINGS
      CRENTDAYS
      CRENTAMT
      PBOOKINGS
      PRENTDAYS
      PRENTAMT
      LEVEL_DESC
BY AROLL_DSC5
BY AROLL_DSC6
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL) AND (INVOICE_DATE LE &&TXDATE.EVAL)
-IF &&SETF EQ 'CGSLTMSD' THEN GOTO CDOM ELSE GOTO NOCDOM;
-CDOM;
WHERE INT_DOM EQ 'D'
-NOCDOM;
-INCLUDE RPTPARMS
ON TABLE HOLD AS CAR1
END
-RUN
 
 
TABLE FILE CAR1
SUM CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT
COMPUTE CAVGL/D8S = IF ((CBOOKINGS LT 1) AND (CBOOKINGS GT (-1))) THEN 0 ELSE
                       (CRENTDAYS/CBOOKINGS);
COMPUTE PAVGL/D8S = IF ((PBOOKINGS LT 1) AND (PBOOKINGS GT (-1))) THEN 0 ELSE
                       (PRENTDAYS/PBOOKINGS);
BY AROLL_DSC5
BY AROLL_DSC6
ON TABLE HOLD AS CARHLD
END
-RUN
-*********************************************************************************
-* Extract Hotel data
-*********************************************************************************
 
-IF &&FROMAVRP NE 'Y' GOTO :TA3;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:TA3
 
EX HTLUSE
-RUN
 
DEFINE FILE HR_50
 CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
 LEVEL_DESC/A60 = &&LDESC;
 NBR_DAYS/D12=NUM_DAYS;
 NEW_TOTAL_AMT/D12.2 = TOTAL_AMT;
 NNROOMS/D12 = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
 PRIOR/A1 = IF INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
-********** Defines on Previous -*****************************************************
BOOKINGS/D8 = IF NEW_TOTAL_AMT GE 0 THEN 1 ELSE (-1);
CHNIGHTS/D12 = IF CURRENT EQ 'Y' THEN NNROOMS ELSE 0;
PHNIGHTS/D12 = IF PRIOR EQ 'Y' THEN NNROOMS ELSE 0;
CHAMT/D12 = IF CURRENT EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
PHAMT/D12 = IF PRIOR EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
CBOOK/D12 = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
PBOOK/D12 = IF PRIOR EQ 'Y' THEN BOOKINGS ELSE 0;
END
-RUN
 
 
TABLE FILE HR_50
PRINT CHNIGHTS
      CHAMT
      CBOOK
      LEVEL_DESC
      PHNIGHTS
      PHAMT
      PBOOK
BY AROLL_DSC5
BY AROLL_DSC6
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL) AND (INVOICE_DATE LE &&TXDATE.EVAL)
-IF &&SETF EQ 'CGSLTMSD' THEN GOTO HDOM ELSE GOTO NOHDOM;
-HDOM;
WHERE INT_DOM EQ 'D'
-NOHDOM;
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTL1
END
-RUN
 
TABLE FILE HTL1
SUM CHNIGHTS CHAMT CBOOK PHNIGHTS PHAMT PBOOK
COMPUTE CHAVGL/D8S = IF ((CBOOK LT 1) AND (CBOOK GT (-1))) THEN 0 ELSE
                        (CHNIGHTS/CBOOK);
COMPUTE PHAVGL/D8S = IF ((PBOOK LT 1) AND (PBOOK GT (-1))) THEN 0 ELSE
                        (PHNIGHTS/PBOOK);
 
BY AROLL_DSC5
BY AROLL_DSC6
ON TABLE HOLD AS HTLHLD
END
-RUN
-**********************************************************************************************
-* Match air, car, htl data
-**********************************************************************************************
MATCH FILE AIRHLD
SUM CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
BY AROLL_DSC5
BY AROLL_DSC6
ON TABLE HOLD
RUN
FILE CARHLD
SUM CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
BY AROLL_DSC5
BY AROLL_DSC6
AFTER MATCH HOLD AS AIRCAR OLD-OR-NEW
END
-RUN
 
MATCH FILE AIRCAR
SUM CFARE CNET_TKTS CNET_TKTD CDOMFP CADVT CONL CTRAD PFARE PNET_TKTS PNET_TKTD PDOMFP PADVT PONL PTRAD
    CRENTDAYS CRENTAMT PRENTDAYS PRENTAMT CAVGL PAVGL
BY AROLL_DSC5
BY AROLL_DSC6
ON TABLE HOLD
RUN
FILE HTLHLD
SUM CHNIGHTS CHAMT CBOOK PHNIGHTS PHAMT PBOOK CHAVGL PHAVGL
BY AROLL_DSC5
BY AROLL_DSC6
AFTER MATCH HOLD AS AIRCARHTL OLD-OR-NEW
END
-RUN
 
-**************************************************************************
-* Generate Code for Multiple Worksheets
-**************************************************************************
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-INCLUDE MULTIWS
-RUN
 
