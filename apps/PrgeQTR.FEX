-* Initial Creation - Steve Cords - 04/06/2004

-* Copy over the focus and master files for the correct quartered
-* databases to K:\PurgeData\.... into the correct quarters folder.
-* This is to save off this data for future use - which will be
-* zipped and put onto a CD for storage.

-* Copy over the master files for the correct quartered databases
-* into D:\TNT\TTRACKER\DATA\
-* Manually have to change the quarter to update (create files - below)
-* Copy over the Rollup database to D:\TNT\TTRACKER\SUDATA\
-* CHANGE DATES BELOW TO NEW DATES AND RUN IN GGDEMO

-* In the AS/400 - Delete records from MKPCURA0
-* DELETE SQL('from mkpcura0 where CURDAT < 20011001')
-* And then REORGANIZE the file
-* SBMJOB CMD(RGZPFM FILE(TNTPDLIB/MKPCURA0))


USE CLEAR *
USE
    D:\TNT\TTRACKER\SUDATA\ROLLUP.FOC
    D:\TNT\TTRACKER\SUDATA\ROLLQTR.FOC
END

SET MESSAGE=ON
-*=======================================================================
SET ASNAMES=ON
DEFINE FILE ROLLUP
XL01/A1 = 'W' ;
END
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP AS 'ROLLUP_CODE'
      XL01   AS 'PR_STAMP'
IF QTR_ENABLE EQ 'X'
ON TABLE HOLD
END
MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-*======================================================================
USE CLEAR *
-RUN
SET TEMPDIR=D:\FOCTEMP
SET MESSAGE=ON
SET WARNING=OFF
SET YRTHRESH=30
SET DEFCENT=19
-RUN
-*=======================================================================
-TOP
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP COMP
BY ROLLUP_CODE NOPRINT
IF RECORDLIMIT EQ 1
IF QTR_ENABLE EQ 'X'
IF PR_STAMP EQ 'W'
ON TABLE SAVE AS CUSTS
END
-IF &RECORDS EQ 0 GOTO STOP;
-RUN
-READ CUSTS &&ROLLUP.A8 &&COMP.A6.
-RUN
-*=======================================================================
-*                 CREATE DATA FILES FOR PROCESSING
-*=======================================================================
-SET &&COMP = &&COMP;
-SET &&AIRD = 'A4'||&&COMP;
-SET &&TKTD = 'T4'||&&COMP;
-SET &&SEGD = 'S4'||&&COMP;
-SET &&MRKD = 'M4'||&&COMP;
-SET &&DSTD = 'D4'||&&COMP;
-SET &&CARD = 'C4'||&&COMP;
-SET &&HTLD = 'H4'||&&COMP;
-RUN

CREATE FILE &&AIRD
CREATE FILE &&TKTD
CREATE FILE &&SEGD
CREATE FILE &&MRKD
CREATE FILE &&DSTD
CREATE FILE &&CARD
CREATE FILE &&HTLD
-RUN

SET ASNAMES=ON
DEFINE FILE ROLLUP
XL01/A1 = 'Z' ;
END
TABLE FILE ROLLUP
BY ROLLUP NOPRINT
PRINT ROLLUP AS 'ROLLUP_CODE'
      XL01   AS 'PR_STAMP'
WHERE ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE HOLD
END
MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH UPDATE PR_STAMP
ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-GOTO TOP
-RUN
-*======================================================================
-STOP

-***********************
-* Copy these to GOTHAM GRINDS to Update in Production
-* Update ROLLUP and ROLLQTR

-* Need to update ROLLUP with the correct available date ranges
TABLE FILE ROLLUP
PRINT ROLLUP START_DATE
IF QTR_ENABLE EQ 'X'
IF START_DATE EQ '10012001'
ON TABLE HOLD
END

MODIFY FILE ROLLUP
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH COMPUTE START_DATE='01012002';
ON MATCH UPDATE START_DATE
ON NOMATCH REJECT
DATA ON HOLD
END

-* Need to update ROLLQTR with the correct date ranges
TABLE FILE ROLLQTR
PRINT ROLLUP Q4FMDT Q4TODT
ON TABLE HOLD
END

MODIFY FILE ROLLQTR
FIXFORM FROM HOLD
MATCH ROLLUP
ON MATCH COMPUTE Q4FMDT='20041001';
ON MATCH UPDATE Q4FMDT
ON MATCH COMPUTE Q4TODT='20041231';
ON MATCH UPDATE Q4TODT
ON NOMATCH REJECT
DATA ON HOLD
END
-EXIT
-*****************




