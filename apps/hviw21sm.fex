-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
-* File HVIEW2SM.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-*6/19/02      STEVE/DEB            COMMENTED OUT DRILLABLE STYLESHEET
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*12/22/11     GSE                  Removed parens from around CLIST for
-*                                  7.7.03
-*3/28/13    DEB                CHANGED AVGS DEFINE TO USE NBR_DAYS
-*                              INSTEAD OF NNROOMS 
-**********************************************************************
-SET &&HTL_LMT=3;
-SET &&CTY_LMT=10000;
SET ASNAMES = ON
-RUN

-***************************************************************************************
-* Hotel City Ranking
-***************************************************************************************
TABLE FILE HTLHOLD
SUM BOOKINGS
   NBR_DAYS
    NEW_TOTAL_AMT
    NNROOMS
    PREF_NNR
BY CITY_ST
BY CHAIN_CODE
ON TABLE HOLD AS HOLD1
END
-RUN


-IF &LINES EQ 0 THEN GOTO NoCurHtl;

TABLE FILE HOLD1
SUM CNT.CHAIN_CODE AS 'HTL_CNT'
BY CITY_ST
BY CHAIN_CODE
ON TABLE HOLD AS HTLCNT
END
-RUN

MATCH FILE HOLD1
PRINT BOOKINGS
      NEW_TOTAL_AMT
      NNROOMS
      PREF_NNR
      NBR_DAYS
BY CITY_ST
BY CHAIN_CODE
ON TABLE HOLD
RUN
FILE HTLCNT
SUM HTL_CNT
BY CITY_ST
BY CHAIN_CODE
AFTER MATCH HOLD AS HTLCNT1 OLD-AND-NEW
END
-RUN

TABLE FILE HTLCNT1
PRINT BOOKINGS
    CHAIN_CODE
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
    PREF_NNR
    NBR_DAYS
BY CITY_ST
RANKED BY HIGHEST NNROOMS NOPRINT
ON TABLE HOLD AS HOLD2
END
-RUN

DEFINE FILE HOLD2
 RANK_LIMIT/I5 = &&HTL_LMT;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN

TABLE FILE HOLD2
PRINT BOOKINGS
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
    PREF_NNR
    NBR_DAYS
BY CITY_ST
BY RANK_VALUE
ON TABLE HOLD AS HOLD3
END
-RUN


TABLE FILE HOLD3
SUM BOOKINGS
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
    PREF_NNR
    NBR_DAYS
BY CITY_ST
BY RANK_VALUE
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE HOLD1
SUM BOOKINGS
    NEW_TOTAL_AMT
    NNROOMS
    PREF_NNR
BY CITY_ST
ON TABLE HOLD AS RHOLD1
END
-RUN

TABLE FILE RHOLD1
PRINT *
RANKED BY HIGHEST NNROOMS AS 'DAYS_RANK'
ON TABLE HOLD AS RHOLD2
END
-RUN

MATCH FILE HOLD4
PRINT *
BY CITY_ST
ON TABLE HOLD
RUN
FILE RHOLD2
SUM DAYS_RANK
BY CITY_ST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN

TABLE FILE HOLD5
PRINT *
RANKED BY HIGHEST DAYS_RANK
ON TABLE HOLD AS HOLD6
END
-RUN


DEFINE FILE HOLD6
  CTY_LIMIT/I2 = &&CTY_LMT;
  CLIMIT/A2    = EDIT(CTY_LIMIT) ;
  CRANK/D5     = RANK;    
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF CITY_ST NE LAST CITY_ST THEN (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6=EDIT(FRANKX,'$999999');
  CTY_VALUE/A9 = IF CTY_LIMIT EQ 0 THEN BRANK ELSE
                 IF LIST GT CTY_LIMIT THEN 'All Other' ELSE FRANK;  
  CTY_PRINT/A36 = IF CTY_VALUE LT CLIMIT THEN CITY_ST ELSE 'All Other';
  RANK_VAL/A1 =  IF CTY_VALUE EQ 'All Other' AND RANK_VALUE EQ 'OTHER' THEN ' ' ELSE EDIT(RANK_VALUE, '$$$$9');
END
-RUN

TABLE FILE HOLD6
SUM BOOKINGS
  HTL_CNT
  NEW_TOTAL_AMT
  NNROOMS
  PREF_NNR
  NBR_DAYS
BY CTY_VALUE
BY CTY_PRINT
BY RANK_VAL
ON CTY_PRINT RECOMPUTE AS 'TOTAL FOR'
ON TABLE HOLD AS HOLD7
END
-RUN


DEFINE FILE HOLD7
HNNROOMS/D12 = IF RANK_VAL NE 'R' THEN NNROOMS ELSE 0;
END

TABLE FILE HOLD7
SUM BOOKINGS
    HNNROOMS
    HTL_CNT
    NEW_TOTAL_AMT
    NNROOMS
    PREF_NNR
    NBR_DAYS
BY CTY_VALUE
BY CTY_PRINT
ON TABLE HOLD AS HOLD8
END
-RUN   

TABLE FILE HOLD8
SUM BOOKINGS HNNROOMS HTL_CNT NEW_TOTAL_AMT NNROOMS 
COMPUTE AVGRR/D12 = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                       NEW_TOTAL_AMT/NNROOMS;
COMPUTE AVGS/D12 =  IF ((BOOKINGS LT 1) AND (BOOKINGS GT (-1))) THEN 0 ELSE
                       NBR_DAYS/BOOKINGS;
COMPUTE T3PCT/D6 = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                       ((HNNROOMS/NNROOMS)*100);
COMPUTE PRFPCT/D6% = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                       ((PREF_NNR/NNROOMS)*100);
BY CTY_VALUE
BY CTY_PRINT
ON TABLE HOLD AS BHOLD1
END
-RUN


TABLE FILE BHOLD1
PRINT BOOKINGS HNNROOMS HTL_CNT NEW_TOTAL_AMT NNROOMS PREF_NNR CTY_VALUE 
      AVGRR AVGS T3PCT PRFPCT NBR_DAYS
BY CTY_PRINT
ON TABLE HOLD AS BHOLD2
END
-RUN

DEFINE FILE BHOLD2
FLAG/A1 = 'A';
END
-RUN
TABLE FILE BHOLD2
PRINT NNROOMS NEW_TOTAL_AMT PREF_NNR BOOKINGS HTL_CNT AVGRR AVGS T3PCT PRFPCT NBR_DAYS
BY FLAG
BY CTY_VALUE
BY CTY_PRINT
ON TABLE HOLD AS BPHOLD2 FORMAT FOCUS
END
-RUN

DEFINE FILE BHOLD2
CTY_VALUE/A9 = 'Total';
CTY_PRINT/A36 = 'Total';
FLAG/A1 = 'B';
END
-RUN
TABLE FILE BHOLD2
SUM NNROOMS NEW_TOTAL_AMT BOOKINGS HTL_CNT 
COMPUTE AVGRR/D12 = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                       NEW_TOTAL_AMT/NNROOMS;
COMPUTE AVGS/D12 =  IF ((BOOKINGS LT 1) AND (BOOKINGS GT (-1))) THEN 0 ELSE
                       NBR_DAYS/BOOKINGS;
COMPUTE T3PCT/D6 = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                       ((HNNROOMS/NNROOMS)*100);
COMPUTE PRFPCT/D6% = IF ((NNROOMS LT 1) AND (NNROOMS GT (-1))) THEN 0 ELSE
                       ((PREF_NNR/NNROOMS)*100);                       

BY FLAG
BY CTY_VALUE
BY CTY_PRINT
ON TABLE HOLD AS BTTL
END
-RUN

TABLE FILE BTTL
PRINT NNROOMS NEW_TOTAL_AMT PREF_NNR BOOKINGS HTL_CNT AVGRR AVGS T3PCT PRFPCT NBR_DAYS
BY FLAG
BY CTY_VALUE
BY CTY_PRINT
ON TABLE HOLD AS BTTL1 FORMAT FOCUS
END
-RUN


-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &BPHOLD1=&HLDPATH || 'BPHOLD2.FOC';
-SET &BPHOLD2=&HLDPATH || 'BTTL1.FOC';
-RUN

USE CLEAR *
-RUN

EX SUUSES
-RUN

USE ADD 
&BPHOLD1 AS BPHOLD2
&BPHOLD2 AS BPHOLD2
END
-RUN

TABLE FILE BPHOLD2
PRINT NNROOMS NEW_TOTAL_AMT PREF_NNR BOOKINGS HTL_CNT AVGRR AVGS T3PCT PRFPCT NBR_DAYS
BY CTY_VALUE
BY CTY_PRINT
ON TABLE HOLD AS BPHOLD3
END
-RUN

TABLE FILE BPHOLD3 
PRINT NNROOMS NEW_TOTAL_AMT PREF_NNR BOOKINGS HTL_CNT AVGRR AVGS T3PCT PRFPCT NBR_DAYS
BY CTY_VALUE 
BY CTY_PRINT 
ON TABLE HOLD AS HTLSUM
END
-RUN


-NoCurHtl



-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-**********************************************************************
DEFINE FILE HTLSUM
ACMPCT/A7 = FTOA(T3PCT,'(D6)',ACMPCT);
ACMPCT1/A8 = ACMPCT | '%';
NOWTOD/A8 WITH CTY_VALUE = HHMMSS (NOWTOD);
END

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN


TABLE FILE HTLSUM
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10


-INCLUDE FOOTERSM
ON TABLE COLUMN-TOTAL
-*ON TABLE SET STYLE &&SMSTY
-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**********************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&PASTYS

-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-**********************************************************************
-* -IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;
-* -INCLUDE &&DSTY

-SKIP_DRILL

ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;

EX REPORT_3

-SKIP_R3
