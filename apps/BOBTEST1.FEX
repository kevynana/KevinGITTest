-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-* Focexec is used to help debug problems with the eTTek Review
-* reports.  This focexec will simulate the code that is executed
-* through Report Caster for jobs that are scheduled in a 'Run Now'
-* state.  Once the variables are supplied this job can be executed 
-* multiple times without having to log back into the User Interface
-* of the application and reschedule the report(s).
-*
-*
-* Changable variables
-*
-*   &&ROLLUP - Company you are running for
-*
-*   &&USERID - Userid of person running reports
-*
-*   &&OUTFMT - Output Format of Reports
-SET &&OUTFMT='PDF';
-*
-*   &&OUTTO - Ouput destination 
-SET &&OUTTO='T';
-*
-*   &&RP_PM - Report Group type
-*
-*   &&RT_PM - Report name 
-*
-*   &&SE_PM - Set file name
-*
-*   &&SK_PM - Set Key
-*
-*   &&SN_PM - Set name
-*
-*   &&ST_PM - Stream
-*
-*   &&IN_PM - Instance Key
-*
-*
-RECHECK

DEFINE FILE RPT_QUE
SET_KEY/A30=ROLLUP_CODE | SET_ID;
END
TABLE FILE RPT_QUE
PRINT ROLLUP_CODE
      USER_NAME
      RPT_TYPE
      RPT_NAME
      GLOBAL_PARM
      SET_KEY
      SET_NAME
      INST_KEY
      INSTANCE_ID
      RPT_STREAM
ON TABLE SAVE AS HLDRQUE
WHERE ROLLUP_CODE EQ 'METX-R'
WHERE EX_FLAG EQ 'M'
WHERE INTIME EQ 'A'
WHERE RECORDLIMIT EQ 1
END
-RUN

-IF &RECORDS EQ 0 GOTO DONE;   
-*
-
-READ HLDRQUE &&ROLLUP.A8, &&USERID.A20, &&RP_PM.A3, &&RT_PM.A50,
- &&SE_PM.A8, &&SK_PM.A30, &&SN_PM.A40, &&IN_PM.A72, &&IX_PM.A22, &&ST_PM.A8
-RUN
? &&

TABLE FILE ROLLUP
ON TABLE SET PAGE-NUM OFF
PRINT  ROLLUP_CODE COMP UPD_STAT CLSELECT
WHERE ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE NOTOTAL
ON TABLE HOLD AS HOLDPASS
END
-RUN
-READ HOLDPASS &X.8 &COMP.6 &USTAT.1 &CLSEL.1
-RUN
-SET &COMPID = 'D:\TNT\TTRACKER\DATA\AL' || &COMP || '.FOC AS HIER50';
USE ADD
&COMPID.EVAL
END
-RUN

CREATE FILE CONTROL
-RUN

-TYPE Before first Control MODIFY
MODIFY FILE CONTROL
FIXFORM ROLLUP_CODE/A8
FIXFORM COMP/A6
FIXFORM UPD_STAT/A1
FIXFORM CL_SEL/A1
COMPUTE CONTROL_ID/A3  =  'PGM';
COMPUTE USER_ID/A20 = '&&USERID.EVAL';
COMPUTE SECURITY_LEVEL/I1 = 9;
COMPUTE WEB_RUN/A1 = 'Y';
COMPUTE WDOMAIN/A50 = 'https://www.etraveltracker.com/asp/';
MATCH CONTROL_ID
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HOLDPASS
END
-RUN
CREATE FILE CATEGORY
JOIN ROLLUP_CODE IN CONTROL TO ROLLUP_CODE IN CLIENT AS AA
TABLE FILE CLIENT
BY CATEGORY NOPRINT
SUM CATEGORY
WHERE ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE HOLD AS HOLDCAT
END
-RUN
MODIFY FILE CATEGORY
FIXFORM CATLIST/A15
MATCH CATLIST
ON NOMATCH INCLUDE
ON MATCH INCLUDE
DATA ON HOLDCAT
END
JOIN CLEAR AA
-RUN

DEFINE FILE RPT_INST
E_FLAG/A1 WITH INST_KEY ='N';
R_SERVER/A20 WITH INST_KEY ='&&USERID.EVAL';
END
TABLE FILE RPT_INST
PRINT E_FLAG AS EXECUTE_FLAG 
      R_SERVER AS RPT_SERVER
BY INST_KEY
WHERE INST_KEY EQ 
'&&IN_PM.EVAL'
ON TABLE HOLD AS HOLD1
ON TABLE SET ASNAMES ON
END
MODIFY FILE RPT_INST
FIXFORM FROM HOLD1
MATCH INST_KEY
  ON NOMATCH REJECT
  ON MATCH UPDATE EXECUTE_FLAG RPT_SERVER
DATA ON HOLD1
END
-RUN

? PATH
-RUN

EX WRUNINDV outfmt='&&OUTFMT.EVAL',
outto='&&OUTTO.EVAL',
rp_pm='&&RP_PM.EVAL',
rt_pm='&&RT_PM.EVAL',
se_pm='&&SE_PM.EVAL',
sk_pm='&&SK_PM.EVAL',
sn_pm='&&SN_PM.EVAL',
st_pm='&&ST_PM.EVAL',
ix_pm='&&IX_PM.EVAL',
in_pm='&&IN_PM.EVAL'
-RUN

TABLE FILE RPT_QUE
PRINT INST_KEY
WHERE INST_KEY EQ 
'&&IN_PM.EVAL'
ON TABLE HOLD AS HOLD1
END
MODIFY FILE RPT_QUE
FIXFORM FROM HOLD1
MATCH INST_KEY
 ON NOMATCH REJECT
 ON MATCH DELETE
DATA ON HOLD1
END
-RUN
-*-GOTO RECHECK


-DONE