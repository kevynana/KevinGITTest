-*======================================================================
-*  DATE        PROGRAMMER  CHANGE
-*  ----        ----------  ---------------------------------
-*  07/21/2015  GSE         S-08380 Initial version
-*======================================================================
-* File Populate_TKT_Purch_Tempfex.fex
-*
SET HOLDLIST = PRINTONLY
SET ASNAMES = ON
SET PCOMMA = ON
SET FIXFRMINPUT = NONCOND
SET MESSAGE = OFF
-*
FILEDEF TKTPROLL DISK TTSYSDATA/TKT_Purchase_rollups.txt
-RUN
-*
DEFINE FILE ROLLUP
FROM_DATE/MDYY = DATEADD(END_DATE, 'D', -6);
END
TABLE FILE ROLLUP
PRINT COMP QTR_ENABLE END_DATE FROM_DATE
BY ROLLUP_CODE
WHERE ROLLUP_CODE EQ (TKTPROLL)
WHERE ROLLUP_CODE NE 'ABCD2-R' OR 'ABCD3-R' OR 'TOTAL-R'
WHERE UPD_ENABLE EQ 'X'
WHERE COMP NE ' '
ON TABLE HOLD AS ROLLLIST
END
-RUN
-*
-READROLL
 
-READFILE ROLLLIST
-RUN
-IF &IORETURN NE 0 GOTO XXIT;
-*
EX REVIEW_50_SQL_LOADS/concat_all_dbs_bench ROLL=&ROLLUP_CODE.EVAL,FID=SEG,CLEAR=Y
-RUN
-*
JOIN VAL_AIRLINE IN SR_50 TO AIRLINE IN AIRLINE AS J0
-*
DEFINE FILE SR_50
DBL_AIRLINE/D3L = EDIT(AIRLINE_NUM);
AIRLINE_TKT_NUM/A13 = (FTOA(DBL_AIRLINE,'(D3L)','A3')) | TKT_NUM;
FIRST_DEPART_DATE_ALPHA/A8YYMD = GETTOK(DEPART_DATES, 144, 1, '-', 8, 'A8MDYY');
FIRST_DEPART_DATE_DATE/YYMD = FIRST_DEPART_DATE_ALPHA;
FINAL_ARRIVAL_DATE/MDYY = DATEADD(FIRST_DEPART_DATE_DATE, 'D', TRIP_LENGTH);
END
TABLE FILE SR_50
SUM
      COMPUTE STATUS/A1 = ' ';
      COMPUTE ADD_DATE/HYYMDI = HGETC(8, 'HYYMDI');
BY TOTAL COMPUTE ROLLUP_CODE/A8 = '&ROLLUP_CODE.EVAL';
BY AIRLINE_TKT_NUM AS TKT_NUM
-*WHERE RECORDLIMIT EQ 20
WHERE INTL_DOM EQ 'I'
WHERE FINAL_ARRIVAL_DATE FROM &FROM_DATE.EVAL TO &END_DATE.EVAL
WHERE CATEGORY EQ 'BUSINESS'
ON TABLE HOLD AS TEMPUPDT
END
-RUN
-*
-SET &UPDRECS = &LINES;
-*
FILEDEF REJECT DISK REJECT
-RUN
-*
SET MESSAGE = ON
MODIFY FILE TKT_PURCHASE_TEMP
  LOG DUPL ON REJECT MSG OFF
FIXFORM FROM TEMPUPDT
MATCH ROLLUP_CODE TKT_NUM
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON TEMPUPDT
END
SET MESSAGE = OFF
-RUN
-*
-TYPE &ACCEPTS
-TYPE &REJECTS
-SET &SUPPRESS = IF &ACCEPTS GT 0 THEN FOC_NONE ELSE ' ';
-RUN
TABLE FILE ROLLUP
BY ROLLUP_CODE NOPRINT
WHERE RECORDLIMIT EQ 1
HEADING
" "
" TKT_Purchase_Temp Processing Results "
" Records retrieved - &UPDRECS "
" Records inserted - &ACCEPTS "
" Records rejected due to duplicate Rollup/TKT_NUM - &REJECTS "
" Warning - No records were inserted into TKT_Purchase_Temp &SUPPRESS "
" "
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE SET STYLE *
TYPE=HEADING,
     LINE=2,
	 JUSTIFY=CENTER,
	 STYLE=BOLD,
	 $
TYPE=HEADING,
     LINE=6,
	 STYLE=BOLD,
	 COLOR=RED,
	 SIZE=11,
	 $
ENDSTYLE
END
-*
-XXIT
