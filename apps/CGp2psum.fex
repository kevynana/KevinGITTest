-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Removed Conagra Special from here so that they could have the standard
-* report as well as their specialized version JK 10/16/03
-* Added Current Total Average Ticket Price per Patti JK 2/24/04
-* Updated per Neely to remove some fields that were not requested JK 7/3/14
-*10/29/14 Added Passenger Name - JK
-*1/12/15 Updated to sort by level1 - STORY ID - 06904 - JK
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-*  09/21/2016  REJ  S-24432 ER5 Testing updates.  
-*11/16/2016 - RSB - S-26926 - Change reports to read employee ID from EMP_ID
-*                               Changed LEVEL1 to EMP_ID

-* 02/24/2017 - DLV - S-22486 CHANGED ettek Review to Post-trip Reporting

-INCLUDE SETECHO
SET ASNAMES=ON
 
-IF &&FROMAVRP EQ 'Y' GOTO :AB1;
-SET &FMM=EDIT(&&FROMDT,'$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'9999');
-SET &TOM=EDIT(&&TODT,'$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'9999');
-GOTO :AB1_CONT;
-:AB1
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
 
-:AB1_CONT
 
-SET &&SP = '|';
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&CSLP = IF &&RCNCT.EVAL GE 10 OR &&CSL.EVAL GE 10 THEN 60 ELSE 68;
-SET &&ROLLCS11 = TRUNCATE(&&ROLLCS);
-SET &&RCNCT1 = TRUNCATE(&&ROLLCNCT);
-SET &&RPTSUF = &TOY.EVAL||&TOM.EVAL||&TOD.EVAL;
 
 
TABLE FILE AIRHLD
PRINT ONLT 21ADVT 14ADVT 7ADVT PREFAIRT DISSAVINGS LSTT HTLBT FLAGA ROLLUP_CODE EXCH
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT_NUM
BY RES_SYS
ON TABLE HOLD AS AHLD
END
-RUN
 
 
TABLE FILE CARHLD
PRINT CPREF1 PREF_CSIZ1 ONLC1 FLAGC
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT_NUM
BY RES_SYS
ON TABLE HOLD AS CHLD
END
-RUN
 
MATCH FILE AHLD
PRINT ONLT 21ADVT 14ADVT 7ADVT PREFAIRT DISSAVINGS LSTT HTLBT FLAGA ROLLUP_CODE EXCH
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT_NUM
BY RES_SYS
ON TABLE HOLD
RUN
FILE CHLD
SUM CPREF1 PREF_CSIZ1 ONLC1 FLAGC
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT_NUM
BY RES_SYS
AFTER MATCH HOLD AS ACHLD OLD-OR-NEW
END
-RUN
 
DEFINE FILE ACHLD
TKT1/A10 = IF TKT_NUM EQ 'CAR ONLY' THEN 'CAR ONLY' ELSE TKT_NUM;
END
-RUN
TABLE FILE ACHLD
PRINT ONLT 21ADVT 14ADVT 7ADVT PREFAIRT DISSAVINGS LSTT HTLBT CPREF1 PREF_CSIZ1 ONLC1 FLAGA FLAGC ROLLUP_CODE EXCH
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT1
BY RES_SYS
ON TABLE HOLD AS ACHLD1
END
-RUN
 
 
TABLE FILE HTLHLD
PRINT HPREF1 ONLH1 FLAGH FLAGHP
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKTN AS 'TKT1'
BY RES_SYS
ON TABLE HOLD AS HHLD
END
-RUN
 
MATCH FILE ACHLD1
PRINT ONLT 21ADVT 14ADVT 7ADVT PREFAIRT DISSAVINGS LSTT HTLBT CPREF1 PREF_CSIZ1 ONLC1 FLAGA FLAGC ROLLUP_CODE EXCH
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT1
BY RES_SYS
ON TABLE HOLD
RUN
FILE HHLD
SUM HPREF1 ONLH1 FLAGH FLAGHP
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT1
BY RES_SYS
AFTER MATCH HOLD AS AHHLD OLD-OR-NEW
END
-RUN
 
DEFINE FILE AHHLD
TKT/A10 = IF TKT1 EQ 'HTL ONLY' THEN 'HTL ONLY' ELSE
          IF TKT1 EQ 'CAR ONLY' THEN 'CAR ONLY' ELSE TKT1;
 
ONL/A10 = IF TKT EQ 'CAR ONLY' THEN ONLC1 ELSE
         IF TKT EQ 'HTL ONLY' THEN ONLH1 ELSE ONLT;
 
 
END
TABLE FILE AHHLD
SUM ONL 21ADVT 14ADVT 7ADVT PREFAIRT DISSAVINGS LSTT HTLBT CPREF1 PREF_CSIZ1 HPREF1 FLAGA FLAGC FLAGH FLAGHP ROLLUP_CODE EXCH
BY HIER
BY PASSNGR_NAME
BY EMP_ID
BY PNR_LOCATOR
BY TKT
BY RES_SYS
ON TABLE HOLD AS AHHLD
END
-RUN
 
-INCLUDE FDEFRPTS
-RUN
 
 
-* -SET &&OUTLINE2='FORMAT COMT';
-* -RUN
 
DEFINE FILE AHHLD
NOWTOD/A8 WITH HIER = HHMMSS (NOWTOD);
ROLLUP_CODE/A8 = '&&ROLLUP.EVAL';
END
TABLE FILE AHHLD
PRINT RES_SYS AS 'GDS'
      PASSNGR_NAME AS 'PASSENGER_NAME'
      PNR_LOCATOR AS 'PNR'
      HIER AS 'HIERARCHY'
-*       EMP_ID AS 'EMPLOYEE_ID'
      ONL AS 'BOOKED_ONLINE'
      21ADVT AS 'ADVANCE_21'
      14ADVT AS 'ADVANCE_14'
      7ADVT AS 'ADVANCE_7'
      PREFAIRT AS 'PREF_AIR_VENDOR'
      LSTT AS 'LOW_FARE'
      EXCH AS 'EXCHANGE'
      HTLBT AS 'AIR_HTL'
      HPREF1 AS 'PREF_HTL_VENDOR'
      CPREF1 AS 'PREF_CAR_VENDOR'
      PREF_CSIZ1 AS 'PREF_CAR_SIZE'
-* FLAGA AS 'AIR_BOOKED'
-*      FLAGH AS 'HOTEL_BOOKED'
-*      FLAGHP AS 'PREFERRED_HOTEL_AVAILABLE'
-*      FLAGC AS 'CAR_BOOKED'
BY ROLLUP_CODE NOPRINT
BY RES_SYS NOPRINT
BY EMP_ID AS 'EMPLOYEE_ID'
BY PASSNGR_NAME
BY PNR_LOCATOR NOPRINT
BY HIER NOPRINT
BY EMP_ID NOPRINT
BY TKT NOPRINT
HEADING
 "&&DTDESC &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"<&&CSLP &&ROLLNME <+0> &&SP <+0> &&ROLLCS11 <+0> &&SP3 <+0> &&RCNCT1 <+0> &&SP4 <+0> &&ROLLPHN"
"</2"
"<10 &&RPT_TTL1"
"<10 &&RPT_TTL2"
"</2"
FOOTING BOTTOM
"Post-trip Reporting &DATE AT <NOWTOD <60 &&FOOTR"
-* ON TABLE SET LINES 60000
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=1.250000, RIGHTMARGIN=0.250000,
    TOPMARGIN=1.000000, BOTTOMMARGIN=0.250000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
-* TYPE=REPORT, TITLETEXT=EXLOVER, ROWOVERFLOW=OFF,$
TYPE=REPORT,FONT=ARIAL, SIZE=8, COLOR=BLACK,
     BACKCOLOR=NONE, STYLE=NORMAL, $
 
 
TYPE=FOOTING, SIZE=7, FONT=ARIAL, COLOR=RGB(70 136 177), $
TYPE=TABFOOTING,SIZE=7, FONT=ARIAL, COLOR=RGB(70 136 177), $
 
TYPE=TITLE, SIZE=8, FONT=ARIAL, JUSTIFY=LEFT, STYLE=BOLD, $
TYPE=HEADING, SIZE=9, STYLE=BOLD, COLOR=RGB(70 136 177), $
TYPE=HEADING, LINE=6, SIZE=10, FONT=ARIAL, STYLE=BOLD, COLOR=RGB(70 136 177), $
TYPE=HEADING, LINE=7, SIZE=10, FONT=ARIAL, STYLE=BOLD, COLOR=RGB(70 136 177), $
TYPE=DATA, JUSTIFY=LEFT, $
TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(&&TNTTRPOS), SIZE=(&&TNTTRSIZ), $
TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLTRPOS), SIZE=(&&ROLTRSIZ), $
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
 
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS ELSE GOTO XXIT;
 
 
-NORECORDS
-SET &&RPTSUF = 'MSG';
-INCLUDE FDEFRPTS
-RUN
JOIN SET_KEY IN BRPTINST TO SET_KEY IN BRPT_SET AS J1
DEFINE FILE BRPTINST
EMPTY/A35 = 'NO RECORDS FOUND FOR THIS SELECTION';
NOWTOD/A8 WITH INST_KEY = HHMMSS (NOWTOD);
END
TABLE FILE BRPTINST
-INCLUDE HEADERDD
PRINT EMPTY AS 'REPORT,MESSAGE'
BY ROLLUP_CODE AS 'ROLLUP,CODE'
BY RPT_NAME AS 'REPORT,NAME'
BY SELECT_LEV AS 'BREAK,LEVEL'
IF INST_KEY EQ &&IKEY
IF RECORDLIMIT EQ 1
-INCLUDE FOOTERDT
ON TABLE SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET PAGE-NUM OFF
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
 
-XXIT;
