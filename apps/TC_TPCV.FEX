-**************************************************************
-* Date       Author    Modification
-**************************************************************
-* 01/23/2012 GSE       Removed parens from around CLIMIT to
-*                      prevent errors in 7.7.03
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-**************************************************************
-INCLUDE SETECHO
 
CREATE FILE TCCVND1
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCCARDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TPCVDRDD
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-*========================================================================
-*  GET THE FINAL REPORT
-*========================================================================
-SET &&RANK_METH = IF &&RANK_METH EQ 'CAR_AMT' THEN 'CAR_AMT'
- ELSE IF &&RANK_METH EQ 'NMBR_DAYS' THEN 'NMBR_DAYS'
- ELSE 'NMBR_DAYS';
 
 
TABLE FILE TCCVND1
SUM CBOOK/D12C
    PBOOK/D12C
    CAMT/D12CM AS 'CAR_AMT'
    PAMT/D12CM
    CDAYS/D12C AS 'NMBR_DAYS'
    PDAYS/D12C
BY VENDOR_NAME
ON TABLE HOLD AS HOLD1
END
-RUN
 
TABLE FILE HOLD1
SUM CAR_AMT
    NMBR_DAYS
    CBOOK
    PBOOK
    PDAYS
    PAMT
RANKED BY HIGHEST &&RANK_METH NOPRINT
BY VENDOR_NAME
ON TABLE HOLD AS HOLD6
END
-RUN
 
DEFINE FILE HOLD6
  CTY_LIMIT/I2 = 10;
  CLIMIT/A2    = EDIT(CTY_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF VENDOR_NAME NE LAST VENDOR_NAME THEN
                        (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6=EDIT(FRANKX,'$999999');
  VNDR_VALUE/A6 = IF CTY_LIMIT EQ 0 THEN BRANK ELSE
               IF RANK GT CTY_LIMIT THEN 'OTHER' ELSE BRANK;
  VNDR/A30 = IF VNDR_VALUE LT CLIMIT THEN VENDOR_NAME ELSE  'OTHER';
  FLAG/A1 = 'A';
END
-RUN
 
TABLE FILE HOLD6
SUM CAR_AMT
    PAMT
    CBOOK
    PBOOK
    NMBR_DAYS
    PDAYS
BY FLAG
BY VNDR_VALUE
BY VNDR
ON TABLE HOLD AS HOLD7
END
-RUN
 
TABLE FILE HOLD6
SUM NMBR_DAYS AS 'TCDAYS'
    PDAYS AS 'TPDAYS'
    CBOOK AS 'TCBOOK'
    PBOOK AS 'TPBOOK'
    CAR_AMT AS 'TCAMT'
    PAMT AS 'TPAMT'
BY FLAG
ON TABLE HOLD AS HOLD8
END
-RUN
 
 
MATCH FILE HOLD7
PRINT CAR_AMT
    NMBR_DAYS
    CBOOK
    PBOOK
    PDAYS
    PAMT
    VNDR_VALUE
    VNDR
BY FLAG
ON TABLE HOLD
RUN
FILE HOLD8
SUM TCDAYS
    TPDAYS
    TCBOOK
    TPBOOK
    TCAMT
    TPAMT
BY FLAG
AFTER MATCH HOLD AS HOLD9 OLD-OR-NEW
END
-RUN
 
TABLE FILE HOLD9
SUM NMBR_DAYS
    PDAYS
    TCDAYS
    TPDAYS
    CAR_AMT
    PAMT
    TCAMT
    TPAMT
    CBOOK
    PBOOK
    TCBOOK
    TPBOOK
-* AVERAGE DAILY RATE
COMPUTE CAVDR/D12CM = IF ((NMBR_DAYS LT 1) AND (NMBR_DAYS GT (-1))) THEN 0 ELSE (CAR_AMT/NMBR_DAYS);
COMPUTE PAVDR/D12CM = IF ((PDAYS LT 1) AND (PDAYS GT (-1))) THEN 0 ELSE (PAMT/PDAYS);
-* AVERAGE RENTAL LENGTH
COMPUTE CAVL/D12.2C = IF ((CBOOK LT 1) AND (CBOOK GT (-1))) THEN 0 ELSE (NMBR_DAYS/CBOOK);
COMPUTE PAVL/D12.2C = IF ((PBOOK LT 1) AND (PBOOK GT (-1))) THEN 0 ELSE (PDAYS/PBOOK);
-* CAR VENDOR %
COMPUTE CVP/D8.1% = IF ((TCBOOK LT 1) AND (TCBOOK GT (-1))) THEN 0 ELSE ((CBOOK/TCBOOK)*100);
COMPUTE PVP/D8.1% = IF ((TPBOOK LT 1) AND (TPBOOK GT (-1))) THEN 0 ELSE ((PBOOK/TPBOOK)*100);
-* VARIANCE AMTS/COUNTS/%
COMPUTE VDY1/D12C = NMBR_DAYS - PDAYS;
COMPUTE VDY2/D8% = IF ((NMBR_DAYS LT 1) AND (NMBR_DAYS GT (-1))) THEN 0 ELSE ((VDY1/NMBR_DAYS)*100);
COMPUTE VAM1/D12CM = CAR_AMT - PAMT;
COMPUTE VAM2/D8% = IF ((CAR_AMT LT 1) AND (CAR_AMT GT (-1))) THEN 0 ELSE ((VAM1/CAR_AMT)*100);
COMPUTE VBK1/D12C = CBOOK - PBOOK;
COMPUTE VBK2/D8% = IF ((CBOOK LT 1) AND (CBOOK GT (-1))) THEN 0 ELSE ((VBK1/CBOOK)*100);
COMPUTE VADR1/D12CM = CAVDR - PAVDR;
COMPUTE VADR2/D8% = IF ((CAVDR LT 1) AND (CAVDR GT (-1))) THEN 0 ELSE ((VADR1/CAVDR)*100);
COMPUTE VARD1/D12.2C = CAVL - PAVL;
COMPUTE VARD2/D8% = IF ((CAVL LT 1) AND (CAVL GT (-1))) THEN 0 ELSE ((VARD1/CAVL)*100);
COMPUTE VARVP/D8.1% = CVP-PVP;
BY VNDR_VALUE
BY VNDR
ON TABLE HOLD AS HOLD10
END
-RUN
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
 
DEFINE FILE HOLD10
NOWTOD/A8 WITH VNDR_VALUE = HHMMSS (NOWTOD);
END
 
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS';
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
 
 
TABLE FILE HOLD10
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
SUM NMBR_DAYS AS 'CURRENT,# DAYS'
    PDAYS AS 'PRIOR,# DAYS'
    VDY1 AS 'VAR'
    VDY2 AS '% VAR'
    CAR_AMT AS 'CURRENT,RENTAL AMT'
    PAMT AS 'PRIOR,RENTAL AMT'
    VAM1 AS 'VAR'
    VAM2 AS '% VAR'
    CBOOK AS 'CURRENT, # BOOKINGS'
    PBOOK AS 'PRIOR, # BOOKINGS'
    VBK1 AS 'VAR'
    VBK2 AS '% VAR'
    CAVDR AS 'CURRENT,AVG DLY RATE'
    PAVDR AS 'PRIOR, AVG DLY RATE'
    VADR1 AS 'VAR'
    VADR2 AS '% VAR'
    CAVL AS 'CURRENT, AVG LENGTH'
    PAVL AS 'PRIOR, AVG LENGTH'
    VARD1 AS 'VAR'
    VARD2 AS '% VAR'
    CVP AS 'CURRENT,BOOKINGS %'
    PVP AS 'PRIOR,BOOKINGS %'
    VARVP AS 'VAR'
BY VNDR_VALUE NOPRINT
BY VNDR AS 'VENDOR'
ON VNDR RECOMPUTE MULTILINES NMBR_DAYS PDAYS VDY1 CAR_AMT PAMT VAM1 CBOOK PBOOK VBK1 CAVDR PAVDR VADR1 VARD1 CVP PVP VARVP AS 'TOTAL FOR'
 
-INCLUDE FOOTERSM
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
