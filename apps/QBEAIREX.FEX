
-INCLUDE SETECHO

-*06/06/2017-JEM-S-34232-Creation for new report QBECBCFP
-*09/08/2017 DLV  S-37268   - Updated AT_FLAG

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN 


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';


DEFINE FILE &&EXTRACT

AT_FLAG/A1 = EDIT(EMBARKX, '$$$$$$9');

CLS_CAT1/A1 = IF TKT_CL_CAT EQ 'F' THEN 'F' ELSE
             IF TKT_CL_CAT EQ 'U' THEN 'U' ELSE
             IF TKT_CL_CAT EQ 'B' THEN 'B' ELSE
             IF TKT_CL_CAT EQ 'P' THEN 'P' ELSE 'E';
             
CLS_CAT/A1 = IF CLS_CAT1 EQ 'U' AND FARE_BASE_CODE CONTAINS 'F' THEN 'F' ELSE
             IF CLS_CAT1 EQ 'U' AND FARE_BASE_CODE CONTAINS 'B' THEN 'B' ELSE
             IF CLS_CAT1 EQ 'U' AND FARE_BASE_CODE CONTAINS 'P' THEN 'P' ELSE 
             IF CLS_CAT1 EQ 'U' THEN 'E' ELSE CLS_CAT1;
             
-* FARE_BAS1/A1 = EDIT(FARE_BAS, '9');

FLAG/A10 = IF AT_FLAG EQ 'A' THEN 'Air' ELSE 'Train';

LEVEL_DESC/A60 = &&LDESC;

AIR_VOL/D12.2 = SEG_AMT + SEG_TAX ;
NET_TKTS/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (AIR_VOL GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
                            

                          
-* DEFINES ON PREVIOUS DEFINES
-* CLS_CAT1 = IF CLS_CAT EQ 'D' AND 

CLASS_CAT/A15 = DECODE CLS_CAT ('F' 'First'     'P' 'Premium Economy'   
			        'B' 'Business'
			        'E' 'Economy');


MILES/D12 = SEG_MILES;
SEGS/D12 = SEG_COUNT;


DOM_MILES/D12 = IF (AIR_MAIN.INTL_DOM EQ 'D') THEN SEG_MILES ELSE 0;
INT_MILES/D12 = IF (AIR_MAIN.INTL_DOM EQ 'I') THEN SEG_MILES ELSE 0;

DOM_SEGS/D12 = IF (AIR_MAIN.INTL_DOM EQ 'D') THEN SEG_COUNT ELSE 0;
INT_SEGS/D12 = IF (AIR_MAIN.INTL_DOM EQ 'I') THEN SEG_COUNT ELSE 0;
IDOM/A15 = IF AIR_MAIN.INTL_DOM EQ 'D' THEN 'Domestic' ELSE 'International';

MILDSC/A5 = 'Miles';


-* Defines on Previous                           

END
-RUN

TABLE FILE &&EXTRACT
PRINT MILES
      SEGS
      MILDSC
      FLAG
      IDOM
      CLASS_CAT
      LEVEL_DESC
BY AT_FLAG
WHERE VOID_DATE EQ 0
-INCLUDE &AIRDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN

-* Extract Air only data (no train)

TABLE FILE RPTHOLD
SUM MILES
    SEGS
    MILDSC
BY FLAG
BY AT_FLAG
BY IDOM
BY CLASS_CAT
BY LEVEL_DESC
WHERE AT_FLAG EQ 'A'
ON TABLE HOLD AS HOLDONEA
END
-RUN


DEFINE FILE HOLDONEA
KEY1/A1 = ' ';
KEY/A1 = '1';
HAUL_DESC/A15 = ' ';
END
-RUN
TABLE FILE HOLDONEA
SUM MILES
    SEGS 
    MILDSC
BY KEY
BY FLAG
BY IDOM
BY KEY1
BY HAUL_DESC
BY CLASS_CAT
BY LEVEL_DESC
ON TABLE HOLD AS HOLDONE FORMAT FOCUS INDEX FLAG
END
-RUN


DEFINE FILE RPTHOLD
SEG_MILES1/D12 = IF MILES LT 0 THEN MILES * -1 ELSE MILES;
HAUL_DESC/A15 = IF (SEG_MILES1 GT 0) AND (SEG_MILES1 LE 999) THEN 'Short Haul' ELSE 'Long Haul';

END

TABLE FILE RPTHOLD
SUM MILES 
    SEGS
    MILDSC
BY FLAG
BY HAUL_DESC  
BY CLASS_CAT
BY LEVEL_DESC
WHERE AT_FLAG EQ 'A'
ON TABLE HOLD AS HOLDTWOA
END
-RUN


DEFINE FILE HOLDTWOA
KEY1/A1 = IF HAUL_DESC EQ 'Short Haul' THEN 'A' ELSE 'B';
KEY/A1 = '2';
IDOM/A15 = ' ';
END
-RUN

TABLE FILE HOLDTWOA
SUM MILES
    SEGS
    MILDSC
BY KEY    
BY FLAG
BY IDOM
BY KEY1
BY HAUL_DESC
BY CLASS_CAT
BY LEVEL_DESC
ON TABLE HOLD AS HOLDTWO FORMAT FOCUS INDEX KEY
END
-RUN

-* Extract Train only data (no air)


TABLE FILE RPTHOLD
SUM MILES
    SEGS
    MILDSC
BY FLAG
BY LEVEL_DESC
WHERE AT_FLAG EQ 'T'
ON TABLE HOLD AS HOLD3A
END
-RUN

DEFINE FILE HOLD3A
KEY1/A1 = ' ';
KEY/A1 = '3';
HAUL_DESC/A15 = 'Subway';
CLASS_CAT/A15 = ' ';
IDOM/A15 = ' ';
END
-RUN


TABLE FILE HOLD3A
SUM MILES
    SEGS
    MILDSC
BY KEY    
BY FLAG
BY IDOM
BY KEY1
BY HAUL_DESC
BY CLASS_CAT
BY LEVEL_DESC
ON TABLE HOLD AS HOLDTHREE FORMAT FOCUS INDEX KEY
END
-RUN

-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &HRANK1=&HLDPATH || 'HOLDONE.FOC';
-SET &HRANK2=&HLDPATH || 'HOLDTWO.FOC';
-SET &HRANK3=&HLDPATH || 'HOLDTHREE.FOC';
-RUN

USE CLEAR *
-RUN

EX SUUSES
-RUN

USE ADD 
&HRANK1 AS HOLDONE
&HRANK2 AS HOLDONE
&HRANK3 AS HOLDONE
END
-RUN


TABLE FILE HOLDONE
SUM MILES
    SEGS
    MILDSC
BY KEY
BY FLAG
BY IDOM
BY KEY1
BY HAUL_DESC
BY CLASS_CAT
BY LEVEL_DESC
ON TABLE HOLD AS HOLDT
END
-RUN


     

-SET &&AIRREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';




 

 


 
