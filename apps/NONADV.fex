-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 2/25/05 - DEB - ADDED CODE FOR GLOBAL CURRENCY

-INCLUDE SETECHO

SET ASNAMES=ON

DEFINE FILE GNONHLD
CY/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
           TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PY/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
MONTH/Mtr = TRN_DATE;
               
MXADV/A8  = IF (ADV_PURCH LT 7) THEN '00 - 06' 
               ELSE ' ';
BITMAP/A12 WITH NET_TKT_CNT = 'ADVPUR.SVG';
FLAG/A1 = 'A';
CT_TKT/D12 = IF CY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
PT_TKT/D12 = IF PY EQ 'Y' THEN NET_TKT_CNT ELSE 0;
END

TABLE FILE GNONHLD
SUM NET_TKT_CNT
    BITMAP
    NAMEC
BY FLAG   
BY MONTH
BY MXADV
WHERE CY EQ 'Y'
WHERE MXADV NE ' '
ON TABLE HOLD AS CURADV
END
-RUN

TABLE FILE GNONHLD
SUM CT_TKT PT_TKT
BY FLAG
BY MONTH
ON TABLE HOLD AS TTKTH
END
-RUN

TABLE FILE GNONHLD
SUM NET_TKT_CNT AS 'NET_TKT_CNT1'
    BITMAP
    NAMEC
BY FLAG    
BY MONTH
BY MXADV
WHERE PY EQ 'Y'
WHERE MXADV NE ' '
ON TABLE HOLD AS PRADV
END
-RUN
 


MATCH FILE CURADV
PRINT NET_TKT_CNT
      BITMAP
      NAMEC
BY FLAG
BY MONTH      
BY MXADV
ON TABLE HOLD
RUN
FILE PRADV
SUM NET_TKT_CNT1
    BITMAP
    NAMEC
BY FLAG
BY MONTH
BY MXADV
AFTER MATCH HOLD AS TTLADV OLD-OR-NEW
END
-RUN


TABLE FILE TTLADV
PRINT NET_TKT_CNT BITMAP NAMEC MXADV NET_TKT_CNT1 
BY FLAG
BY MONTH
ON TABLE HOLD AS TTLH1
END
-RUN


MATCH FILE TTLH1
PRINT NET_TKT_CNT BITMAP NAMEC MXADV NET_TKT_CNT1 
BY FLAG
BY MONTH
ON TABLE HOLD
RUN
FILE TTKTH
SUM CT_TKT PT_TKT
BY FLAG
BY MONTH
AFTER MATCH HOLD AS TTLH2 OLD-AND-NEW
END
-RUN

TABLE FILE TTLH2
SUM NET_TKT_CNT CT_TKT NET_TKT_CNT1 PT_TKT
BY FLAG
BY MONTH
BY MXADV
ON TABLE HOLD AS TTLH3
END
-RUN




-SET &RECFOUND = &RECORDS;

-IF &RECFOUND EQ 0 GOTO NOGRAPH;


GRAPH FILE TTLH3
SUM COMPUTE CADV/D8 = IF ((CT_TKT LT 1) AND (CT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT/CT_TKT); AS 'Period 1'
    COMPUTE PADV/D8 = IF ((PT_TKT LT 1) AND (PT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT1/PT_TKT); AS 'Period 2'
BY FLAG NOPRINT    
BY MONTH AS ' '
-* WHERE MXADV EQ '00 - 06'
ON GRAPH SET LOOKGRAPH VLINE
ON GRAPH SET GRAPHEDIT SERVER
ON GRAPH SET BARNUMB OFF
ON GRAPH SET GRID ON
ON GRAPH SET VAXIS 140
ON GRAPH SET HAXIS 390
ON GRAPH SET UNITS POINTS
ON GRAPH SET GRMERGE ON
ON GRAPH SAVE AS ADVPUR FORMAT SVG
ON TABLE SET GRAPHSTYLE *
setFillColor(getFrame(), new Color(239,239,239));
setO1AxisSide(0);
setO1MajorGridDisplay(true);
setO1MajorGridStyle(0);
setO1MinorGridDisplay(false);
setAxisAssignment(0,0);
setUseSeriesShapes(true);

setO1LabelDisplay(true);
setO1LabelRotate(3);
setFontSizeAbsolute(getO1Label(),true);
setAutofit(getO1Label(),false); 
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),8);
setFontName(getO1Label(),"Arial");
setFillColor(getO1Label(),new Color(5,76,149));
setTextJustHoriz(getO1Label(),2);



setY1MustIncludeZero(true);
setY1LabelDisplay(true);
setTextFormatPreset(getY1Label(),2);
setTextJustHoriz(getY1Label(),0);
setFontName(getY1Label(),"Times New Roman");
setFontSizeAbsolute(getY1Label(),true);
setFillColor(getY1Label(),new Color(5,76,149));
setFontSize(getY1Label(),9);
setFontStyle(getY1Label(),1);
setY1MajorGridDisplay(false);

setTitleString(" ");
setTextJustHoriz(getSubtitle(), 1);
setSubtitleString("0-6 Advanced Purchase %");
setFontSizeAbsolute(getSubtitle(),true);
setAutofit(getSubtitle(),false);
setFontSize(getSubtitle(),10);
setFontStyle(getSubtitle(),0);
setFontName(getSubtitle(),"Times New Roman");
 

setTextJustHoriz(getLegendText(),1);
setFontSizeAbsolute(getLegendText(),true);
setFontSize(getLegendText(),9);
setFontStyle(getLegendText(),0);
setFontName(getLegendText(),"Times New Roman");
setTextRotation(getLegendText(),0);
setTextWrap(getLegendText(),false);
setFillColor(getLegendText(),new Color(5,76,149));
setLegendDisplay(true);
setLegendPosition(2);
 
setSeriesFillColor(0, new Color, 0 0 138));
setSeriesFillColor(1, new Color, 115 162 247));
ENDSTYLE
END
-RUN

-IF &&OUTFMT NE 'PDF' GOTO NOGRAPH ELSE GOTO GRAPH;
-GRAPH
-SET &&A_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';
-GOTO HLDF
-NOGRAPH
-SET &&A_GIF='N';

-HLDF


TABLE FILE TTLH2
SUM BITMAP
    NET_TKT_CNT
    NET_TKT_CNT1
COMPUTE CYADV_PCT/D16 = IF ((CT_TKT LT 1) AND (CT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT/CT_TKT)*100; 
COMPUTE PYADV_PCT/D16 = IF ((PT_TKT LT 1) AND (PT_TKT GT (-1))) THEN 0 ELSE (NET_TKT_CNT1/PT_TKT)*100;     
    NAMEC
BY MXADV
ON TABLE HOLD AS ADVTTL
END
-RUN


DEFINE FILE ADVTTL
BITMAP/A12 WITH MXADV = 'ADVPUR.GIF';
LABEL/A30 = 'Advanced Purchase';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 2;
END
-RUN

TABLE FILE ADVTTL
PRINT CATEGORY/A61 AND MXADV/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND NET_TKT_CNT/D16 AS 'VOL_CURR' AND 
CYADV_PCT/D16 AS 'TKT_CURR' AND NET_TKT_CNT1/D16 AS 'VOL_PRIOR'
AND PYADV_PCT/D16 AS 'TKT_PRIOR'
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;
WHERE MXADV EQ '00 - 06'
ON TABLE HOLD AS OVADVC1
END
-RUN

TABLE FILE OVADVC1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR AND
      VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS OVADVC2
END
-RUN

MODIFY FILE OV_OFF
LOG DUPL MSG OFF
FIXFORM FROM OVADVC2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON OVADVC2
END
-RUN






