-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
 
-* File CPDEMO.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*3/16/00      TANDT-SS             CREATED NEW TO USE INTERNET/
-*                                  NON-INTERNET INSTEAD OF CARRIER
-*5/21/01      TANDT-SS               ADDED DEFINE TO NORECORDS SO MESSAGE
-*                                    "NO RECORDS FOUNDS FOR THIS SELECTION"
-*                                    WILL APPEAR IN EXCEL 97
-*4/30/02      TANDT-JK             COMMENTED OUT THE USE OF CPCOVPG
-*5/29/02      TANDT-DB             ADDED NEW SORT CGBOOK TO USE AGENT BOOKED/
-*                                  ONLINE INSTEAD OF INTERNET/NON-INTERNET
-*8/14/03      TANDT-JK             Changed report not to sort by Level desc
-*                                  throughout the report.
-*                                  Also removed ibook sort -  incorrect result
-*                                  when sorted in this manner.  Using
-*                                  just cgbook
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.
-*4/24/2017-JEM-S-33318-updated logic for no records report

-**********************************************************************
 
 
EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS
 
-IF &&DRILLED_RUN EQ 'Y' GOTO SkipSaveBDBs;
-INCLUDE SETPARMTIME
-RUN
-INCLUDE SetBDBsForDrill
-RUN
-SkipSaveBDBs
-TYPE &&OUTPUTDEST
-IF &&OUTLINE2 EQ 'OFFLINE' THEN GOTO XOFFLINE ELSE GOTO XONLINE;
 
-XONLINE
-TYPE Before ONLINE
ONLINE
-TYPE After ONLINE
-RUN
-GOTO CONT
 
-XOFFLINE
-SET &&OUTLINE2 = '';
-TYPE Before OFFLINE
OFFLINE
-TYPE After OFFLINE
-RUN
-GOTO CONT
 
-CONT
-SET &&RPT_HOLD = 'RPTHOLD';
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :TA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-INCLUDE SELCATQT
-RUN
-:TA1
 
EX AIRUSE
-RUN
 
EX &&EXPARM
-RUN
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW=&&EXPARM;
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS
 
-IF &RECORDS GT 0 THEN GOTO DOIT;
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-NORECORDS
-IF &&FROMAVRP NE 'Y' GOTO :AVSF2;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF2

-INCLUDE NORECRPT
-RUN
-GOTO XXIT
 
-DOIT
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
DEFINE FILE TPMP ADD
 
  LABEL1/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMO ELSE ' ';
 
 LABEL2/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMD ELSE ' ';
END
-RUN
 
 
TABLE FILE TPMP
SUM
    COMM_SEG
    CP_FARE_PD AS 'FARE'
    DISSAVINGS
    EMB_APT
    LABEL1
    LABEL2
    LEVEL_DESC
    MILES
    NET_COMM
    NET_FARE
    NET_TKT_CNT
    NEW_SEG_COUNT
    RTE_APT
    SAVINGS
    TOTAL_FARE
    VASAVINGS
BY ORGDST
BY CGBOOK
ON TABLE HOLD AS HOLD1
END
-RUN
 
TABLE FILE HOLD1
PRINT CGBOOK
      COMM_SEG
      DISSAVINGS
      EMB_APT
      FARE
      LABEL1
      LABEL2
      LEVEL_DESC
      MILES
      NET_COMM
      NET_FARE
      NET_TKT_CNT
      NEW_SEG_COUNT
      RTE_APT
      SAVINGS
      TOTAL_FARE
      VASAVINGS
BY ORGDST
RANKED BY HIGHEST &&RANK_METH
ON TABLE HOLD AS HOLD2
END
-RUN
 
DEFINE FILE HOLD2
  DRANK/D5      = RANK ;
  ARANKX/A6     = FTOA(DRANK, '(D5)', 'A6');
  ARANK/A5      = EDIT(ARANKX,'$99999');
  RANK_LIMIT/I5 = &&RANK_LIMIT2 ;
  RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                  IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END
-RUN
 
TABLE FILE HOLD2
PRINT
      CGBOOK
      COMM_SEG
      DISSAVINGS
      EMB_APT
      FARE
      LABEL1
      LABEL2
      LEVEL_DESC
      MILES
      NET_COMM
      NET_FARE
      NET_TKT_CNT
      NEW_SEG_COUNT
      RTE_APT
      SAVINGS
      TOTAL_FARE
      VASAVINGS
BY ORGDST
BY RANK_VALUE
ON TABLE HOLD AS HOLD3
END
-RUN
 
DEFINE FILE HOLD3
-*  IBOOK_PRINT/A33 = IF RANK_VALUE LE '&&RANK_LIMIT2' THEN IBOOK ELSE 'OTHER';
  CGBOOK_PRINT/A20 = IF RANK_VALUE LE '&&RANK_LIMIT2' THEN CGBOOK ELSE 'OTHER';
END
-RUN
 
TABLE FILE HOLD3
SUM
    COMM_SEG
    DISSAVINGS
    EMB_APT
    FARE
    LABEL1
    LABEL2
    LEVEL_DESC
    MILES
    NET_COMM
    NET_FARE
    NET_TKT_CNT
    NEW_SEG_COUNT
    RTE_APT
    SAVINGS
    TOTAL_FARE
    VASAVINGS
BY ORGDST
BY RANK_VALUE
BY CGBOOK_PRINT
ON TABLE HOLD AS HOLD4
END
-RUN
 
TABLE FILE HOLD1
SUM &&RANK_METH
BY ORGDST
ON TABLE HOLD AS RHOLD1 FORMAT ALPHA
END
-RUN
 
TABLE FILE RHOLD1
PRINT &&RANK_METH
RANKED BY HIGHEST &&RANK_METH AS 'SEG_RANK'
BY ORGDST
ON TABLE HOLD AS RHOLD2
END
-RUN
 
MATCH FILE HOLD4
PRINT
      CGBOOK_PRINT
      COMM_SEG
      DISSAVINGS
      EMB_APT
      FARE
      LABEL1
      LABEL2
      LEVEL_DESC
      MILES
      NET_COMM
      NET_FARE
      NET_TKT_CNT
      NEW_SEG_COUNT
      RANK_VALUE
      RTE_APT
      SAVINGS
      TOTAL_FARE
      VASAVINGS
BY ORGDST
ON TABLE HOLD
RUN
FILE RHOLD2
SUM SEG_RANK
BY ORGDST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN
 
TABLE FILE HOLD5
PRINT CGBOOK_PRINT
      COMM_SEG
      DISSAVINGS
      EMB_APT
      FARE
      LABEL1
      LABEL2
      LEVEL_DESC
      MILES
      NET_COMM
      NET_FARE
      NET_TKT_CNT
      NEW_SEG_COUNT
      ORGDST
      RANK_VALUE
      RTE_APT
      SAVINGS
      TOTAL_FARE
      VASAVINGS
RANKED BY HIGHEST SEG_RANK
ON TABLE HOLD AS HOLD6
END
-RUN
 
DEFINE FILE HOLD6
  ORG_LIMIT/I2 = &&RANK_LIMIT ;
  OLIMIT/A2    = EDIT(ORG_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF ORGDST NE LAST ORGDST THEN (LAST LIST + 1)
    ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999');
  ORG_VALUE/A6 = IF ORG_LIMIT EQ 0 THEN BRANK ELSE
               IF LIST GT ORG_LIMIT THEN 'OTHER' ELSE FRANK;
END
-RUN
 
TABLE FILE HOLD6
SUM
    COMM_SEG
    DISSAVINGS
    EMB_APT
    FARE
    LABEL1
    LABEL2
    LEVEL_DESC
    MILES
    NET_COMM
    NET_FARE
    NET_TKT_CNT
    NEW_SEG_COUNT
    OLIMIT
    ORG_LIMIT
    RTE_APT
    SAVINGS
    SEG_RANK
    TOTAL_FARE
    VASAVINGS
BY ORG_VALUE
BY ORGDST
BY RANK_VALUE
BY CGBOOK_PRINT
ON TABLE HOLD AS HOLD7
END
-RUN
 
DEFINE FILE HOLD7
  EMBARK_APT/A21 = IF ORG_VALUE LT OLIMIT THEN EMB_APT ELSE 'ALL OTHER';
  ORG_PRINT/A55 =IF ORG_VALUE LT OLIMIT THEN ORGDST ELSE 'ALL OTHER';
-* RO_PRINT/A33   =IF ORG_VALUE LT OLIMIT THEN IBOOK_PRINT ELSE 'OTHER';
  RO_PRINT1/A20 =IF ORG_VALUE LT OLIMIT THEN CGBOOK_PRINT ELSE 'OTHER';
  ROUTE_APT/A21 = IF ORG_VALUE LT OLIMIT THEN RTE_APT ELSE 'ALL OTHER';
  EMB_LBL/A25 = IF ORG_VALUE LT OLIMIT THEN LABEL1 ELSE 'ALL OTHER';
  RTE_LBL/A25 = IF ORG_VALUE LT OLIMIT THEN LABEL2 ELSE 'ALL OTHER';
END
-RUN
 
TABLE FILE HOLD7
SUM
    COMM_SEG
    DISSAVINGS
    EMBARK_APT
    EMB_LBL
    FARE
    LEVEL_DESC
    MILES
    NET_COMM
    NET_FARE
    NET_TKT_CNT
    NEW_SEG_COUNT
    ROUTE_APT
    RTE_LBL
    SAVINGS
    SEG_RANK
    TOTAL_FARE
    VASAVINGS
BY ORG_VALUE
BY ORG_PRINT
BY RANK_VALUE
BY RO_PRINT1
ON TABLE HOLD AS HOLD8
END
-RUN
 
TABLE FILE HOLD7
SUM MILES AS 'C_MILES'
    NET_FARE AS 'C_FARE'
    NET_COMM AS 'NC_FARE'
    NET_TKT_CNT AS 'C_SEGMT'
BY ORG_PRINT
ON TABLE HOLD AS HOLD9
END
-RUN
 
MATCH FILE HOLD8
PRINT COMM_SEG
      DISSAVINGS
      EMBARK_APT
      EMB_LBL
      FARE
      LEVEL_DESC
      MILES
      NET_COMM
      NET_FARE
      NET_TKT_CNT
      NEW_SEG_COUNT
      ORG_VALUE
      RANK_VALUE
      RANK_VALUE
      RO_PRINT1
      ROUTE_APT
      RTE_LBL
      SAVINGS
      SEG_RANK
      TOTAL_FARE
      VASAVINGS
BY ORG_PRINT
ON TABLE HOLD
RUN
FILE HOLD9
SUM C_FARE
    C_MILES
    C_SEGMT
    NC_FARE
BY ORG_PRINT
AFTER MATCH HOLD AS HOLD10 OLD-OR-NEW
END
-RUN
 
TABLE FILE HOLD10
SUM MILES
    C_MILES
BY ORG_PRINT
ON TABLE HOLD AS HOLD11 FORMAT ALPHA
END
-RUN
 
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
 
DEFINE FILE HOLD10
  PER_FARE/D8.1=    IF ((C_FARE LT 1) AND (C_FARE GT (-1))) THEN 0
                    ELSE ((FARE/C_FARE)*100);
  PER_NFARE/D8.1=    IF ((NC_FARE LT 1) AND (NC_FARE GT (-1))) THEN 0
                    ELSE ((NET_COMM/NC_FARE)*100);
  SEGPER/D8.1 =     IF ((C_SEGMT LT 1) AND (C_SEGMT GT (-1))) THEN 0
                    ELSE ((NET_TKT_CNT/C_SEGMT)*100);
  MILE_PCT/D8.1=    IF ((C_MILES LT 1) AND (C_MILES GT (-1))) THEN 0
                    ELSE ((MILES/C_MILES)*100);
  COSTPER_MILE/D8.2=IF ((MILES LT 1) AND (MILES GT (-1))) THEN 0
                    ELSE (FARE/MILES);
  AVGSEG_AMT/D8.2 = IF ((SEGMT LT 1) AND (SEGMT GT (-1))) THEN 0
                    ELSE (NET_FARE/SEGMT);
  AVGFLT/D8.2 =     IF ((NET_TKT_CNT LT 1) AND (NET_TKT_CNT GT (-1))) THEN 0
                    ELSE (NET_FARE/NET_TKT_CNT);
 
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
-RUN
 
TABLE FILE HOLD10
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
 
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
 
 
ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW='CPDEMOI';
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS
 
-XXIT
 
-* REJ EX RESTART
-*-EXIT
 
