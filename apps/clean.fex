-* File READTREE.FEX
-* CREATES AND READS A FILE TREE IN DOS
-*
-************
-INIT_ENV
-************
SET YRTHRESH=30
SET DEFCENT=19


-INCLUDE SETECHO
-*
-* NUMBER OF DAYS TO DELETE PAST
-SET &NUMDAYS = '45';
-* INITIAL DIRECTORY FOR TREE
-SET &INITDIR = 'D:\ZZWEB';
-* FILEDEF FOR INITIAL TREE OUTPUT
-SET &FILELOC = 'D:\TEMP\OUTPUT.FTM';
-* FILEDEF FOR FILE NAMES FROM TREE OUTPUT
-SET &FILETREE = 'D:\TEMP\FILETREE.FTM';
-* FILEDEF FOR BAT TO GENERATE FILE ATTRIBUTES
-SET &FILEBAT = 'D:\TEMP\FILEATRB.BAT';
-* FILEDEF FOR RAW FILES AND FILE ATTRIBUTES
-SET &FILEATRB = 'D:\TEMP\FILEATRB.FTM';
-* FILEDEF FOR FILES AND FILE ATTRIBUTES
-SET &ALLATRIB = 'D:\TEMP\ALLATRIB.FTM';
-* FILEDEF FOR DELETETION BAT FILE
-SET &FILEDELS = 'D:\TEMP\FILEDELS.BAT';
-*
FILEDEF FILETREE DISK &FILETREE.EVAL
FILEDEF FILEATRB DISK &FILEATRB.EVAL
FILEDEF FILEBAT  DISK &FILEBAT.EVAL
FILEDEF ALLATRIB DISK &ALLATRIB.EVAL
FILEDEF FILEDELS DISK &FILEDELS.EVAL
-RUN
-*
-************
-CREATE_TREE
-************
DOS TREE &INITDIR /F /A >> &FILELOC.EVAL
-RUN
FILEDEF TREE0 DISK &FILELOC.EVAL
-RUN
-*
-************
-HOLD_TREE
-************
DEFINE FILE TREE0
 VAL/A4    =IF EDIT(TREE,'9999') EQ '+---' OR '\---'
             THEN 'DIR1' ELSE
            IF EDIT(TREE,'99999999') EQ
	     '|   +---' OR '|   \---' OR
	     '    +---' OR '    \---'
	     THEN 'DIR2' ELSE
            IF EDIT(TREE,'999999999999') EQ
	     '|           ' OR '|   |       ' OR
	     '    |       ' OR '            '
	     THEN 'FILE' ELSE 'DIR0';
 DIR/A4    =IF VAL EQ 'FILE' AND EDIT(TREE,'$$$$$$$$$$$$9') EQ ' '
             THEN 'DIR0' ELSE VAL;
-*
 COMP   /A30=EDIT(TREE,'$$$$999999999999999999999999999999');
 TPATH1A/A50=IF DIR EQ 'DIR1' THEN '&INITDIR' | '\' | COMP
             ELSE ' ';
 TPATH1 /A50=IF TPATH1A NE ' ' THEN TPATH1A ELSE LAST TPATH1;
-*
 USER   /A30=EDIT(TREE,'$$$$$$$$999999999999999999999999999999');
 TPATH2A/A100=IF DIR EQ 'DIR2' THEN TPATH1 || '\' || USER
             ELSE ' ';
 TPATH2 /A100=IF TPATH2A NE ' ' THEN TPATH2A ELSE LAST TPATH2;
-*
 FILE1  /A30=EDIT(TREE,'$$$$$$$$$$$$999999999999999999999999999999');
 TPATH3A/A150=IF DIR EQ 'FILE' THEN TPATH2 || '\' || FILE1
             ELSE ' ';
 TPATH3 /A150=IF TPATH3A NE ' ' THEN TPATH3A ELSE LAST TPATH3;
 -*
END
-*
TABLE FILE TREE0
ON TABLE SET PAGE-NUM OFF
ON TABLE SET LINES 999999
PRINT
  TPATH2 AS ''
BY TPATH1 NOPRINT
WHERE DIR EQ 'DIR2'

ON TABLE HOLD AS FILETREE FORMAT ALPHA
END
-RUN
-*
-************
-CREATE_BAT
-************
FILEDEF TREE1 DISK &FILETREE.EVAL
-RUN
DEFINE FILE TREE1
PARTTREE/A50=EDIT(TREE,'99999999999999999999999999999999999999999999999999');
 BATOUT/A150 = 'DIR ' | PARTTREE | ' /S >> ' | '&FILEATRB';
END
TABLE FILE TREE1
PRINT BATOUT

ON TABLE HOLD AS FILEBAT FORMAT ALPHA
END
-RUN
-*
DOS &FILEBAT
-RUN-*
-************
-READ_DATES
-************
FILEDEF TREE1 DISK &FILEATRB.EVAL
-RUN
-*
DEFINE FILE TREE1
GETINFO1/A1=IF EDIT(TREE,'$$9') EQ '/' AND
     EDIT(TREE,'$$$$$$$$$$$$$$$$$$$$$$$$99999') NE '<DIR>'
     THEN 'Y' ELSE 'N';
ATTRIB/A150 = IF GETINFO1 EQ 'Y' THEN TREE ELSE ' ';

DIRECT1/A1 =IF EDIT(TREE,'9999999999') EQ ' Directory'
     THEN 'Y' ELSE 'N';
DIRECT2/A50=IF DIRECT1 EQ 'Y' THEN
EDIT(TREE,'$$$$$$$$$$$$$$99999999999999999999999999999999999999999999999999')
ELSE ' ';
DIRECT/A50 =IF DIRECT2 NE ' ' THEN DIRECT2 ELSE LAST DIRECT;

END
-*
TABLE FILE TREE1
PRINT
 DIRECT AS ''
 ATTRIB AS ''
WHERE GETINFO1 EQ 'Y'
ON TABLE HOLD AS ALLATRIB FORMAT ALPHA
END
-RUN
-*
-************
-CREATE_DEL
-************
FILEDEF TREE2 DISK &ALLATRIB.EVAL
-RUN

DEFINE FILE TREE2
 FULLFILE/A275 = 'DEL '|DIRECT||'\'||CHKFILE;
 THISDATE/MDY WITH CDATE ='&MDY';
 FILEAGE/I9 = THISDATE - CDATE;
 THISD/YYMD = THISDATE;
 CDATEX/YYMD=CDATE;
 AGE/I9 = THISD - CDATEX;
END
TABLE FILE TREE2
PRINT FULLFILE
WHERE AGE GE &NUMDAYS
ON TABLE HOLD AS FILEDELS FORMAT ALPHA
END
-RUN
-IF &LINES EQ 0 THEN GOTO CLEAN_UP;

DOS &FILEDELS
-RUN
-*
-************
-CLEAN_UP
-************
DOS DEL &FILELOC.EVAL
DOS DEL &FILETREE.EVAL
DOS DEL &FILEBAT.EVAL
DOS DEL &FILEATRB.EVAL
DOS DEL &ALLATRIB.EVAL
DOS DEL &FILEDELS.EVAL
-RUN

-*
-************
-LEAVE
-************
-*-EXIT
