-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* STEVE - 05/23/2003 - CHANGED CADVTP TO CADVTPX - DEFINED TWICE
-* DEB - 5/17/04 - CHANGED CNVSA AND CNVSA1 DEFINE TO EXCLUDE 'VI4246' INSTEAD
-*                 OF 'VI424604' PER PATTI
-* DEB   6/28/04 - CHANGED NONCC TO EXCLUDE NEW CONAGRA PREFERRED AIRLINES
-* JOY   7/19/04 - CHANGED NONCC TO EXCLUDE BA THEY ARE NOW PREFERRED
-* DEB  12/21/04 - CHANGED MXTRAD DEFINE TO INCLUDE ADDTL DOD_TYPES
-* STEVE07/21/05 - Changed MXTRA_TKT to use AIR_MAIN.INTL_DOM instead of CITYPAIR
-* DEB 10/11/2005 - ADDED DOD_TYPE H TO MXTRAD DEFINE
-* DEB 3/13/07 - CHANGED NONCC TO EXCLUDE CO THEY ARE NOW PREFERRED 
-*               CHANGED NONCC TO INCLUDE BA AS THEY ARE NO LONGER PREFERRED
-* ADDED TRANTYPE INCLUDE - JK 12/16/14 STORY ID S-06492
-*01/19/16 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*4/20/2017-JEM-S-33318-updated logic for no records report

SET ASNAMES = ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT 
  MXTRAD/A1 = IF (DOD_TYPE NE 'H' OR 'I' OR 'J' OR 'M' OR 'X' OR 'A' OR 'L'
                 OR 'N' OR 'S') THEN 'Y' ELSE 'N';
  LEVEL_DESC1/A80 = &&LDESC;
  FARE_PAID/D12 = SEG_AMT + SEG_TAX;
  FLAG/A1 = 'A';
  NET_TKT_CNT/P8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TKT_AMOUNT/D12.2CS  = TICKET_AMT + TAX_AMT;

-* DEFINES ON PREVIOUS DEFINES
DECLINE_AMT/D12.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         TKT_AMOUNT - LOWEST_AMT;
MXADV/A1 = IF (ADV_PURCH LT 7) THEN 'Y' ELSE 'N';
MXADV1/A1 = IF (ADV_PURCH GE 7) AND (ADV_PURCH LT 14) THEN 'Y' ELSE 'N';
MXADV_TKT/D8CS = IF (MXADV EQ 'Y') THEN NET_TKT_CNT ELSE 0;
MXADV_TKT1/D8CS = IF (MXADV1 EQ 'Y') THEN NET_TKT_CNT ELSE 0;
MXTRA_TKT/D8CS = IF (MXTRAD EQ 'Y') AND (AIR_MAIN.INTL_DOM EQ 'D') 
                    THEN NET_TKT_CNT ELSE 0;
CGDOM_TKT/P8CS = IF (AIR_MAIN.INTL_DOM EQ 'D') THEN NET_TKT_CNT ELSE 0;
NONCC/D8CS = IF (VAL_AIR.VAL_AIRLINE NE 'NW' OR 'DL' OR 'AA' OR 'UA' OR 'CO')
                THEN NET_TKT_CNT ELSE 0;

CNVSA/D8CS= IF (CREDIT_CARD OMITS '4246' OR '473077' OR 'TP') AND
               (BR_CL_IDX NE 'RF892075' OR 'RF740019' OR 'RF891788') AND
               (RF_FLAG EQ ' ') AND (EX_FLAG EQ ' ') THEN NET_TKT_CNT ELSE 0;
CNVSA1/D12= IF (CREDIT_CARD OMITS '4246' OR '473077' OR 'TP') AND
              (BR_CL_IDX NE 'RF892075' OR 'RF740019' OR 'RF891788') AND
              (RF_FLAG EQ ' ') AND (EX_FLAG EQ ' ') THEN FARE_PAID ELSE 0;

LS100/D8CS = IF ((DECLINE_AMT GE 100) OR (DECLINE_AMT LE (-100))) AND
                (REFUSE_CODE NE 'LE' OR 'NF') THEN NET_TKT_CNT ELSE 0; 
CGDFB/P8CS = IF (CL_CAT EQ 'F' OR 'B') AND (AIR_MAIN.INTL_DOM EQ 'D') THEN NET_TKT_CNT ELSE 0;

-INCLUDE TRANTYPE
-INCLUDE IDLEVDEFINE 

END
-RUN

TABLEF FILE &&EXTRACT
PRINT CNVSA
  CGDFB
  CNVSA1
  CGDOM_TKT
  FARE_PAID
  LEVEL_DESC
  LS100
  MXADV_TKT
  MXADV_TKT1
  MXTRA_TKT
  NONCC
  NET_TKT_CNT
  REFUSE_CODE
WHERE VOID.VOID_DATE EQ 0
-INCLUDE &AIRDATES
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN

TABLE FILE RPTHOLD
SUM CNVSA
    CGDFB
    CGDOM_TKT
    CNVSA1
    FARE_PAID
    LS100
    MXADV_TKT
    MXADV_TKT1
    MXTRA_TKT
    NET_TKT_CNT
    NONCC
BY LEVEL_DESC
ON TABLE HOLD AS CGNONHLD
END
-RUN

-SET &&AIRREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';
