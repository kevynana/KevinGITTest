-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Carried through non_tkts ref_tkts JK 9/29/04
-* ADDED CODE FOR GLOBAL CURRENCY - DEB 11/9/04
-* CARRIED THROUGH AA_TKT DL_TKT NW_TKT OTH_TKT UA_TKT - DEB 11/17/04
-* 07/03/2017 - RSB - S-35420 Report view to be updated with standard footer (part 3)
-*                             Worked the view though there are NO current setfiles using AVI110.
-* 09/13/2017   RSB  S-39793 Footer changes to be backed out of story S-35420
-*
-INCLUDE SETECHO
-* File SMRYRPT.FEX
-**********************************************************************
-**********************************************************************
-*
SET ASNAMES = ON
-RUN

-INCLUDE SETECHO 


-IF &&CURRENCY EQ 'USD' THEN GOTO NEXT;

-SET &&SUBHEAD = <NAMEC;
-SET &FPAID_CUR = 'FARE PAID, (' | &&CURRENCY || ')';
-SET &GCPK_CUR= 'COST,/KLMTR, (' | &&CURRENCY || ')';
-SET &&S_SUBJ2='PAID_FAREX/D16.2CS AS ' | '''&FPAID_CUR.EVAL'''  ;
-SET &&S_SUBJ4='KILO/P9CS AS ''TOTAL KLMTRS''';
-SET &&S_SUBJ5='COMPUTE GCPK/P14.2CS= IF ((KILO LT 1) AND'; 
-SET &&S_SUBJ6='(KILO GT (-1))) THEN 0 ELSE';
-SET &&S_SUBJ7='(PAID_FAREX/KILO); AS ' | '''&GCPK_CUR.EVAL''';





-NEXT;

JOIN TOPDST.DST_APCX IN TOPDST TO AIRPORT.APCODEX IN AIRPORT AS J2
END



TABLE FILE TOPDST
SUM NET_TKT_CNT 
    FARE_PAID
    PAID_FAREX/D16.2CS
    KILO
    NAMEC 
    NEW_SEG_MILES
    AIRPORT_NAME
BY ORG_APC
BY DST_APC
BY VAL_AIRLINE
   
ON TABLE HOLD AS HOLD1
END
-RUN




-SET &&RANK_METH = NET_TKT_CNT;

TABLE FILE HOLD1
PRINT NET_TKT_CNT
      FARE_PAID
      PAID_FAREX
      KILO
      NAMEC
      NEW_SEG_MILES
      AIRPORT_NAME
      VAL_AIRLINE
BY ORG_APC
BY DST_APC
RANKED BY HIGHEST &&RANK_METH
ON TABLE HOLD AS RNKHLD1
END
-RUN

-SET &&RANK_LIMIT2 = 15;

DEFINE FILE RNKHLD1
  DRANK/D5      = RANK ;
  ARANKX/A6     = FTOA(DRANK, '(D5)', 'A6');
  ARANK/A5      = EDIT(ARANKX,'$99999');
  RANK_LIMIT/I5 = &&RANK_LIMIT2 ;
  RNK_VAL/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                 IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE ARANK;
END



TABLE FILE RNKHLD1
PRINT NET_TKT_CNT
      FARE_PAID
      PAID_FAREX
      KILO
      NAMEC
      NEW_SEG_MILES
      AIRPORT_NAME
      VAL_AIRLINE
BY ORG_APC
BY DST_APC
BY RNK_VAL
ON TABLE HOLD AS HOLD3
END
-RUN 


DEFINE FILE HOLD3
AIRLINE_PRINT/A12 = IF RNK_VAL LE '&&RANK_LIMIT2' 
	THEN VAL_AIRLINE ELSE 'ALL OTHER';
	



END




TABLE FILE HOLD3
SUM NET_TKT_CNT
      FARE_PAID
      PAID_FAREX
      KILO
      NAMEC
      NEW_SEG_MILES
      AIRPORT_NAME
      VAL_AIRLINE
BY ORG_APC
BY DST_APC
BY RNK_VAL
BY AIRLINE_PRINT
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE TOPDST
SUM &&RANK_METH
BY ORG_APC
BY DST_APC
ON TABLE HOLD AS RHOLD1
END
-RUN


TABLE FILE RHOLD1
PRINT &&RANK_METH
RANKED BY HIGHEST &&RANK_METH AS 'SEG_RANK'
BY ORG_APC
BY DST_APC
ON TABLE HOLD AS RHOLD2
END
-RUN


MATCH FILE HOLD4
PRINT NET_TKT_CNT
      FARE_PAID
      PAID_FAREX
      KILO
      NAMEC
      NEW_SEG_MILES
      AIRPORT_NAME
      RNK_VAL
      AIRLINE_PRINT
      VAL_AIRLINE
BY ORG_APC
BY DST_APC
ON TABLE HOLD
RUN
FILE RHOLD2
SUM SEG_RANK
BY ORG_APC
BY DST_APC
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN




TABLE FILE HOLD5
PRINT NET_TKT_CNT
      FARE_PAID
      PAID_FAREX
      KILO
      NAMEC
      NEW_SEG_MILES
      AIRPORT_NAME
      RNK_VAL
      AIRLINE_PRINT
      ORG_APC
      DST_APC
      VAL_AIRLINE
RANKED BY HIGHEST SEG_RANK
ON TABLE HOLD AS HOLD6
END
-RUN


-SET &&RANK_LIMIT = 25;

DEFINE FILE HOLD6
  ORG_LIMIT/I2 = &&RANK_LIMIT ;
  OLIMIT/A2    = EDIT(ORG_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF DST_APC NE LAST DST_APC THEN (LAST LIST + 1) 
    ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6  = EDIT(FRANKX,'$999999'); 
  ORG_VALUE/A9 = IF ORG_LIMIT EQ 0 THEN BRANK ELSE
               IF LIST GT ORG_LIMIT THEN 'ALL OTHER' ELSE FRANK;
  RANK_LABEL1/A60 = IF ORG_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL1
                     ELSE ' ';
  RANK_LABEL2/A60 = IF ORG_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL2
                      ELSE ' ';
  RANK_LABEL3/A60 = IF ORG_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL3
                      ELSE ' ';             
END


TABLE FILE HOLD6
SUM NET_TKT_CNT
      FARE_PAID
      PAID_FAREX
      KILO
      NAMEC
      NEW_SEG_MILES
      AIRPORT_NAME
      DST_APC
      ORG_APC
      VAL_AIRLINE
BY ORG_VALUE
BY RNK_VAL
BY RANK_LABEL1
BY RANK_LABEL2
BY RANK_LABEL3
BY AIRLINE_PRINT
ON TABLE HOLD AS HOLD8
END
-RUN  


 DEFINE FILE HOLD8
  
 RANK_VALUE/A5 = IF ORG_VALUE EQ 'ALL OTHER' THEN '1' ELSE RNK_VAL;
 NOWTOD/A8 WITH ORG_VALUE = HHMMSS (NOWTOD);
 AIRPORT_NAME/A25 = IF ORG_VALUE EQ 'ALL OTHER' THEN ' ' ELSE AIRPORT_NAME;
 AIRLINE_PRINT/A12 = IF ORG_VALUE EQ 'ALL OTHER' THEN ' ' ELSE AIRLINE_PRINT;
 END     

-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-**********************************************************************


-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN


TABLE FILE HOLD8


-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
   

-INCLUDE FOOTERSM
ON TABLE COLUMN-TOTAL
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**********************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYS

-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-**********************************************************************
-IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;
-INCLUDE &&DSTY

-SKIP_DRILL

ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;

EX REPORT_3

-SKIP_R3
-*-*-EXIT




