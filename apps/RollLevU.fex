-****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIMUM FOR PROGRAMS**

-* File ROLLLEVU.FEX
-*
-*****   This routine retags the roll_keys in IHIER_SL file and also
-*****   the RPT_INST file from the ROL_LEVEL field in IHIER_SL
-*****   AUTHOR:  Steve Cords
-*****   PRODUCTION DATE: 12/27/2001
-*
SET ASNAMES=ON
-SET &&COMPANY='KIEW';
-SET &&HPCOMP='HP'||&&COMPANY;

-NEWSTEP
-* UPDATE SEL_KEY AND SEL_DESC AND SEL_LEVEL FROM IHIER_SL
-* BASED OFF OF ROL_LEVEL

TABLE FILE IHIER_SL
PRINT ROL_LEVEL INST_KEY SEL_KEY
BY ROL_LEVEL NOPRINT
IF ROL_LEVEL NE ' ';
IF INST_KEY CONTAINS '&&COMPANY';
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE &&HPCOMP
PRINT ROLL_LEV ROLL_KEY LEVEL_DESC
BY ROLL_LEV NOPRINT
ON TABLE HOLD AS HOLD2
END
-RUN

MATCH FILE HOLD1
PRINT ROL_LEVEL INST_KEY SEL_KEY
BY ROL_LEVEL NOPRINT
RUN
FILE HOLD2
SUM ROLL_LEV ROLL_KEY LEVEL_DESC
BY ROLL_LEV NOPRINT
AFTER MATCH HOLD AS HOLD3 OLD
END
-RUN

TABLE FILE HOLD3
PRINT INST_KEY SEL_KEY
ON TABLE HOLD AS HOLD33
END
-RUN

MODIFY FILE IHIER_SL
FIXFORM FROM HOLD33
MATCH INST_KEY SEL_KEY
ON MATCH DELETE
ON NOMATCH REJECT
DATA ON HOLD33
END
-RUN

DEFINE FILE HOLD3
NEWROLL/A18 = EDIT(ROLL_KEY, '999999999999999999');
FIRST/A2 = EDIT(NEWROLL, '$$99$$$$$$$$$$$$$$');
SECOND/A2 = EDIT(NEWROLL, '$$$$99$$$$$$$$$$$$');
THIRD/A2 = EDIT(NEWROLL, '$$$$$$99$$$$$$$$$$');
FOURTH/A2 = EDIT(NEWROLL, '$$$$$$$$99$$$$$$$$');
FIFTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$99$$$$$$');
SIXTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$$$99$$$$');
SEVENTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$$$$$99$$');
EIGHTH/A2 = EDIT(NEWROLL, '$$$$$$$$$$$$$$$$99');
END
-RUN

TABLE FILE HOLD3
PRINT INST_KEY ROLL_KEY AS 'SEL_KEY' LEVEL_DESC AS 'SEL_DESC'
AND COMPUTE SEL_LEVEL/I2 = IF FIRST EQ '00' THEN 01 ELSE
IF SECOND EQ '00' THEN 02 ELSE
IF THIRD EQ '00' THEN 03 ELSE
IF FOURTH EQ '00' THEN 04 ELSE
IF FIFTH EQ '00' THEN 05 ELSE
IF SIXTH EQ '00' THEN 06 ELSE
IF SEVENTH EQ '00' THEN 07 ELSE
IF EIGHTH EQ '00' THEN 08 ELSE 09;
ROL_LEVEL
ON TABLE HOLD AS HOLD4
END
-RUN

TABLE FILE HOLD4
PRINT INST_KEY SEL_KEY SEL_DESC SEL_LEVEL ROL_LEVEL
BY INST_KEY NOPRINT
BY SEL_KEY NOPRINT
ON TABLE HOLD AS HOLD5
END
-RUN

MODIFY FILE IHIER_SL
FIXFORM FROM HOLD5
MATCH INST_KEY SEL_KEY
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON HOLD5
END
-RUN

-* UPDATE LEVEL_KEY AND LEV_SELECT AND SELECT_LEV AND IF_LN1 FROM RPT_INST
-* BASED OFF OF ROL_LEVEL (IHIER_SL)

-NEWSTEP2

TABLE FILE IHIER_SL
PRINT INST_KEY SEL_KEY AS 'LEVEL_KEY' SEL_DESC AS 'LEV_SELECT'
AND SEL_LEVEL AS 'SELECT_LEV'
BY INST_KEY NOPRINT
IF INST_KEY CONTAINS '&&COMPANY';
ON TABLE HOLD AS IHOLD1
END
-RUN

MODIFY FILE RPT_INST
FIXFORM FROM IHOLD1
MATCH INST_KEY
ON MATCH UPDATE LEVEL_KEY
ON MATCH UPDATE LEV_SELECT
ON MATCH UPDATE SELECT_LEV
ON NOMATCH REJECT
DATA ON IHOLD1
END
-RUN

TABLE FILE IHIER_SL
PRINT IHIER_SL.SEL_KEY AND IHIER_SL.SEL_DESC AND IHIER_SL.SEL_LEVEL AND
IHIER_SL.ROL_LEVEL BY IHIER_SL.INST_KEY
WHERE IHIER_SL.ROL_LEVEL EQ ' ';
IF INST_KEY CONTAINS '&&COMPANY';
END
-RUN
-EXIT
