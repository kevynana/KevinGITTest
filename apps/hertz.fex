-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO
-* File HERTZ.FEX
-**************************************************************************
-*DATE         NAME             DESCRIPTION
-*7/31/00     IBISTL-KR         ADDED DEFINE FOR NOWTOD AND
-*                              REPLACED &FOCSYSTOD WITH HHMMSS SUBROUTINE
-*10/16/00    IBISTL-KR         ADDED CODE TO PASS DRILL PARAMS
-*10/15/00    IBISTL-SB         REPLACED &BITMP3 STATEMENT WITHOUT OUTPUT
-*                              DESTINATION FIELD CONCATENATION
-*10/15/00    IBISTL-SB         CHANGED THE FORMAT OF ON GRAPH STATEMENT
-*                              TO GIF FORMAT
-*10/15/00    IBISTL-SB         CHANGED THE DOS STATE STATEMENT WITH 
-*                              &&WEB_PATH FIELD TO LOCATE THE GIF FILE
-*10/15/00    IBISTL-SB         REPLACED THE IMAGE FIELD IN THE STYLE 
-*                              REPORT WITH THE &&WEB_PATH1 LOCATION
-*10/16/00    IBISTL-SB         ADDED WITH STATEMENT TO NOWTOD DEFINE
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-**************************************************************************
-SET &&OUTPUT1 = 'HERTZ';
-RUN

-SET &BITMP3 = 'HERTZ.SVG';
-RUN

-SET &&RPTSUF = 'HERTZ';
-INCLUDE FDEFRPTS
-RUN

DEFINE FILE &&EXTRACT
HERTZ/A5 = IF VENDOR_NAME EQ 'HERTZ' THEN
                'HERTZ' ELSE 'OTHER';
CYBOOKING/D12S = IF INVOICE_DATE GE &&CYFMDT.EVAL AND 
                  INVOICE_DATE LE &&CYTODT.EVAL 
                  THEN 1 ELSE 0;
PYBOOKING/D12S = IF INVOICE_DATE GE &&PYFMDT.EVAL AND 
                  INVOICE_DATE LE &&PYTODT.EVAL 
                  THEN 1 ELSE 0;
CMBOOKING/D12S = IF INVOICE_DATE GE &&CMFMDT.EVAL AND 
                  INVOICE_DATE LE &&CMTODT.EVAL 
                  THEN 1 ELSE 0;
PMBOOKING/D12S = IF INVOICE_DATE GE &&PMFMDT.EVAL AND 
                  INVOICE_DATE LE &&PMTODT.EVAL 
                  THEN 1 ELSE 0;
INV_MONTH/MT=INVOICE_DATE;
BITMAP/A12 WITH NUM_CARS='HERTZ.SVG';
END
-RUN

TABLE FILE &&EXTRACT
SUM CMBOOKING PMBOOKING
    CYBOOKING PYBOOKING
    BITMAP
BY HERTZ
&&WHERE1
&&WHERE2
&&WHERE3
WHERE ((INVOICE_DATE GE &&CMFMDT.EVAL AND INVOICE_DATE LE &&CMTODT.EVAL) 
   OR (INVOICE_DATE GE &&PMFMDT.EVAL AND INVOICE_DATE LE &&PMTODT.EVAL)
   OR (INVOICE_DATE GE &&CYFMDT.EVAL AND INVOICE_DATE LE &&CYTODT.EVAL) 
   OR (INVOICE_DATE GE &&PYFMDT.EVAL AND INVOICE_DATE LE &&PYTODT.EVAL))

-INCLUDE RPTPARMS

ON TABLE HOLD AS CARHLD2
END
-RUN

TABLE FILE &&EXTRACT
PRINT CYBOOKING PYBOOKING
BY INV_MONTH 
&&WHERE1
&&WHERE2
&&WHERE3
WHERE ((INVOICE_DATE GE &&CYFMDT.EVAL AND INVOICE_DATE LE &&CYTODT.EVAL) 
   OR (INVOICE_DATE GE &&PYFMDT.EVAL AND INVOICE_DATE LE &&PYTODT.EVAL))
WHERE HERTZ EQ 'HERTZ'

-INCLUDE RPTPARMS

ON TABLE HOLD AS HERTZ
END
-RUN

-IF &RECORDS EQ 0 GOTO SKIP_GRAPH;
-RUN

DEFINE FILE &&EXTRACT
HERTZ/A5 = IF VENDOR_NAME EQ 'HERTZ' THEN
                'HERTZ' ELSE 'OTHER';
CYBOOKING/D12S = IF INVOICE_DATE GE &&CYFMDT.EVAL AND 
                  INVOICE_DATE LE &&CYTODT.EVAL 
                  THEN 1 ELSE 0;
PYBOOKING/D12S = IF INVOICE_DATE GE &&PYFMDT.EVAL AND 
                  INVOICE_DATE LE &&PYTODT.EVAL 
                  THEN 1 ELSE 0;
CMBOOKING/D12S = IF INVOICE_DATE GE &&CMFMDT.EVAL AND 
                  INVOICE_DATE LE &&CMTODT.EVAL 
                  THEN 1 ELSE 0;
PMBOOKING/D12S = IF INVOICE_DATE GE &&PMFMDT.EVAL AND 
                  INVOICE_DATE LE &&PMTODT.EVAL 
                  THEN 1 ELSE 0;
INV_MONTH/MT=INVOICE_DATE;
BITMAP/A12 WITH NUM_CARS='HERTZ.SVG';
END
-RUN

GRAPH FILE &&EXTRACT
SUM CYBOOKING AS 'CURRENT' PYBOOKING AS 'PRIOR' 
ACROSS INV_MONTH AS ' '
&&WHERE1
&&WHERE2
&&WHERE3
WHERE ((INVOICE_DATE GE &&CYFMDT.EVAL AND INVOICE_DATE LE &&CYTODT.EVAL) 
   OR (INVOICE_DATE GE &&PYFMDT.EVAL AND INVOICE_DATE LE &&PYTODT.EVAL))
WHERE HERTZ EQ 'HERTZ'

-INCLUDE RPTPARMS
ON GRAPH SAVE AS HERTZ FORMAT SVG
ON GRAPH SET GRAPHSTYLE *
setGraphType(24);
setLegendDisplay(true);
setLegendPosition(2);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),false);
setFontSize(getLegendText(),12);
setFontStyle(getLegendText(),0);
setLegendMarkerPosition(1);
setO1LabelDisplay(true);
setFontSizeAbsolute(getO1Label(),true); 
setAutofit(getO1Label(),false);
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),15);
setO1LabelRotate(0);
setO1MajorGridDisplay(false);
setO1MinorGridDisplay(true);
setFontSizeAbsolute(getY1Label(),true);
setAutofit(getY1Label(),false);
setY1LabelDisplay(true);
setY1LabelFormat(1);
setFontSize(getY1Label(),15);
setFontStyle(getY1Label(),0);
setY1AxisSide(0);
setTextJustHoriz(getTitle(), 1);
setTitleString("Hertz Bookings");
setFontSizeAbsolute(getTitle(),true);
setAutofit(getTitle(),false);
setFontSize(getTitle(),15);
setFontStyle(getTitle(),0);
setSubtitleString(" ");
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 115 160 247));
ENDSTYLE 
END
-RUN

-SKIP_GRAPH
-RUN

-IF &LINES GT 0 GOTO THIS;
-IF &LINES EQ 0 GOTO THAT;
-THIS
-SET &THERE = 'THERE';
-RUN
-GOTO WHERE
-THAT
-SET &THERE = 'NOT THERE';
-RUN
-WHERE
-INCLUDE DTRANGE
-RUN

-**************************************************************************
-*DATE         NAME                DESCRIPTION
-*7/31/00    IBISTL-KR             ADDED DEFINE FOR NOWTOD 
-*10/16/00   IBISTL-SB             ADDED WITH STATEMENT TO NOWTOD DEFINE
-**************************************************************************
DEFINE FILE CARHLD2
NOWTOD/A8 WITH CYBOOKING = HHMMSS (NOWTOD);
END
-RUN

TABLE FILE CARHLD2
ON TABLE SET PAGE-NUM OFF
PRINT PCT.CMBOOKING AS 'Current,Period 1 %'
      PCT.PMBOOKING AS ' Prior,Period 1 %'
      PCT.CYBOOKING AS 'Current,Period 2 %'
      PCT.PYBOOKING AS 'Prior,Period 2 %'
     BITMAP NOPRINT
BY HERTZ AS ''

&&OUTLINE1
&&OUTLINE2
HEADING 
"</5"
"<29 % OF HERTZ CARS BOOKED"
"<12 &&LVL_DSC </1"
"<26 Current Period 1: <38 &CMFMM/&CMFMD/&CMFMY - &CMTOM/&CMTOD/&CMTOY"
"<26   Prior Period 1: <38 &PMFMM/&PMFMD/&PMFMY - &PMTOM/&PMTOD/&PMTOY"
"<26 Current Period 2: <38 &CYFMM/&CYFMD/&CYFMY - &CYTOM/&CYTOD/&CYTOY"
"<26   Prior Period 2: <38 &PYFMM/&PYFMD/&PYFMY - &PYTOM/&PYTOD/&PYTOY"
"</3"
-**************************************************************************
-*DATE         NAME                DESCRIPTION
-*7/31/00    IBISTL-KR             REPLACED &FOCSYSTOD WITH HHMMSS SUBROUTINE
-**************************************************************************
FOOTING BOTTOM
"&&FOOTER"
"HERTZ
<36 PAGE 15 <50 A REPORT FROM eTTek Review RUN ON &DATE AT <NOWTOD"
ON TABLE SET STYLE *

-***********************IBISTL DRILLDOWN**********************

TYPE=DATA, COLUMN=N2, \
 URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRHERTZ' CPFLAG='C1' CATGRY=HERTZ \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLUMN=N3, \
 URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRHERTZ' CPFLAG='P1' CATGRY=HERTZ \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLUMN=N4, \
 URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRHERTZ' CPFLAG='C2' CATGRY=HERTZ \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLUMN=N5, \
 URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRHERTZ' CPFLAG='P2' CATGRY=HERTZ \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

-***********************IBISTL DRILLDOWN**********************

UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=.60000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.50000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FOCEXEC=NONE, FONT=TIMES NEW ROMAN, SIZE=9, COLOR=BLACK, 
    BACKCOLOR=NONE, STYLE=NORMAL, $
-IF &THERE EQ 'NOT THERE' GOTO SKIP_IT;
TYPE=REPORT,
 IMAGE=HERTZ.SVG ,
    POSITION=(+1.2 +4.0), SIZE=(6.5 3.2), $
-SKIP_IT
TYPE=TITLE, SIZE=10, STYLE=BOLD, $
TYPE=HEADING, LINE=7, SIZE=13, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=8, SIZE=13, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=10, FONT=COURIER NEW, STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=11, FONT=COURIER NEW, STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=12, FONT=COURIER NEW, STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=13, FONT=COURIER NEW, STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=21, STYLE=BOLD+UNDERLINE, $ 
TYPE=SUBTOTAL, STYLE=BOLD, $
TYPE=FOOTING, SIZE=6, $
TYPE=REPORT, COLUMN=N2, POSITION=+1.000, $
TYPE=REPORT, COLUMN=N3, POSITION=+1.000, $
TYPE=REPORT, COLUMN=N4, POSITION=+1.000, $
TYPE=REPORT, COLUMN=N5, POSITION=+1.000, $
TYPE=REPORT, IMAGE=&&TNTPIC,  POSITION=(&&TNTSMPOS), SIZE=(&&TNTSMSIZ), $
TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLSMPOS), SIZE=(&&ROLSMSIZ), $
ENDSTYLE
END
-RUN

