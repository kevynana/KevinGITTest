-************************************************************************************
-* ChkUserHS.fex
-* Check User's Hierarchy Security
-*
-* 1. If there is NO limitation in the hierarchy security file USERCNTL, which
-*    is setup through Admin Functions, it respects the hierarchy selections from 
-*    Report Settings or Report Programs screen.
-* 2. If there is hierarchy security in USERCNTL file, the allowed access level in 
-*    the file is lower than the hierarchy selected from Report Settings,
-*    it will use hierarchy data from security file USERCNTL, default to all
-*    organizational members in that highest level in USERCNTL.
-* 3. If SEL_LEVL in USERCNTL are identical to SEL_LEVEL from Report Settings, the 
-*    organizational members selected from Report Settings must also exist in USERCNTL.
-*    Otherwise, some selected organizational members may be ignored.
-* 4. BREAKBY field in RPT_INST database should be updated accordingly as well.
-*
-* Date: 07/08/2008
-************************************************************************************
-INCLUDE SETECHO

TABLE FILE USERCNTL
PRINT SEL_LEVEL
WHERE USER_NAME EQ '&&UNAME.EVAL' AND ROLLUP_CODE EQ '&&ROLLUP.EVAL'
ON TABLE SAVE AS USERSEC1
END
-RUN
-IF &LINES EQ 0 THEN GOTO NoHSLimit;
-READ USERSEC1 &SECLEVEL.2.
-CLOSE USERSEC1;

TABLE FILE BRPTFLDS
PRINT SEL_LEVEL
ON TABLE SAVE AS USERLVL1
END
-RUN
-IF &LINES EQ 0 THEN GOTO DefaultSec ELSE GOTO UpdUserSec;

-UpdUserSec
-READ USERLVL1 &RPTLEVEL.2.
-CLOSE USERLVL1;
-SET &RPTLEVEL = IF '&RPTLEVEL.EVAL' EQ ' ' THEN '01' ELSE &RPTLEVEL;




-*in the case, the selected hierarchy level in RPT_FLDS is higher than the selected hierarchy
-*level in USERCNTL, then use DefaultSec,and ignore the user's hierarchy selections.

-IF '&RPTLEVEL.EVAL' GE '&SECLEVEL.EVAL' THEN GOTO useRPTLevel 
- ELSE IF '&RPTLEVEL.EVAL' LT '&SECLEVEL.EVAL' THEN  GOTO DefaultSec;

-useRPTLevel
-*Still need to check members if it is allowed to access
DEFINE FILE USERCNTL
  MEMBER/A17 = '''' | ROLL_LEV | '''';
END

TABLE FILE USERCNTL
PRINT MEMBER
BY ROLL_LEV NOPRINT
WHERE USER_NAME EQ '&&UNAME.EVAL' AND ROLLUP_CODE EQ '&&ROLLUP.EVAL' AND LEVEL EQ '&RPTLEVEL.EVAL'
ON TABLE SAVE AS USERSEC2
END

TABLE FILE BRPT_QUE
PRINT HIEORG2.SEQ_NO HIEORG2.MEMBER LEVEL
BY RPT_INST.INST_KEY
WHERE MEMBER IN FILE USERSEC2
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_SEC01;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_SEC01
ON TABLE SAVE AS HLDSEC02
END
-RUN

-IF &LINES EQ 0 THEN GOTO DefaultSec;

MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.MEMBER/A15 LEVEL/A2
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG2
  ON NOMATCH INCLUDE
  ON MATCH UPDATE MEMBER
DATA ON HLDSEC02
END
-RUN

-GOTO ChkSecDone;

-DefaultSec
-*check BREAKBY field in RPT_INST
DEFINE FILE BRPTINST
  BK/I1 = IF BREAKBY GT &SECLEVEL THEN BREAKBY ELSE &SECLEVEL;
END
TABLE FILE BRPTINST
PRINT ID BK AS 'BREAKBY'
BY INST_KEY
ON TABLE SAVE AS 'HLDBRKBY'
END
-RUN
MODIFY FILE BRPTINST
FIXFORM INST_KEY/7 ID/1 BREAKBY/1
REPOSITION INST_KEY
MATCH INST_KEY
   ON MATCH CONTINUE
   ON NOMATCH REJECT
MATCH ID
   ON NOMATCH REJECT
   ON MATCH UPDATE BREAKBY
DATA ON HLDBRKBY
END
-RUN

-*Use the data here to update Member in BRPTFLDS, &SECLEVEL to update SEL_LEVEL in BRPTFLDS
-*update SEL_LEVEL based on user's hierarchy security in Seg HIeorg1

DEFINE FILE CONTROL
 ID/I1 = 1;
 SEL_LEVEL/A2 = '&SECLEVEL.EVAL';
 INSTKEY/I7 = &&IKEY;
 INCL1/I1 = 1;
 INCL2/I1 = 1;
 INCL3/I1 = 1;
END
TABLE FILE CONTROL
PRINT INCL3 SEL_LEVEL
BY INSTKEY AS 'INST_KEY'
BY INST_KEY NOPRINT
BY ID
BY INCL1
BY INCL2
ON TABLE SAVE AS 'HLDHS01'
END
-RUN

MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HIEORG1.ID/1 HIEORG1.INCL1/1  HIEORG1.INCL2/1  HIEORG1.INCL3/1 HIEORG1.SEL_LEVEL/A2
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG1 
  ON NOMATCH INCLUDE
  ON MATCH UPDATE HIEORG1.INCL3 HIEORG1.SEL_LEVEL
DATA ON HLDHS01
END
-RUN

-*update hierarchy selections based on user's hierarchy security
DEFINE FILE USERCNTL
  CNT/I2 WITH LEVEL = CNT + 1;
  INST_KEY/I7 = &&IKEY;
END

TABLE FILE USERCNTL
PRINT CNT AS 'HIEORG2.SEQ_NO' ROLL_LEV AS 'MEMBER' LEVEL
BY INST_KEY
WHERE USER_NAME EQ '&&UNAME.EVAL' AND ROLLUP_CODE EQ '&&ROLLUP.EVAL' AND LEVEL EQ '&SECLEVEL.EVAL'
ON TABLE SAVE AS HOLDHS02
END
-RUN

MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.MEMBER/A15 LEVEL/A2
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG2
  ON NOMATCH INCLUDE
  ON MATCH UPDATE MEMBER LEVEL
DATA ON HOLDHS02
END
-RUN

-GOTO ChkSecDone;
-*************************************************
-*End to Check User's Hierarchy Security
-*************************************************
-ChkSecDone

-*Update client and catg selections in BRPFLDS 
TABLE FILE BRPT_QUE
PRINT HIEORG2.SEQ_NO CLIENT CATG
BY RPT_INST.INST_KEY
WHERE CLIENT NE ' ' OR CATG NE ' '
-IF '&&WRRUN.EVAL' EQ 'Y' GOTO NO_WHERE10;
WHERE BRPT_QUE.RPT_QUE.INTIME EQ HRPTFLDS.RPT_INST.INTIME
-NO_WHERE10
ON TABLE SAVE AS HOLDRPT0
END
-RUN
  
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HIEORG2.SEQ_NO/2 HIEORG2.CLIENT/A8 HIEORG2.CATG/A15
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HIEORG2
  ON NOMATCH INCLUDE
  ON MATCH UPDATE CLIENT CATG
DATA ON HOLDRPT0
END
-RUN
-SET &DONEHS = 'Y';

-NoHSLimit

