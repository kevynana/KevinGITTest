-* This extract gathers udid information 
-* Created 9/30/05  - JK
-*10/11/05 -DV - ADDED DOD_TYPE H TO ALL DEFINES INCL DOD_TYPE

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
  CLASS_CAT/A8 = DECODE CL_CAT ('F' 'FIRST'     'C' 'COACH'   
                              'D' 'DISCOUNT'  'B' 'BUSINESS'
                             ' ' 'OTHER');  
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;   
  LEVEL_DESC/A60 = &&LDESC;
  ADV_CAT/A8          = IF (ADV_PURCH LT 7) THEN '0 - 6' ELSE
                        IF (ADV_PURCH LT 14) THEN '07 - 13' ELSE
                        IF (ADV_PURCH LT 21) THEN '14 - 20' ELSE '21 +';
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  NEW_ADV_PURCH/D9     = ADV_PURCH;
  NEW_SEG_AMT/D12.2CS  = SEG_AMT;
  NEW_SEG_DISC/D12.2CS = SEG_DISC;
  NEW_SEG_MILES/D9= SEG_MILES;
  REF_EXCH_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                            (SEG_NBR EQ '01') AND
                            (FARE_PAID GE 0) AND
                            (SEG_COUNT EQ 1) AND 
                            (CONJ_REL EQ 1) THEN 0 ELSE 
                         IF (TICKET_CODE EQ '1') AND
                            (SEG_NBR EQ '01') AND 
                            (EX_FLAG EQ 'F') THEN 1 ELSE 
                         IF (TICKET_CODE EQ '1') AND
                            (SEG_NBR EQ '01') AND
                            (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  SAVINGS/D11.2CS      = SEG_STANDARD - FARE_PAID; 
  TKT_PURCH/D8CS       = IF (TICKET_CODE EQ '1') AND
                            (SEG_NBR EQ '01') AND
                            (FARE_PAID GE 0)  AND                    
                            (SEG_COUNT EQ 1)  AND
                            (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                            (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                         IF (SEG_COUNT LE 0) AND 
                            (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                         IF (SEG_COUNT LE 0) AND 
                             (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                         IF (SEG_COUNT LE 0) AND 
                           (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  XAP/A8               = EDIT(ADV_PURCH);
  XDPT_DT/MDYY         = DPT_DATE;
  XFST_DPT/MDYY = FST_DPT_DATE;
  XPSNGR_NM/A15        = EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
-* ****DEFINES ON PREVIOUS DEFINES****
  ADV_DAYS/A8        = IF ADV_PURCH LT 21 THEN XAP ELSE ADV_CAT;
  AIRLINE_FEE/D8.2CS = IF EMBARK EQ 'FP2' OR 'FP3' 
     THEN FARE_PAID ELSE 0;
  DISSAVINGS/D11.2CS = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST; 
    
-INCLUDE TRANTYPE  
               
END
TABLEF FILE &&EXTRACT
PRINT ADV_CAT
  ADV_DAYS    
  AIR_KEY	
  AIR_MAIN.INTL_DOM AS 'INTL_DOM'	
  AIRLINE	
  AIRLINE_FEE	
  AIRLINE_NAME	
  ARR_TIME
  C_ITIN
  CL_CAT	
  CLASS	
  CLASS_CAT	
  DISSAVINGS	
  DPT_TIME	
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'	
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'
  DOC_TYPE
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
  EX_FLAG
  FARE_PAID	
  FLIGHT	
  INVOICE_NUM
  LEVEL1
  LEVEL_DESC	
  NET_TKT_CNT	
  NEW_ADV_PURCH	
  NEW_SEG_AMT
  NEW_SEG_DISC	
  NEW_SEG_MILES
  REF_EXCH_CNT
  REFUSAL_DESC	
  REFUSE_CODE	
  REFUSE_KEY
  RF_FLAG
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
  RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'
  SAVINGS	
  SEG_COUNT	
  SEG_DESIG	
  SEG_LOWEST	
  SEG_NBR	
  SEG_STANDARD	
  TKT_DATE	
  TKT_NUM
  TKT_PURCH	
  TKT_SORT	
  TKT_TYPE	
  TRN_DATE
  XDPT_DT	
  XPSNGR_NM
  XTRAN_DT	
  STD_FARE
  LOWEST_AMT
  XFST_DPT
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS

ON TABLE HOLD AS &&RPT_HOLD
END
-RUN

