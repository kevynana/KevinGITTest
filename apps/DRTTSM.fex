-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*5/23/14   DEB   ADDED EX5 TO EX_RF_FLAG
-* ADDED TRANTYPE INCLUDE - JK 12/19/14 STORY ID S-06492
-*12/31/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-* 08/31/16 - DLV - S-22735 - Adding code to run in Available reports
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-INCLUDE SETECHO

-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';
-DEFAULT &&FIELD = 'XX';


-SET &&DTSTY = 'DT';
-SET &&PAX = &ROW_LABEL;
-SET &&FIELD = &FLAG;
-SET &&LEVD = &LSEL;
-SET &&IDLEVELBREAK = &PARM2;

-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;

-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';

EX RPTRESET
-RUN
EX RESET
-RUN

-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;

-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';

-CHKFILE
STATE &FILEXIST
-RUN

-TYPE &RETCODE

-IF &RETCODE NE 0 GOTO NOFILE;
-**********************************************************************
-* USE ORIGINAL SETFILE
-**********************************************************************
-*EX TSSETDB SETFILE=DRTRNSF
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-RUN
-SET &&SETF='TEMPDRIL';
EX SETDBLS SETFILE=&&SETF
-RUN

 

-* DLV added for er5 Test
-IF &&FROMAVRP NE 'Y' GOTO :DRD1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:DRD1

-INCLUDE GetForDrillDown
-RUN

-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DDTA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-RUN
-:DDTA1
 
-INCLUDE SELCATQT
-RUN
 



-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';   



-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT



-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&FULLREFCHK = ' ';
-SET &&FULLEXCHK = ' ';
-SET &&PARTEXCHK = ' ';
-SET &&AIRFEECHK = ' ';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKT,CNT';
-SET &&FAREAMT = 'FARE_AMT AS FARE,AMOUNT';
-SET &&TKTN = 'TICKET_NMBR AS TICKET,NUMBER';
-SET &&TKTN1 = ' ';

-IF &&IDLEVELBREAK EQ ' ' GOTO LEV1;

-SET &&LCHK = 'WHERE LEVEL_DESC EQ ''&&LEVD.EVAL''';


-GOTO ENDLEV;


-LEV1;
- IF &&LDESC EQ 'AROLL_DSC1' THEN GOTO ROLL1
- ELSE IF &&LDESC EQ 'AROLL_DSC2' THEN GOTO ROLL2
- ELSE IF &&LDESC EQ 'AROLL_DSC3' THEN GOTO ROLL3
- ELSE IF &&LDESC EQ 'AROLL_DSC4' THEN GOTO ROLL4
- ELSE IF &&LDESC EQ 'AROLL_DSC5' THEN GOTO ROLL5
- ELSE IF &&LDESC EQ 'AROLL_DSC6' THEN GOTO ROLL6
- ELSE IF &&LDESC EQ 'AROLL_DSC7' THEN GOTO ROLL7
- ELSE IF &&LDESC EQ 'AROLL_DSC8' THEN GOTO ROLL8
- ELSE IF &&LDESC EQ 'AROLL_DSC9' THEN GOTO ROLL9
- ELSE IF &&LDESC EQ 'AROLL_DSC10' THEN GOTO ROLL10
- ELSE IF &&LDESC EQ 'AROLL_DSC11' THEN GOTO ROLL11
- ELSE IF &&LDESC EQ 'AROLL_DSC12' THEN GOTO ROLL12
- ELSE IF &&LDESC EQ 'AROLL_DSC13' THEN GOTO ROLL13
- ELSE IF &&LDESC EQ 'AROLL_DSC14' THEN GOTO ROLL14
- ELSE IF &&LDESC EQ 'AROLL_DSC15' THEN GOTO ROLL15
- ELSE GOTO FIRST;

-ROLL1
-SET &&LCHK='WHERE AROLL_DSC1 
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL2
-SET &&LCHK='WHERE AROLL_DSC2
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL3
-SET &&LCHK='WHERE AROLL_DSC3
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL4
-SET &&LCHK='WHERE AROLL_DSC4
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL5
-SET &&LCHK='WHERE AROLL_DSC5
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL6
-SET &&LCHK='WHERE AROLL_DSC6
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL7
-SET &&LCHK='WHERE AROLL_DSC7
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL8
-SET &&LCHK='WHERE AROLL_DSC8
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL9
-SET &&LCHK='WHERE AROLL_DSC9
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL10
-SET &&LCHK='WHERE AROLL_DSC10
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL11
-SET &&LCHK='WHERE AROLL_DSC11
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL12
-SET &&LCHK='WHERE AROLL_DSC12
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL13
-SET &&LCHK='WHERE AROLL_DSC13
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL14
-SET &&LCHK='WHERE AROLL_DSC14
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ROLL15
-SET &&LCHK='WHERE AROLL_DSC15
- EQ ''&&LEVD.EVAL''';
-GOTO ENDLEV
-RUN

-ENDLEV
-RUN



-FIELDS
-IF &&FIELD EQ 'NETVOL' THEN GOTO NETVOLUME
-  ELSE IF &&FIELD EQ 'NETTKT' THEN GOTO NETTKTS
-  ELSE IF &&FIELD EQ 'AIRFEE' THEN GOTO AIRLINEFEES
-  ELSE IF &&FIELD EQ 'FULLEX' THEN GOTO FULLEXCHANGES
-  ELSE IF &&FIELD EQ 'FULLRF' THEN GOTO FULLREFUNDS
-  ELSE IF &&FIELD EQ 'PARTEX' THEN GOTO PARTEXCHANGES
-  ELSE IF &&FIELD EQ 'VOID' THEN GOTO VOIDS
-  ELSE GOTO NETVOLUME;

-GOTO OUTSIDE

-NETVOLUME
-SET &&RPT_TTL1 = 'NET FARE PAID DETAIL';
-GOTO OUTSIDE

-NETTKTS
-SET &&RPT_TTL1 = 'NET TICKETS DETAIL';
-GOTO OUTSIDE

-AIRLINEFEES   
-SET &&RPT_TTL1 = 'AIRLINE FEES DETAIL';
-SET &&AIRFEECHK = 'WHERE EMBARK EQ ''FP2'' OR ''FP3''';
-GOTO OUTSIDE

-FULLEXCHANGES
-SET &&RPT_TTL1 = 'FULL EXCHANGES DETAIL';
-SET &&VOIDCHK = ' ';
-SET &&FULLEXCHK = 'WHERE EX_FLAG EQ ''F''';
-SET &&NTCOUNT = 'EXG_ISS AS ''EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-GOTO OUTSIDE

-PARTEXCHANGES
-SET &&RPT_TTL1 = 'PARTIAL EXCHANGES DETAIL';
-SET &&PARTEXCHK = 'WHERE EX_FLAG EQ ''P''';
-SET &&NTCOUNT = 'PEXG_ISS AS ''PARTIAL,EXCHANGE,COUNT''';
-SET &&TKTN = 'TICKET_NMBR AS ''ORIGINAL,TICKET,NUMBER''';
-SET &&TKTN1 = 'TKTN AS ''NEW EXCHG SALE,TICKET,NUMBER''';
-GOTO OUTSIDE

-VOIDS
-SET &&RPT_TTL1 = 'VOID DETAIL';
-SET &&VOIDCHK = 'WHERE VOID_DATE NE 0';
-SET &&NTCOUNT = 'VOIDS AS ''VOID,COUNT''';
-SET &&FAREAMT = 'VOID_AMT AS ''VOID,AMOUNT''';
-GOTO OUTSIDE

-FULLREFUNDS
-SET &&RPT_TTL1 = 'FULL REFUNDS DETAIL';
-SET &&FULLREFCHK = 'WHERE RF_FLAG EQ ''F''';
-SET &&NTCOUNT = 'FULL_RFNDS AS ''REFUND,COUNT''';
-GOTO OUTSIDE


-OUTSIDE



FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN

-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************


-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

-*EX AIRUSE
EX AIRUSE
-RUN

-SET &&RPTGRP = 'AIR';
EX UDIDGEN
-RUN


-TYPE Before Extract
SET ASNAMES = ON 
-SET HOLDATTR = ON;



-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN
 

DEFINE FILE &&EXTRACT
 ALN_NAME/A10 = IF VAL_AIR.VAL_AIRLINE EQ 'UA' OR 'LH' THEN 'UA/LH' ELSE 'OTHER';

 CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE' 
                                ' ' 'DEFAULT'); 
                            

  CLASS1/A25 = IF TKT_CL_CAT CONTAINS 'F' THEN 'FIRST' ELSE
               IF TKT_CL_CAT CONTAINS 'B' THEN 'BUSINESS' ELSE
               IF TKT_CL_CAT CONTAINS 'P' THEN 'PREMIUM ECONOMY' ELSE
               IF TKT_CL_CAT CONTAINS 'E' THEN 'ECONOMY' ELSE
               IF TKT_CL_CAT CONTAINS 'U' THEN 'UPGRADE' ELSE 'DEFAULT';
              
               
  COMM_SEG/D12.2S = SEG_COM;
  CITY/A6 = SUBSTR (176, CNON_STP, 1, 6, 6, CITY);
  DCTRY/A65 = EDIT(TKT_MAIN.CTRY_COD,'$$$9999999999999999999999999999999999999999999999999999999999999999');
  DGZONE/A64 = EDIT(TKT_MAIN.GEO_ZONE,'$$$$9999999999999999999999999999999999999999999999999999999999999999');
  FARE_PAID/D12.2CS   = IF VOID_DATE EQ 0 THEN SEG_AMT + SEG_TAX ELSE 0;
  FARE_PAID1/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC1/A80      = &&LDESC;
  MXEXC1/A1 = IF (ORG_DST EQ 'BOSLGA') OR (ORG_DST EQ 'DCALGA') OR
                 (ORG_DST EQ 'BOSDCA') OR (ORG_DST EQ 'LGAPVD') OR
                 (ORG_DST EQ 'BOSEWR') THEN 'N' ELSE 'Y';
  MXEXC2/A1 = IF (ORG_DST EQ 'BOSLGA') OR (ORG_DST EQ 'DCALGA') OR
                 (ORG_DST EQ 'BOSDCA') THEN 'Y' ELSE 'N';               
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
                           
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) AND (VOID_DATE EQ 0) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') AND (VOID_DATE EQ 0) THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') AND (VOID_DATE EQ 0) THEN (-1) ELSE 0;
                           
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT - SEG_COM ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_DISC/D12.2CS = SEG_DISCOUNT;
  OCTRY/A2 = EDIT(TKT_MAIN.CTRY_COD, '99');
  OGZONE/A3 = EDIT(TKT_MAIN.GEO_ZONE, '999');
  
  REF_EXCH_CNT/D8.2CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  TAX/D12.2S = SEG_TAX;
  TKTN1/A10 = EDIT(TKT_SORT, '9999999999');
  TKTNA/A10 = IF TKTN1 EQ TKT_NUM THEN TKT_NUM ELSE TKTN1;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  VASAVINGS/D12.2CS    = NEW_SEG_DISC - FARE_PAID;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;


-* ****DEFINES ON PREVIOUS DEFINES****
  AFLAG/A1 = IF ALN_NAME EQ 'OTHER' THEN '2' ELSE '1';
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
                         
-* Added for new AD reason code to split out the value added savings
-*   ADVSV/D12.2CS = IF REFUSE_CODE EQ 'AD' THEN VASAVINGS ELSE 0;
ADVSV/D12.2CS = SEG_AGENCY_DISC - FARE_PAID;
-*  NFVSV/D12.2CS = IF REFUSE_CODE NE 'AD' THEN VASAVINGS ELSE 0; 
NFVSV/D12.2CS = VASAVINGS;

-*The following 2 defined fields are only used for saving/lost saving reports
-*02/04/2003
  SAVING1/P12.2CS = (STD_FARE - (TICKET_AMT + TAX_AMT));
  LSTSAVE1/P12.2CS = ((TICKET_AMT + TAX_AMT) - LOWEST_AMT); 
-***** Added for Kiewit exchange by Validating airline Report
ADDCOLLECT/D12.2CSM = IF (VOID_DATE EQ 0) AND (SEG_NBR EQ '01') THEN EXCH_NET_FARE+EXCH_NET_TAX;
AIRLINE_FEE/D12.2CSM = IF (VOID_DATE EQ 0) AND (EMBARK EQ 'FP2' OR 'FP3') THEN FARE_PAID ELSE 0;
EXG_ISS/D12CS = IF (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1) AND (SEG_NBR EQ '01') AND (VOID_DATE EQ 0) THEN 1 ELSE 0;
PEXG_ISS/D12CS = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'P') AND (TKT_NUM NE LAST TKT_NUM) THEN 1 ELSE 0;
FEXG_AMT/D12.2CSM = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'F') THEN FARE_PAID ELSE 0;
NET_AMT/D12.2CSM = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'F') THEN FARE_PAID ELSE 0;
FULL_RFNDS/D9CS = IF (RF_FLAG EQ 'F') AND (DOC_TYPE NE '1') AND (SEG_COUNT EQ -1) AND (SEG_NBR EQ '01') THEN 1 ELSE 0;
VOIDS/D9CS = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01') THEN 1 ELSE 0;	
VOID_AMT/D12.2CS = IF VOID_DATE NE 0 THEN SEG_AMT + SEG_TAX ELSE 0;
-* CONG REPORT
EX_RF_FLAG/A1 = IF (((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 'FP2' OR 'EX5') AND 
                     (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1')) THEN 'R' ELSE 
                  IF (((EXSALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EMBARK NE 'DT1') AND 
                      (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (RF_FLAG EQ ' ')))  THEN 'E'  ELSE ' ';
TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' '; 
-INCLUDE TRANTYPE                          
-INCLUDE IDLEVDEFINE 

END
-RUN

TABLEF FILE &&EXTRACT
PRINT 	
    AIRLINE
    AIRLINE.AIRLINE_NAME AS 'ARLN_NAME'
    ARR_TIME
    CITY
    CITYPAIR.EMBARK AS 'EMB_APT_CD'
    CITYPAIR.ROUTE  AS 'RTE_APT_CD'
    CLASS
    CLASS1
    CLASS_CAT
    COMM_SEG
    CONJ_REL
    DGZONE
    DCTRY
    DISSAVINGS	
    DOC_TYPE
    DOC_NUMBER
    DPT_TIME
    EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
    EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
    EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'
    EX_FLAG
    EXCHG_SALE
    FARE_PAID	
    FARE_PAID1
    FARE_BAS	
    INT_CODE	
    LEVEL_DESC
    LEVEL2
    MXEXC1
    MXEXC2
    MRK_TKT_CNT
    NET_FARE
    NET_TKT_CNT
    NEW_ADV_PURCH	
    NEW_SEG_DISC
    NFVSV
    OCTRY
    OGZONE
    PASSNGR_NAME
    PNR_LOCATOR	
    REF_EXCH_CNT	
    REFUSE_KEY        	
    REFUSE_CODE        	
    RESV_BRANCH	
    RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
    RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
    RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'
    SEG_DESIG
    SEG_LOWEST
    SEG_NBR	
    TAX
    TKT_MAIN.GEO_ZONE AS 'GEO_ZONE'
    TKT_MAIN.CTRY_COD AS 'CNTRY_CODE'
    TKT_DATE
    TKT_NUM
    TKTNA
    TKT_PURCH
    TKT_SORT
    TKT_TYPE
    TOUR_CODE
    TRN_DATE
    VAL_AIR.AIRLINE_NAME 	
    VAL_AIRLINE	
    VASAVINGS	
    XDPT_DT	
    XTRAN_DT	
    XPSNGR_NM
-* Kiewit Defines
    ADDCOLLECT
    AIRLINE_FEE
    EXG_ISS
    FEXG_AMT
    NET_AMT
    PEXG_ISS
    EX_RF_FLAG
    TKT_TYPE
    VOIDS
    VOID_AMT
    FULL_RFNDS
-INCLUDE &AIRDATES

-* WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
&&FULLREFCHK
&&PARTEXCHK
&&VOIDCHK
&&FULLEXCHK
&&AIRFEECHK
&&LCHK

-INCLUDE RPTPARMS
ON TABLE HOLD AS EXHLD
END
-RUN


-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN


 

-TYPE After Extract

-TYPE Before Report
TABLE FILE EXHLD
SUM NET_TKT_CNT AS 'TTKT'
FARE_PAID AS 'TFARE'
FULL_RFNDS AS 'TRFND'
AIRLINE_FEE AS 'TAFEE'
EXG_ISS AS 'TFEXC'
PEXG_ISS AS 'TPEXC'
VOID_AMT AS 'TVOIDA'
-* BY PASSNGR_NAME
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

TABLE FILE EXHLD
PRINT AIRLINE     
      AIRLINE_FEE
      CLASS_CAT
      EMB_APT_NM
      FARE_PAID 
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM 
      TKTNA
      TKT_TYPE	
      DPT_TIME
      XDPT_DT
      XTRAN_DT  
      TRN_DATE
      EXG_ISS
      VOIDS
      PEXG_ISS
      FULL_RFNDS
BY LEVEL_DESC      
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT AIRLINE     
      AIRLINE_FEE
      CLASS_CAT
      EMB_APT_NM
      FARE_PAID 
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM  
      TKTNA
      TKT_TYPE	
      DPT_TIME
      TRN_DATE
      XDPT_DT
      XTRAN_DT      
      EXG_ISS
      VOIDS
      PEXG_ISS
      FULL_RFNDS
BY LEVEL_DESC      
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
SUM TTKT
    TFARE
    TRFND
    TAFEE
    TFEXC
    TPEXC
    TVOIDA
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW
END
-RUN

TABLE FILE XTHREE
PRINT AIRLINE     
      AIRLINE_FEE
      CLASS_CAT
      EMB_APT_NM
      FARE_PAID 
      LEVEL_DESC
      PASSNGR_NAME
      REFUSE_CODE 
      RTE_APT_NM  
      TKT_NUM  
      TKTNA
      TKT_TYPE	
      TRN_DATE
      DPT_TIME
      XDPT_DT
      XTRAN_DT      
      EXG_ISS
      VOIDS
      PEXG_ISS
      FULL_RFNDS
      TTKT
      TFARE
      TRFND
      TAFEE
      TFEXC
      TPEXC
      TVOIDA
      
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN



JOIN ROLL WITH TKT_SORT IN AIRHOLD3 TO ROLLUP_CODE IN ROLLUP AS J1
-RUN

-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-SET &&SP = '|';
-SET &&SP3 = IF &&ROLLCNCT EQ ' ' THEN ' ' ELSE &&SP;
-SET &&SP4 = IF &&ROLLPHN EQ ' ' THEN ' ' ELSE &&SP;
-SET &&RCNCT = &&ROLLCNCT.LENGTH;
-SET &&CSL = &&ROLLCS.LENGTH;
-SET &&CSLP = IF &&RCNCT.EVAL GE 15 OR &&CSL.EVAL GE 10 THEN 61 ELSE 68;
-RUN

-SET &&SUBHEAD=IF &&IDLEVELBREAK EQ ' ' THEN ' ' ELSE '<LEVEL_DESC';


 
DEFINE FILE AIRHOLD3
NET_TKT_CNT1/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
TKTN/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKTNA ELSE ' ';
TTYPE/A4 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_TYPE ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/D12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
VOID_AMT/D12.2CSM = IF SRT_DT NE LAST SRT_DT THEN TVOIDA ELSE 0;
ROLL/A8='&&ROLLUP.EVAL';
LVL/A60='&&LVL_DSC.EVAL';
RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
TDATE1/A8MDYY='&MDYY';
TDATE2/A10=EDIT(TDATE1,'99/99/9999');
TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
RD/A10 = 'Run Date:';
RT/A10 = 'Run Time:';
END
-RUN


-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
-RUN

-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0


TABLE FILE AIRHOLD3
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT &&TKTN
&&TKTN1
REASON_CD AS 'RSN,CDE'
XDPT_DT AS 'DEPART,DATE'
EMB_APT_NM AS 'ORIGIN'
RTE_APT_NM AS 'DESTINATION'
AIRLINE AS 'CR'
CLASS_CAT AS 'CLASS,CATEGORY'
&&FAREAMT 
&&NTCOUNT
 
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY LEVEL_DESC NOPRINT
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY XTKT_SRT NOPRINT BY TKT_SORT NOPRINT 
BY TRN_DATE NOPRINT BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY SEG_NBR NOPRINT
BY TTYPE AS 'TKT,TYPE'
BY XTRAN_DTE AS 'TRAN,DATE'

-*


-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************

WHERE PASSNGR_NAME EQ '&&PAX.EVAL'

ON LEVEL_DESC RECOMPUTE AS 'TOTAL FOR'
ON PASSNGR_NAME RECOMPUTE AS 'TOTAL FOR'


FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
"</4"
"<12 PAGE <TABPAGENO  <25 DRTTSM  <40 &M1.EVAL <60 &M2.EVAL  <75 &M3.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
" "
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&PASTYD

ENDSTYLE

-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;


-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-GOTO XXIT;


-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-XXIT

