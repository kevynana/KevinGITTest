-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-****************************************************************************
-* This program will automate the running of QUARTERLY management cycle reports
-* through Report Broker.
-*
-* DATE           NAME             DESCRIPTION
-* 4/18/01        TANDT-SS         NEW PROGRAM
-*10/14/02        STEVE            ADDED CODE FOR ETS - EXECUTIVE OVERVIEW
-*
-****************************************************************************

-*------------------------------------
-* Find sets to run from RPT_SET
-*------------------------------------
TABLE FILE RPT_SET
PRINT *
WHERE ROLLUP_CODE EQ '&&ROLL'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'QUARTERLY'
ON TABLE HOLD AS SET1
END
-RUN
-IF &RECORDS EQ 0 GOTO NODATA ;
-RUN


DEFINE FILE SET1
 NEW_ZIP_FLAG/A1 = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE 'Y';
 NEWSET_EX_FLAG/A1 = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE 'Y';
 NEWSET_FM_DATE/MDYY = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE &&QFM_DATE;
 NEWSET_TO_DATE/MDYY = IF (SET_NAME CONTAINS 'TRAVEL' OR 'Travel' OR 'travel')
    THEN ' ' ELSE &&QTO_DATE;
END
-RUN

TABLE FILE SET1
PRINT SET_KEY
 ROLLUP_CODE
 SET_ID
 SET_FREQ
 SET_NAME
 SET_TITLE1
 SET_TITLE2
 SET_ESC
 PRINTER_TYP
 NEWSET_FM_DATE AS 'FM_DATE'
 NEWSET_TO_DATE AS 'TO_DATE'
 OUTPUT_DEST
 OUTPUT_FORMAT
 NEWSET_EX_FLAG AS 'EXECUTE_FLAG'
 CONTROL_ID
 RUN_SEQ
 USER_NAME
 USER_SECURITY
 NEW_ZIP_FLAG AS 'ZIP_FLAG'
ON TABLE HOLD AS SETA
END
-RUN

-*------------------------------------
-* Update RPT_SET
-*------------------------------------
MODIFY FILE RPT_SET
 LOG NOMATCH MSG OFF
 LOG DUPL MSG OFF
 LOG INVALID MSG OFF
 LOG FORMAT MSG OFF
FIXFORM FROM SETA
MATCH SET_KEY
ON MATCH UPDATE FM_DATE TO_DATE EXECUTE_FLAG ZIP_FLAG
ON NOMATCH REJECT
DATA ON SETA
END
-RUN


DEFINE FILE RPT_SET
CTR/I5 WITH SET_KEY = CTR + 1;
ITIME/I6 WITH SET_KEY = IF CTR EQ 1 THEN &&TSTAMPT1 + 300
                        ELSE ITIME + 1;
ITIME1/I6L WITH SET_KEY = ITIME + 1;
INTIME/A18 WITH SET_KEY = '&YYMD.EVAL' | '  ' | EDIT(ITIME1,'99.99.99');
END
TABLE FILE RPT_SET
BY SET_KEY NOPRINT
PRINT *
      INTIME
WHERE ROLLUP_CODE EQ '&&ROLL'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'QUARTERLY'
ON TABLE HOLD AS HSETS1
END
MODIFY FILE HRPT_SET
FIXFORM FROM HSETS1
MATCH SET_KEY INTIME
  ON NOMATCH INCLUDE
  ON MATCH REJECT
DATA ON HSETS1
END
-RUN

-*--------------------------------------------
-* Find reports to run in RPT_INST
-*--------------------------------------------
TABLE FILE RPT_SET
PRINT *
WHERE ROLLUP_CODE EQ '&&ROLL'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'QUARTERLY'
ON TABLE HOLD AS SET1A FORMAT FOCUS INDEX SET_KEY
END
-RUN

JOIN SET_KEY IN SET1A TO ALL SET_KEY IN RPT_INST AS JOINSET1

DEFINE FILE SET1A
 NEW_FM_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN ' ' ELSE &&QFM_DATE;
 NEW_TO_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN ' ' ELSE &&QTO_DATE;
 NEW_FX_DATE/MDYY = IF RPT_TYPE NE 'ETS' THEN ' ' ELSE &&QPMFM_DATE;
 NEW_TX_DATE/MDYY = IF RPT_TYPE NE 'ETS' THEN ' ' ELSE &&QPMTO_DATE;
 NEW_CMFM_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN &&QCMFM_DATE ELSE ' ';
 NEW_CMTO_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN &&QCMTO_DATE ELSE ' ';
 NEW_PMFM_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN &&QPMFM_DATE ELSE ' ';
 NEW_PMTO_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN &&QPMTO_DATE ELSE ' ';
 NEW_CYFM_DATE/MDYY = IF (RPT_TYPE EQ 'STS') 
   AND ('&&FY' EQ '01') THEN &&QCYFM_DATE ELSE IF (RPT_TYPE EQ 'STS') 
   AND ('&&FY' NE '01') THEN &&KCYFM_DATE ELSE ' ';
 NEW_CYTO_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN &&QCYTO_DATE ELSE ' ';
 NEW_PYFM_DATE/MDYY = IF (RPT_TYPE EQ 'STS') 
   AND ('&&FY' EQ '01') THEN &&QPYFM_DATE ELSE IF (RPT_TYPE EQ 'STS') 
   AND ('&&FY' NE '01') THEN &&KPYFM_DATE ELSE ' ';
 NEW_PYTO_DATE/MDYY = IF RPT_TYPE EQ 'STS' THEN &&QPYTO_DATE ELSE ' ';
 NEW_RPT_SERVER/A20 = 'master';
 NEW_EXECUTE_FLAG/A1 = 'Y';
-*  NEW_DISTRB_FLAG/A2 = 'E';
END
-RUN

TABLE FILE SET1A
PRINT  INST_KEY
 RPT_INST.ROLLUP_CODE
 SET_ID
 RPT_ID
 INSTANCE_ID
 SET_KEY
 RPT_INST.OUTPUT_DEST
 RPT_INST.OUTPUT_FORMAT
 NEW_EXECUTE_FLAG AS 'EXECUTE_FLAG'
 SRD_FLAG
 RPT_NAME
 RPT_GROUP
 RPT_TYPE
 RPT_STREAM
 RPT_HEAD1
 RPT_HEAD2
 RPT_FOOT1
 RPT_FOOT2
 STYLE1
 STYLE2
 RPT_TITLE1
 RPT_TITLE2
 TITLE_ESC
 BODY_ESC
 STYLE3
 RPT_LOOK
 NEW_RPT_SERVER AS 'RPT_SERVER'
 GLOBAL_PARM
 USE_PARM
 NEW_CMFM_DATE AS 'CMFM_DATE'
 NEW_CMTO_DATE AS 'CMTO_DATE'
 NEW_PMFM_DATE AS 'PMFM_DATE'
 NEW_PMTO_DATE AS 'PMTO_DATE'
 NEW_CYFM_DATE AS 'CYFM_DATE'
 NEW_CYTO_DATE AS 'CYTO_DATE'
 NEW_PYFM_DATE AS 'PYFM_DATE'
 NEW_PYTO_DATE AS 'PYTO_DATE'
 LEVEL_KEY
 LEV_SELECT
 SELECT_LEV
 RPT_INST.RUN_SEQ
 NEW_FM_DATE AS 'FM_DATE'
 NEW_TO_DATE AS 'TO_DATE'
 NEW_FX_DATE AS 'FXDATE'
 NEW_TX_DATE AS 'TXDATE'
 PAIR_DB
 PRINTER_TYP
 RPT_INST.CONTROL_ID
 COVER_DEST
 SUMM_DEST
 RANK_DEST
 SECURE_FLAG
-*  NEW_DISTRB_FLAG AS 'DISTRB_FLAG'
 DISTRB_FLAG
 LOGO_1
 LOGO_2
ON TABLE HOLD AS INST1
END
-RUN

-*--------------------------------------------
-* Update RPT_INST
-*--------------------------------------------
MODIFY FILE RPT_INST
 LOG NOMATCH MSG OFF
 LOG DUPL MSG OFF
 LOG INVALID MSG OFF
 LOG FORMAT MSG OFF
FIXFORM FROM INST1
MATCH INST_KEY
ON MATCH UPDATE EXECUTE_FLAG RPT_SERVER CMFM_DATE CMTO_DATE
ON MATCH UPDATE PMFM_DATE PMTO_DATE CYFM_DATE CYTO_DATE
-* ON MATCH UPDATE PYFM_DATE PYTO_DATE FM_DATE TO_DATE DISTRB_FLAG
ON MATCH UPDATE PYFM_DATE PYTO_DATE FM_DATE TO_DATE FXDATE TXDATE
ON NOMATCH REJECT
DATA ON INST1
END
-RUN

-* JOIN CLEAR *
DEFINE FILE RPT_SET
CTR/I5 WITH SET_KEY = CTR + 1;
ITIME/I6 WITH SET_KEY = IF CTR EQ 1 THEN &&TSTAMPT1 + 300
                        ELSE ITIME + 1;
ITIME1/I6L WITH SET_KEY = ITIME + 1;
INTIME/A18 WITH SET_KEY = '&YYMD.EVAL' | '  ' | EDIT(ITIME1,'99.99.99');
END
TABLE FILE RPT_SET
BY SET_KEY NOPRINT
PRINT SET_KEY INTIME
WHERE ROLLUP_CODE EQ '&&ROLL'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'QUARTERLY'
ON TABLE HOLD AS HSETS2
END
-RUN

JOIN SET_KEY IN HSETS2 TO ALL SET_KEY IN RPT_INST AS J1.
-RUN
TABLE FILE HSETS2
PRINT SEG.GLOBAL_PARM
      INTIME
ON TABLE HOLD AS HINST1
END
TABLE FILE HSETS2
PRINT SEG.PRT_LN1
BY INST_KEY
BY INTIME
ON TABLE HOLD AS HINST2
END
TABLE FILE HSETS2
PRINT SEG.SUMM_SUBJ1
BY INST_KEY
BY INTIME
ON TABLE HOLD AS HINST3
END
MODIFY FILE HRPTINST
FIXFORM FROM HINST1
MATCH INST_KEY INTIME
 ON NOMATCH INCLUDE
 ON MATCH REJECT
DATA ON HINST1
END
MODIFY FILE HRPTINST
FIXFORM FROM HINST2
MATCH INST_KEY INTIME
 ON NOMATCH REJECT
 ON MATCH CONTINUE TO PRT_LN1
  ON MATCH INCLUDE
  ON NOMATCH INCLUDE
DATA ON HINST2
END
MODIFY FILE HRPTINST
FIXFORM FROM HINST3
MATCH INST_KEY INTIME
 ON NOMATCH REJECT
 ON MATCH CONTINUE TO SUMM_SUBJ1
  ON MATCH INCLUDE
  ON NOMATCH INCLUDE
DATA ON HINST3
END
-RUN

-* JOIN CLEAR *
JOIN SET_KEY IN HSETS2 TO ALL SET_KEY IN RPT_INST AS J1.
JOIN INST_KEY IN HSETS2 TO INST_KEY IN I_SUMM AS J2.
-RUN

TABLE FILE HSETS2
PRINT SEG.HSEL1
      INTIME
ON TABLE HOLD AS HISUMM1
END
MODIFY FILE HI_SUMM
FIXFORM FROM HISUMM1
MATCH INST_KEY INTIME
 ON NOMATCH INCLUDE
 ON MATCH REJECT 
DATA ON HISUMM1
END
-RUN

-*----------------------------------------------
-* Copy reports to run to RPT_QUE
-*----------------------------------------------
DEFINE FILE RPT_SET
CTR/I5 WITH SET_KEY = CTR + 1;
ITIME/I6 WITH SET_KEY = IF CTR EQ 1 THEN &&TSTAMPT1 + 300
                        ELSE ITIME + 1;
ITIME1/I6L WITH SET_KEY = ITIME + 1;
INTIME/A18 WITH SET_KEY = '&YYMD.EVAL' | '  ' | EDIT(ITIME1,'99.99.99');
END
TABLE FILE RPT_SET
BY SET_KEY NOPRINT
PRINT * INTIME
WHERE ROLLUP_CODE EQ '&&ROLL'
WHERE USER_NAME EQ 'master'
WHERE SET_FREQ EQ 'QUARTERLY'
ON TABLE HOLD AS SET2 FORMAT FOCUS INDEX SET_KEY
END
-RUN


JOIN SET_KEY IN SET2 TO ALL SET_KEY IN RPT_INST AS JOINSET2

DEFINE FILE SET2
 ROLL_SEQ/I6=0;
END
TABLE FILE SET2
PRINT  INST_KEY
 INTIME
 RPT_INST.ROLLUP_CODE
 USER_NAME
 SET_ID
 INSTANCE_ID
 RPT_INST.EXECUTE_FLAG AS 'EX_FLAG'
 SET_NAME
 RPT_NAME
 RPT_GROUP
 RPT_TYPE
 RPT_STREAM
 GLOBAL_PARM
 ROLL_SEQ
 SET2.RUN_SEQ AS 'SET_SEQ'
 RPT_INST.RUN_SEQ AS 'RPT_SEQ'
ON TABLE HOLD AS INST3
END
-RUN

-*----------------------------
-* Update RPT_QUE
-*----------------------------
MODIFY FILE RPT_QUE
 LOG NOMATCH MSG OFF
 LOG DUPL MSG OFF
 LOG INVALID MSG OFF
 LOG FORMAT MSG OFF
FIXFORM FROM INST3
MATCH INST_KEY INTIME
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON INST3
END
-RUN

-********************************
-* Update HRpt_flds
-********************************
EX SUBMITRF
-RUN

-NODATA
-TYPE FORCED EXIT NO RECORDS SUBMIT300
-RUN
-EXIT




