-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the new quarterly review - JK 8/2/05
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK

-******************************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT  

BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
NBR_DAYS/D8 = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
CCQ/A1 = IF INVOICE_DATE GE '&&CQFMDT.EVAL' AND
                INVOICE_DATE LE '&&CQTODT.EVAL' THEN 'Y' ELSE 'N';                 
TC_AMT/D12.2 = RENTAL_AMT;


-* Defines on Previous Defines

CLIENTCODE/A8 = '&&ROLLUP.EVAL';
-INCLUDE QRFTRANC

END


TABLEF FILE &&EXTRACT
PRINT BOOKINGS       
      PICK_CTY.CITY_NAME/A20 AS 'PICK_CTY_NAME'	
      PICKUP_CITY
      NBR_DAYS
      VENDOR_CODE
      VENDOR_NAME
      CLIENTCODE AS 'ROLLUP_CODE'
      TC_AMT
      YEAR
      TRAN
      CURRENT
      CCQ
      PRIOR
      PYTD
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL') OR
      (INVOICE_DATE GE '&&FXDATE.EVAL') AND (INVOICE_DATE LE '&&TXDATE.EVAL')
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
ON TABLE HOLD AS HOLDCARP
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;

DEFINE FILE HOLDCARP
CDAY/D8 = IF CURRENT EQ 'Y' THEN NBR_DAYS ELSE 0;
PDAY/D8 = IF PYTD EQ 'Y' THEN NBR_DAYS ELSE 0;
CAMT/P12.2 = IF CURRENT EQ 'Y' THEN TC_AMT ELSE 0;
PAMT/P12.2 = IF PYTD EQ 'Y' THEN TC_AMT ELSE 0;
CBOOKINGS/D8 = IF CURRENT EQ 'Y' THEN BOOKINGS ELSE 0;
PBOOKINGS/D8 = IF PYTD EQ 'Y' THEN BOOKINGS ELSE 0;
END

TABLE FILE HOLDCARP
SUM CDAY
    CAMT
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
    VENDOR_NAME
BY ROLLUP_CODE
BY VENDOR_CODE
ON TABLE HOLD AS PEERCAR 
END
-RUN

DEFINE FILE PEERCAR
RANK/A9 = ' ';
END
TABLE FILE PEERCAR
SUM CDAY
    CAMT
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
    VENDOR_NAME
    RANK
BY ROLLUP_CODE
BY VENDOR_CODE
WHERE VENDOR_CODE IN FILE SAVCVDR
ON TABLE HOLD AS PEERCAR2
END
-RUN

TABLE FILE PEERCAR2
PRINT ROLLUP_CODE VENDOR_NAME RANK CDAY CBOOKINGS CAMT PDAY PBOOKINGS PAMT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRCVDR
FIXFORM ROLLUP_CODE/8 VENDOR_NAME/12 RANK/9 CDAY/8 CBOOKINGS/8 CAMT/12 PDAY/8 PBOOKINGS/8 PAMT/12
MATCH ROLLUP_CODE VENDOR_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


DEFINE FILE PEERCAR
VND_NAME/A12 = 'ALL OTHER';
RANK/A9 = 'OTHER';
END
TABLE FILE PEERCAR
SUM CDAY
    CAMT
    CBOOKINGS
    PDAY
    PAMT
    PBOOKINGS
    VND_NAME AS 'VENDOR_NAME'
    RANK
BY ROLLUP_CODE
WHERE NOT VENDOR_CODE IN FILE SAVCVDR
ON TABLE HOLD AS PEERCAR3
END
-RUN



TABLE FILE PEERCAR3
PRINT ROLLUP_CODE VENDOR_NAME RANK CDAY CBOOKINGS CAMT PDAY PBOOKINGS PAMT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRCVDR
FIXFORM ROLLUP_CODE/8 VENDOR_NAME/12 RANK/9 CDAY/8 CBOOKINGS/8 CAMT/12 PDAY/8 PBOOKINGS/8 PAMT/12
MATCH ROLLUP_CODE VENDOR_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN



TABLE FILE HOLDCARP
SUM CAMT
    CDAY
    PICK_CTY_NAME
BY ROLLUP_CODE
BY PICKUP_CITY
WHERE CURRENT EQ 'Y'
ON TABLE HOLD AS PEERCTY
END
-RUN


DEFINE FILE PEERCTY
RANK/A9 = ' ';
END
TABLE FILE PEERCTY
SUM CAMT 
    CDAY
    RANK
BY ROLLUP_CODE
BY PICK_CTY_NAME
WHERE PICKUP_CITY IN FILE SAVCITY
ON TABLE HOLD AS PEERCTY2
END
-RUN


TABLE FILE PEERCTY2
PRINT ROLLUP_CODE PICK_CTY_NAME RANK CDAY CAMT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRCCTY
FIXFORM ROLLUP_CODE/8 PICK_CTY_NAME/20 RANK/9 CDAY/8 CAMT/12
MATCH ROLLUP_CODE PICK_CTY_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN

DEFINE FILE PEERCTY
PKCTY/A20 = 'ALL OTHER';
RANK/A9 = 'OTHER';
END
TABLE FILE PEERCTY
SUM CAMT 
    CDAY
    PKCTY AS 'PICK_CTY_NAME'
    RANK
BY ROLLUP_CODE
WHERE NOT PICKUP_CITY IN FILE SAVCITY
ON TABLE HOLD AS PEERCTY3
END
-RUN

TABLE FILE PEERCTY3
PRINT ROLLUP_CODE PICK_CTY_NAME RANK CDAY CAMT
ON TABLE SAVE
END
-RUN

MODIFY FILE QRCCTY
FIXFORM ROLLUP_CODE/8 PICK_CTY_NAME/20 RANK/9 CDAY/8 CAMT/12
MATCH ROLLUP_CODE PICK_CTY_NAME
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN

DEFINE FILE HOLDCARP
RIGHT_TRAN/A7 = RJUST(7, TRAN, RIGHT_TRAN);
END
TABLE FILE HOLDCARP
SUM NBR_DAYS
    TC_AMT 
BY ROLLUP
BY YEAR
BY RIGHT_TRAN AS 'TRAN'
ON TABLE HOLD AS PQADR
END
-RUN



TABLE FILE PQADR
PRINT ROLLUP_CODE YEAR TRAN NBR_DAYS TC_AMT
ON TABLE SAVE
END
-RUN


MODIFY FILE QRCADR
FIXFORM ROLLUP_CODE/8 YEAR/4 TRAN/7 NBR_DAYS/8 TC_AMT/12
MATCH ROLLUP_CODE YEAR TRAN
ON NOMATCH INCLUDE 
DATA ON SAVE
END
-RUN





-NEXTONE

