-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
-* File AVIW35DT.FEX
-*********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*03/30/01     DEB BUCKNER/WTORRES  VIEW AMENDED TO NOT INCLUDE CARRIERS
-*                                  W/NO NAME
-*04/09/01     SCORDS/JKALHORN      COMMENTED OUT THE CARRIERS W/NO NAME
-*                                  COMMENTED OUT THE IF FARE > 0
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*10/22/03    JOY               CARRIED THRU LEVEL1
-*4/28/8/04   JOY               REMOVED LEVEL_DESC SORT TO ELIMINATE ERRORS
-*                              WHEN CHOOSING LOWER LEVEL_DESC SORT
-*10/29/04    DEB               CHANGED TO ADD GLOBAL CURRENCY
-*03/15/05    DEB               CREATED NEW VIEW FOR DUAL DATE CARRIER USAGE
-*5/9/13     jk                 Changed the header to include travel date
-*06/26/17   DLV       S-35283  PRISM reports Carried through NFVSVX  
-*                              Code report style sheet for Position to be variable instead of hard coded
-* 09/18/2017 RSB S-38044 Place the footer date/time stamp text in a separate file to be -INCLUDED in the stylesheets
-**********************************************************************
SET ASNAMES = ON
-RUN
-*
-SET &&RANK_METH = 'BASE_FARE';
-SET &&RANK_LIMIT = 10;



-IF &&SETF EQ 'ABCARIDD' AND &&CURRENCY NE 'USD' THEN GOTO CARIR
-ELSE GO TO NEXT;

-CARIR;

-SET &&SUBHEAD = <NAMEC;
-SET &FBASE_CUR = 'BASE FARE, (' | &&CURRENCY || ')';
-SET &&PRINT7='PAID_FARE1/D16.2CS AS ' | '''&FBASE_CUR.EVAL''';
-SET &&PRINT8='KILO AS ''TOTAL,KLMTRS''';

-NEXT;

-IF &&SETF EQ 'ABCARIDB' AND &&CURRENCY NE 'USD' THEN GOTO CARIRDB
-ELSE GO TO NEXT2;

-CARIRDB;

-SET &&SUBHEAD = <NAMEC;
-SET &FBASE_CUR = 'BASE FARE, (' | &&CURRENCY || ')';
-SET &GCCS_CUR= 'CONTRACT,SAVINGS, (' | &&CURRENCY ||')';
-SET &&PRINT7='PAID_FARE1/D16.2CS AS ' | '''&FBASE_CUR.EVAL''';
-SET &&PRINT8='KILO AS ''TOTAL,KLMTRS''';
-SET &&PRINT9='TNFVSV/D16.2CS AS ' | '''&GCCS_CUR.EVAL''';

-NEXT2;

-*INSERT AVIW35SM

-TYPE CREATE RNKHLD1
TABLE FILE AIRCARI3
SUM BASE_FARE 
  COMM_SEG	
  CP_FARE_PD AS 'FARE'
  FARE_BASE1/D20.6CS AS 'GBASE_FARE'
  FARE_TTL1/D16.2CS AS 'GTTL_FARE'  
  INTCNT
  KILO
  MILES
  NAMEC	
  NET_COMM
  NET_FARE
  NET_TKT_CNT
  PAID_FARE1/D16.2CS AS 'GFARE'	
  TAX_TTL1/D16.2CS AS 'GTTL_TAX'
  TOTAL_FARE
  TOTAL_TAX
  NFVSVX/D16.2CS AS 'GVSAV'
BY AIRLINE_NAME
ON TABLE HOLD AS RNKHLD1
END
-RUN


-TYPE CREATE RNKHLD2
TABLE FILE RNKHLD1
SUM BASE_FARE 
  COMM_SEG	
  FARE
  GBASE_FARE
  GFARE
  GTTL_FARE
  GTTL_TAX
  INTCNT
  KILO
  NAMEC
  MILES	
  NET_COMM
  NET_FARE
  NET_TKT_CNT
  TOTAL_FARE
  TOTAL_TAX
  GVSAV
RANKED BY HIGHEST &&RANK_METH NOPRINT
BY AIRLINE_NAME
ON TABLE HOLD AS RNKHLD2 
END
-RUN

DEFINE FILE RNKHLD2
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = LAST LIST + 1;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE  
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE 
                     CRANK;
  RANK_LABEL/A12 = IF RANK_VALUE NE 'ALL OTHER' THEN AIRLINE_NAME
                      ELSE 'OTHER';
END
-RUN

-TYPE CREATE RNKHLD3
TABLE FILE RNKHLD2
SUM FARE AS 'TFARE'
  GFARE AS 'GTFARE'
  KILO AS 'TKILO'	
  MILES AS 'TMILES'	
  NET_COMM AS 'TCOM'	
  NET_TKT_CNT AS 'TTKTS'
  GVSAV AS 'TVSAV'
BY RANK_LABEL
ON TABLE HOLD AS RNKHLD3
END
-RUN


-TYPE CREATE MATCH RNKHLD4
MATCH FILE RNKHLD2
PRINT 
  AIRLINE_NAME
  BASE_FARE	
  COMM_SEG
  GBASE_FARE	
  GVSAV
  INTCNT
  KILO
  FARE
  GFARE
  GTTL_FARE
  GTTL_TAX	
  MILES
  NAMEC	
  NET_COMM	
  NET_TKT_CNT	
  RANK_VALUE
  TOTAL_FARE
  TOTAL_TAX
BY RANK_LABEL
ON TABLE HOLD
RUN
FILE RNKHLD3
SUM GTFARE
  TFARE
  TKILO
  TMILES
  TTKTS
  TVSAV
BY RANK_LABEL
AFTER MATCH HOLD AS RNKHLD4 OLD-AND-NEW
END
-RUN 

DEFINE FILE RNKHLD4
  PCTFARE/D8.1= IF ((TFARE LT 1) AND (TFARE GT (-1))) THEN 0
                ELSE ((FARE/TFARE)*100);
  PCTTKT/D8.1=  IF ((TTKT LT 1) AND (TTKT GT (-1))) THEN 0
                ELSE ((NET_TKT_CNT/TTKT)*100); 
  PTMILES/D8.1= IF ((TMILES LT 1) AND (TMILES GT (-1))) THEN 0
                ELSE ((MILES/TMILES)*100);
END
-RUN

-*END OF INSERT AVIW35SM

-TYPE CREATE HOLD4
TABLE FILE RNKHLD4
PRINT 
  AIRLINE_NAME
  COMM_SEG	
  INTCNT
  FARE 
  GFARE
  GTTL_FARE
  GTTL_TAX
  GVSAV
  KILO	
  MILES
  NAMEC	
  NET_COMM	
  NET_TKT_CNT	
  RANK_VALUE
  TOTAL_FARE
  TOTAL_TAX	
  TFARE
  TKILO
  TMILES
  TTKTS
  TVSAV
  PCTFARE
  PCTTKT
  PTMILES
BY RANK_LABEL
ON TABLE HOLD AS HOLD4 FORMAT FOCUS INDEX RANK_LABEL
END
-RUN



-*This starts the detail

-TYPE CREATE AIRHOLD1
TABLE FILE AIRCARI3
SUM 
  COMM_SEG AS 'TCOMM'
  CP_FARE_PD AS 'PBFARE'
  FARE_TTL1/D16.2CS AS 'GPTFARE' 
  KILO AS 'PTKILO' 
  MILES AS 'PTMILES'
  NET_COMM AS 'NFARE'
  PAID_FARE1/D16.2CS AS 'GPBFARE'
  TAX_TTL1/D16.2CS AS 'GTTAX'
  TOTAL_FARE AS 'TFARE'
  TOTAL_TAX AS 'TTAX'
  NFVSVX/D16.2CS AS 'TNFVSV'
BY AIRLINE_NAME
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN

-TYPE CREATE XTWO
TABLE FILE AIRCARI3
PRINT 
  AIRLINE	
  COMM_SEG	
  CP_FARE_PD	
  DPT_DAY	
  DPT_TIME	
  EMB_APT_CD	
  EMB_APT_NM	
  EMB_CTY_NM
  FARE_TTL1/D16.2CS
  INTCNT
  KILO
  LEVEL1
  LEVEL_DESC
  MILES
  NAMEC	
  NET_COMM	
  NET_FARE	
  NET_TKT_CNT
  NFVSV/D16.2CS
  PAID_FARE1/D16.2CS  	
  REFUSE_CODE	
  RTE_APT_CD	
  RTE_APT_NM	
  RTE_CTY_NM
  TAX_TTL1/D16.2CS 	
  TKT_NUM	
  TKT_TYPE	
  TOTAL_FARE	
  TOTAL_TAX	
  XDPT_DT	
  XTRAN_DT	
BY AIRLINE_NAME
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN

-TYPE CREATE MATCH AIRHOLD2
MATCH FILE XTWO
PRINT 
  AIRLINE	
  COMM_SEG	
  CP_FARE_PD	
  DPT_DAY	
  DPT_TIME	
  EMB_APT_CD	
  EMB_APT_NM	
  EMB_CTY_NM
  FARE_TTL1	
  INTCNT
  KILO
  LEVEL_DESC
  LEVEL1
  MILES	
  NET_COMM	
  NET_FARE	
  NET_TKT_CNT
  NFVSV
  PAID_FARE1	
  REFUSE_CODE	
  RTE_APT_CD	
  RTE_APT_NM	
  RTE_CTY_NM
  TAX_TTL1	
  TKT_NUM	
  TKT_TYPE	
  TOTAL_FARE	
  TOTAL_TAX	
  XDPT_DT	
  XTRAN_DT	
BY AIRLINE_NAME
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT
  GPTFARE
  GPBFARE
  GTTAX
  PBFARE
  NFARE
  TCOMM 
  TFARE
  PTMILES	
  TTAX
  PTKILO
  TNFVSV
BY AIRLINE_NAME
BY XPSNGR_NM
BY TKT_SORT
BY TRN_DATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN

-TYPE CREATE AIRHOLD3 FOCUS
TABLE FILE AIRHOLD2
PRINT 
  AIRLINE	
  AIRLINE_NAME	
  PBFARE	
  COMM_SEG	
  CP_FARE_PD	
  DPT_DAY	
  DPT_TIME	
  EMB_APT_CD	
  EMB_APT_NM	
  EMB_CTY_NM
  FARE_TTL1
  GPTFARE
  GPBFARE
  GTTAX	
  INTCNT
  KILO
  LEVEL_DESC
  LEVEL1
  MILES	
  NET_COMM	
  NET_FARE	
  NET_TKT_CNT	
  NFARE
  NFVSV
  PAID_FARE1
  PTKILO	
  REFUSE_CODE	
  RTE_APT_CD	
  RTE_APT_NM	
  RTE_CTY_NM
  TAX_TTL1	
  TCOMM	
  TFARE	
  TKT_NUM	
  TKT_TYPE	
  PTMILES	
  TOTAL_FARE	
  TOTAL_TAX	
  TRN_DATE	
  TTAX	
  XDPT_DT	
  XPSNGR_NM	
  XTRAN_DT
  TNFVSV
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3 FORMAT FOCUS INDEX AIRLINE_NAME
END
-RUN

-TYPE JOIN HOLD4 AND AIRHOLD3

JOIN AIRHOLD3.AIRLINE_NAME IN AIRHOLD3 TO ALL HOLD4.RANK_LABEL IN HOLD4
-RUN




DEFINE FILE AIRHOLD3
   PASNGR_NAME/A15 = IF TKT_NUM NE LAST TKT_NUM THEN XPSNGR_NM ELSE ' ';
   RREFUSE_CODE/A2 = IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE '';
   TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
   XT_DTE/A8=EDIT(TRN_DATE);
   XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
  
-* ****DEFINES ON PREVIOUS DEFINES****
   SRT_DT/A100 = TKT_SORT||XT_DTE||LEVEL_DESC||XPSNGR_NM;
   GBASE_FARE/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN GPBFARE ELSE 0;
   BASE_FARE/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN PBFARE ELSE 0;
   COMM_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TCOMM ELSE 0;
   FARE_PAID/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
   NET_FARE/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN NFARE ELSE 0;
   TOTAL_KILO/D9CS = IF SRT_DT NE LAST SRT_DT THEN PTKILO ELSE 0;
   TOTAL_MILES/D9CS = IF SRT_DT NE LAST SRT_DT THEN PTMILES ELSE 0;
   TOTAL_TAX/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TTAX ELSE 0;
-*********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-**********************************************************************
  NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
 

-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_DRIL;
-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'AIRHOLD3';
-INCLUDE DRILWHRS
-************************************************************
-SKIP_DRIL

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIRD' ELSE 'HDREXLPRISM'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
-RUN

 
TABLE FILE AIRHOLD3
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDRPRISMSM' GOTO NOSPC;

-*"</2"
-NOSPC;
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*

&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-*
-**********************************************************************
-* Where statements for Drilled Report
-**********************************************************************
-*
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
 

-*
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10
-*
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&PASTYD

ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
