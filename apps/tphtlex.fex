-* 1/30/13 - DEB changed due to expansion of hotel database to
-* include trip purpose
-* CHANGED CODE DUE TO DRILL DOWN NOT WORKING - DEB 11/18/13
-* Added Preferred Hotel Defines - JK 3/31/14
-* Added logic for CONG-R to have different define for Pref nights per Jami 4/2/14
-* 9/9/14 Updated Pref_non define to use exclude_flags - jk
-* 9/26/14 Updated to use exclude flags in % - JK
-*  03/31/2015  REJ  S-05681 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-*02/12/16 - DLV - S-13286  Updated to allow reports to sort/subtotal by id-levels
-*04/14/16 - DLV - S-17840 Updated NNROOMS3 define to include PNP_FLAG due to 
-*                         overstatement of preferred pct %
-*04/20/16 - DLV - S-17840 changed define back to original due to preferred hotel %
-*                         always showing 100%

-INCLUDE SETECHO
 
FILEDEF RPTPARMS CLEAR
-RUN
 
EX HTLUSE
-RUN
 
-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN
 
DEFINE FILE BRPTINST
  RT/A3 = 'HTL';
END
TABLE FILE BRPTINST
PRINT RT AS 'RPT_GROUP'
BY INST_KEY
ON TABLE SAVE AS AIRACH00
END
-RUN
 
 
MODIFY FILE BRPTINST
-IF &&FROMAVRP EQ 'Y' GOTO :TPH1;
FIXFORM INST_KEY/7 RPT_GROUP/3
-GOTO :TPH1_CONT;
-:TPH1
FIXFORM INST_KEY/11 RPT_GROUP/3
-:TPH1_CONT
MATCH INST_KEY
  ON MATCH UPDATE RPT_GROUP
DATA ON AIRACH00
END
-RUN
 
-IF &&FROMAVRP EQ 'Y' GOTO :TPH2;
-* rej - don't understand what this modify is doing??
MODIFY FILE BRPTFLDS
FIXFORM INST_KEY/7 HTL.ID/1 HTL.INCL1/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG HTL
  ON NOMATCH INCLUDE
  ON MATCH UPDATE HTL.INCL1
DATA ON AIRACH00
END
-RUN
-:TPH2
 
EX CODEGEN
-RUN

-SET &&IDLEVELBREAK = ' ';
-RUN
 
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
HBOOK/P8CS = IF TOTAL_AMT GE 0 THEN 1 ELSE (-1);
LEVEL_DESC1/A60      = &&LDESC;
NBR_DAYS/D12 =  NUM_DAYS;
NNROOMS/P12CS = IF ((NBR_DAYS LT 0) AND (NUM_ROOMS LT 0)) THEN
                ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
XPSNGR_NM/A15 = EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
HTL_DT/MDYY         = CHKIN_DATE;
HTL_AMT/D12.2 = TOTAL_AMT;
TRN_DATE/YYMD = INVOICE_DATE;
TRIP_KEY/A2   = EDIT(TRP_KEY, '$$$$$$$$99');
TRIPRSN/A10 = IF TRIP_KEY NE ' ' THEN TRIP_KEY ELSE 'ZZZZZZZZZZ';
TRIP_RSN/A10 = IF TRIPRSN EQ 'ZZZZZZZZZZ' THEN 'NONE GIVEN' ELSE TRIPRSN;   
PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
PREF_NGT/P12CS = IF PNP_FLAG EQ 'I' THEN NNROOMS ELSE 0;
PREF_NGT1/P12CS = IF PNP_FLAG EQ 'I' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 0;
NNROOMSPN/P12CS = IF PNP_FLAG EQ 'I' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 
                   IF PNP_FLAG EQ 'E' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 0;    
      
NNROOMS3/P12CS = IF PNP_IE EQ 'I' THEN NNROOMS ELSE 0;  
-*NNROOMS3/P12CS = IF PNP_FLAG EQ 'I' AND PNP_IE EQ 'I' THEN NNROOMS ELSE 0;  

-INCLUDE IDLEVDEFINE  

END                
TABLE FILE &&EXTRACT
PRINT HBOOK NNROOMS NNROOMS3 LEVEL_DESC HTL_AMT TRIP_KEY TRIP_RSN  TRP_REF.TRP_DESC PREF_NGT PREF_NGT1
      

BY HTL_KEY
-INCLUDE &HTLDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHLDH
END
-RUN


-*JOIN HTL_KEY IN RPTHLDH TO HTL_KEY IN HTLUDID
-*-RUN
 
 
TABLE FILE RPTHLDH
PRINT HBOOK NNROOMS NNROOMS3 HTL_AMT PREF_NGT PREF_NGT1  
BY LEVEL_DESC
BY TRIP_KEY
BY TRIP_RSN
BY TRP_DESC
 
-*WHERE UDID EQ 'TP-'
ON TABLE HOLD AS HTLHLDA
END
-RUN
 
TABLE FILE HTLHLDA
SUM HBOOK NNROOMS NNROOMS3 HTL_AMT PREF_NGT PREF_NGT1
BY LEVEL_DESC
BY TRIP_KEY
BY TRIP_RSN
BY TRP_DESC
ON TABLE HOLD AS HTLHLD
END
-RUN
 
 
 
