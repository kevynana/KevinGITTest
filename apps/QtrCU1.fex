-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Increased the Rank Limit to 10 per Patti Cavlovic - JK 5/11/04
-* ADDED TRANTYPE INCLUDE - JK 12/30/14 STORY ID S-06492

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE MR_50  
  FARE_PAID/D12.2 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D8.2CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;  
-INCLUDE TRANTYPE                           
END
-RUN

TABLEF FILE MR_50
PRINT FARE_PAID
  NET_TKT_CNT
  TRN_DATE
  AIRLINE_NAME AS 'AIRLINE'
  VOID_DATE 	
WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3



-INCLUDE RPTPARMS
ON TABLE HOLD AS CUHLD1
END
-RUN

DEFINE FILE CUHLD1
CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
FLAG/A1 = 'A';
END

TABLE FILE CUHLD1
PRINT CURRENT
  NET_TKT_CNT
  PRIOR	
  VOID_DATE 
BY FLAG
BY AIRLINE
ON TABLE HOLD AS CUHLD2
END
-RUN

DEFINE FILE CUHLD2
CMARK/D15 = IF (CURRENT EQ 'Y') AND (VOID_DATE EQ 0) THEN NET_TKT_CNT ELSE 0;
PMARK/D15 = IF (PRIOR EQ 'Y') AND (VOID_DATE EQ 0) THEN NET_TKT_CNT ELSE 0;
END

TABLE FILE CUHLD2
SUM CMARK
    PMARK
BY FLAG
BY AIRLINE
ON TABLE HOLD AS CUHLD3
END
-RUN

TABLE FILE CUHLD3
SUM CMARK AS 'TCMARK'
    PMARK AS 'TPMARK'
BY FLAG
ON TABLE HOLD AS MUHLD
END
-RUN

-* JOIN CLEAR *
-RUN

MATCH FILE CUHLD3
PRINT CMARK
      PMARK
      AIRLINE
BY FLAG
ON TABLE HOLD
RUN
FILE MUHLD
SUM TCMARK
    TPMARK
BY FLAG
AFTER MATCH HOLD AS TCUHLD OLD-OR-NEW
END
-RUN

TABLE FILE TCUHLD
PRINT CMARK
      PMARK
      TCMARK
      TPMARK
      FLAG
      AIRLINE
RANKED BY HIGHEST CMARK NOPRINT
ON TABLE HOLD AS CUHLD3
END
-RUN

DEFINE FILE CUHLD3
LIST/D9 = IF AIRLINE NE LAST AIRLINE THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 10 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL/A30 = IF RANK_VALUE NE 'OTHER' THEN AIRLINE
                      ELSE 'ALL OTHER';
-* DEFINES ON PREVIOS DEFINES
 RANK_LAB/A30 = LCWORD(30, RANK_LABEL, RANK_LAB);
END
-RUN
TABLE FILE CUHLD3
SUM     
COMPUTE CMPCT/D15 = IF ((TCMARK LT 1) AND (TCMARK GT (-1))) THEN 0 ELSE
                       ((CMARK/TCMARK)*100);
COMPUTE PMPCT/D15 = IF ((TPMARK LT 1) AND (TPMARK GT (-1))) THEN 0 ELSE
                       ((PMARK/TPMARK)*100);
COMPUTE IDMPCT/D15 = IF ((PMPCT LT 1) AND (PMPCT GT (-1))) THEN 0 ELSE
                        (CMPCT-PMPCT);
BY RANK_VALUE
BY RANK_LAB
ON TABLE HOLD AS CUHLD4
END
-RUN


DEFINE FILE CUHLD4
CATEGORY/A50 = 'Carrier Usage Percentage';
CAT_ORDER/I2 = 14;
END
-RUN

TABLE FILE CUHLD4
PRINT CATEGORY/A50 AND RANK_LAB/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CMPCT/D15 AS 'CURRENT' AND
PMPCT/D15 AS 'PRIOR' IDMPCT AS 'IDPERCENT' 
COMPUTE ROW_ORDER/I2 = 1;  
WHERE RANK_LAB NE 'All Other';
ON TABLE HOLD AS CUHLD5
END
-RUN

TABLE FILE CUHLD5
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS CUHLD6
END
MODIFY FILE QTR_REV1
FIXFORM FROM CUHLD6
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON CUHLD6
END
-RUN

-* Carrier Usage % Graph Creation
DEFINE FILE CUHLD3
LIST/D9 = IF AIRLINE NE LAST AIRLINE THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 5 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL/A30 = IF RANK_VALUE NE 'OTHER' THEN AIRLINE
                      ELSE 'ALL OTHER';
-* DEFINES ON PREVIOS DEFINES
 RANK_LAB/A30 = LCWORD(30, RANK_LABEL, RANK_LAB);
END
-RUN

TABLE FILE CUHLD3
SUM     
COMPUTE CMPCT/D15 = IF ((TCMARK LT 1) AND (TCMARK GT (-1))) THEN 0 ELSE
                       ((CMARK/TCMARK)*100);
COMPUTE PMPCT/D15 = IF ((TPMARK LT 1) AND (TPMARK GT (-1))) THEN 0 ELSE
                       ((PMARK/TPMARK)*100);
COMPUTE IDMPCT/D15 = IF ((PMPCT LT 1) AND (PMPCT GT (-1))) THEN 0 ELSE
                        (CMPCT-PMPCT);
BY RANK_VALUE
BY RANK_LAB
ON TABLE HOLD AS CUHLD3G
END
-RUN


-IF &&SETF EQ 'ABQTRRV1' OR 'ABQTRRV2' THEN GOTO SKIP;

DEFINE FILE CUHLD3G
  CP/I9 = IF ((TCMARK LT 1) AND (TCMARK GT (-1))) THEN 0 ELSE 1;
  PP/I9 = IF ((TPMARK LT 1) AND (TPMARK GT (-1))) THEN 0 ELSE 1;
END
TABLE FILE CUHLD3G
SUM CP PP
ON TABLE SAVE AS CHK1
END
-RUN
-READ CHK1 &CP.9. &PP.9.
-SET &PLINE1 = &CP;
-SET &PLINE2 = &PP;


GRAPH FILE CUHLD3G
SUM
COMPUTE CMPCT/D12.2 = IF ((TCMARK LT 1) AND (TCMARK GT (-1))) THEN 0 ELSE
                         (CMARK/TCMARK); AS 'Period1'
COMPUTE PMPCT/D12.2 = IF ((TPMARK LT 1) AND (TPMARK GT (-1))) THEN 0 ELSE
                         (PMARK/TPMARK); AS 'Period2'
ACROSS RANK_LAB AS ''
WHERE RANK_LAB NE 'All Other'
ON TABLE SET 3D OFF
ON TABLE SAVE AS CRUSG FORMAT SVG
ON TABLE SET GRAPHSTYLE *
setGraphType(17);
setLegendDisplay(true);
setLegendPosition(2);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),false);
setFontSize(getLegendText(),15);
setFontStyle(getLegendText(),0);
setLegendMarkerPosition(1);
setAutofit(getDataText(),true); 
setDataTextDisplay(true);
setDataTextFormat(2); 
setFontSizeAbsolute(getDataText(),true);
setAutofit(getDataText(),false);
setFontStyle(getDataText(),0);
setFontSize(getDataText(),10);
setO1LabelDisplay(true);
setFontSizeAbsolute(getO1Label(),true); 
setAutofit(getO1Label(),false);
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),15);
setO1LabelRotate(0);
setO1MajorGridDisplay(false);
setO1MinorGridDisplay(false);
setFontSizeAbsolute(getY1Label(),true);
setAutofit(getY1Label(),false);
setY1LabelDisplay(true);
setY1LabelFormat(2);
setFontSize(getY1Label(),15);
setFontStyle(getY1Label(),0);
setY1AxisSide(0);
setFontSizeAbsolute(getTitle(),true); 
setAutofit(getTitle(),false);
setTitleString("Carrier Usage Percentage");
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),0);
setFontSize(getTitle(),15);
setFontSizeAbsolute(getSubtitle(),true); 
setAutofit(getSubtitle(),false);
setSubtitleString("Per Airline");
setTextJustHoriz(getSubtitle(),1);
setFontStyle(getSubtitle(),0);
setFontSize(getSubtitle(),15);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 255 255 0));
setSeriesFillColor(2, new Color, 115 160 247));


-IF &PLINE1 GT 0 THEN GOTO showYVal;
setDisplay(getDataText(0),false);
-GOTO ChkXVal;

-showYVal
setDisplay(getDataText(0),true);

-ChkXVal
-IF &PLINE2 GT 0 THEN GOTO showXVal; 
setDisplay(getDataText(1),false);
-GOTO CHKXYDONE;

-showXVal
setDisplay(getDataText(1),true);

-CHKXYDONE


ENDSTYLE 
END
-RUN


-SET &&G_GIF10=IF &LINES GT 0 THEN 'Y' ELSE 'N'


-* Carrier Usage by Airline Spend Graph Creation
DEFINE FILE CUHLD1
CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
FLAG/A1 = 'A';
END

TABLE FILE CUHLD1
PRINT CURRENT
  FARE_PAID
  PRIOR	
  VOID_DATE 
BY FLAG
BY AIRLINE
ON TABLE HOLD AS CUAMT1
END
-RUN

DEFINE FILE CUAMT1
CFARE/D12.2 = IF (CURRENT EQ 'Y') AND (VOID_DATE EQ 0) THEN FARE_PAID ELSE 0;
PFARE/D12.2 = IF (PRIOR EQ 'Y') AND (VOID_DATE EQ 0) THEN FARE_PAID ELSE 0;
END

TABLE FILE CUAMT1
SUM CFARE
    PFARE
BY FLAG
BY AIRLINE
ON TABLE HOLD AS CUAMT2
END
-RUN

TABLE FILE CUAMT2
PRINT CFARE
      PFARE
      FLAG
      AIRLINE
RANKED BY HIGHEST CFARE NOPRINT
ON TABLE HOLD AS CUAMT3
END
-RUN

DEFINE FILE CUAMT3
LIST/D9 = IF AIRLINE NE LAST AIRLINE THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 5 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL/A30 = IF RANK_VALUE NE 'OTHER' THEN AIRLINE
                      ELSE 'ALL OTHER';
-* DEFINES ON PREVIOS DEFINES
 RANK_LAB/A30 = LCWORD(30, RANK_LABEL, RANK_LAB);
END
-RUN
TABLE FILE CUAMT3
SUM  CFARE 
     PFARE 
BY RANK_VALUE
BY RANK_LAB
ON TABLE HOLD AS CUAMT4
END
-RUN

GRAPH FILE CUAMT4
SUM CFARE AS 'Period1'
    PFARE AS 'Period2'
ACROSS RANK_LAB AS ''
WHERE RANK_LAB NE 'All Other'
ON TABLE SET 3D OFF
ON TABLE SAVE AS CRUAMT FORMAT SVG
ON TABLE SET GRAPHSTYLE *
setGraphType(17);
setLegendDisplay(true);
setLegendPosition(2);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),false);
setFontSize(getLegendText(),15);
setFontStyle(getLegendText(),0);
setLegendMarkerPosition(1);
setAutofit(getDataText(),true); 
setDataTextDisplay(true);
setDataTextFormat(6); 
setFontSizeAbsolute(getDataText(),true);
setAutofit(getDataText(),false);
setFontStyle(getDataText(),0);
setFontSize(getDataText(),10);
setO1LabelDisplay(true);
setFontSizeAbsolute(getO1Label(),true); 
setAutofit(getO1Label(),false);
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),15);
setO1LabelRotate(0);
setO1MajorGridDisplay(false);
setO1MinorGridDisplay(false);
setFontSizeAbsolute(getY1Label(),true);
setAutofit(getY1Label(),false);
setY1LabelDisplay(true);
setY1LabelFormat(6);
setFontSize(getY1Label(),15);
setFontStyle(getY1Label(),0);
setY1AxisSide(0);
setFontSizeAbsolute(getTitle(),true); 
setAutofit(getTitle(),false);
setTitleString("Carrier Usage Amount");
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),0);
setFontSize(getTitle(),15);
setFontSizeAbsolute(getSubtitle(),true); 
setAutofit(getSubtitle(),false);
setSubtitleString("Per Airline");
setTextJustHoriz(getSubtitle(),1);
setFontStyle(getSubtitle(),0);
setFontSize(getSubtitle(),15);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 255 255 0));
setSeriesFillColor(2, new Color, 115 160 247));


-IF &PLINE1 GT 0 THEN GOTO showYVal2;
setDisplay(getDataText(0),false);
-GOTO ChkXVal2;

-showYVal2
setDisplay(getDataText(0),true);

-ChkXVal2
-IF &PLINE2 GT 0 THEN GOTO showXVal2; 
setDisplay(getDataText(1),false);
-GOTO CHKXYDONE2;

-showXVal2
setDisplay(getDataText(1),true);

-CHKXYDONE2


ENDSTYLE 
END
-RUN


-SET &&G_GIF11=IF &LINES GT 0 THEN 'Y' ELSE 'N'




-SKIP






