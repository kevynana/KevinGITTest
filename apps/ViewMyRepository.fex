-* File ViewMyRepository.fex.fex.fex
-* CREATES AND READS A FILE TREE IN DOS
-*
-*  05/26/2016  REJ  S-###### Changes made for ER5.
-*  10/19/2016  REJ  S-22499 Updated the logo to Post Trip Reporting.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

 
-INCLUDE SETECHO
-DEFAULT &ROLLUP = 'ABCD-R';
-DEFAULT &USER = 'lusheng';
 
TABLE FILE CONTROL
PRINT ROLLUP_CODE USER_ID
ON TABLE SAVE
END
-RUN
-READ SAVE &ROLLUP.8. &USER.20.
-CLOSE SAVE
 
-SET &LLEN = ARGLEN(8, '&ROLLUP.EVAL', 'I1');
-SET &ROLLUP = TRIM ('B',&ROLLUP,8,' ',1,'A&LLEN.EVAL');
-SET &LLEN = ARGLEN(20, '&USER.EVAL', 'I2');
-SET &USER = TRIM ('B',&USER,20,' ',1,'A&LLEN.EVAL');
 
-* INITIAL DIRECTORY FOR TREE
-*-SET &INITDIR = '&&WEBSRV.EVAL' || '\TTWEB';
-SET &INITDIR = '&&WEBSRV.EVAL' || '\TTWEB\' | '&ROLLUP.EVAL' || '\' | '&USER.EVAL' ;
 
-* FILEDEF FOR INITIAL TREE OUTPUT
-*-SET &FILELOC = '&&WEBSRV.EVAL' || '\TEMP\OUTPUTX.FTM';
-SET &FILELOC = '&&WEBSRV.EVAL' || '\TEMP\' | '&USER.EVAL' | '.FTM';
 
-SET &WPATH = &&SRVR || '/'|| 'TTWEB/' | '&ROLLUP.EVAL' || '/' | '&USER.EVAL' || '/' ;
 
-************
-CREATE_TREE
-************
DOS DEL &FILELOC
DOS TREE &INITDIR /F /A >> &FILELOC.EVAL
-RUN
FILEDEF TREE00 DISK &FILELOC.EVAL
-RUN
 
-*SET TEMPERASE = OFF
 
TABLE FILE TREE00
PRINT TREE
WHERE TREE CONTAINS '.PDF' OR '.pdf' OR '.PPT' OR '.PHT' OR '.pht' OR 'XLS' OR 'xls' OR '.zip' OR '.CSV'
ON TABLE HOLD
END
-RUN
-IF &LINES EQ 0 THEN GOTO NOREPORTS;
 
DEFINE FILE HOLD
 PATHNAME/A80= '&WPATH.EVAL' ;
 FILENAME/A200=TRIM('L',TREE,200,' ', 1, 'A200');
 FULLPATH/A290 = PATHNAME || FILENAME;
END
TABLE FILE HOLD
BY FULLPATH
BY FILENAME
ON TABLE HOLD
END
-RUN
DEFINE FILE HOLD
  CNT/I5 = CNT + 1;
  REMIN/I5 = IMOD(CNT, 2, REMIN);
END
TABLE FILE HOLD
-*HEADING CENTER "<IMG SRC="eReview_logo_sm.gif" Reports for &USER.EVAL"
HEADING CENTER
"Reports for &USER.EVAL"
"Client Code: &ROLLUP.EVAL "
"Click on the link to open the report"
PRINT REMIN NOPRINT
BY FULLPATH NOPRINT
BY FILENAME AS ''
 
ON TABLE HOLD AS RPSTRYTMP FORMAT HTMTABLE
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET PAGE OFF
ON TABLE SET HTMLCSS ON
ON TABLE SET LINES 99999
ON TABLE SET STYLE *
TYPE=HEADING, LINE=1, SIZE=12, STYLE=BOLD, FONT='TIMES', COLOR=BLACK, $
TYPE=HEADING, LINE=2, SIZE=10, COLSPAN=11, JUSTIFY=CENTER, FONT='TIMES',  STYLE=BOLD, COLOR=BLACK,$
TYPE=HEADING, LINE=3, SIZE=9,  COLSPAN=11, JUSTIFY=CENTER, FONT='TIMES',  STYLE=BOLD, COLOR=BLACK,$
 
TYPE=REPORT, UNITS=IN,
     PAGESIZE='SCREEN',
     LEFTMARGIN=0.000000,
     RIGHTMARGIN=0.000000,
     TOPMARGIN=0.000000,
     BOTTOMMARGIN=0.000000,
     SQUEEZE=ON,
     ORIENTATION=LANDSCAPE,
$
TYPE=REPORT,GRID=OFF,
     FONT='ARIAL',
     SIZE=9,
     COLOR=RGB(70 136 177),
     BACKCOLOR='NONE',
     STYLE=NORMAL,
     LEFTGAP=0.5000,
     RIGHTGAP=0.25000,$
TYPE=REPORT, IMAGE=\image\PostTripHeader.png, POSITION=(0 0), $
TYPE=DATA,COLUMN=FILENAME,BACKCOLOR=RGB(238 238 238), WHEN=REMIN GT 0, $
TYPE=DATA,STYLE=UNDERLINE,COLUMN=FILENAME,JAVASCRIPT=ShowReport(FULLPATH), $
ENDSTYLE
END
-RUN
-HTMLFORM RPSTRYFORM
 
-GOTO XXIT;
 
-NOREPORTS
DEFINE FILE CONTROL
 
END
TABLE FILE CONTROL
HEADING CENTER
"Reports for &USER.EVAL </1>"
"Client Code:&ROLLUP.EVAL"
"</1 "
"There is no file in this folder..."
BY USER_ID NOPRINT
 
ON TABLE SET ONLINE-FMT PDF
ON TABLE SET PAGE OFF
ON TABLE SET STYLE *
TYPE=REPORT,UNITS=IN,ORIENTATION=LANDSCAPE,$
TYPE=HEADING, LINE=1, SIZE=14, JUSTIFY=CENTER,FONT='TIMES',  STYLE=BOLD, COLOR=BLACK, $
TYPE=HEADING, LINE=3, SIZE=12, JUSTIFY=CENTER,FONT='TIMES',  STYLE=BOLD, COLOR=BLACK,$
TYPE=HEADING, LINE=3, SIZE=10, JUSTIFY=CENTER, FONT='TIMES',  STYLE=BOLD, COLOR=BLACK,$
TYPE=HEADING, LINE=4, SIZE=10, JUSTIFY=CENTER, FONT='TIMES',  STYLE=BOLD, COLOR=BLACK,$
TYPE=HEADING, LINE=5, SIZE=10, JUSTIFY=CENTER, FONT='TIMES',  STYLE=BOLD, COLOR=BLACK,$
TYPE=DATA, SIZE=12, FONT='TIMES', COLOR=BLACK,$
TYPE=REPORT, IMAGE=eReview_Logo_sm.jpg, POSITION=(0 0), $
ENDSTYLE
END
-RUN
 
-XXIT
