-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* 1/19/05 - DEB - ADDED CODE FOR GLOBAL CURRENCY
-*11/27/06 - DEB ADDED IBOOK
-*1/29/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*12/22/15 - DLV - S-13284   Updated to allow reports to sort/subtotal by id-levels

-DEFAULT &IDOM = ' ';
-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN 

TABLE FILE BRPTFLDS
PRINT  CAR.ID INTDOMC
BY INST_KEY NOPRINT
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE AS CAR1
END
-RUN
 

-IF &LINES EQ 0 THEN GOTO CONT;

 
-READ CAR1 &ID.1.  &INTDOMC.1. 

-SET &IDOM = IF '&INTDOMC.EVAL' EQ 1 THEN ' ' ELSE 
-             IF '&INTDOMC.EVAL' EQ 2 THEN 'D' ELSE 'I';


 
-CONT

-SET &&IDLEVELBREAK = ''; 
-RUN

-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN

DEFINE FILE &&EXTRACT
  LEVEL_DESC1/A60  = &&LDESC;
  NEW_TOTAL_AMT/D14.2C = TOTAL_AMT;
  BOOKINGS/D8 = IF ROOM_RATE GE 0 THEN 1 ELSE (-1);
  IBOOK/A13 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'INTERNET' ELSE
              'NON-INTERNET';
-INCLUDE IDLEVDEFINE 

END
-RUN

TABLEF FILE &&EXTRACT
PRINT BOOKINGS
-*      BOOKINGS AS 'HBOOK'
      CURR_DATE
      LEVEL_DESC
-*      NEW_TOTAL_AMT AS 'HAMT'
      NEW_TOTAL_AMT
      RESV_BRANCH
      TKT_NUM
      IBOOK
-INCLUDE &HTLDATES      
-*WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) 
WHERE TKT_NUM EQ '0000000000'
-IF '&IDOM.EVAL' EQ 'D' THEN GOTO DOM;
-IF '&IDOM.EVAL' EQ 'I' THEN GOTO INTL ELSE GOTO ALL;
-DOM
WHERE CITY.INTL_DOM EQ 'D'
-GOTO ALL
-INTL
WHERE CITY.INTL_DOM EQ 'I'
-ALL

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS HTLHOLD
END
-RUN

 

TABLE FILE HTLHOLD
PRINT BOOKINGS
      LEVEL_DESC
      NEW_TOTAL_AMT
      RESV_BRANCH
      TKT_NUM
      IBOOK
BY CURR_DATE
ON TABLE HOLD AS HTHOLD FORMAT FOCUS INDEX CURR_DATE
END
-RUN

-* Global Reporting JOIN
JOIN HTHOLD.CURR_DATE IN HTHOLD TO GLOBAL.DATE IN GLOBAL AS J1
END

DEFINE FILE HTHOLD
NAMEC/A30 = CNAME;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
AMT_TOTALX/D20.6 = NEW_TOTAL_AMT * CURR1X;

END

TABLE FILE HTHOLD
PRINT AMT_TOTALX/D16.2CS AS 'GHAMT'
      BOOKINGS AS 'HBOOK'
      CURR_DATE
      LEVEL_DESC
      NAMEC
      NEW_TOTAL_AMT AS 'HAMT'
      RESV_BRANCH
      TKT_NUM
      IBOOK
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS HHOLD
END
-RUN


TABLE FILE HHOLD
SUM GHAMT/D16.2CS 
   HBOOK
   HAMT
   NAMEC    
BY LEVEL_DESC
BY RESV_BRANCH
BY IBOOK
ON TABLE HOLD AS HTLBK
END
-RUN








