-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-* File SMRYRPT.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*12/07/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*4/4/16 - JEM - S-17441 Updated PCTN to be defined instead of being computed to correct grand total percentage
-**********************************************************************

SET ASNAMES = ON
-RUN

TABLE FILE &&RPT_HOLD
SUM FARE_PAID/D12.2CS AS 'TFARE'	
  NET_TKT_CNT AS 'NTTKT'	
  NEW_SEG_DISC AS 'TSDISC'	
  REF_EXCH_CNT AS 'TRFEX'	
  TKT_PURCH AS 'TTKT'	
  VASAVINGS AS 'VASAV'	
BY LEVEL_DESC  
ON TABLE HOLD AS HOLDONE
END
-RUN

-IF &&SETF EQ 'JHMULTI' GOTO JHA;

TABLE FILE &&RPT_HOLD
PRINT 
  AIRLINE_FEE	
  AIRLINE_NAME
  BOOK_DT
  FARE_PAID	
  LEVEL1
  LEVEL2
  LEVEL3
  NET_TKT_CNT	
  NEW_SEG_DISC	
  REF_EXCH_CNT	
  STOPOVER
  TKT_PURCH	
  VASAVINGS	
BY LEVEL_DESC
BY REFUSAL_DESC
BY REFUSE_CODE
BY XPSNGR_NM
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT 
  AIRLINE_FEE	
  AIRLINE_NAME
  BOOK_DT	
  FARE_PAID	
  LEVEL1
  LEVEL2
  LEVEL3
  NET_TKT_CNT	
  NEW_SEG_DISC	
  REF_EXCH_CNT	
  STOPOVER
  TKT_PURCH	
  VASAVINGS
BY LEVEL_DESC
BY REFUSAL_DESC
BY REFUSE_CODE
BY XPSNGR_NM 
ON TABLE HOLD
RUN
FILE HOLDONE
SUM NTTKT	
    TFARE	
    TRFEX	
    TSDISC	
    TTKT	
    VASAV	
BY LEVEL_DESC
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW 
END
-RUN
-GOTO PROCEED

-JHA
TABLE FILE &&RPT_HOLD
SUM NET_TKT_CNT
BY LEVEL_DESC
BY STOPOVER
ON TABLE HOLD AS XTWO
END
-RUN
MATCH FILE XTWO
PRINT NET_TKT_CNT	
  STOPOVER
BY LEVEL_DESC
ON TABLE HOLD
RUN
FILE HOLDONE
SUM NTTKT/P12	
BY LEVEL_DESC
AFTER MATCH HOLD AS XTHREE OLD-OR-NEW 
END
-RUN


-PROCEED
DEFINE FILE XTHREE
  PCTFARE/D8.1S= IF ((TFARE LT 1) AND (TFARE GT (-1))) THEN 0 
                 ELSE ((FARE_PAID/TFARE)*100);
  PCTNETTKT/D8.1S= IF ((NTTKT LT 1) AND (NTTKT GT (-1))) THEN 0
                   ELSE ((NET_TKT_CNT/NTTKT)*100);
  PCTRFEX/D8.1S= IF ((TRFEX LT 1) AND (TRFEX GT (-1))) THEN 0
                 ELSE ((REF_EXCH_CNT/TRFEX)*100);
  PCTTKT/D8.1S= IF ((TTKT LT 1) AND (TTKT GT (-1))) THEN 0
                ELSE ((TKT_PURCH/TTKT)*100);
  PCTN/D8.1S = IF NTTKT EQ 0 THEN 0 ELSE NET_TKT_CNT/NTTKT*100;
                

-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
-RUN

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN

TABLE FILE XTHREE
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
&&BREAKOPTION
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
-INCLUDE FOOTERSM
ON TABLE COLUMN-TOTAL
-*ON TABLE SET STYLE &&SMSTY
-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**********************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
-INCLUDE &&PASTYS

-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-**********************************************************************
-IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;
-INCLUDE &&DSTY

-SKIP_DRILL
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;

EX REPORT_3

-SKIP_R3
-*-EXIT


