-* 12/28/04  - DEB - ADDED DOD_TYPE 'J' TO CONL CDONL CONGONL DEFINE



-INCLUDE SETECHO

CREATE FILE TCABNM1
CREATE FILE TCCBNM
CREATE FILE TCHBNM
-RUN

-INCLUDE TCGETROLL
-RUN

-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCAIRDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;

EX TCBNA1EX
-RUN


EX TCCARDB
-RUN


EX TCBNCEX
-RUN

EX TCHTLDB
-RUN

EX TCBNHEX
-RUN


-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2

-INCLUDE SetParmtime
-RUN

-**************************************************************
-* GET THE FINAL REPORT
-**************************************************************


        

TABLE FILE TCABNM1
SUM CADVT
    CADVTP
    CADVT1
    CADVT2
    CADVT3
    CADVTPX
    CANET
    CAVOL
    CDANET
    CDAVOL
    CDFARE
    CDLSAV
    CDLS
    CDNTKT
    CDONL
    CDTKT
    CIANET
    CINTKT
    CLSAV
    CLS1
    CLS2
    CLS3
    CLS4
    CLS5
    CMILES
    CNR
    CNTP
    CSAV
    CSAV1
    CNTKT
    CVSAV
    PAVOL    
    PDAVOL
    PDFARE
    PDTKT   
COMPUTE CAVAR/D15.2CS = CAVOL - PAVOL; 
        CAVAR1/D15.2CS = IF ((PAVOL LT 1) AND (PAVOL GT (-1))) THEN 0 ELSE
                           (((CAVOL-PAVOL)/PAVOL)*100);
        CAVGT/D15.2CS = IF ((CDTKT LT 1) AND (CDTKT GT (-1))) THEN 0 ELSE
                           (CDFARE/CDTKT);
        PAVGT/D15.2CS = IF ((PDTKT LT 1) AND (PDTKT GT (-1))) THEN 0 ELSE
                           (PDFARE/PDTKT);
        AAVGV/D15.2CS = CAVGT - PAVGT;
        AAVGV1/D15.2CS = IF ((PAVGT LT 1) AND (PAVGT GT (-1))) THEN 0 ELSE
                           (((CAVGT-PAVGT)/PAVGT)*100);
        CADVP1/P15.2 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0
                          ELSE ((CADVT/CNTKT)*100);
        CADVP2/P15.2 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0
                          ELSE ((CADVT1/CNTKT)*100);
        CADVP3/P15.2 = IF ((CNTP LT 1) AND (CNTP GT (-1))) THEN 0
                          ELSE ((CADVTP/CNTP)*100);
        CADVP4/P15.2 = IF ((CNTP LT 1) AND (CNTP GT (-1))) THEN 0
                          ELSE ((CADVTPX/CNTP)*100);
        CADVP5/P15.2 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0
                          ELSE ((CADVT2/CNTKT)*100);
        CADVP6/P15.2 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0
                          ELSE ((CADVT3/CNTKT)*100);
 -*       CLSP/P15.2 = IF ((CLS2001 LT 1) AND (CLS2001 GT (-1))) THEN 0
 -*                        ELSE ((CLS200/CLS2001)*100);
 -*       CONLP/P15.2 = IF ((CNTKT1 LT 1) AND (CNTKT1 GT (-1))) THEN 0 
 -*                        ELSE ((CONL/CNTKT1)*100);
        CDONLP/P12.2 = IF ((CDNTKT LT 1) AND (CDNTKT GT (-1))) THEN 0 
                         ELSE ((CDONL/CDNTKT)*100);
-*        CONLP1/P15.2 = IF ((CNTP1 LT 1) AND (CNTP1 GT (-1))) THEN 0 
-*                         ELSE ((CONGONL/CNTP1)*100);
        CNRP/P15.2 = IF ((CNTKT LT 1) AND (CNTKT GT (-1))) THEN 0
                         ELSE ((CNR/CNTKT)*100);
-*        CNFSV/P15.2 = IF ((CSAV LT 1) AND (CSAV GT (-1))) THEN 0
-*                         ELSE ((CSAV1/CSAV)*100);
        CTLS/P15.2 =  IF ((CAVOL LT 1) AND (CAVOL GT (-1))) THEN 0
                         ELSE ((CLSAV/CAVOL)*100);
        CCLS1/P15.2 = IF ((CLSAV LT 1) AND (CLSAV GT (-1))) THEN 0 
                         ELSE ((CLS1/CLSAV)*100);
        CCLS2/P15.2 = IF ((CLSAV LT 1) AND (CLSAV GT (-1))) THEN 0 
                         ELSE ((CLS2/CLSAV)*100);
        CCLS3/P15.2 = IF ((CLSAV LT 1) AND (CLSAV GT (-1))) THEN 0 
                         ELSE ((CLS3/CLSAV)*100);
        CCLS4/P15.2 = IF ((CLSAV LT 1) AND (CLSAV GT (-1))) THEN 0 
                         ELSE ((CLS4/CLSAV)*100);
        CCLS5/P15.2 = IF ((CLSAV LT 1) AND (CLSAV GT (-1))) THEN 0 
                         ELSE ((CLS5/CLSAV)*100);
        CPMILE/P15.2 = IF ((CMILES LT 1) AND (CMILES GT (-1))) THEN 0
                         ELSE (CAVOL/CMILES);
        CTLSV/P15.2 = IF ((CDAVOL LT 1) AND (CDAVOL GT (-1))) THEN 0 ELSE
                          ((CDLSAV/CDAVOL)*100);
        CDATKT/P15.2 = IF ((CDNTKT LT 1) AND (CDNTKT GT (-1))) THEN 0 ELSE
                          (CDAVOL/CDNTKT);
        CDANTKT/P15.2 = IF ((CDNTKT LT 1) AND (CDNTKT GT (-1))) THEN 0 ELSE
                         (CDANET/CDNTKT);
        CIANTKT/P15.2 = IF ((CINTKT LT 1) AND (CINTKT GT (-1))) THEN 0 ELSE
                          (CIANET/CINTKT);
 
BY ROLLCD
ON TABLE HOLD AS TRANTWO 
END
-RUN


DEFINE FILE TCCBNM
CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE &&FXDATE.EVAL AND
               INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
END

TABLE FILE TCCBNM
PRINT 
  BOOKINGS
  CAR_DESC	
  CAR_KEY	
  CAR_RATE 	
  CAR_TYPE 
  CURRENT	
  DAILY_RATE
  MIDAMT
  MIDBOOK
  MID_DAYS	
  NOMID_DAYS
  NBR_DAYS 
  PRIOR
  RENTAL_AMT
  RENTAL_DAYS
BY ROLLCD
ON TABLE HOLD AS CARTWO
END
-RUN

DEFINE FILE CARTWO
CRENTAL/D12S = IF CURRENT EQ 'Y' THEN RENTAL_DAYS ELSE 0;
CNOMID_DAYS/D12S = IF CURRENT EQ 'Y' THEN NOMID_DAYS ELSE 0;
CRENTALA/D12.2CS = IF CURRENT EQ 'Y' THEN MIDAMT ELSE 0;
CRENTALD/D12 = IF CURRENT EQ 'Y' THEN MID_DAYS ELSE 0;
END

TABLE FILE CARTWO
SUM CRENTAL
    CNOMID_DAYS 
COMPUTE PCTNOMID/P12.2 = IF ((CRENTAL LT 1) AND (CRENTAL GT (-1))) THEN 0 ELSE
                            ((CNOMID_DAYS/CRENTAL)*100);
COMPUTE AVGMID/D12.2CS = IF ((CRENTALD LT 1) AND (CRENTALD GT (-1))) THEN 0
                         ELSE (CRENTALA/CRENTALD);
BY ROLLCD
ON TABLE HOLD AS CARTHREE FORMAT FOCUS INDEX ROLLCD
END
-RUN


DEFINE FILE TCHBNM
CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE &&FXDATE.EVAL AND
               INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
END

TABLE FILE TCHBNM
PRINT 
  CURRENT
  NBR_DAYS
  NNROOMS
  NEW_TOTAL_AMT
  PRIOR
BY ROLLCD
ON TABLE HOLD AS HTLHLD1
END
-RUN

DEFINE FILE HTLHLD1
CHTTLA/D12.2CS = IF CURRENT EQ 'Y' THEN NEW_TOTAL_AMT ELSE 0;
CHTTLN/P8CS = IF CURRENT EQ 'Y' THEN NNROOMS ELSE 0;
END

TABLE FILE HTLHLD1
SUM CHTTLA
    CHTTLN
BY ROLLCD
ON TABLE HOLD AS HTLHLD2
END
-RUN

TABLE FILE HTLHLD2
SUM CHTTLA 
COMPUTE AVGRATE/D12.2CS = IF ((CHTTLN LT 1) AND (CHTTLN GT (-1))) THEN 0 ELSE
                             (CHTTLA/CHTTLN);
BY ROLLCD
ON TABLE HOLD AS HTLHLD3 FORMAT FOCUS INDEX ROLLCD
END
-RUN


-SET &&RPTSUF = 'BENCHMARK';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-* PRIOR DATES
-SET &FXMM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXMD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXMY=EDIT(&&FXDATE,'$9999');
-SET &TXOM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXOD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXOY=EDIT(&&TXDATE,'$9999');

-INCLUDE FDEFRPTS
-RUN

JOIN TRANTWO.ROLLCD IN TRANTWO TO CARTHREE.ROLLCD IN CARTHREE AS J1
JOIN TRANTWO.ROLLCD IN TRANTWO TO HTLHLD3.ROLLCD IN HTLHLD3 AS J2
END
-RUN


DEFINE FILE TRANTWO
NOWTOD/A8 WITH CAVOL = HHMMSS (NOWTOD);
BLANK/A1 = ' ';
END
-RUN

TABLE FILE TRANTWO
SUM CAVOL AS 'Total,Volume' 
PCT.CAVOL AS '% of,Total,Volume'
CAVAR1 AS 'Total,Volume,Variance %'
CANET AS 'Total,Net Volume'
CNTKT AS 'Net,Tickets' 
CLSAV AS 'TOTAL $,Lost Savings' 
CTLS AS '% of,Total,Lost Savings' 
CCLS2 AS '% of,Lost Savings Due to,Time Specified' 
CCLS1 AS '% of,Lost Savings Due to,Declined Connection' 
CCLS3 AS '% of,Lost Savings Due to,Airline/Flight Specified'
CCLS4 AS '% of,Lost Savings Due to,Refused Discount due to Penalty/Restrict'
CCLS5 AS '% of,Lost Savings Due to,Section/Fare Type Specified'
CTLSV AS '% of,Total Airline,Lost Savings'
CVSAV AS 'Value Added Savings' 
CAVGT AS 'Average,Ticket,Price' 
AAVGV1 AS 'Average,Ticket Price,Variance %' 
CDANTKT AS 'Domestic,Average,Net Ticket,Price'
CIANTKT AS 'International,Average,Net Ticket,Price' 
CADVP1 AS '% of, Net Tickets Purchased,0-6 Days' 
CADVP2 AS '% of, Net Tickets Purchased,7-13 Days' 
CADVP5 AS '% of, Net Tickets Purchased,14-20 Days' 
CADVP6 AS '% of, Net Tickets Purchased,21+ Days' 
CDONLP AS '%,Domestic of Online Bookings' 
CNRP AS '%,of NonRefundable Tickets' 
CPMILE AS 'Cost Per Mile'
PCTNOMID AS '%,of Cars Larger,Than Mid-size' 
AVGMID AS 'Average Midsize Car, Rate'
AVGRATE AS 'Average,Hotel,Cost Per,Night' 
BY FLAG NOPRINT
BY TITLE AS ' '
-GOTO HEAD

-HEAD
HEADING
"</5"
" "
"<10 &&RPT_TTL1"
"<10 &&RPT_TTL2"
" "
" "
" <34 PERIOD1: <40 &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
" <34 PERIOD2: <40 &FXMM/&FXMD/&FXMY - &TXOM/&TXOD/&TXOY"
"</2"
FOOTING BOTTOM
"&&FOOTER1"
"&&FOOTER2"
"NET VOLUME IS BASED ON THE FOLLOWING CALCULATION:"
"FARE PAID MINUS TAXES AND COMMISSION"
"NET TICKET PRICE DOES NOT INCLUDE TAX OR COMMISSION"
" "
"eTTek Review &DATE AT <NOWTOD <60 &&FOOTR"
"Copyright (c) 1996-2003, Travel and Transport - Confidential/Proprietary"
ON TABLE SET PAGE-NUM OFF
ON TABLE NOTOTAL
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=1.250000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=1.000000, BOTTOMMARGIN=0.250000, SQUEEZE=OFF, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=9, COLOR=BLACK, 
    BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=FOOTING, SIZE=6, $
TYPE=TITLE, SIZE=8, STYLE=BOLD, $
TYPE=HEADING, LINE=7, SIZE=14, FONT=COURIER NEW, STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=10, SIZE=10, STYLE=BOLD, $
TYPE=HEADING, SIZE=10, STYLE=BOLD+ITALIC, FONT=COURIER NEW, $
TYPE=REPORT, IMAGE=&&TNTPIC, POSITION=(&&TNTTRPOS), SIZE=(&&TNTTRSIZ), $
TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLTRPOS), SIZE=(&&ROLTRSIZ), $
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
-*
END
-RUN


-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT

