-* File Exchange_Summary.fex
-* 5/23/14 - DEB - ADDED EX5 TO EX_FL_FLAG DEFINE
-* Story ID S-10086 Added &&overdrill flag logic for use in 5.0 to choose to for the report to be drillable or non-drillable - 9/15/15 - JM
-*12/31/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels



-INCLUDE SETECHO

SET ASNAMES=ON
-SET HOLDATTR = ON;

-SET &&IDLEVELBREAK = '';


-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

-INCLUDE IDLEVROLLDEF
-RUN


DEFINE FILE &&EXTRACT
 ALN_NAME/A10 = IF VAL_AIR.VAL_AIRLINE EQ 'UA' OR 'LH' THEN 'UA/LH' ELSE 'OTHER';

 CLASS_CAT/A15 = DECODE CL_CAT ('F' 'FIRST'     'P' 'PREMIUM ECONOMY'   
			        'D' 'DEFAULT'   'B' 'BUSINESS'
			        'E' 'ECONOMY'   'U'  'UPGRADE' 
                                ' ' 'DEFAULT'); 
                            

  CLASS1/A25 = IF TKT_CL_CAT CONTAINS 'F' THEN 'FIRST' ELSE
               IF TKT_CL_CAT CONTAINS 'B' THEN 'BUSINESS' ELSE
               IF TKT_CL_CAT CONTAINS 'P' THEN 'PREMIUM ECONOMY' ELSE
               IF TKT_CL_CAT CONTAINS 'E' THEN 'ECONOMY' ELSE
               IF TKT_CL_CAT CONTAINS 'U' THEN 'UPGRADE' ELSE 'DEFAULT';
              
               
  COMM_SEG/D12.2S = SEG_COM;
  CITY/A6 = SUBSTR (176, CNON_STP, 1, 6, 6, CITY);
  DCTRY/A65 = EDIT(TKT_MAIN.CTRY_COD,'$$$9999999999999999999999999999999999999999999999999999999999999999');
  DGZONE/A64 = EDIT(TKT_MAIN.GEO_ZONE,'$$$$9999999999999999999999999999999999999999999999999999999999999999');
  FARE_PAID/D12.2CS   = IF VOID_DATE EQ 0 THEN SEG_AMT + SEG_TAX ELSE 0;
  FARE_PAID1/D12.2CS   = SEG_AMT + SEG_TAX;
  LEVEL_DESC1/A80      = &&LDESC;
  MXEXC1/A1 = IF (ORG_DST EQ 'BOSLGA') OR (ORG_DST EQ 'DCALGA') OR
                 (ORG_DST EQ 'BOSDCA') OR (ORG_DST EQ 'LGAPVD') OR
                 (ORG_DST EQ 'BOSEWR') THEN 'N' ELSE 'Y';
  MXEXC2/A1 = IF (ORG_DST EQ 'BOSLGA') OR (ORG_DST EQ 'DCALGA') OR
                 (ORG_DST EQ 'BOSDCA') THEN 'Y' ELSE 'N';               
  MRK_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (EX_FLAG NE ' ') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (RF_FLAG NE ' ') THEN (-1) ELSE 0;
                           
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) AND (VOID_DATE EQ 0) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') AND (VOID_DATE EQ 0) THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') AND (VOID_DATE EQ 0) THEN (-1) ELSE 0;
                           
  NET_FARE/D12.2S  = IF (EX_FLAG EQ ' ') AND (RF_FLAG EQ ' ')
                         THEN SEG_AMT - SEG_COM ELSE 0;
  NEW_ADV_PURCH/D9    = ADV_PURCH;
  NEW_SEG_DISC/D12.2CS = SEG_DISCOUNT;
  OCTRY/A2 = EDIT(TKT_MAIN.CTRY_COD, '99');
  OGZONE/A3 = EDIT(TKT_MAIN.GEO_ZONE, '999');
  
  REF_EXCH_CNT/D8.2CS   = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 0 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN 1 ELSE 0;
  TAX/D12.2S = SEG_TAX;
  TKT_PURCH/D8CS      = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
  TRAN_MONTH/MT        = TRN_DATE;
  VASAVINGS/D12.2CS    = NEW_SEG_DISC - FARE_PAID;
  XDPT_DOW/W           = DPT_DATE;
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        = 
  EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;


-* ****DEFINES ON PREVIOUS DEFINES****
  AFLAG/A1 = IF ALN_NAME EQ 'OTHER' THEN '2' ELSE '1';
  DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
                         
-* Added for new AD reason code to split out the value added savings
-*   ADVSV/D12.2CS = IF REFUSE_CODE EQ 'AD' THEN VASAVINGS ELSE 0;
ADVSV/D12.2CS = SEG_AGENCY_DISC - FARE_PAID;
-*  NFVSV/D12.2CS = IF REFUSE_CODE NE 'AD' THEN VASAVINGS ELSE 0; 
NFVSV/D12.2CS = VASAVINGS;

-*The following 2 defined fields are only used for saving/lost saving reports
-*02/04/2003
  SAVING1/P12.2CS = (STD_FARE - (TICKET_AMT + TAX_AMT));
  LSTSAVE1/P12.2CS = ((TICKET_AMT + TAX_AMT) - LOWEST_AMT); 
-***** Added for Kiewit exchange by Validating airline Report
ADDCOLLECT/D12.2CSM = IF (VOID_DATE EQ 0) AND (SEG_NBR EQ '01') THEN EXCH_NET_FARE+EXCH_NET_TAX;
AIRLINE_FEE/D12.2CSM = IF (VOID_DATE EQ 0) AND (EMBARK EQ 'FP2' OR 'FP3') THEN FARE_PAID ELSE 0;
EXG_ISS/D12CS = IF (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1) AND (SEG_NBR EQ '01') AND (VOID_DATE EQ 0) THEN 1 ELSE 0;
PEXG_ISS/D12CS = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'P') AND (TKT_NUM NE LAST TKT_NUM) THEN 1 ELSE 0;
FEXG_AMT/D12.2CSM = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'F') THEN FARE_PAID ELSE 0;
NET_AMT/D12.2CSM = IF (VOID_DATE EQ 0) AND (EX_FLAG EQ 'F') THEN FARE_PAID ELSE 0;
FULL_RFNDS/D9CS = IF (RF_FLAG EQ 'F') AND (DOC_TYPE NE '1') AND (SEG_COUNT EQ -1) AND (SEG_NBR EQ '01') THEN 1 ELSE 0;
VOIDS/D9CS = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01') THEN 1 ELSE 0;	
-* CONG REPORT
EX_RF_FLAG/A1 = IF (((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 'FP2' OR 'EX5') AND 
                     (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1')) THEN 'R' ELSE 
                  IF (((EXSALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EMBARK NE 'DT1') AND 
                      (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (RF_FLAG EQ ' ')))  THEN 'E'  ELSE ' ';
TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' '; 
-INCLUDE IDLEVDEFINE 

END
-RUN

TABLEF FILE &&EXTRACT
PRINT 	
    AIRLINE
    AIRLINE.AIRLINE_NAME AS 'ARLN_NAME'
    ARR_TIME
    CITY
    CITYPAIR.EMBARK AS 'EMB_APT_CD'
    CITYPAIR.ROUTE  AS 'RTE_APT_CD'
    CLASS
    CLASS1
    CLASS_CAT
    COMM_SEG
    CONJ_REL
    DGZONE
    DCTRY
    DISSAVINGS	
    DOC_TYPE	
    DPT_TIME
    EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'	
    EMB_CTY.CITY_NAME    AS 'EMB_CTY_NM'	
    EMB_CTY.EMB_CITY     AS 'EMB_CTY_CD'
    EX_FLAG
    FARE_PAID	
    FARE_PAID1
    FARE_BAS	
    INT_CODE	
    LEVEL_DESC
    LEVEL2
    MXEXC1
    MXEXC2
    MRK_TKT_CNT
    NET_FARE
    NET_TKT_CNT
    NEW_ADV_PURCH	
    NEW_SEG_DISC
    NFVSV
    OCTRY
    OGZONE
    PASSNGR_NAME
    PNR_LOCATOR	
    REF_EXCH_CNT	
    REFUSE_KEY        	
    REFUSE_CODE        	
    RESV_BRANCH	
    RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'	
    RTE_CTY.CITY_NAME    AS 'RTE_CTY_NM'	
    RTE_CTY.RTE_CITY     AS 'RTE_CTY_CD'
    SEG_DESIG
    SEG_LOWEST
    SEG_NBR	
    TAX
    TKT_MAIN.GEO_ZONE AS 'GEO_ZONE'
    TKT_MAIN.CTRY_COD AS 'CNTRY_CODE'
    TKT_DATE
    TKT_NUM
    TKT_PURCH
    TKT_SORT
    TKT_TYPE
    TOUR_CODE
    TRN_DATE
    VAL_AIR.AIRLINE_NAME 	
    VAL_AIRLINE	
    VASAVINGS	
    XDPT_DT	
    XTRAN_DT	
    XPSNGR_NM
-* Kiewit Defines
    ADDCOLLECT
    AIRLINE_FEE
    EXG_ISS
    FEXG_AMT
    NET_AMT
    PEXG_ISS
    EX_RF_FLAG
    TKT_TYPE
    VOIDS
    FULL_RFNDS
-INCLUDE &AIRDATES

-* WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS EXHLD
END
-RUN


TABLE FILE EXHLD
SUM FARE_PAID
    NET_TKT_CNT
    EXG_ISS
    PEXG_ISS
    AIRLINE_FEE
    ADDCOLLECT
    FULL_RFNDS
    VOIDS
BY LEVEL_DESC    
BY PASSNGR_NAME
ON TABLE HOLD AS EXHLD1
END
-RUN

DEFINE FILE EXHLD
CG_PCNT/D12CS = IF TKT_NUM NE LAST TKT_NUM THEN 1 ELSE 0;
END
TABLE FILE EXHLD
SUM CG_PCNT
BY LEVEL_DESC
BY PASSNGR_NAME
WHERE EX_RF_FLAG EQ 'E'
WHERE TKT_TYPE EQ 'P-EX'
ON TABLE HOLD AS PEXHLD
END
-RUN


MATCH FILE EXHLD1
PRINT FARE_PAID
      NET_TKT_CNT
      EXG_ISS
      AIRLINE_FEE
      ADDCOLLECT
      FULL_RFNDS
      VOID
BY LEVEL_DESC
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE PEXHLD
PRINT CG_PCNT
BY LEVEL_DESC
BY PASSNGR_NAME
AFTER MATCH HOLD AS TTHLD OLD-OR-NEW
END
-RUN



 

-SET &&NOEXCHREC = IF &LINES EQ 0 THEN 'Y' ELSE 'N';

-IF &&NOEXCHREC EQ 'Y' THEN GOTO XXITA;




-* Final Defines

DEFINE FILE TTHLD

NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END

-SET &&RPTSUF = 'SMY';

-INCLUDE FDEFRPTS
-RUN

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN

TABLE FILE TTHLD
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10


-INCLUDE FOOTERSM

ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *

-IF &&OVERDRILL EQ 'Y' GOTO OVERDRILL;

TYPE=DATA, COLOR=RGB(0 0 138), JUSTIFY=LEFT, COLUMN=N3, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTTSM' ROW_LABEL=N2 FLAG='NETVOL' LSEL=LEVEL_DESC PARM2='&&IDLEVELBREAK.EVAL'\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLOR=RGB(0 0 138), JUSTIFY=LEFT, COLUMN=N4, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTTSM' ROW_LABEL=N2 FLAG='NETTKT' LSEL=LEVEL_DESC PARM2='&&IDLEVELBREAK.EVAL'\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLOR=RGB(0 0 138), JUSTIFY=LEFT, COLUMN=N5, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTTSM' ROW_LABEL=N2 FLAG='FULLEX' LSEL=LEVEL_DESC PARM2='&&IDLEVELBREAK.EVAL'\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLOR=RGB(0 0 138), JUSTIFY=LEFT, COLUMN=N6, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTTSM' ROW_LABEL=N2 FLAG='PARTEX' LSEL=LEVEL_DESC PARM2='&&IDLEVELBREAK.EVAL'\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLOR=RGB(0 0 138), JUSTIFY=LEFT, COLUMN=N7, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTTSM' ROW_LABEL=N2 FLAG='AIRFEE' LSEL=LEVEL_DESC PARM2='&&IDLEVELBREAK.EVAL'\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLOR=RGB(0 0 138), JUSTIFY=LEFT, COLUMN=N8, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTTSM' ROW_LABEL=N2 FLAG='FULLRF' LSEL=LEVEL_DESC PARM2='&&IDLEVELBREAK.EVAL'\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

TYPE=DATA, COLOR=RGB(0 0 138), JUSTIFY=LEFT, COLUMN=N9, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRTTSM' ROW_LABEL=N2 FLAG='VOID' LSEL=LEVEL_DESC PARM2='&&IDLEVELBREAK.EVAL'\
 SETFNM='&&DTTMSTRM.EVAL' \
 FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $

-OVERDRILL;

-INCLUDE GLOBAL_LOGO_SM
 
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.500000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FONT=TIMES NEW ROMAN, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
TYPE=HEADING, SIZE=8, STYLE=BOLD, FONT='ARIAL', COLOR=RGB(31 114 164), BACKCOLOR=NONE, $
TYPE=HEADING, HEADALIGN=BODY, $
TYPE=HEADING, LINE=1, COLSPAN=2, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, COLSPAN=2, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, COLSPAN=10, SIZE=9, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=4, COLSPAN=10, SIZE=9,JUSTIFY=CENTER, $
TYPE=HEADING, LINE=5, COLSPAN=2, JUSTIFY=LEFT, $

TYPE=TABFOOTING, FONT=ARIAL, COLOR=GRAY,$
     
TYPE=FOOTING, FONT=ARIAL, COLOR=GRAY,$

TYPE=SUBHEAD,
     SIZE=9,
     STYLE=BOLD,
$

TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=8, 
     BACKCOLOR=(RGB(242 242 242) 'WHITE' ),$

TYPE=TITLE,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-STYLE=RIDGE,
     BORDER-BOTTOM-STYLE=RIDGE,
     BORDER-LEFT-STYLE=RIDGE,
     BORDER-RIGHT-STYLE=RIDGE,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER',
     SIZE=9,
     COLOR='WHITE',
     BACKCOLOR=RGB(31 114 164),
     STYLE=BOLD,$


TYPE=SUBFOOT, SIZE=8, JUSTIFY=LEFT, STYLE=BOLD, $

TYPE=SUBTOTAL, 
     STYLE=BOLD, 
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     BORDER-TOP-COLOR='SILVER',
     BORDER-BOTTOM-COLOR='SILVER',
     BORDER-LEFT-COLOR='SILVER',
     BORDER-RIGHT-COLOR='SILVER', $

TYPE=SUBTOTAL, JUSTIFY=LEFT, STYLE=BOLD, $     
     
TYPE=GRANDTOTAL, JUSTIFY=LEFT, STYLE=BOLD, $

TYPE=REPORT, COLUMN=N2, JUSTIFY=LEFT, SQUEEZE=2.0, $
TYPE=REPORT, COLUMN=N3, JUSTIFY=LEFT, SQUEEZE=1.0, $
TYPE=REPORT, COLUMN=N4, JUSTIFY=LEFT, SQUEEZE=1.0, $
TYPE=REPORT, COLUMN=N5, JUSTIFY=LEFT, SQUEEZE=1.0, $
TYPE=REPORT, COLUMN=N6, JUSTIFY=LEFT, SQUEEZE=1.0, $
TYPE=REPORT, COLUMN=N7, JUSTIFY=LEFT, SQUEEZE=1.0, $
TYPE=REPORT, COLUMN=N8, JUSTIFY=LEFT, SQUEEZE=1.0, $
TYPE=REPORT, COLUMN=N9, JUSTIFY=LEFT, SQUEEZE=1.0, $
TYPE=REPORT, COLUMN=N10, JUSTIFY=LEFT, SQUEEZE=1.0, $
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN



-XXITA;