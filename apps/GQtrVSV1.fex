-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* ADDED TRANTYPE INCLUDE - JK 12/29/14 STORY ID S-06492

SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE SR_50  
  FARE_PAID/D12 = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D8CS  =   IF (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) THEN 1 ELSE 
                        IF (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') AND (SEG_COUNT EQ -1)
				   THEN (-1) ELSE 
                        IF (DOC_TYPE NE '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') AND
				   (SEG_COUNT EQ -1)
				   THEN (-1) ELSE 0;

-* Defines on Previous Defines
   VASAVINGS/D12 = SEG_DISCOUNT - FARE_PAID;
-INCLUDE TRANTYPE  
END
-RUN

TABLEF FILE SR_50
PRINT FARE_PAID	
  NET_TKT_CNT
  SEG_COUNT
  SEG_DISCOUNT
  TKT_NUM
  TKT_DATE
  TRN_DATE
  VASAVINGS
  VAL_AIR.AIRLINE_NAME AS 'AIRLINE'
  VOID_DATE 	

WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) OR
      (TRN_DATE GE &&FXDATE.EVAL) AND (TRN_DATE LE &&TXDATE.EVAL)
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3

-INCLUDE RPTPARMS
ON TABLE HOLD AS QTRVSV
END
-RUN

TABLE FILE QTRVSV
PRINT FARE_PAID	
  NET_TKT_CNT
  SEG_COUNT
  SEG_DISCOUNT
  TKT_NUM
  TRN_DATE
  VASAVINGS
  AIRLINE
  VOID_DATE 
BY TKT_DATE 
ON TABLE HOLD AS QTRVSV1 FORMAT FOCUS INDEX TKT_DATE
END
-RUN

-* Global Reporting JOIN
JOIN QTRVSV1.TKT_DATE IN QTRVSV1 TO GLOBAL.DATE IN GLOBAL AS J1


DEFINE FILE QTRVSV1
CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;

-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
PAID_FAREX/D20.6 = FARE_PAID * CURR1X;
VASAVEX/D20.6 = VASAVINGS * CURR1X;
SEG_DISCX/D20.6 = SEG_DISCOUNT * CURR1X;

END
-RUN  

TABLE FILE QTRVSV1
PRINT FARE_PAID	
  NET_TKT_CNT
  SEG_COUNT
  SEG_DISCOUNT
  TKT_NUM
  TRN_DATE
  VASAVINGS
  AIRLINE
  VOID_DATE 
  TKT_DATE
  NAMEC
  PAID_FAREX/D16.2
  VASAVEX/D16.2
  SEG_DISCX/D16.2
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS VSV
END
-RUN   	 

	

DEFINE FILE VSV
CURRENT/A1 = IF TRN_DATE GE &&FROMDT.EVAL AND
                TRN_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF TRN_DATE GE &&FXDATE.EVAL AND
               TRN_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';
FLAG/A1 = 'A';
END

TABLE FILE VSV
PRINT CURRENT
  FARE_PAID
  NET_TKT_CNT
  PRIOR	
  SEG_DISCOUNT
  VASAVINGS
  VOID_DATE
  NAMEC
  PAID_FAREX/D16.2
  VASAVEX/D16.2 
  SEG_DISCX/D16.2
BY FLAG
BY AIRLINE
ON TABLE HOLD AS VSV1
END
-RUN

DEFINE FILE VSV1
CVSV/D15 = IF (CURRENT EQ 'Y') AND (VOID_DATE EQ 0) THEN VASAVINGS ELSE 0;
GCVSV/D16 = IF (CURRENT EQ 'Y') AND (VOID_DATE EQ 0) THEN VASAVEX ELSE 0;
CSEG/D15 = IF (CURRENT EQ 'Y') AND (VOID_DATE EQ 0) THEN SEG_DISCOUNT ELSE 0;
GCSEG/D16 = IF (CURRENT EQ 'Y') AND (VOID_DATE EQ 0) THEN SEG_DISCX ELSE 0;
PVSV/D15 = IF (PRIOR EQ 'Y') AND (VOID_DATE EQ 0) THEN VASAVINGS ELSE 0;
GPVSV/D16 = IF (PRIOR EQ 'Y') AND (VOID_DATE EQ 0) THEN VASAVEX ELSE 0;
PSEG/D15 = IF (PRIOR EQ 'Y') AND (VOID_DATE EQ 0) THEN SEG_DISCOUNT ELSE 0;
GPSEG/D16 = IF (PRIOR EQ 'Y') AND (VOID_DATE EQ 0) THEN SEG_DISCX ELSE 0;
-* DEFINES ON PREVIOUS DEFINES
END

TABLE FILE VSV1
SUM CSEG
    PSEG
    CVSV
    PVSV
    GCSEG
    GPSEG
    GCVSV
    GPVSV
    NAMEC
BY AIRLINE
ON TABLE HOLD AS VSV2
END
-RUN

TABLE FILE VSV2
SUM CSEG
    PSEG
    CVSV
    PVSV
    GCSEG
    GPSEG
    GCVSV
    GPVSV
    NAMEC
BY AIRLINE
ON TABLE HOLD AS VSV3
END
-RUN

TABLE FILE VSV3
PRINT CSEG
    PSEG
    CVSV
    PVSV
    GCSEG
    GPSEG
    GCVSV
    GPVSV
    AIRLINE
    NAMEC
RANKED BY HIGHEST GCVSV NOPRINT
ON TABLE HOLD AS RNKVSV
END
-RUN

DEFINE FILE RNKVSV
LIST/D9 = IF AIRLINE NE LAST AIRLINE THEN 
           (LAST LIST + 1) ELSE LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 5 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL/A30 = IF RANK_VALUE NE 'OTHER' THEN AIRLINE
                      ELSE 'ALL OTHER';
-* DEFINES ON PREVIOS DEFINES
 RANK_LAB/A30 = LCWORD(30, RANK_LABEL, RANK_LAB);
END
-RUN

TABLE FILE RNKVSV
SUM CVSV
    PVSV
    GCVSV
    GPVSV
    NAMEC
COMPUTE CVPCT/D15 = IF ((CSEG LT 1) AND (CSEG GT (-1))) THEN 0 ELSE
                       ((CVSV/CSEG)*100);
COMPUTE PVPCT/D15 = IF ((PSEG LT 1) AND (PSEG GT (-1))) THEN 0 ELSE
                       ((PVSV/PSEG)*100);
COMPUTE IDAMT/D15 = IF ((PVSV LT 1) AND (PVSV GT (-1))) THEN 0 ELSE
                       (((CVSV-PVSV)/PVSV)*100);
COMPUTE IDPCT/D15 = IF ((PVPCT LT 1) AND (PVPCT GT (-1))) THEN 0 ELSE
                       (CVPCT-PVPCT);
COMPUTE GCVPCT/D16 = IF ((GCSEG LT 1) AND (GCSEG GT (-1))) THEN 0 ELSE
                       ((GCVSV/GCSEG)*100);
COMPUTE GPVPCT/D16 = IF ((GPSEG LT 1) AND (GPSEG GT (-1))) THEN 0 ELSE
                       ((GPVSV/GPSEG)*100);
COMPUTE GIDAMT/D16 = IF ((GPVSV LT 1) AND (GPVSV GT (-1))) THEN 0 ELSE
                       (((GCVSV-GPVSV)/GPVSV)*100);
COMPUTE GIDPCT/D16 = IF ((GPVPCT LT 1) AND (GPVPCT GT (-1))) THEN 0 ELSE
                       (GCVPCT-GPVPCT);

BY RANK_VALUE
BY RANK_LAB
ON TABLE HOLD AS RNKVSV1
END
-RUN


DEFINE FILE RNKVSV1
LABEL/A45 = 'Contract Svgs Amount Per Airline';
CATEGORY/A81 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 10;
END
-RUN

TABLE FILE RNKVSV1
PRINT CATEGORY/A81 AND RANK_LAB/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND GCVSV/D16 AS 'CURRENT' AND
GPVSV/D16 AS 'PRIOR' GIDAMT AS 'IDPERCENT' 
COMPUTE ROW_ORDER/I2 = 1;  
WHERE RANK_LAB NE 'All Other';
ON TABLE HOLD AS RNKVSV2
END
-RUN

TABLE FILE RNKVSV2
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS RNKVSV3
END
MODIFY FILE GQTRREV1
FIXFORM FROM RNKVSV3
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON RNKVSV3
END
-RUN


DEFINE FILE RNKVSV1
LABEL/A45 = 'Contract Svgs Percentage Per Airline';
CATEGORY/A81 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 11;
END
-RUN

TABLE FILE RNKVSV1
PRINT CATEGORY/A81 AND RANK_LAB/A50 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND GCVPCT/D16 AS 'CURRENT' AND
GPVPCT/D16 AS 'PRIOR' GIDPCT AS 'IDPERCENT'
COMPUTE ROW_ORDER/I2 = 1; 
WHERE RANK_LAB NE 'All Other';
ON TABLE HOLD AS RNKPCT1
END
-RUN

TABLE FILE RNKPCT1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND CURRENT AND PRIOR AND IDPERCENT
ON TABLE HOLD AS RNKPCT2
END
MODIFY FILE GQTRREV1
FIXFORM FROM RNKPCT2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON RNKPCT2
END
-RUN







