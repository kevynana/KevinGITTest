-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/26/04    JOY                Added/Changed Norecords logic
-*  10/10/2016  REJ  S-24432 ER5 Testing updates.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox

 
-INCLUDE SETECHO
 
-DEFAULT &&MFLAG ='XXX';
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
 
-SET &&DTSTY = 'DT';
 
-SET &&FIELD = &ROW_LABEL;
-SET &&MFLAG = &PARM1;
 
-***************************************************
-* CHECK FOR NON-DRILLABLE FIELD
-***************************************************
-IF (&&FIELD EQ 'AVERAGE TICKET')
- OR (&&FIELD EQ 'AVERAGE NET TICKET')
- OR (&&FIELD EQ 'DOMESTIC NET AVERAGE')
- OR (&&FIELD EQ 'DOMESTIC %')
- OR (&&FIELD EQ 'INTERNATIONAL NET AVERAGE')
- OR (&&FIELD EQ 'INTERNATIONAL %')
- OR (&&FIELD EQ 'LOST SAVINGS %')
- OR (&&FIELD EQ 'TOTAL TRAVEL COST') THEN GOTO FIRST ELSE GOTO SECOND;
 
-SECOND
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
 
-IF &RETCODE NE 0 GOTO NOFILE;
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-RUN
-SET &&SETF='TEMPDRIL';
EX SETDBLS SETFILE=&&SETF
-RUN
 
-IF &&FROMAVRP EQ '' GOTO :OTER5;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:OTER5
 
-INCLUDE GetForDrillDown
-RUN
 
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-****
-* &&MFLAG WILL EQUAL ONE OF THE FOLLOWING:
-* JAN, FEB, MAR, APR, MAY, JUN, JUL, AUG, SEP, OCT, NOV, DEC, YTD
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&VOIDCHK = 'WHERE VOID_DATE EQ 0';
-SET &&DEPARTD = 'XDPT_DT AS DEPART,DATE';
-SET &&DEPARTT = 'DPT_TIME AS TIME';
-SET &&NTCOUNT = 'NET_TKT_CNT AS NET,TKT,CNT';
-SET &&TICOUNT = 'TTKT_ISS AS TKT,ISS';
-SET &&FAREAMT = 'FARE_AMT AS AIR,VOLUME';
-SET &&NETVOLUME = 'SEG_TOT AS NET,FARE';
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SAVINGS';
-SET &&LSAVECHK = ' ';
-SET &&DISCOUNT = ' ';
-SET &&DICHK = ' ';
-SET &&LOWEST = ' ';
 
 
-IF &&MFLAG EQ 'JAN' THEN GOTO JAN
-  ELSE IF &&MFLAG EQ 'FEB' THEN GOTO FEB
-  ELSE IF &&MFLAG EQ 'MAR' THEN GOTO MAR
-  ELSE IF &&MFLAG EQ 'APR' THEN GOTO APR
-  ELSE IF &&MFLAG EQ 'MAY' THEN GOTO MAY
-  ELSE IF &&MFLAG EQ 'JUN' THEN GOTO JUN
-  ELSE IF &&MFLAG EQ 'JUL' THEN GOTO JUL
-  ELSE IF &&MFLAG EQ 'AUG' THEN GOTO AUG
-  ELSE IF &&MFLAG EQ 'SEP' THEN GOTO SEP
-  ELSE IF &&MFLAG EQ 'OCT' THEN GOTO OCT
-  ELSE IF &&MFLAG EQ 'NOV' THEN GOTO NOV
-  ELSE IF &&MFLAG EQ 'DEC' THEN GOTO DEC
-  ELSE GOTO YTD;
 
-JAN
-SET &&MCHK='WHERE TRNM EQ ''JAN''';
-SET &&SUBHEAD = 'JANUARY';
-GOTO ENDDATE
 
-FEB
-SET &&MCHK='WHERE TRNM EQ ''FEB''';
-SET &&SUBHEAD = 'FEBRUARY';
-GOTO ENDDATE
 
-MAR
-SET &&MCHK='WHERE TRNM EQ ''MAR''';
-SET &&SUBHEAD = 'MARCH';
-GOTO ENDDATE
 
-APR
-SET &&MCHK='WHERE TRNM EQ ''APR''';
-SET &&SUBHEAD = 'APRIL';
-GOTO ENDDATE
 
-MAY
-SET &&MCHK='WHERE TRNM EQ ''MAY''';
-SET &&SUBHEAD = 'MAY';
-GOTO ENDDATE
 
-JUN
-SET &&MCHK='WHERE TRNM EQ ''JUN''';
-SET &&SUBHEAD = 'JUNE';
-GOTO ENDDATE
 
-JUL
-SET &&MCHK='WHERE TRNM EQ ''JUL''';
-SET &&SUBHEAD = 'JULY';
-GOTO ENDDATE
 
-AUG
-SET &&MCHK='WHERE TRNM EQ ''AUG''';
-SET &&SUBHEAD = 'AUGUST';
-GOTO ENDDATE
 
-SEP
-SET &&MCHK='WHERE TRNM EQ ''SEP''';
-SET &&SUBHEAD = 'SEPTEMBER';
-GOTO ENDDATE
 
-OCT
-SET &&MCHK='WHERE TRNM EQ ''OCT''';
-SET &&SUBHEAD = 'OCTOBER';
-GOTO ENDDATE
 
-NOV
-SET &&MCHK='WHERE TRNM EQ ''NOV''';
-SET &&SUBHEAD = 'NOVEMBER';
-GOTO ENDDATE
 
-DEC
-SET &&MCHK='WHERE TRNM EQ ''DEC''';
-SET &&SUBHEAD = 'DECEMBER';
-GOTO ENDDATE
 
-YTD
-SET &&MCHK='WHERE TRNY EQ ''YTD''';
-SET &&SUBHEAD = 'YEAR-TO-DATE';
-GOTO ENDDATE
 
 
-ENDDATE
 
 
-FIELDS
-IF &&FIELD EQ 'AIR VOLUME' THEN GOTO AIRVOLUME
-  ELSE IF &&FIELD EQ 'NET VOLUME' THEN GOTO NETVOLUME
-  ELSE IF &&FIELD EQ 'TICKETS ISSUED' THEN GOTO TKTISSD
-  ELSE IF &&FIELD EQ 'FULL REFUNDS' THEN GOTO REFUNDS
-  ELSE IF &&FIELD EQ 'NET TICKETS' THEN GOTO NETTKTS
-  ELSE IF &&FIELD EQ 'DOMESTIC NET VOLUME' THEN GOTO DOMNVOL
-  ELSE IF &&FIELD EQ 'DOMESTIC NET TICKETS' THEN GOTO DOMNTKT
-  ELSE IF &&FIELD EQ 'INTERNATIONAL NET VOLUME' THEN GOTO INTNVOL
-  ELSE IF &&FIELD EQ 'INTERNATIONAL NET TICKETS' THEN GOTO INTNTKT
-  ELSE IF &&FIELD EQ 'LOST SAVINGS' THEN GOTO LOSTSAVE
-  ELSE GOTO AIRVOLUME;
 
-GOTO OUTSIDE
 
-AIRVOLUME
-SET &&RPT_TTL1 = '                     AIR VOLUME DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&NETVOLUME = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
 
-NETVOLUME
-SET &&RPT_TTL1 = '                     NET VOLUME DETAIL     ';
-SET &&TICOUNT = ' ';
-SET &&FAREAMT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
 
-TKTISSD
-SET &&RPT_TTL1 = '                   TICKETS ISSUED DETAIL   ';
-SET &&VOIDCHK = ' ';
-SET &&NTCOUNT = ' ';
-SET &&FAREAMT = ' ';
-SET &&NETVOLUME = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
-REFUNDS
-SET &&RPT_TTL1 = '                  TICKETS REFUNDED DETAIL  ';
-SET &&REFUNDCHK = 'WHERE RF_FLAG EQ ''F''';
-SET &&TICOUNT = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
-NETTKTS
-SET &&RPT_TTL1 = '                     NET TICKETS DETAIL    ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&DICHK = ' ';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
-DOMNVOL
-SET &&RPT_TTL1 = '             DOMESTIC NET VOLUME DETAIL    ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
-DOMNTKT
-SET &&RPT_TTL1 = '             DOMESTIC NET TICKETS DETAIL   ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''D''';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
-INTNVOL
-SET &&RPT_TTL1 = '         INTERNATIONAL NET VOLUME DETAIL    ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
-INTNTKT
-SET &&RPT_TTL1 = '          INTERNATIONAL NET TICKETS DETAIL   ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-SET &&FAREAMT = ' ';
-SET &&DICHK = 'WHERE AIR_MAIN.INTL_DOM EQ ''I''';
-SET &&LSTSVGS= ' ';
-GOTO OUTSIDE
 
 
 
-LOSTSAVE
-SET &&RPT_TTL1 = '                    LOST SAVINGS DETAIL    ';
-SET &&LOWEST = 'LOW_TOT AS LOWEST,AMOUNT';
-SET &&LSTSVGS = 'LOST_TOT AS LOST,SAVINGS,AMOUNT';
-SET &&LSAVECHK = 'WHERE DISSAVINGS NE 0';
-SET &&NETVOLUME = ' ';
-SET &&NTCOUNT = ' ';
-SET &&TICOUNT = ' ';
-SET &&REFUNDCHK = ' ';
-GOTO OUTSIDE
 
-OUTSIDE
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DCA1;
-SET &&RPTGRP_OVERRIDE='AIR';
EX CODEGEN
-RUN
-:DCA1
 
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX AIRUSE
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
-SET &WHATDATE = IF '&&DATETYPE.EVAL' EQ 'I' THEN 'TRN_DATE' ELSE 'FST_DPT_DATE';
 
DEFINE FILE &&EXTRACT
  FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (EX_FLAG EQ 'F') THEN (-1) ELSE
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
TRNM/MT = IF '&&DATETYPE.EVAL' EQ 'I' THEN TRN_DATE ELSE FST_DPT_DATE;
TRNY/A3 = 'YTD';
 
 TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';
TKT_ISS/D12.2 = IF (SEG_NBR EQ '01') AND (SEG_AMT+SEG_TAX GE 0)
                        AND (SEG_COUNT EQ 1) THEN 1 ELSE 0;
 
  XDPT_DT/MDYY         = DPT_DATE;
  XTRAN_DT/MDYY        = TRN_DATE;
  XPSNGR_NM/A15        =
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
-* DEFINES TO PREVIOUS DEFINES
DISSAVINGS/D11.2CS  = IF EMBARK EQ 'DT1' THEN 0 ELSE
                         FARE_PAID - SEG_LOWEST;
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT
  AIRLINE.AIRLINE AS 'AIRLINE'
  DISSAVINGS
  DPT_TIME
  EMB_APT.AIRPORT_NAME AS 'EMB_APT_NM'
  FARE_PAID
  NET_TKT_CNT
  RTE_APT.AIRPORT_NAME AS 'RTE_APT_NM'
  REFUSE_CODE
  REFUSAL_DESC
  &WHATDATE
  TKT_ISS
  TKT_TYPE
  TRNM
  TRNY
  XDPT_DT
  XTRAN_DT
  XPSNGR_NM
  TKT_NUM
  TKT_SORT
  SEG_AMT
  SEG_COUNT
  SEG_DISCOUNT
  SEG_LOWEST
  SEG_NBR
  SEG_STANDARD
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
-INCLUDE &AIRDATES
&&WHERE1
&&WHERE2
&&WHERE3
&&VOIDCHK
&&MCHK
&&REFUNDCHK
&&DICHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
 
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
TABLE FILE RPTHOLD
SUM DISSAVINGS AS 'TDSV'
NET_TKT_CNT AS 'TTKT'
FARE_PAID AS 'TFARE'
SEG_AMT AS 'TSEG'
SEG_STANDARD AS 'TSTD'
SEG_LOWEST AS 'TLOW'
SEG_DISCOUNT AS 'TDIS'
TKT_ISS AS 'TISS'
BY XPSNGR_NM
BY TKT_SORT
BY &WHATDATE
ON TABLE HOLD AS AIRHOLD1
END
-RUN
 
TABLE FILE RPTHOLD
PRINT AIRLINE
      EMB_APT_NM
      SEG_AMT
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID
      REFUSE_CODE
      RTE_APT_NM
      TKT_NUM
      TKT_TYPE
      DPT_TIME
      XDPT_DT
      XTRAN_DT
BY XPSNGR_NM
BY TKT_SORT
BY &WHATDATE
BY SEG_NBR
ON TABLE HOLD AS XTWO
END
-RUN
 
MATCH FILE XTWO
PRINT AIRLINE
      EMB_APT_NM
      SEG_AMT
      SEG_LOWEST
      SEG_STANDARD
      SEG_DISCOUNT
      FARE_PAID
      REFUSE_CODE
      RTE_APT_NM
      TKT_NUM
      TKT_TYPE
      DPT_TIME
      XDPT_DT
      XTRAN_DT
BY XPSNGR_NM
BY TKT_SORT
BY &WHATDATE
BY SEG_NBR
ON TABLE HOLD
RUN
FILE AIRHOLD1
PRINT TDSV
      TFARE
      TISS
      TTKT
      TSEG
      TSTD
      TLOW
BY XPSNGR_NM
BY TKT_SORT
BY &WHATDATE
AFTER MATCH HOLD AS AIRHOLD2 OLD-OR-NEW
END
-RUN
 
TABLE FILE AIRHOLD2
PRINT AIRLINE
      EMB_APT_NM
      SEG_AMT
      SEG_STANDARD
      SEG_LOWEST
      SEG_DISCOUNT
      FARE_PAID
      XPSNGR_NM
      REFUSE_CODE
      RTE_APT_NM
      TDSV
      TFARE
      TISS
      TSEG
      TSTD
      TLOW
      TKT_NUM
      TKT_TYPE
      &WHATDATE
      TTKT
      XDPT_DT
      DPT_TIME
      XTRAN_DT
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD3
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
DEFINE FILE AIRHOLD3
NET_TKT_CNT/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN XPSNGR_NM ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
TTKT_ISS/D9S = IF (SEG_NBR EQ '01') THEN TISS ELSE 0;
XT_DTE/A8 = EDIT(&WHATDATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;
 
-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A100 = TKT_SORT||XT_DTE||XPSNGR_NM;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
SEG_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TSEG ELSE 0;
LOST_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TDSV ELSE 0;
LOW_TOT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TLOW ELSE 0;
NOWTOD/A8 WITH TKT_SORT = HHMMSS (NOWTOD);
END
-RUN
 
TABLE FILE AIRHOLD3
-INCLUDE HEADSCH
"&&SUBHEAD"
"</2"
-*
-*******************************************************************
-* Print statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
REASON_CD AS 'REASON,CODE'
&&DEPARTD
&&DEPARTT
EMB_APT_NM AS 'ORG'
RTE_APT_NM AS 'DST'
AIRLINE AS 'CR'
&&NETVOLUME
&&FAREAMT
&&NTCOUNT
&&TICOUNT
&&LOWEST
&&LSTSVGS
 
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY XPSNGR_NM AS 'PASSENGER NAME'
BY XTKT_SRT NOPRINT
BY TKT_SORT NOPRINT
BY &WHATDATE NOPRINT
BY SEG_NBR NOPRINT
BY XDPT_DT NOPRINT BY DPT_TIME NOPRINT
BY TICKET_NMBR AS 'TICKET,NUMBER'
BY TKT_TYPE AS 'TKT,TYPE'
 
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
ON XTKT_SRT SKIP-LINE
 
-INCLUDE FOOTERDT
 
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE &&DTSTY
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;
 
 
-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
-NOFILE
 
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-GOTO XXIT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-RUN
 
-XXIT
