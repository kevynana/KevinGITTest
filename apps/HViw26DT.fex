-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO
-*                     FOCEXEC: HVIEW3DT
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*
-*----           ----               ---------------------
-* ADDED HTL_PHONE/PROP_PHONE TO THE JN6KEY/JN8KEY
-* THIS TELLS BETWEEN MULTIPLE HOTELS WITHIN THE SAME CITY JK 7/16/01
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-* 9/16/02   Joy                Changed to give a non-drillable message when
-*                              All other is drilled upon 
-*10/24/03   Joy                Added Set commmands to set the variable prior
-*                              to the program to fix the drilldown 
-*3/31/04    DEB                CHANGED TO SUM DEFINED FIELD BOOKINGS INSTEAD
-*                              OF CNT.NUM_DAYS AS 'BOOKINGS'
-*01/30/06   DEB                ADDED FLAG AND FLAGN DEFINE IN ORDER TO 
-*                              SUBTOTAL RANKED HOTELS
-*06/07/06   STEVE              CHANGED ADDRESS DEFINE TO A25 FROM A35
-*10/5/06    DEB                ADDED SORT BY PROP_CODE
-* 04/20/15 - S-08393 - DEB -   ADDED CHAIN CODE TO VXTPHDD1
-*03/2217   DLV        S-22486 CHANGED TO Post-trip Reporting

-**************************************************************************
SET ASNAMES=ON 
-RUN

-SET &&RANK_LIMIT = 9999;
-SET &&RANK_LIMIT2 = 9999;
-SET &&CTY_LMT = &&RANK_LIMIT;
-SET &&HTL_LMT = &&RANK_LIMIT2;
-SET &&RANK_METH = 'CDAY';
-* -SET &&RANK_METH = IF &&RANK_METH EQ 'ROOM_AMT' THEN 'ROOM_AMT'
-* - ELSE IF &&RANK_METH EQ 'NBR_DAYS' THEN 'NBR_DAYS' 
-* - ELSE 'NBR_DAYS';

TABLE FILE &&RPT_HOLD
SUM PREF_NON AS 'PN'
BY PROP_NAME
ON TABLE HOLD AS PNHLD
END


TABLE FILE &&RPT_HOLD
SUM CDAY
    PDAY
    CAMT
    PAMT
    CBK
    PBK
    CSVG
    PSVG   
    PREF_NON
BY CITY_ST
BY PROP_NAME
BY HOTEL_PHONE
ON TABLE HOLD AS HOLD1
END
-RUN

MATCH FILE HOLD1
PRINT CDAY
      PDAY
      CAMT
      PAMT
      CBK
      PBK
      CSVG
      PSVG
      CITY_ST
      HOTEL_PHONE
BY PROP_NAME
ON TABLE HOLD
RUN
FILE PNHLD
SUM PN 
BY PROP_NAME
AFTER MATCH HOLD AS HOLD1A OLD-OR-NEW
END


TABLE FILE HOLD1A
PRINT 
    CDAY
    PDAY
    CAMT
    PAMT
    CBK
    PBK
    CSVG
    PSVG    
    PROP_NAME
    PN
    HOTEL_PHONE
BY CITY_ST
RANKED BY HIGHEST &&RANK_METH NOPRINT
ON TABLE HOLD AS HOLD2
END
-RUN


DEFINE FILE HOLD2
 RANK_LIMIT/I5 = &&HTL_LMT;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 RANK_VALUE/A5 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                 IF RANK GT RANK_LIMIT THEN ARANK ELSE ARANK; 
                 
                 
-*  RANK_VALUE/A10 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
-*                  IF CDAY EQ 0 THEN 'All Other' ELSE
-*                  IF RANK GT RANK_LIMIT THEN 'All Other' ELSE ARANK;  
-*  CHAIN_CD/A2= IF RANK_VALUE LE '&&HTL_LMT' THEN CHAIN_CODE ELSE ' ';
 PNPR/A1 = IF RANK_VAL LE '&&HTL_LMT' THEN PN ELSE ' ';
-*  HTL_ADDRESS/A35= IF RANK_VALUE LE '&&HTL_LMT' THEN PROP_ADDRESS ELSE ' ';	
 HTL_NAME/A30 = IF RANK_VALUE LE '&&HTL_LMT' THEN PROP_NAME ELSE ' ';
 PROP_PHONE/A15= IF RANK_VALUE LE '&&HTL_LMT' THEN HOTEL_PHONE ELSE ' ';
-*  ZIP5/A5 = IF RANK_VALUE LE '&&HTL_LMT' THEN PROP_ZIP5 ELSE ' ';
         
                 
END
-RUN

TABLE FILE HOLD2
SUM CDAY
    PDAY
    CAMT
    PAMT
    CBK
    PBK
    CSVG
    PSVG
BY CITY_ST
BY RANK_VALUE
BY HTL_NAME
BY PNPR
BY PROP_PHONE
ON TABLE HOLD AS HOLD3
END
-RUN

TABLE FILE HOLD1
SUM CDAY
BY CITY_ST
ON TABLE HOLD AS RHOLD1
END

TABLE FILE RHOLD1
PRINT *
RANKED BY HIGHEST &&RANK_METH AS 'DAYS_RANK'
ON TABLE HOLD AS RHOLD2
END
-RUN

MATCH FILE HOLD3
PRINT *
BY CITY_ST
ON TABLE HOLD
RUN
FILE RHOLD2
SUM DAYS_RANK
BY CITY_ST
AFTER MATCH HOLD AS HOLD5 OLD-OR-NEW
END
-RUN

TABLE FILE HOLD5
PRINT *
RANKED BY HIGHEST DAYS_RANK
ON TABLE HOLD AS HOLD6
END
-RUN

DEFINE FILE HOLD6
  CTY_LIMIT/I3 = &&CTY_LMT;
  CLIMIT/A3    = EDIT(CTY_LIMIT) ;
  CRANK/D5     = RANK;    
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF CITY_ST NE LAST CITY_ST THEN (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6= EDIT(FRANKX,'$999999');
  CTY_VALUE/A9 = IF CTY_LIMIT EQ 0 THEN BRANK ELSE
                 IF LIST GT CTY_LIMIT THEN 'All Other' ELSE FRANK;
  JN6KEY/A80 = CITY_ST | HTL_NAME | PROP_PHONE;
                  
-* CTY_PRINT/A35 = IF CTY_VALUE NE 'All Other' THEN CITY_ST ELSE 'All Other';	
                 
-* CHAIN_CD1/A2= IF CTY_VALUE NE 'All Other' THEN CHAIN_CD ELSE ' ';
-* PNPR1/A1 = IF CTY_VALUE NE 'All Other' THEN PNPR ELSE ' ';
-* HTL_ADDRESS1/A35= IF CTY_VALUE NE 'All Other' THEN HTL_ADDRESS ELSE ' ';	
-* HTL_NAME1/A30 = IF CTY_VALUE NE 'All Other' THEN HTL_NAME ELSE ' ';
-* HTL_NAME1A/A30 = IF CTY_VALUE EQ 'All Other' THEN 'All Other' ELSE HTL_NAME1;
-* PROP_PHONE1/A15= IF CTY_VALUE NE 'All Other' THEN PROP_PHONE ELSE ' ';
-* ZIP51/A5 = IF CTY_VALUE NE 'All Other' THEN ZIP5 ELSE ' ';	
-* RANK_VAL/A10 = IF CTY_VALUE EQ 'All Other' THEN '9999999999' ELSE RANK_VALUE;
END
-RUN

TABLE FILE HOLD6
SUM CDAY
    PDAY
    CAMT
    PAMT
    CBK
    PBK
    CSVG
    PSVG
    CLIMIT
BY JN6KEY    
BY CTY_VALUE
BY RANK_VALUE
BY HTL_NAME
BY PNPR
BY PROP_PHONE
ON TABLE HOLD AS HOLD7 FORMAT FOCUS INDEX JN6KEY
END
-RUN

DEFINE FILE &&RPT_HOLD
  JN8KEY/A80 = CITY_ST | PROP_NAME| HOTEL_PHONE;
END

TABLE FILE &&RPT_HOLD
SUM NNROOMS
    TH_AMT
    SAVINGS
    BOOKINGS
BY JN8KEY      
BY CITY_ST
BY PROP_NAME
BY PROP_CODE
BY PROP_ADDRESS
BY PROP_ZIP5
BY PASSNGR_NAME
BY PERIOD
BY INV_DT
BY INVOICE_NUM
BY IN_DATE
BY OUT_DATE
BY CHAIN_CODE
ON TABLE HOLD AS DHOLD7 FORMAT FOCUS INDEX JN8KEY
END
-RUN


JOIN DHOLD7.JN8KEY IN DHOLD7 TO HOLD7.JN6KEY IN HOLD7 AS J1
-RUN

     

TABLE FILE DHOLD7
SUM CDAY
    NNROOMS
    TH_AMT
    SAVINGS
    BOOKINGS
    CLIMIT
BY CTY_VALUE      
BY CITY_ST
BY RANK_VALUE
BY HTL_NAME
BY PROP_NAME
BY PROP_CODE AS 'PROP_C1'
BY PROP_ZIP5
BY PROP_ADDRESS
BY PROP_PHONE
BY INV_DT
BY INVOICE_NUM
BY PASSNGR_NAME
BY IN_DATE
BY OUT_DATE
BY CHAIN_CODE
BY PERIOD
BY PNPR
ON TABLE HOLD AS HOLD10
END
-RUN

DEFINE FILE HOLD10
CTY_PRINT/A35 = IF CTY_VALUE NE 'All Other' THEN CITY_ST ELSE 'All Other';	
-* RANK_VAL/A9 = IF CTY_PRINT EQ 'All Other' THEN 'OTHER' ELSE RANK_VALUE;
RANK_VAL/A5 = IF CTY_VALUE NE 'All Other' THEN RANK_VALUE ELSE ' ';
HTL_NAME1A/A30 = IF CTY_VALUE NE 'All Other' THEN HTL_NAME ELSE ' ';
HTL_ADDRESS1/A35= IF CTY_VALUE NE 'All Other' THEN PROP_ADDRESS ELSE ' ';	
PROP_PHONE1/A15 = IF CTY_VALUE NE 'All Other' THEN PROP_PHONE ELSE ' ';
-* CHAIN_CD1/A2= IF CTY_VALUE NE 'All Other' THEN CHAIN_CD ELSE ' ';
-* HTL_NAME1/A30 = IF CTY_VALUE NE 'All Other' THEN HTL_NAME ELSE ' ';
-* HTL_NAME1A/A30 = IF CTY_VALUE EQ 'All Other' THEN 'All Other' ELSE HTL_NAME1;
PROP_PHONE1/A15= IF CTY_VALUE NE 'All Other' THEN PROP_PHONE ELSE ' ';
-* PROP_CODE/A7 = IF CTY_VALUE NE 'All Other' THEN PROP_CODE1 ELSE  ' ';
ZIP51/A5 = IF CTY_VALUE NE 'All Other' THEN PROP_ZIP5 ELSE ' ';	
PNPR1/A1 = IF CTY_VALUE NE 'All Other' THEN PNPR ELSE ' ';
CHAIN_CD/A2 = IF CTY_VALUE NE 'All Other' THEN CHAIN_CODE ELSE ' ';
NOWTOD/A8 WITH CTY_PRINT = HHMMSS (NOWTOD);
SPACE/A1 = ' ';
FLAG/A1 = IF CTY_PRINT EQ 'All Other' THEN 'B' ELSE 'A';
END

-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-SET &FXM=EDIT(&&FXDATE,'$$$$$$99');
-SET &FXD=EDIT(&&FXDATE,'$$$$$$$$$99');
-SET &FXY=EDIT(&&FXDATE,'$9999');
-SET &TXM=EDIT(&&TXDATE,'$$$$$$99');
-SET &TXD=EDIT(&&TXDATE,'$$$$$$$$$99');
-SET &TXY=EDIT(&&TXDATE,'$9999');
-SET &SP='|';

-SET &ROLLNME = TRUNCATE(&&RPTNAM);

-SET &ROLLCTY1 = LCWORD(15, '&&ROLLCTY.EVAL', 'A15');

-SET &ROLLCNCT = TRUNCATE(&&ROLLCNCT);
-SET &ROLLCS = TRUNCATE(&ROLLCTY1) || EDIT(&&ROLLSTA,', 99');

-SET &ROLLPHN = EDIT(&&ROLLFON,'9-999-999-9999');
 
-SET &ROLLCS = IF &&ROLLCS EQ ',' THEN ' ' ELSE &ROLLCS;
-SET &ROLLCS = IF &&ROLLSTA EQ '  ' THEN &&ROLLCTY ELSE &ROLLCS;
-SET &ROLLPHN = IF &&ROLLPHN EQ ' -   -   -    ' THEN ' ' ELSE &ROLLPHN;
-RUN

-* -IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' GOTO EXLFMT;
-* -RUN

-EXLFMT;
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_DRIL;
-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'HOLD10';
-INCLUDE DRILWHRS
-************************************************************
-SKIP_DRIL

TABLE FILE HOLD10
HEADING 
"&ROLLNME"
"&ROLLCNCT"
"&ROLLPHN" 
"&&DTDESC &FXM/&FXD/&FXY - &TOM/&TOD/&TOY"
" "
"&&RPT_TTL1" 
"&&RPT_TTL2"
" "
-*
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*
&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
ON PERIOD SUBHEAD
"<PERIOD"
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10


-*
-**********************************************************************
-* Report specific footer(s)
-**********************************************************************
-*
FOOTING
"</3"
"&&FOOTER1"
"&&FOOTER2"
"<55 Post-trip Reporting &DATE AT <NOWTOD <95 &&FOOTR"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
" "
" "
" "
" "
-INCLUDE SBFOOT

ON TABLE SET STYLE *
-INCLUDE GLOBAL_LOGO

UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.030000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
    
TYPE=REPORT, FONT=ARIAL, SIZE=8, BACKCOLOR=NONE, STYLE=NORMAL, $
  

TYPE=HEADING, HEADALIGN=BODY,$

TYPE=HEADING,
     SIZE=9,
     JUSTIFY=LEFT,
     FONT='ARIAL',
     STYLE=BOLD,
     COLOR=RGB(70 136 177),
$

TYPE=HEADING,
     SIZE=9,
     LINE=4,
     JUSTIFY=LEFT,
     FONT='ARIAL',
     STYLE=BOLD,
     COLSPAN=2, 
     COLOR=RGB(70 136 177),
$

TYPE=HEADING,
     LINE=6,
     SIZE=10,
     COLSPAN=15,
     JUSTIFY=CENTER,
     FONT='ARIAL',
     STYLE=BOLD,
     COLOR=RGB(70 136 177),
$

TYPE=HEADING,
     LINE=7,
     SIZE=10,
     COLSPAN=15,
     JUSTIFY=CENTER,
     FONT='ARIAL',
     STYLE=BOLD,
     COLOR=RGB(70 136 177),
$

TYPE=HEADING, 
     LINE=9,
     ITEM=1, 
     COLSPAN=2,
$
TYPE=HEADING,
     LINE=9,
     ITEM=2,
     SIZE=9,
     COLSPAN=7,
     JUSTIFY=LEFT,
     STYLE=BOLD,
$

TYPE=HEADING,
     LINE=9,
     ITEM=3,
     SIZE=9,
     COLSPAN=1,
     JUSTIFY=LEFT,
     STYLE=BOLD,
$

TYPE=HEADING,
     LINE=9,
     ITEM=4,
     SIZE=9,
     COLSPAN=7,
     JUSTIFY=LEFT,
     STYLE=BOLD,
$

TYPE=SUBHEAD,
     FONT='ARIAL',
     STYLE=BOLD,
     SIZE=9,
     JUSTIFY=LEFT,
     COLSPAN=12,
     BACKCOLOR=RGB(70 136 177),
$

TYPE=TITLE,
     FONT='ARIAL',
     STYLE=BOLD,
     SIZE=9,
     JUSTIFY=LEFT,
     BACKCOLOR=NONE,
$


     
TYPE=FOOTING,
     SIZE=7,
     STYLE=BOLD,
     FONT='ARIAL',
$ 


TYPE=REPORT, COLUMN=N2, SQUEEZE=1.5, $
TYPE=REPORT, COLUMN=N3, SQUEEZE=1.5, $
TYPE=DATA, JUSTIFY=LEFT, $
TYPE=DATA, COLUMN=N2, COLOR=RGB(0 0 138), $
TYPE=DATA, COLUMN=N4, COLOR=RGB(0 0 138), $

TYPE=SUBTOTAL, SIZE=9, JUSTIFY=LEFT, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, JUSTIFY=LEFT, STYLE=BOLD,$
ENDSTYLE
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END



-GOTO XXIT
-*****************************************************************
-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'HOLD10';
-INCLUDE DRILWHRS
-************************************************************
-SKIP_DRIL

TABLE FILE HOLD10
-INCLUDE HEADAIR
"&&SUBHEAD"
"</2"
-*
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*
&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10



-*
-**********************************************************************
-* Report specific footer(s)
-**********************************************************************
-*
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**********************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&DTSTY     
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*


-GOTO XXIT

-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;

-NODRILL
-HTMLFORM BEGIN
<HTML>
<HEAD>

</HEAD>
<BODY>


<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>

<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER> 

</BODY>
</HTML>
-HTMLFORM END

-XXIT

JOIN CLEAR MYJOIN
-RUN
