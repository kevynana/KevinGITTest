
-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*                     FOCEXEC: AIRRPT
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE

-*9/30/03        DEB                CARRIED THROUGHT EMB_APT_CD RTE_APT_CD 
-*11/25/03       JOY                ADDED CLASS
-* 2/17/15 - DEB - S-07456 ADDED &&PAX_LMT 
-*12/17/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*07/12/16   RSB   S-20630 - New Employee ID database field
-*                           Added EMP_ID 
-* 02/24/2017 - DLV - S-22486 CHANGED ettek Review to Post-trip Reporting
-*09/28/2017 - JEM - S-35422 Place the footer date/time stamp text in a separate file to be -INCLUDED in the stylesheets

-**************************************************************************
-INCLUDE SETECHO

SET ASNAMES = ON
-RUN

-IF &&SETF NE 'HNTPLSV' THEN GOTO Skipthis;


-SET &&RANK_METH = DISSAVINGS;

-Skipthis
-SET &&PAX_LMT = &&RANK_LIMIT;


TABLE FILE &&RPT_HOLD
SUM NET_TKT_CNT 
    FARE_PAID 
    NEW_SEG_MILES
    EMP_ID
    LEVEL1
    TKT_PURCH
    REF_EXCH_CNT
    DISSAVINGS
BY LEVEL_DESC 
BY PASSNGR_NAME   
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
SUM NET_TKT_CNT
      FARE_PAID
      NEW_SEG_MILES
      EMP_ID
      LEVEL1
      TKT_PURCH
      REF_EXCH_CNT
      DISSAVINGS
BY LEVEL_DESC
RANKED BY HIGHEST &&RANK_METH
BY PASSNGR_NAME
ON TABLE HOLD AS RNKHLD1
END 
-RUN


TABLE FILE &&RPT_HOLD
PRINT
      AIRLINE
      AIRLINE_FEE
      AIRFEE_CNT
      CLASS
      EMB_APT_CD     
      EMB_APT_NM
      DISSAVINGS
      FARE_PAID
      LEVEL_DESC
      NET_TKT_CNT
      SAVINGS
      REF_EXCH_CNT
      REFUSE_CODE
      RTE_APT_CD 
      RTE_APT_NM  
      SEG_NBR
      TKT_NUM 
      TKT_PURCH
      EMP_ID
      LEVEL1
      TKT_SORT
      TKT_TYPE
      TRN_DATE
      DPT_TIME
      XDPT_DT
      XTRAN_DT 
BY PASSNGR_NAME
ON TABLE HOLD AS HOLD5
END 

-RUN

TABLE FILE &&RPT_HOLD
SUM NET_TKT_CNT AS 'TTKT'
    FARE_PAID AS 'TFARE'
    NEW_SEG_MILES AS 'TMILE'
    DISSAVINGS AS 'TDSAVE'
    TKT_PURCH AS 'TTPUR'
    REF_EXCH_CNT AS 'TREXC'
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
BY TRN_DATE   
ON TABLE HOLD AS HOLD2
END
-RUN



-* -SET &&RANK_LIMIT = 9999;


DEFINE FILE RNKHLD1
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = (LAST LIST + 1);
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE 
                  IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE 
                  CRANK;
  RANK_VAL/A9 = IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                IF RANK GT &&RANK_LIMIT THEN 'ALL OTHER' ELSE
                ARANK;
  RANK_LAB1/A80 = IF RANK_VAL NE 'ALL OTHER' THEN &&RANK_LABEL1
                  ELSE 'ALL OTHER';

END
-RUN 


TABLE FILE RNKHLD1
SUM 
      NET_TKT_CNT
      FARE_PAID
      NEW_SEG_MILES
      RANK_VALUE
      EMP_ID
      LEVEL1
      TKT_PURCH
      REF_EXCH_CNT
      DISSAVINGS      
BY LEVEL_DESC
RANKED BY HIGHEST &&RANK_METH
BY PASSNGR_NAME
ON TABLE HOLD AS RNKHLD2
END
-RUN

-* -SET &&RANK_LIMIT2 = 10;


DEFINE FILE RNKHLD2
  PAX_LIMIT/I5 = &&PAX_LMT;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
-*  LIST/D6      = IF PASSNGR_NAME NE LAST PASSNGR_NAME THEN (LAST LIST + 1)
-*                 ELSE LAST LIST;    
  LIST/D6      = (LAST LIST + 1);
  FRANKX/A7    = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6     = EDIT(FRANKX,'$999999');
  PAX_VALUE/A9 = IF PAX_LIMIT EQ 0 THEN BRANK ELSE
                 IF RANK GT PAX_LIMIT THEN 'OTHER' ELSE BRANK;
  PAX_VALUE2/A9 = IF PAX_LIMIT EQ 0 THEN BRANK ELSE
                 IF RANK GT PAX_LIMIT THEN 'OTHER' ELSE
                 IF DISSAVINGS EQ 0 THEN 'OTHER' ELSE BRANK;
  RANK_LABEL1/A80 = IF PAX_VALUE NE 'OTHER' THEN &&RANK_LABEL1
                    ELSE ' ';
  RANK_LABEL2/A80 = IF PAX_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL2
                     ELSE ' ';
  RANK_LABEL3/A80 = IF PAX_VALUE NE 'ALL OTHER' THEN &&RANK_LABEL3
                       ELSE ' ';
END
-RUN

TABLE FILE RNKHLD2
PRINT
      NET_TKT_CNT
      FARE_PAID
      NEW_SEG_MILES
      RANK_VALUE
      PAX_VALUE
      RANK_LABEL1
      RANK_LABEL2
      RANK_LABEL3
      EMP_ID
      LEVEL1
      TKT_PURCH
      DISSAVINGS
      REF_EXCH_CNT
BY LEVEL_DESC
BY PASSNGR_NAME
ON TABLE HOLD AS RNKHLD3
END
-RUN 

MATCH FILE HOLD2
PRINT TTKT
    TFARE
    TMILE
    TDSAVE 
    TTPUR 
    TREXC 
    TKT_SORT
    TRN_DATE
BY LEVEL_DESC
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE RNKHLD3
SUM NET_TKT_CNT
    FARE_PAID
    NEW_SEG_MILES
    RANK_VALUE
    PAX_VALUE
    RANK_LABEL1
    RANK_LABEL2
    RANK_LABEL3
    EMP_ID
    LEVEL1
BY LEVEL_DESC
BY PASSNGR_NAME
AFTER MATCH HOLD AS XONE OLD-OR-NEW
END
-RUN





MATCH FILE HOLD5
PRINT AIRLINE
      AIRLINE_FEE
      AIRFEE_CNT
      CLASS
      EMB_APT_CD     
      EMB_APT_NM
      FARE_PAID
      DISSAVINGS
      NET_TKT_CNT
      SAVINGS
      REF_EXCH_CNT
      REFUSE_CODE
      RTE_APT_CD 
      RTE_APT_NM  
      SEG_NBR
      TKT_NUM 
      EMP_ID
      LEVEL1
      TKT_PURCH
      TKT_TYPE
      TRN_DATE
      DPT_TIME
      XDPT_DT
      XTRAN_DT
BY LEVEL_DESC 
BY PASSNGR_NAME
BY TKT_SORT
ON TABLE HOLD
RUN
FILE XONE
SUM
    TMILE
    RANK_VALUE
    TTKT
    TFARE
    TDSAVE 
    TTPUR 
    TREXC 
    PAX_VALUE
    RANK_LABEL1
    RANK_LABEL2
    RANK_LABEL3
    EMP_ID
    LEVEL1
BY LEVEL_DESC
BY PASSNGR_NAME
BY TKT_SORT
AFTER MATCH HOLD AS XTWO OLD
END
-RUN






TABLE FILE XTWO
PRINT AIRLINE
      AIRLINE_FEE
      AIRFEE_CNT
      CLASS
      EMB_APT_CD   
      EMB_APT_NM
      FARE_PAID
      DISSAVINGS
      LEVEL_DESC
      NET_TKT_CNT
      PASSNGR_NAME
      PAX_VALUE
      SAVINGS
      RANK_LABEL1
      RANK_LABEL2
      RANK_LABEL3
      RANK_VALUE
       REFUSE_CODE
      RTE_APT_CD
      RTE_APT_NM  
-*       SEG_NBR
      TKT_NUM     
-*       TKT_SORT
      EMP_ID
      LEVEL1
      TKT_TYPE
      TRN_DATE
      TTKT
      TFARE
      TDSAVE 
      TTPUR 
      TREXC 
      TMILE
      DPT_TIME
      XDPT_DT
      XTRAN_DT 
BY TKT_SORT
BY SEG_NBR
ON TABLE HOLD AS AIRHOLD4 FORMAT FOCUS
END


DEFINE FILE AIRHOLD4
NET_TKT_CNT/D9S = IF (SEG_NBR EQ '01') THEN  TTKT ELSE 0;
PASNGR_NAME/A33 = IF TKT_NUM NE LAST TKT_NUM THEN PASSNGR_NAME ELSE ' ';
REASON_CD/A2 =  IF TKT_NUM NE LAST TKT_NUM THEN REFUSE_CODE ELSE ' ';
TICKET_NMBR/A10 = IF TKT_NUM NE LAST TKT_NUM THEN TKT_NUM ELSE ' ';
XT_DTE/A8 = EDIT(TRN_DATE);
XTKT_SRT/A10 = EDIT(TKT_SORT,'9999999999$');
XTRAN_DTE/MDYY = IF TKT_NUM NE LAST TKT_NUM THEN XTRAN_DT ELSE 0;

-* ****DEFINES ON PREVIOUS DEFINES****
SRT_DT/A120 = TKT_SORT||XT_DTE||PASSNGR_NAME;
FARE_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TFARE ELSE 0;
LOST_SAV_AMT/D12.2CS = IF SRT_DT NE LAST SRT_DT THEN TDSAVE ELSE 0;
TOTAL_MILES/D8CS = IF TKT_SORT NE LAST TKT_SORT THEN TMILE ELSE 0;
-* NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
XTS_FLAG/A1 = ' ';


END

-RUN



-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*7/31/00     IBISTL-KR             ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERDT
-**************************************************************************


-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_DRIL;
-************************************************************
-* INCLUDE PROGRAM TO GEN WHERE STATEMENTS
-************************************************************
-SET &&DRILLRPT = 'AIRHOLD4';
-INCLUDE DRILWHRS
-SKIP_DRIL
-************************************************************

-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
-RUN

TABLE FILE AIRHOLD4
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
-*
-**********************************************************************
-* Print statement with fields passed from the user profile database
-**********************************************************************
-*
&&PRINT1
&&PRINT2
&&PRINT3
&&PRINT4
&&PRINT5
&&PRINT6
&&PRINT7
&&PRINT8
&&PRINT9
&&PRINT10
-*
-**********************************************************************
-* By statements (one per field)
-**********************************************************************
-*
&&BY1
&&BY2
&&BY3
&&BY4
&&BY5
&&BY6
&&BY7
&&BY8
&&BY9
&&BY10
-*
-*********************************************************************
-*DATE           NAME               DESCRIPTION 
-*8/18/00      IBISTL-KR            INCLUDE WHERE STATEMENTS WHEN
-*                                  DRILLED_RUN EQ 'Y'.
-*********************************************************************
-*
-**********************************************************************
-* Where statements for Drilled Report
-**********************************************************************
-*
-IF &&DRILLED_RUN EQ 'N' GOTO SKIP_WHR;
-INCLUDE &&DRILLWHR
-SKIP_WHR
-*
-**********************************************************************
-* On Table statements for code appropriate to On Table commands
-**********************************************************************
-*
&&BREAKOPTION
&&ONTABLE1
&&ONTABLE2
&&ONTABLE3
&&ONTABLE4
&&ONTABLE5
&&ONTABLE6
&&ONTABLE7
&&ONTABLE8
&&ONTABLE9
&&ONTABLE10
-*
-**********************************************************************
-* Report specific footer(s)
-**********************************************************************
-*

-IF &&SETF EQ 'HNTPLSV' THEN GOTO SpclFoot;
-*
-GOTO Other1;
-*
-*********
-SpclFoot
-*********
-SET &FTR_AddlLines = 3;
FOOTING BOTTOM
" "
" "
"INCLUDES RECORDS ONLY WITH A LOST SAVINGS"
-*
-******
-Other1
-******
-INCLUDE FOOTERDT
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYD
ENDSTYLE
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*

-* REJ EX RESTART 
-*-*-EXIT


