-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File BVAIREX.FEX
-********************************************************************
-* MODIFICAIONS:
-*8/11/16-JEM-S-21166-Created for new report abachlfl
 
-********************************************************************
 
-********************************************************************
-INCLUDE SETECHO
-RUN
 
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
 
 
DEFINE FILE &&EXTRACT
  BOOK_DT/MDYY         = RESV_DATE;
  OT_BOOK/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';
  CARD/A2=EDIT(CREDIT_CARD, '99$$$$$$$$$$$$$$$$');
  FARE_PAID/P12.2CS   = SEG_AMT + SEG_TAX;
  AFLAG/A1='A';
  LEVEL_DESC/A60 = &&LDESC;
  MNON_REF/A15 = DECODE NONREF_FLAG  ('N' 'NON-REFUNDABLE'
                                     'R' 'REFUNDABLE'
                                     ' ' 'REFUNDABLE');
  SO_FLAG/A10 = DECODE STOP_CON ('O' 'STOPOVER'
                                 'X' 'CONNECTION'
                                 ' ' ' ');
 
-* TKT_TYPE/A10 = IF RF_FLAG EQ ' ' AND EX_FLAG EQ ' '  AND VOID_DATE EQ 0 THEN 'SALE' ELSE
-*                IF (((RF_FLAG EQ 'F' OR 'P') AND (EMBARK NE 'EX1' OR 'FP2' OR 'EX5') AND (SEG_COUNT LE 0)) OR (EMBARK EQ 'RF1'))  THEN 'REFUND' ELSE
-*                IF (((EXSALE NE ' ') AND (SEG_COUNT GE 0)) OR ((EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (RF_FLAG EQ ' '))) THEN 'EXCHANGE' ELSE ' ';
 
 
VOID/A1 =  IF (VOID_STATUS EQ '1') AND (VOID_DATE NE 0) THEN 'Y' ELSE 'N';
TKT_TYPE1/A4 = IF (SEG_COUNT LE 0) AND (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
              IF (SEG_COUNT LE 0) AND (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
              IF (SEG_COUNT LE 0) AND (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
              IF (SEG_COUNT LE 0) AND (EX_FLAG EQ 'F') THEN 'F-EX' ELSE
              IF VOID EQ 'Y' THEN 'VOID'  ELSE ' ';
 
 
TKT_TYPE/A40 = IF (TKT_TYPE1 EQ '' AND EXSALE EQ '') THEN 'Original Sale' ELSE
 	       IF (TKT_TYPE1 EQ '' AND EXSALE NE '') THEN 'Exchange Sale'	ELSE
 	       IF (TKT_TYPE1 EQ 'F-EX') THEN 'Original Ticket Fully Exchanged' ELSE
               IF (TKT_TYPE1 EQ 'P-EX')    THEN 'Original Ticket Partially Exchanged' ELSE
               IF (TKT_TYPE1 EQ 'F-RF')    THEN 'Original Ticket Fully Refunded' ELSE
               IF (TKT_TYPE1 EQ 'P-RF')    THEN 'Original Ticket Partially Refunded' ELSE
               IF (TKT_TYPE1 EQ 'VOID')    THEN 'Original Ticket Voided';
 
 
 
  XDPT_DT/MDYY         = DPT_DATE;
  XPSNGR_NM/A15        =
    EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
  XTRAN_DT/MDYY        = TRN_DATE;
  FST_DD/MDYY = FST_DPT_DATE;
RTNDATE/A8 = GETTOK(DEPART_DATES, 144, -1, '-', 8, RTNDATE);
RTN_YR/A4 = EDIT(RTNDATE, '9999');
RTN_M/A2 = EDIT(RTNDATE, '$$$$99');
RTN_D/A2 = EDIT(RTNDATE, '$$$$$$99');
RTN_DATE/A10 = RTN_M|| '/' || RTN_D || '/' || RTN_YR ;
-* ****DEFINES ON PREVIOUS DEFINES****
CARD_FLAG/A1 = IF CARD EQ 'CA' THEN 'Y' ELSE 'N';
-INCLUDE TRANTYPE
 
END
TABLEF FILE &&EXTRACT
PRINT
  AFLAG
  VAL_AIRLINE AS 'AIRLINE'
  ARR_DATE
  BOOK_DT
  CARD_FLAG
  CITYPAIR.EMBARK      AS 'EMB_APT_CD'
  CITYPAIR.INTL_DOM
  CITYPAIR.ROUTE       AS 'RTE_APT_CD'
  C_ITIN
  CLASS
  DPT_DATE
  DPT_TIME
  FARE_PAID
  FST_DD
  FNL_DEST_ARRIVAL_DATE
  FNL_DEST_DEPART_DATE
  DEPART_DATES
  FLIGHT
  LEVEL_DESC
  LEVEL1
  LEVEL2
  LEVEL3
  MNON_REF
  OT_BOOK
  PASSNGR_NAME
  PNR_LOCATOR
  REFUSE_CODE
  RTN_DATE
  SEG_COUNT
  SEG_LOWEST
  SEG_NBR
  SO_FLAG
  TKT_NUM
  TKT_SORT
  TKT_TYPE
  TRN_DATE
  XDPT_DT
  XPSNGR_NM
  XTRAN_DT	AS 'TRAN_DATE'
 
-INCLUDE &AIRDATES
-*WHERE TRN_DATE GE &&FROMDT.EVAL AND TRN_DATE LE &&TODT.EVAL
-* WHERE VOID_DATE EQ 0
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
 
-INCLUDE RPTPARMS
 
ON TABLE HOLD AS ABAIRHLD
END
-RUN
 
 
 
TABLE FILE ABAIRHLD
SUM AIRLINE
    C_ITIN
    FARE_PAID
    PASSNGR_NAME
BY LEVEL_DESC
BY PASSNGR_NAME
BY PNR_LOCATOR
BY TRAN_DATE
BY TKT_NUM
BY TKT_TYPE
BY FST_DD
BY RTN_DATE
BY AFLAG
ON TABLE HOLD AS ABAIR
END
-RUN
 
 
 
