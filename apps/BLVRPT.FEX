-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* This was created for the BLV-R Weekly Volumne Report
-* 10/11/05 - ADDED DOD_TYPE H TO BOOKING TYPE DEFINES - DEB
-*1/27/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-*4/27/2017-JEM-S-33318-updated logic for no records report

-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT    
FARE_PAID/D12.2CS= SEG_AMT + SEG_TAX;
IFLAG/A42 = DOD_DESC;

LEVEL_DESC/A60 = &&LDESC;
TKT_CNT/D8CS = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
  EQ 1) THEN 1 ELSE 0;  
-* DEFINES ON PREVIOUS DEFINES
ORIG_SALE/D9 = IF (SEG_NBR EQ '01') AND (FARE_PAID GE 0) AND (SEG_COUNT 
 EQ 1) AND (EXCHG_SALE EQ ' ') THEN 1 ELSE 0;
END

TABLEF FILE &&EXTRACT
PRINT DOC_TYPE
      EMBARK
      EX_FLAG
      EXCHG_SALE
      IFLAG
      AIR_MAIN.INTL_DOM AS 'INTDOM'
      ONLINE_TRADITIONAL
      ORIG_SALE
      RF_FLAG
      SEG_COUNT
      SEG_NBR
      TKT_CNT
      TKT_NUM
      VOID_DATE
-INCLUDE &AIRDATES
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10


-INCLUDE RPTPARMS

ON TABLE HOLD AS RPT_HOLD
END
-RUN

-SET &&AIRREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';


TABLE FILE RPT_HOLD
PRINT DOC_TYPE
      EMBARK
      EX_FLAG
      EXCHG_SALE
      IFLAG
      INTDOM
      ONLINE_TRADITIONAL
      ORIG_SALE
      RF_FLAG
      SEG_COUNT
      SEG_NBR
      TKT_NUM
      TKT_CNT
      VOID_DATE
BY TKT_NUM
BY SEG_COUNT
ON TABLE HOLD AS BLVH
END
-RUN

DEFINE FILE BLVH
EXCHGS/D9 = IF (EX_FLAG EQ 'F' OR 'P') AND (SEG_COUNT LE 0) AND (EMBARK 
  NE 'DT1' OR 'RF1' OR 'FP3') THEN TKT_CNT ELSE
  IF (DOC_TYPE EQ '1') AND (EXCHG_SALE EQ 'E') THEN 0 ELSE
  IF (SEG_COUNT GE 0) AND (EXCHG_SALE NE ' ') THEN TKT_CNT ELSE 0;
FLAG/A1 = 'A';
FULL_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'F') AND (DOC_TYPE NE '1')
           AND (TKT_NUM NE LAST TKT_NUM) AND (SEG_COUNT LT 0)) THEN 1 ELSE 0;
PART_RFNDS/D9 = IF ((VOID_DATE EQ 0) AND (RF_FLAG EQ 'P') AND (TKT_NUM 
  NE LAST TKT_NUM) AND (SEG_COUNT LE 0)) THEN 1 ELSE 0; 
-* DEFINES ON PREVIOUS DEFINES
REFUNDS/D9 = FULL_RFNDS + PART_RFNDS;
VOIDS/D9 = IF (VOID_DATE NE 0) AND (SEG_COUNT EQ 1) AND (SEG_NBR EQ '01') 
  THEN 1 ELSE IF (DOC_TYPE EQ '1' AND VOID_DATE NE 0) THEN 1 ELSE 0;
END

TABLE FILE BLVH
SUM EXCHGS
    ORIG_SALE
    REFUNDS
    VOIDS
BY IFLAG
BY INTDOM
ON TABLE HOLD AS BLVAIR
END
-RUN

EX CARUSE
-RUN

-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';
DEFINE FILE &&EXTRACT
BOOKINGS/D12 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
IFLAG/A41 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE BOOKINGS' ELSE
              'TRADITIONAL BOOKINGS'; 
END

TABLE FILE &&EXTRACT
SUM BOOKINGS AS 'CBOOK'
BY IFLAG
BY GEO_ZONE
BY INTL_DOM AS 'INTDOM'
-INCLUDE &CARDATES    
-INCLUDE RPTPARMS
WHERE TKT_NUM EQ '0000000000'
ON TABLE HOLD AS BLVCAR
END
-RUN

-SET &&CARREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';


MATCH FILE BLVAIR
SUM EXCHGS
    ORIG_SALE
    REFUNDS
    VOIDS
BY IFLAG
BY INTDOM
ON TABLE HOLD
RUN
FILE BLVCAR
SUM CBOOK
BY IFLAG
BY INTDOM
AFTER MATCH HOLD AS AIRCAR OLD-OR-NEW
END
-RUN


EX HTLUSE
-RUN

DEFINE FILE &&EXTRACT
BOOKINGS/D8 = IF ROOM_RATE GE 0 THEN 1 ELSE (-1);
IFLAG/A41 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'ONLINE BOOKINGS' ELSE
              'TRADITIONAL BOOKINGS'; 
END
-RUN

-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';
TABLE FILE &&EXTRACT
SUM BOOKINGS AS 'HBOOK'
BY IFLAG
BY INTL_DOM AS 'INTDOM'
-INCLUDE &HTLDATES
-INCLUDE RPTPARMS
WHERE TKT_NUM EQ '0000000000'
ON TABLE HOLD AS BLVHTL
END
-RUN


-SET &&HTLREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';



-IF &&AIRREC EQ 'N' AND &&CARREC EQ 'N' AND &&HTLREC EQ 'N' THEN GOTO xxit;


MATCH FILE AIRCAR
SUM CBOOK
      EXCHGS
      ORIG_SALE
      REFUNDS
      VOIDS
BY IFLAG
BY INTDOM
ON TABLE HOLD
RUN
FILE BLVHTL
SUM HBOOK
BY IFLAG
BY INTDOM
AFTER MATCH HOLD AS ACH OLD-OR-NEW
END
-RUN

-SET &&RPTSUF = 'SMY';

-INCLUDE FDEFRPTS
-RUN

DEFINE FILE ACH
INTLDOM/A25 = IF INTDOM EQ 'D' THEN 'DOMESTIC' ELSE
              IF INTDOM EQ 'I' THEN 'INTERNATIONAL' ELSE ' ';
NOWTOD/A8 WITH IFLAG = HHMMSS (NOWTOD);
END

TABLE FILE ACH
-INCLUDE HEADER 
"&&SUBHEAD"
"</2"
SUM   ORIG_SALE AS '# TICKETS,ISSUED'
      EXCHGS AS '# EXCHANGE,SALES'
      REFUNDS AS '# REFUNDS'
      VOIDS AS '# VOIDS'
      CBOOK AS '# CAR,ONLY,BOOKINGS'
      HBOOK AS '# HOTEL,ONLY,BOOKINGS'
BY IFLAG AS ' '
BY INTLDOM AS ' '
-INCLUDE FOOTERSM
ON IFLAG SUMMARIZE AS 'TOTAL FOR'
ON IFLAG SKIP-LINE
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN



-xxit;
