-**********************************************************************************************
-* UpdateRptfldsfromSetfiles.fex
-* Use this procedure if there are multiple records for a single report instance (repeat).
-* 
-* Remove WHERE statements in setfiles, and put in RPT_FLDS
-* 
-* retrieve all the report instances to be updated, omit those instances 
-*           which has values in those fields already.
-* 
-**********************************************************************************************
-SET &ECHO = ALL;
SET TEMPERASE = OFF
SET ASNAME = ON
SET HOLDLIST=PRINTONLY

-*****************************************************************************
-* STEP 0. retrieve the set of report instances to updated 
-*****************************************************************************

-SET &SETFILE = 'ABACARBN';
-SET &WHERE1 = 'WHERE AIR.INCL3 GT 0';
-INCLUDE getTheInstances
-RUN

-****************************************************************************
-*<WHERE >EMB_CTY.EMB_CITY OMITS ''FP2'' OR ''FP3'' OR ''EX5''';
-*****************************************************************************
-* STEP 1. retrieve records in AIR segment for step 2
-*****************************************************************************
DEFINE FILE RPT_INST
  AIRID/I1 = 1;
  AIRINCL3/I1 = 2;
END

TABLE FILE RPT_INST
PRINT AIRID AS 'AIR.ID' AIRINCL3 AS 'AIR.INCL3'  
BY INST_KEY
WHERE INST_KEY IN FILE INSTKEY
ON TABLE SAVE AS HOLDAIR1
END
-RUN

-*-GOTO STEP7;
-*****************************************************************************
-* STEP 2. UPDATE AIR SEGMENT - Using data saved in step 1
-*****************************************************************************
MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 AIR.ID/1 AIR.INCL3/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG AIR
  ON NOMATCH INCLUDE
  ON MATCH UPDATE AIR.INCL3
DATA ON HOLDAIR1
END
-RUN

-STEP1
-*****************************************************************************
-* STEP 3. retrieve records in AIRFLDS segment for step 4
-*****************************************************************************
-*FILEDEF HOLDALL CLEAR
APP FI HOLDALL DISK HOLDALL.FTM (APPEND

-SET &CNT = 1;
-REPEAT REPEATEND 3 TIMES

-**********************************************************************************
-SET &DATAFLD = IF &CNT EQ 1 THEN 'FP2' ELSE IF &CNT EQ 2 THEN 'FP3' ELSE 'EX5';
-**********************************************************************************

DEFINE FILE RPT_INST
  SEQNO/I2 = &CNT;
  DATAFIELD/A3 = '&DATAFLD.EVAL';
END

TABLE FILE RPT_INST
PRINT SEQNO AS 'SEQ_NO' DATAFIELD AS 'DPT_AIRPORT'  
BY INST_KEY
WHERE INST_KEY IN FILE INSTKEY
ON TABLE HOLD AS HOLDALL
END
-RUN

-SET &CNT = &CNT + 1;
-REPEATEND

TABLE FILE HOLDALL
PRINT SEQ_NO DPT_AIRPORT
BY INST_KEY
ON TABLE SAVE AS HOLDAIR2
END
-RUN

-*****************************************************************************
-* STEP 4. UPDATE AIRFLDS SEGMENT - Using data saved in step 2
-*****************************************************************************
MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 AIRFLDS.SEQ_NO/2 AIRFLDS.DPT_AIRPORT/3
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG AIRFLDS
  ON NOMATCH INCLUDE
  ON MATCH UPDATE AIRFLDS.DPT_AIRPORT
DATA ON HOLDAIR2
END
-RUN

-* update <WHERE >AIRLINE NE ''2V'' OR ''XA''';
-*****************************************************************************
-* STEP 5. retrieve records in AIR segment for step 7
-*****************************************************************************
-SET &WHERE1 = 'WHERE AIR.INCL6 GT 0';
-INCLUDE getTheInstances
-RUN

DEFINE FILE RPT_INST
  AIRID/I1 = 1;
  AIRINCL/I1 = 2;
END

TABLE FILE RPT_INST
PRINT AIRID AS 'AIR.ID' AIRINCL AS 'AIR.INCL6'  
BY INST_KEY
WHERE INST_KEY IN FILE INSTKEY
ON TABLE SAVE AS HOLDAIR5
END
-RUN

-*****************************************************************************
-* STEP 6. UPDATE AIR SEGMENT - Using data saved in step 5
-*****************************************************************************
MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 AIR.ID/1 AIR.INCL6/1
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG AIR
  ON NOMATCH INCLUDE
  ON MATCH UPDATE AIR.INCL6
DATA ON HOLDAIR5
END
-RUN

-STEP7
-*****************************************************************************
-* STEP 7. retrieve records in AIRFLDS segment for step 8
-*****************************************************************************
-*FILEDEF HOLD CLEAR
APP FI HOLDREC DISK HOLDREC.FTM (APPEND

-SET &CNT = 1;
-REPEAT REPEATEND2 2 TIMES

-**********************************************************************************
-SET &DATAFLD = IF &CNT EQ 1 THEN '2V' ELSE IF &CNT EQ 2 THEN 'XA';
-**********************************************************************************

DEFINE FILE RPT_INST
  SEQNO/I2 = &CNT;
  DATAFIELD/A3 = '&DATAFLD.EVAL';
END

TABLE FILE RPT_INST
PRINT SEQNO AS 'SEQ_NO' DATAFIELD AS 'FLOWN_AIR'  
BY INST_KEY
WHERE INST_KEY IN FILE INSTKEY
ON TABLE HOLD AS HOLDREC
END
-RUN
-SET &CNT = &CNT + 1;
-REPEATEND2

TABLE FILE HOLDREC
PRINT SEQ_NO FLOWN_AIR
BY INST_KEY
ON TABLE SAVE AS HOLDAIR6
END
-RUN

-*****************************************************************************
-* STEP 8. UPDATE AIRFLDS SEGMENT - Using data saved in step 6
-*****************************************************************************
MODIFY FILE RPT_FLDS
FIXFORM INST_KEY/7 AIRFLDS.SEQ_NO/2 AIRFLDS.FLOWN_AIR/3
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG AIRFLDS
  ON NOMATCH INCLUDE
  ON MATCH UPDATE AIRFLDS.FLOWN_AIR
DATA ON HOLDAIR6
END
-RUN

