-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-*  03/31/2015  REJ  S-05681 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-*4/20/2017-JEM-S-33318-updated logic for no records report

-********************************************************************
-* MODIFICAIONS:
-*SET &ECHO=ALL;
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
DEFINE FILE &&EXTRACT
BOOKINGS/D12 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
  LEVEL_DESC/A60 = &&LDESC;
  NBR_DAYS/D12S = NUM_DAYS;
  RENTAL_DAYS/D12S = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
  TRAN_MY/MTYY = INVOICE_DATE;
-* ****DEFINES ON PREVIOUS DEFINES****
  MIDBOOK/D12S = IF (DAILY_RATE GE 0) AND
                  (CAR_CAT EQ 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY'
                  OR 'COMPACT') THEN 1
                  ELSE IF (DAILY_RATE LT 0) AND
                  (CAR_CAT EQ 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY'
                  OR 'COMPACT') THEN (-1) ELSE 0;
  MIDAMT/D12S = IF (CAR_CAT EQ 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY'
                   OR 'COMPACT') THEN RENTAL_AMT ELSE 0;
  MIDDAYS/D12S = IF (CAR_CAT EQ 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY'
                   OR 'COMPACT') THEN RENTAL_DAYS ELSE 0;
 
  NOMID_DAYS/D12S = IF (CAR_CAT NE 'INTERMEDIATE' OR 'MIDSIZE' OR 'ECONOMY'
                   OR 'COMPACT') THEN RENTAL_DAYS ELSE 0;
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT AROLL_DSC3
  BOOKINGS
  CAR_CAT
  CAR_DESC
  CAR_KEY
  CAR_RATE
  CAR_TYPE
  DAILY_RATE
  INVOICE_DATE
  LEVEL_DESC
  MIDBOOK
  MIDAMT
  MIDDAYS
  NOMID_DAYS
  NBR_DAYS
  RENTAL_AMT
  RENTAL_DAYS
  TRAN_MY
-IF &&FROMAVRP EQ 'Y' GOTO :BCN1;
WHERE (INVOICE_DATE GE '&&FROMDT.EVAL') AND (INVOICE_DATE LE '&&TODT.EVAL')
-GOTO :BCN1_CONT;
-:BCN1
WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL)
-:BCN1_CONT
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
 
-INCLUDE RPTPARMS
ON TABLE HOLD AS CARHOLD
END
-RUN
 
 
 
TABLE FILE CARHOLD
PRINT AROLL_DSC3
  BOOKINGS
  CAR_DESC
  CAR_KEY
  CAR_RATE
  CAR_TYPE
  DAILY_RATE
  LEVEL_DESC
  MIDAMT
  MIDBOOK
  MIDDAYS
  NOMID_DAYS
  NBR_DAYS
  RENTAL_AMT
  RENTAL_DAYS
BY TRAN_MY
ON TABLE HOLD AS CARTWO
END
-RUN
 
DEFINE FILE CARTWO
CRENTAL/D12S = RENTAL_DAYS ;
CNOMID_DAYS/D12S = NOMID_DAYS;
CRENTALA/D12.2CS = MIDAMT ;
CRENTALD/D12 = MIDDAYS;
END
 
TABLE FILE CARTWO
SUM CRENTAL
    CNOMID_DAYS
COMPUTE PCTNOMID/P12.2 = IF ((CRENTAL LT 1) AND (CRENTAL GT (-1))) THEN 0 ELSE
                            ((CNOMID_DAYS/CRENTAL)*100);
COMPUTE AVGMID/D12.2CS = IF ((CRENTALD LT 1) AND (CRENTALD GT (-1))) THEN 0
                         ELSE (CRENTALA/CRENTALD);
BY TRAN_MY
ON TABLE HOLD AS CARTHREE FORMAT FOCUS INDEX TRAN_MY
END
-RUN
 
 
 -SET &&CARREC=IF &RECORDS EQ 0 THEN 'N' ELSE 'Y';

 
 
 
 
 
 
 
 
