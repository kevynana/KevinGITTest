-* JK added code to select only tnt_company eq 1 or 2 2/21/05
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK

-INCLUDE SETECHO
SET ASNAMES = ON
-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================
-****** IF PERIOD EQ Y THEN TIME PERIOD1 ELSE TIME PERIOD2

DEFINE FILE CR_50
BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
  NBR_DAYS/D12 =  IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                  ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;  
  PERIOD/A1 = IF (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) THEN 'Y' ELSE 'N';              
END

TABLEF FILE CR_50
PRINT BOOKINGS
  NBR_DAYS 	
  PERIOD
  RENTAL_AMT/D12.2CS	
  VENDOR_NAME 		
 -* WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL
 WHERE (INVOICE_DATE GE &&FROMDT.EVAL) AND (INVOICE_DATE LE &&TODT.EVAL) OR
       (INVOICE_DATE GE &&FXDATE.EVAL) AND (INVOICE_DATE LE &&TXDATE.EVAL)
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10

-INCLUDE RPTPARMS
ON TABLE HOLD AS TC_HOLD1A
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACT DATA FROM ABOVE
-*========================================================================

DEFINE FILE TC_HOLD1A
CBOOK/D8 = IF PERIOD EQ 'Y' THEN BOOKINGS ELSE 0;
PBOOK/D8 = IF PERIOD EQ 'N' THEN BOOKINGS ELSE 0;
CDAYS/D12 = IF PERIOD EQ 'Y' THEN NBR_DAYS ELSE 0;
PDAYS/D12 = IF PERIOD EQ 'N' THEN NBR_DAYS ELSE 0;
CAMT/D12.2CS = IF PERIOD EQ 'Y' THEN RENTAL_AMT ELSE 0;
PAMT/D12.2CS = IF PERIOD EQ 'N' THEN RENTAL_AMT ELSE 0;
ROLLCD/A8 = '&&ROLL';
NMB/I5    = NMB + 1;
END
-RUN

TABLE FILE TC_HOLD1A
SUM CBOOK
    PBOOK
    CDAYS
    PDAYS
    CAMT
    PAMT
BY ROLLCD
BY NMB
BY VENDOR_NAME
ON TABLE HOLD AS TCH1 
END
-RUN

MODIFY FILE TCCVND1
FIXFORM FROM TCH1
DATA ON TCH1
END
-RUN

-NEXTONE
