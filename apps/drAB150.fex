-*11/25/03   STEVE              Added -INCLUDE RPTPARMS
-*7/26/04    JOY                Added/Changed Norecords logic
-*2/25/05    DEB                ADDED CODE FOR GLOBAL CURRENCY
-*9/18/06    DEB                ADDED LEVEL2 FOR CER
-*  01/06/2016  REJ  S-13202 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-*01/19/16 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*1/21/16 - JEM - S-14814 Corrected drilldown to pass correct international/domestic flag in rptparms.fex
-*  09/12/2016  REJ  S-24432 Changes for ER5 testing.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
 
-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';
 
-SET &&DTSTY = 'DT';
 
-SET &&FIELD = &ROW_LABEL;
-SET &&IDLEVELBREAK = &PARM2;
 
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
EX RPTRESET
-RUN
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX SETDBLS SETFILE=&&SETF
-RUN
 
-* REJ added for er5 Test
-IF &&FROMAVRP NE 'Y' GOTO :DRD1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:DRD1
 
-INCLUDE GetForDrillDown
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
 
-SET &&RPT_TTL1='          HOTEL RATES $151+ DETAIL';
-SET &&RTECHK='WHERE RATE_RM1 EQ ''Y''';
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
 
 
TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN
 
 
-READ SAVE &LEVEL.2.
 
-IF &&IDLEVELBREAK EQ ' ' GOTO LEV1;
 
-SET &&LCHK = 'WHERE LEVEL_DESC EQ ''&&FIELD.EVAL''';
 
 
-GOTO ARNDLEV1;
 
 -LEV1;
-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &&LCHK='WHERE ' | &AROLL_DSC.EVAL | ' CONTAINS ' | '''&&FIELD.EVAL''';
 
-ARNDLEV1;
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DCA1;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-RUN
-:DCA1
 
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX HTLUSE
-RUN
 
-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN
 
-IF &&FROMAVRP EQ 'Y' GOTO :NOBIU;
DEFINE FILE BRPTINST
  RT/A3 = 'HTL';
END
TABLE FILE BRPTINST
PRINT RT AS 'RPT_GROUP'
BY INST_KEY
ON TABLE SAVE AS AIRACH00
END
-RUN
MODIFY FILE BRPTINST
FIXFORM INST_KEY/7 RPT_GROUP/3
MATCH INST_KEY
  ON MATCH UPDATE RPT_GROUP
DATA ON AIRACH00
END
-RUN
 
EX CODEGEN
-RUN
-:NOBIU
 
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';
 
-INCLUDE IDLEVROLLDEF
-RUN
 
 
DEFINE FILE &&EXTRACT
-* CITY_ST/A35 = PROP_STATE||(', '| PROP_CITY);
CITY_ST/A35 = PROP_CITY||(' '| PROP_STATE);
 
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
LEVEL_DESC1/A80 = &&LDESC;
NBR_DAYS/D12 = NUM_DAYS;
NBR_ROOMS/D12 = NUM_ROOMS;
NEW_ROOM_RATE/D12.2C = ROOM_RATE;
NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;
 
NBR_DAYS1/D12 = IF ((NBR_DAYS LT 0) AND (NBR_ROOMS LT 0)) THEN
                ((NBR_DAYS*NBR_ROOMS) * (-1)) ELSE NBR_DAYS*NBR_ROOMS;
OUT_DATE/MDYY = CHKOUT_DATE;
RATE_RM1/A1 = IF NEW_ROOM_RATE GT 150  OR
                 NEW_ROOM_RATE LT (-150) THEN 'Y' ELSE 'N';
BOOKINGS/D8 = IF NEW_ROOM_RATE GE 0 THEN 1 ELSE (-1);
-INCLUDE IDLEVDEFINE
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT BOOKINGS
  CITY_ST
  CURR_DATE
  IN_DATE
  INVOICE_DATE
  NBR_DAYS1
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT
  OUT_DATE
  PASSNGR_NAME
  PROP_NAME
  PROP_ADDRESS
  LEVEL2
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
 
-INCLUDE RPTPARMS
-INCLUDE &HTLDATES
 
&&RTECHK
&&LCHK
ON TABLE HOLD AS GRPTHOLD
END
-RUN
 
TABLE FILE GRPTHOLD
PRINT BOOKINGS
  CITY_ST
  IN_DATE
  INVOICE_DATE
  LEVEL2
  NBR_DAYS1
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT
  OUT_DATE
  PASSNGR_NAME
  PROP_NAME
  PROP_ADDRESS
BY CURR_DATE
ON TABLE HOLD AS GRPTHLD2 FORMAT FOCUS INDEX CURR_DATE
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-* Global Reporting JOIN
JOIN GRPTHLD2.CURR_DATE IN GRPTHLD2 TO GLOBAL.DATE IN GLOBAL AS J1
END
 
 
DEFINE FILE GRPTHLD2
 
NAMEC/A30 = CNAME;
 
 
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
CURR1X/D20.6 = 1 / USDPU ;
 
-* DEFINES ON PREVIOUS DEFINES
AMT_TOTALX/D20.6 = NEW_TOTAL_AMT * CURR1X;
RATE_ROOMX/D20.6 = NEW_ROOM_RATE * CURR1X;
 
 
END
 
 
TABLE FILE GRPTHLD2
PRINT BOOKINGS
  CITY_ST
  IN_DATE
  INVOICE_DATE
  LEVEL2
  NBR_DAYS1
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT
  OUT_DATE
  PASSNGR_NAME
  PROP_NAME
  PROP_ADDRESS
  CURR_DATE
  NAMEC
  AMT_TOTALX/D16.2CS
  RATE_ROOMX/D16.2CS
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
-TYPE After Extract
 
 
TABLE FILE RPTHOLD
PRINT BOOKINGS AS 'TBOOK'
BY PASSNGR_NAME
ON TABLE HOLD AS RNKHLD
END
-RUN
 
 
TABLE FILE RPTHOLD
PRINT BOOKINGS
    NBR_ROOMS
    NBR_DAYS1
    NEW_ROOM_RATE
    NEW_TOTAL_AMT
    RATE_ROOMX/D16.2CS
    AMT_TOTALX/D16.2CS
    NAMEC
    LEVEL2
BY PASSNGR_NAME
BY INVOICE_DATE
BY IN_DATE
BY OUT_DATE
BY PROP_NAME
BY PROP_ADDRESS
BY CITY_ST
ON TABLE HOLD AS XHLD
END
-RUN
 
 
 
MATCH FILE XHLD
PRINT BOOKINGS
      INVOICE_DATE
      NBR_ROOMS
      NBR_DAYS1
      NEW_ROOM_RATE
      NEW_TOTAL_AMT
      IN_DATE
      OUT_DATE
      PROP_NAME
      PROP_ADDRESS
      CITY_ST
      NAMEC
      AMT_TOTALX
      RATE_ROOMX
      LEVEL2
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE RNKHLD
SUM TBOOK
BY PASSNGR_NAME
AFTER MATCH HOLD AS XTWO OLD-OR-NEW
END
-RUN
 
TABLE FILE XTWO
PRINT BOOKINGS
    NBR_DAYS1
    NBR_ROOMS
    NEW_TOTAL_AMT
    NEW_ROOM_RATE
    TBOOK
    NAMEC
    RATE_ROOMX
    AMT_TOTALX
    LEVEL2
RANKED BY HIGHEST TBOOK NOPRINT
BY PASSNGR_NAME
BY INVOICE_DATE
BY IN_DATE
BY OUT_DATE
BY PROP_NAME
BY PROP_ADDRESS
BY CITY_ST
ON TABLE HOLD AS XTHREE
END
-RUN
 
 
DEFINE FILE XTHREE
RANK_LIMIT/I5 = 999;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 LIST/D6 = IF PASSNGR_NAME NE LAST PASSNGR_NAME THEN (LAST LIST + 1)
    ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999');
 RANK_VALUE/A6 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE FRANK;
END
-RUN
 
TABLE FILE XTHREE
PRINT NBR_DAYS1
    NBR_ROOMS
    NEW_TOTAL_AMT
    NEW_ROOM_RATE
    BOOKINGS
    NAMEC
    AMT_TOTALX
    RATE_ROOMX
    LEVEL2
BY RANK_VALUE
BY PASSNGR_NAME
BY INVOICE_DATE
BY IN_DATE
BY OUT_DATE
BY PROP_NAME
BY PROP_ADDRESS
BY CITY_ST
ON TABLE HOLD AS XFOUR
END
-RUN
 
JOIN ROLL WITH PASSNGR_NAME IN XFOUR TO ROLLUP_CODE IN ROLLUP AS J3
-RUN
 
 
DEFINE FILE XFOUR
  NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
  ROLL/A8='&&ROLLUP.EVAL';
  LVL/A60='&&LVL_DSC.EVAL';
  RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
  RD/A10 = 'Run Date:';
  RT/A10 = 'Run Time:';
  LEV/A25 = EDIT(LEVEL_DESC, '9999999999999999999999999');
  RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
  RN1/A60=TRIM('B', '&&FIELD.EVAL', 60, ' ', 1, 'A60');
  TDATE1/A8MDYY='&MDYY';
  TDATE2/A10=EDIT(TDATE1,'99/99/9999');
  TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
END
-RUN
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS1';
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
 
-TYPE Before Report
 
 
-IF &&CURRENCY EQ 'USD' THEN GOTO STEP2;
 
-SET &TAMT_CUR = 'TTL,AMT, (' | &&CURRENCY || ')';
-SET &AVGDR_CUR = 'AVG,DLY,RTE, (' | &&CURRENCY || ')';
-SET &&SUBHEAD = <NAMEC;
 
TABLE FILE XFOUR
HEADING
-INCLUDE &&PAHDR
" "
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
PASSNGR_NAME AS 'PASSENGER,NAME'
IN_DATE AS 'IN,DATE'
OUT_DATE AS 'OUT,DATE'
PROP_NAME AS 'HOTEL'
PROP_ADDRESS AS 'ADDRESS'
CITY_ST AS 'CITY/,STATE'
BOOKINGS AS 'BKGS'
NBR_DAYS1 AS 'NMBR,NIGHTS'
NBR_ROOMS AS 'NMBR,ROOMS'
AMT_TOTALX/D16.2CSM AS '&TAMT_CUR.EVAL'
COMPUTE AVGDR/D12.2CSM = IF AMT_TOTALX LT 0 AND NBR_DAYS1 LT 0 THEN
                          ((AMT_TOTALX/NBR_DAYS1) * (-1)) ELSE
                        IF ((NBR_DAYS1 LT 0) AND (NBR_DAYS1 GT (-1))) THEN 0
                           ELSE (AMT_TOTALX/NBR_DAYS1);
                           AS '&AVGDR_CUR.EVAL'
-GOTO CONTINUE;
 
-STEP2
 
TABLE FILE XFOUR
HEADING
-INCLUDE &&PAHDR
" "
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
PRINT
-IF '&&ROLLUP.EVAL' NE 'CERN-R' THEN GOTO SkipThis;
LEVEL2 AS 'PROJECT,NUMBER'
-SkipThis
IN_DATE AS 'IN,DATE'
OUT_DATE AS 'OUT,DATE'
PROP_NAME AS 'HOTEL'
PROP_ADDRESS AS 'ADDRESS'
CITY_ST AS 'CITY/STATE'
BOOKINGS AS 'BKGS'
NBR_DAYS1 AS 'NMBR,NIGHTS'
NBR_ROOMS AS 'NMBR,ROOMS'
NEW_TOTAL_AMT/D12.2CSM AS 'TTL,AMT'
COMPUTE AVGDR/D12.2CSM = IF NEW_TOTAL_AMT LT 0 AND NBR_DAYS1 LT 0 THEN
                          ((NEW_TOTAL_AMT/NBR_DAYS1) * (-1)) ELSE
                        IF ((NBR_DAYS1 LT 0) AND (NBR_DAYS1 GT (-1))) THEN 0
                           ELSE (NEW_TOTAL_AMT/NBR_DAYS1);
                           AS 'AVG,DLY,RTE'
-*
-CONTINUE;
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY RANK_VALUE NOPRINT
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY INVOICE_DATE NOPRINT
BY IN_DATE NOPRINT
BY OUT_DATE NOPRINT
BY PROP_NAME NOPRINT
BY PROP_ADDRESS NOPRINT
BY CITY_ST NOPRINT
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PASSNGR_NAME RECOMPUTE MULTILINES AS 'TOTAL FOR'
ON TABLE SET PAGE-NUM OFF
-*
FOOTING BOTTOM
-INCLUDE CRIGHT_FOOTER
"</1"
"<15 &M1.EVAL <45 &M2.EVAL <75 &M3.EVAL"
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYD
 
TYPE=REPORT, COLUMN=N5, WRAP=.5, $
TYPE=REPORT, COLUMN=N8, WRAP=.5, $
 
 
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-GOTO XXIT;
 
-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO ENDIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-ENDIT
-GOTO XXIT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
 
-XXIT
 
