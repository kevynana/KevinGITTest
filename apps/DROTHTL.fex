-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Steve / Deb - added &&NETVOLUME under NETTKTS - 04/06/2004
-* DEB 5/10/04  CHANGED VOD_AMT DEFINE TO BALANCE TRAVEL SUMMARY
-*6/3/04 JK Changed Exg_amt/Ref_amt to calculate correctly
-*7/26/04    JOY                Added/Changed Norecords logic
-* 2/8/10 - DV  CHANGED EXG_AMT DEFINE DUE TO
-*              DETAIL NOT BALANCING TO SUMMARY PAGE
-*1/29/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE
-*10/16/15 - S-11851 Added logic for ABACHLOD (DOMESTIC ONLY) ABACHLOI (INTERNATIONAL ONLY) - JM
-*12/31/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*  03/15/2016  REJ  S-16385 Changes to allow the report to work in ER5.
-*  05/31/2016  REJ  S-16833 ER5 changes so the hierarchy will show on the header line.
 
-INCLUDE SETECHO
 
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&LSEL = 'AROLL_DSC1';
-DEFAULT &&SETF = 'XX';
-* -DEFAULT &&FDATE = ' ';
-* -DEFAULT &&TDATE = ' ';
 
 
-* -SET &&FIELD = &ROW_LABEL;
-SET &&LSEL = &LSEL;
-SET &&IDLEVELBREAK = &PARM2;
-SET &&OTFLAG = &PARM1;
-* -SET &&FDATE = &FDATE;
-* -SET &&TDATE = &TDATE;
-* -SET &FMM=EDIT(&&FDATE,'$$$$$99');
-* -SET &FMD=EDIT(&&FDATE,'$$$$$$$$99');
-* -SET &FMY=EDIT(&&FDATE,'9999');
-* -SET &TOM=EDIT(&&TDATE,'$$$$$99');
-* -SET &TOD=EDIT(&&TDATE,'$$$$$$$$99');
-* -SET &TOY=EDIT(&&TDATE,'9999');
 
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 10/31/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-GOTO CONT;
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX SETDBLS SETFILE=&&SETF
-RUN
 
-IF &&FROMAVRP EQ '' GOTO :OTER5;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-:OTER5
 
-INCLUDE GetForDrillDown
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-****
-IF &&OTFLAG EQ 'O' GOTO ONLINESET ELSE
-IF &&OTFLAG EQ 'T' GOTO TRADSET ELSE GOTO ALLSET;
-RUN
 
-ONLINESET
-IF &&SETF EQ 'ABACHLD' GOTO DOMO;
-IF &&SETF EQ 'ABACHLI' GOTO INTLO;
-SET &&RPT_TTL1 = '                  ONLINE BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''ONLINE''';
-SET &&INTDOM='';
-RUN
-GOTO OUTSIDE
 
-DOMO;
-SET &&RPT_TTL1 = '         DOMESTIC ONLINE BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''ONLINE''';
-SET &&INTDOM='WHERE CITY.INTL_DOM EQ ''D''';
-RUN
-GOTO OUTSIDE
 
-INTLO;
-SET &&RPT_TTL1 = '    INTERNATIONAL ONLINE BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''ONLINE''';
-SET &&INTDOM='WHERE CITY.INTL_DOM EQ ''I''';
-RUN
-GOTO OUTSIDE
 
-TRADSET
-IF &&SETF EQ 'ABACHLD' GOTO DOMT;
-IF &&SETF EQ 'ABACHLI' GOTO INTLT;
-SET &&RPT_TTL1 = '             TRADITIONAL BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''TRADITIONAL''';
-SET &&INTDOM='';
-RUN
-GOTO OUTSIDE
 
-DOMT;
-SET &&RPT_TTL1 = '    DOMESTIC TRADITIONAL BOOKINGS DETAIL     ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''TRADITIONAL''';
-SET &&INTDOM='WHERE CITY.INTL_DOM EQ ''D''';
-RUN
-GOTO OUTSIDE
 
-INTLT;
-SET &&RPT_TTL1 = 'INTERNATIONAL TRADITIONAL BOOKINGS DETAIL    ';
-SET &&ONLCHK = 'WHERE ONLINE_TRADITIONAL EQ ''TRADITIONAL''';
-SET &&INTDOM='WHERE CITY.INTL_DOM EQ ''I''';
-RUN
-GOTO OUTSIDE
 
 
-ALLSET
-IF &&SETF EQ 'ABACHLD' GOTO DOMA;
-IF &&SETF EQ 'ABACHLI' GOTO INTLA;
-SET &&RPT_TTL1 = '                     ALL BOOKINGS DETAIL     ';
-SET &&ONLCHK = ' ';
-SET &&INTDOM='';
-RUN
-GOTO OUTSIDE
 
-DOMA;
-SET &&RPT_TTL1 = '            DOMESTIC ALL BOOKINGS DETAIL     ';
-SET &&ONLCHK = ' ';
-SET &&INTDOM='WHERE CITY.INTL_DOM EQ ''D''';
-RUN
-GOTO OUTSIDE
 
-INTLA;
-SET &&RPT_TTL1 = '        INTERNATIONAL ALL BOOKINGS DETAIL    ';
-SET &&ONLCHK = ' ';
-SET &&INTDOM='WHERE CITY.INTL_DOM EQ ''I''';
-RUN
-GOTO OUTSIDE
 
-OUTSIDE
 
-SET &&SUBHEAD=IF &&IDLEVELBREAK EQ ' ' THEN ' ' ELSE '<LEVEL_DESC';
 
 
TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN
 
 
-READ SAVE &LEVEL.2.
 
-IF &&IDLEVELBREAK EQ ' ' GOTO LEV1;
 
-SET &LCHK = 'WHERE LEVEL_DESC EQ ''&&LSEL.EVAL''';
 
 
-GOTO ARNDLEV1;
 
-LEV1;
-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &LCHK='WHERE ' | &AROLL_DSC.EVAL | ' CONTAINS ' | '''&&LSEL.EVAL''';
 
-ARNDLEV1;
 
 
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
 
EX TS_JAMS
-RUN
-IF &&FROMAVRP NE 'Y' GOTO :DCA1;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-RUN
-:DCA1
 
-INCLUDE SELCATQT
-RUN
 
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX HTLUSE
-RUN
 
-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
 
 
-SET &HTLDATES = &&WEB_PATH || 'HTLDATES.fex';
 
-INCLUDE IDLEVROLLDEF
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-INCLUDE OTHPARMS
-:AVSF1
 
DEFINE FILE &&EXTRACT
CITY_ST/A35 = PROP_CITY||(', '| PROP_STATE);
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
LEVEL_DESC1/A80  = &&LDESC;
NBR_ROOMS/D12 = NUM_ROOMS;
NBR_DAYS/D12 = NUM_DAYS;
NEW_ROOM_RATE/D12.2C = ROOM_RATE;
NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;
BOOKINGS/D8 = IF NEW_TOTAL_AMT GE 0 THEN 1 ELSE (-1);
 
NNROOMS/D12 = IF NBR_DAYS LT 0 AND NUM_ROOMS LT 0 THEN
              ((NBR_DAYS*NUM_ROOMS) * (-1)) ELSE NBR_DAYS*NUM_ROOMS;
OUT_DATE/MDYY = CHKOUT_DATE;
REFUSE_CD/A2 = EDIT(REFUSE_KEY,'$$$$$$$$99');
-INCLUDE IDLEVDEFINE
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT
  BOOKINGS
  CITY_ST
    HTL_KEY
    IN_DATE
    INV_DT
    INVOICE_DATE
    LEVEL_DESC
    NBR_DAYS
    NBR_ROOMS
    NEW_ROOM_RATE
    NEW_TOTAL_AMT
    NNROOMS
    OUT_DATE
    PASSNGR_NAME
    PROP_NAME
    REFUSE_KEY
  REFUSE_CD
-INCLUDE &HTLDATES
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
&&ONLCHK
&LCHK
&&INTDOM
-INCLUDE RPTPARMS
ON TABLE HOLD AS HTLHOLD
END
-RUN
 
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
DEFINE FILE HTLHOLD
NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END
 
-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS';
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
 
 
 
TABLE FILE HTLHOLD
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
 
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM
NNROOMS AS 'NUMBER,NIGHTS'
BOOKINGS AS 'BOOKINGS'
NBR_ROOMS AS 'NUMBER,ROOMS'
NEW_TOTAL_AMT/D12.2CSM AS 'TOTAL,AMOUNT'
-* COMPUTE AVGDR/D12.2CS = IF NEW_TOTAL_AMT LT 0 AND BOOKINGS LT 0
-* THEN ((NEW_TOTAL_AMT/BOOKINGS) * (-1))
-* ELSE NEW_TOTAL_AMT/BOOKINGS; AS 'AVERAGE,BOOKING,RATE'
COMPUTE AVGDR/D12.2CSM = IF ((BOOKINGS LT 1) AND (BOOKINGS GT (-1))) THEN 0 ELSE (NEW_TOTAL_AMT/BOOKINGS); AS 'AVERAGE,BOOKING,RATE'
 
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY LEVEL_DESC NOPRINT
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY PROP_NAME AS 'HOTEL'
BY CITY_ST AS 'CITY/STATE'
BY IN_DATE AS 'CHECKIN,DATE'
BY OUT_DATE AS 'CHECKOUT,DATE'
BY REFUSE_CD AS 'REASON'
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
 
ON LEVEL_DESC SUMMARIZE AS 'TOTAL FOR'
ON LEVEL_DESC PAGE-BREAK
 
 
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
-INCLUDE FOOTERDT
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYD
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-GOTO XXIT;
 
 
-FIRST
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-GOTO XXIT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT;
