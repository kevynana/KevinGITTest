-* File READTREE.FEX
-* CREATES AND READS A FILE TREE IN DOS
-* 1/28/2009 all files for ALL-R (under \TTWEB\ALL-R\) will stay for 365 days, all others for 60 days 
-* 1/15/2014 LS put double quotes arond file name in the case the file path containing spaces.
-************
-INIT_ENV
-************
-*-SET &ECHO=ALL;
SET YRTHRESH=30
SET DEFCENT=19

-INCLUDE SETECHO
SET TEMPERASE = OFF
-*
-* NUMBER OF DAYS TO DELETE PAST
-SET &NUMDAYS = '60';
-*SET 365 FOR ALL-R
-SET &NUMDAYS2 = '365';

-* INITIAL DIRECTORY FOR TREE
-SET &INITDIR = '&&WEBSRV.EVAL' || '\TTWEB';

-* FILEDEF FOR INITIAL TREE OUTPUT
-SET &FILELOC = '&&WEBSRV.EVAL' || '\TEMP\OUTPUT.FTM';

-* FILEDEF FOR FILE NAMES FROM TREE OUTPUT
-SET &FILETREE = '&&WEBSRV.EVAL' || '\TEMP\FILETREE.FTM';

-* FILEDEF FOR BAT TO GENERATE FILE ATTRIBUTES
-SET &FILEBAT = '&&WEBSRV.EVAL' || '\TEMP\FILEATRB.BAT';

-* FILEDEF FOR RAW FILES AND FILE ATTRIBUTES
-SET &FILEATRB = '&&WEBSRV.EVAL' || '\TEMP\FILEATRB.FTM';

-* FILEDEF FOR FILES AND FILE ATTRIBUTES
-SET &ALLATRIB = '&&WEBSRV.EVAL' || '\TEMP\ALLATRIB.FTM';

-* FILEDEF FOR DELETETION BAT FILE
-SET &FILEDELS = '&&WEBSRV.EVAL' || '\TEMP\FILEDELS.BAT';
-*
FILEDEF FILETREE DISK &FILETREE.EVAL
FILEDEF FILEATRB DISK &FILEATRB.EVAL
FILEDEF FILEBAT  DISK &FILEBAT.EVAL
FILEDEF ALLATRIB DISK &ALLATRIB.EVAL
FILEDEF FILEDELS DISK &FILEDELS.EVAL (APPEND
-RUN

-************
-CREATE_TREE
-************
DOS TREE &INITDIR /F /A >> &FILELOC.EVAL
-RUN
FILEDEF TREE00 DISK &FILELOC.EVAL
-RUN
-*
-************
-HOLD_TREE
-************
DEFINE FILE TREE00
 VAL/A4    =IF EDIT(TREE,'9999') EQ '+---' OR '\---' THEN 'DIR1' 
       ELSE IF EDIT(TREE,'99999999') EQ '|   +---' OR '|   \---' OR '    +---' OR '    \---' THEN 'DIR2'
       ELSE IF EDIT(TREE,'999999999999') EQ '|           ' OR '|   |       ' OR '    |       ' OR '            '  THEN 'FILE' 
       ELSE 'DIR0';
 DIR/A4    =IF VAL EQ 'FILE' AND EDIT(TREE,'$$$$$$$$$$$$9') EQ ' '
             THEN 'DIR0' ELSE VAL;

 COMP   /A10=EDIT(TREE,'$$$$9999999999');
 TPATH1A/A50=IF DIR EQ 'DIR1' THEN '&INITDIR' | '\' | COMP
             ELSE ' ';
 TPATH1 /A50=IF TPATH1A NE ' ' THEN TPATH1A ELSE LAST TPATH1;

 USER   /A20=EDIT(TREE,'$$$$$$$$99999999999999999999');
 TPATH2A/A80=IF DIR EQ 'DIR2' THEN TPATH1 || '\' || USER
             ELSE ' ';
 TPATH2 /A80=IF TPATH2A NE ' ' THEN TPATH2A ELSE LAST TPATH2;

-* the following defined fields not used
-* FILE1  /A90=EDIT(TREE,'$$$$$$$$$$$$999999999999999999999999999999999999999999999999999999999999999999999999999999999999999999');

-* TPATH3A/A200=IF DIR EQ 'FILE' THEN TPATH2 || '\' || FILE1
-*             ELSE ' ';
-* TPATH3 /A200=IF TPATH3A NE ' ' THEN TPATH3A ELSE LAST TPATH3;

END
-*
TABLE FILE TREE00
ON TABLE SET PAGE-NUM OFF
ON TABLE SET LINES 999999
PRINT
  TPATH2 AS ''

BY TPATH1 NOPRINT
WHERE DIR EQ 'DIR2'
ON TABLE HOLD AS FILETREE FORMAT ALPHA
END
-RUN

-************
-CREATE_BAT
-************
FILEDEF TREE11 DISK &FILETREE.EVAL
-RUN
DEFINE FILE TREE11
PARTTREE/A50=EDIT(TREE,'99999999999999999999999999999999999999999999999999');
 BATOUT/A150 = 'DIR ' | PARTTREE | ' /S >> ' | '&FILEATRB';
END
TABLE FILE TREE11
PRINT BATOUT

ON TABLE HOLD AS FILEBAT FORMAT ALPHA
END
-RUN
-*

DOS &FILEBAT
-RUN-*
-************
-READ_DATES
-************
FILEDEF TREE11 DISK &FILEATRB.EVAL
-RUN
-*
DEFINE FILE TREE11
GETINFO1/A1=IF EDIT(TREE,'$$9') EQ '/' AND
     EDIT(TREE,'$$$$$$$$$$$$$$$$$$$$$$$$99999') NE '<DIR>'
     THEN 'Y' ELSE 'N';
ATTRIB/A200 = IF GETINFO1 EQ 'Y' THEN TREE ELSE ' ';

DIRECT1/A1 =IF EDIT(TREE,'9999999999') EQ ' Directory'
     THEN 'Y' ELSE 'N';
DIRECT2/A50=IF DIRECT1 EQ 'Y' THEN
EDIT(TREE,'$$$$$$$$$$$$$$99999999999999999999999999999999999999999999999999')
ELSE ' ';
DIRECT/A50 =IF DIRECT2 NE ' ' THEN DIRECT2 ELSE LAST DIRECT;

END
-*
TABLE FILE TREE11
PRINT
 DIRECT AS ''
 ATTRIB AS ''
WHERE GETINFO1 EQ 'Y'
ON TABLE HOLD AS ALLATRIB FORMAT ALPHA
END
-RUN
-*
-************
-CREATE_DEL
-************
FILEDEF TREE2 DISK &ALLATRIB.EVAL 
-RUN

DEFINE FILE TREE2
 -*FULLFILE/A275 = 'DEL '|DIRECT||'\'||CHKFILE;
 FULLFILE/A275 = 'DEL '| '"' | DIRECT||'\'||CHKFILE || '"';
 THISDATE/MDY WITH CDATE ='&MDY';
 FILEAGE/I9 = THISDATE - CDATE;
 THISD/YYMD = THISDATE;
 CDATEX/YYMD=CDATE;
 AGE/I9 = THISD - CDATEX;
END
TABLE FILE TREE2
PRINT FULLFILE
WHERE AGE GE &NUMDAYS
WHERE FULLFILE OMITS '\TTWEB\ALL-R\'
ON TABLE HOLD AS FILEDELS FORMAT ALPHA
END
-RUN
-SET &TOTALLINES = &LINES;

TABLE FILE TREE2
PRINT FULLFILE
WHERE AGE GE &NUMDAYS2
WHERE FULLFILE CONTAINS '\TTWEB\ALL-R\'
ON TABLE HOLD AS FILEDELS FORMAT ALPHA
END
-RUN

-SET &TOTALLINES = &TOTALLINES + &LINES;

-IF &TOTALLINES EQ 0 THEN GOTO CLEAN_UP;

-*DOS CD\
-*-RUN
DOS &FILEDELS
-RUN
-*
-************
-CLEAN_UP
-************
-SET &FILEDELS2 = '&&WEBSRV.EVAL' || '\TEMP\FILEDELS2.BAT';
DOS COPY &FILEDELS.EVAL &FILEDELS2.EVAL
-RUN

DOS DEL &FILELOC.EVAL
DOS DEL &FILETREE.EVAL
DOS DEL &FILEBAT.EVAL
DOS DEL &FILEATRB.EVAL
DOS DEL &ALLATRIB.EVAL
DOS DEL &FILEDELS.EVAL
-RUN

-*
-************
-LEAVE
-************
-******************************************************************************
-*Clear the temporary folders and files generated from the load process.
-******************************************************************************
EX clean_up_load_process
-RUN
-*-EXIT
