-*11/25/03   STEVE              Added -INCLUDE RPTPARMS
-*7/26/04    JOY                Added/Changed Norecords logic
-*12/14/04   DEB - CHANGED SORT SO LEVEL1 WOULD PRINT ON EVERY LINE
-*                 AND RENAMED LEVEL1 TO EMPLOYEE ID - PER PATTI
-* Joy Updated Pref_Non define to contain C/P/N/M/L
-* 9/8/14 Updated Pref_non define to use exclude_flags
-* 9/19/14 Updated to use exclude flags in % - JK
-*01/19/16 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels
-*  09/20/2016  REJ  S-24432 ER5 Testing updates.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.
-* 09/21/2017 CA S-38044 Place the footer datetime stamp text in a separate file to be -INCLUDED in the stylesheets
-*                       Backed out changes for S-36530 
-*

 
-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-DEFAULT &&FIELD = 'XX';
-DEFAULT &&SETF = 'XX';
-DEFAULT &&LSEL = 'XX';
 
-SET &&DTSTY = 'DT';
 
-SET &&FIELD = &ROW_LABEL;
-SET &&IDLEVELBREAK = &PARM2;
 
 
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
EX RPTRESET
-RUN
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX SETDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS
EX CNTLUPD
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DRD1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-INCLUDE GetForDrillDown
-:DRD1
-RUN
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
-SET &&RPT_TTL1='                   NON-PREFERRED HOTELS DETAIL';
-IF &&FROMAVRP EQ 'Y' GOTO :DT1;
-SET &FMM=EDIT(&&FROMDT,'$$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'$9999');
-SET &TOM=EDIT(&&TODT,'$$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'$9999');
-GOTO :DT1_CONT;
-:DT1
-SET &FMM=EDIT(&&FROMDT,'$$$$$99');
-SET &FMD=EDIT(&&FROMDT,'$$$$$$$$99');
-SET &FMY=EDIT(&&FROMDT,'9999');
-SET &TOM=EDIT(&&TODT,'$$$$$99');
-SET &TOD=EDIT(&&TODT,'$$$$$$$$99');
-SET &TOY=EDIT(&&TODT,'9999');
-:DT1_CONT
 
TABLE FILE BRPTINST
BY HIGHEST BREAK_LEVELS
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE
END
-RUN
 
 
-READ SAVE &LEVEL.2.
 
-IF &&IDLEVELBREAK EQ ' ' GOTO LEV1;
 
-SET &&LCHK = 'WHERE LEVEL_DESC EQ ''&&FIELD.EVAL''';
 
 
-GOTO ARNDLEV1;
 
-LEV1;
-SET &AROLL_DSC = IF &LEVEL GT '10' THEN 'AROLL_DSC' | &LEVEL ELSE 'AROLL_DSC' | EDIT(&LEVEL,'$9');
-SET &&LCHK='WHERE ' | &AROLL_DSC.EVAL | ' CONTAINS ' | '''&&FIELD.EVAL''';
 
-ARNDLEV1;
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMS2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DOD1;
-SET &&RPTGRP_OVERRIDE='HTL';
EX CODEGEN
-RUN
-:DOD1
 
-INCLUDE SELCATQT
-RUN
 
-*EX CATQTR
EX &&CATQTRPR
 -RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
 
EX HTLUSE
-RUN
 
-SET &&RPTGRP = 'HTL';
EX UDIDGEN
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
 
-INCLUDE IDLEVROLLDEF
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
DEFINE FILE &&EXTRACT
CITY_ST/A35 = PROP_STATE||('  '| PROP_CITY);
IN_DATE/MDYY = CHKIN_DATE;
INV_DT/YYMD = INVOICE_DATE;
LEVEL_DESC1/A80 = &&LDESC;
 
NBR_DAYS/D12 = NUM_DAYS;
NBR_ROOMS/D12 = NUM_ROOMS;
NEW_ROOM_RATE/D12.2C = ROOM_RATE;
NEW_TOTAL_AMT/D12.2C = TOTAL_AMT;
 
NBR_DAYS1/D12 = IF ((NBR_DAYS LT 0) AND (NBR_ROOMS LT 0)) THEN
                ((NBR_DAYS*NBR_ROOMS) * (-1)) ELSE NBR_DAYS*NBR_ROOMS;
OUT_DATE/MDYY = CHKOUT_DATE;
BOOKINGS/D8 = IF NEW_ROOM_RATE GE 0 THEN 1 ELSE (-1);
-*   PREF_NON/A15 = IF HTL_REF.PREFERRED EQ 'Y' THEN 'PREFERRED' ELSE 'NON-PREFERRED';
 PNP_FLAG/A1 = EDIT(EXCLUDE_FLAGS, '$9');
  PNP_IE/A1 = EDIT(EXCLUDE_FLAGS, '$$9');
-* Defines on Previous Defines
PREF_NON/A15 = IF PNP_FLAG EQ 'I'  THEN 'PREFERRED' ELSE 'NON-PREFERRED';
-INCLUDE IDLEVDEFINE
 
END
-RUN
 
TABLEF FILE &&EXTRACT
PRINT BOOKINGS
  CITY_ST
  IN_DATE
  LEVEL1
  NBR_DAYS1
  NBR_ROOMS
  NEW_ROOM_RATE
  NEW_TOTAL_AMT
  OUT_DATE
  PASSNGR_NAME
  PROP_NAME
  PROP_ADDRESS
  REFUSE_CODE
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
 
-INCLUDE RPTPARMS
WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL
-* -* WHERE REFUSE_CODE NE 'A' OR 'T' OR 'I' OR 'N' OR 'S' OR 'R' OR CLIENT_PREF NE 'P'
-* WHERE CLIENT_PREF NE 'C' OR 'P' OR 'N' OR 'M' OR 'L'
-* WHERE PREF_NON EQ 'NON-PREFERRED'
WHERE PREF_NON EQ 'NON-PREFERRED'
&&LCHK
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN
 
-TYPE After Extract
 
 
TABLE FILE RPTHOLD
SUM BOOKINGS AS 'TBOOK'
BY PASSNGR_NAME
ON TABLE HOLD AS RNKHLD
END
-RUN
 
TABLE FILE RPTHOLD
PRINT BOOKINGS
    NBR_ROOMS
    NBR_DAYS1
    NEW_ROOM_RATE
    NEW_TOTAL_AMT
BY PASSNGR_NAME
BY LEVEL1
BY IN_DATE
BY OUT_DATE
BY PROP_NAME
BY PROP_ADDRESS
BY CITY_ST
BY REFUSE_CODE
ON TABLE HOLD AS XHLD
END
-RUN
 
 
 
MATCH FILE XHLD
PRINT BOOKINGS
      NBR_ROOMS
      NBR_DAYS1
      NEW_ROOM_RATE
      NEW_TOTAL_AMT
      LEVEL1
      IN_DATE
      OUT_DATE
      PROP_NAME
      PROP_ADDRESS
      CITY_ST
      REFUSE_CODE
BY PASSNGR_NAME
ON TABLE HOLD
RUN
FILE RNKHLD
SUM TBOOK
BY PASSNGR_NAME
AFTER MATCH HOLD AS XTWO OLD-OR-NEW
END
-RUN
 
TABLE FILE XTWO
PRINT BOOKINGS
    NBR_DAYS1
    NBR_ROOMS
    NEW_TOTAL_AMT
    NEW_ROOM_RATE
    TBOOK
RANKED BY HIGHEST TBOOK NOPRINT
BY PASSNGR_NAME
BY LEVEL1
BY IN_DATE
BY OUT_DATE
BY PROP_NAME
BY PROP_ADDRESS
BY CITY_ST
BY REFUSE_CODE
ON TABLE HOLD AS XTHREE
END
-RUN
 
 
DEFINE FILE XTHREE
RANK_LIMIT/I5 = 999;
 DRANK/D5      = RANK;
 ARANKX/A6 = FTOA(DRANK, '(D5)', 'A6');
 ARANK/A5=EDIT(ARANKX,'$99999');
 LIST/D6 = IF PASSNGR_NAME NE LAST PASSNGR_NAME THEN (LAST LIST + 1)
    ELSE LAST LIST;
 FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
 FRANK/A6  = EDIT(FRANKX,'$999999');
 RANK_VALUE/A6 = IF (RANK_LIMIT EQ 0) THEN (ARANK) ELSE
                IF RANK GT RANK_LIMIT THEN 'OTHER' ELSE FRANK;
END
-RUN
 
TABLE FILE XTHREE
PRINT NBR_DAYS1
    NBR_ROOMS
    NEW_TOTAL_AMT
    NEW_ROOM_RATE
    BOOKINGS
BY RANK_VALUE
BY PASSNGR_NAME
BY LEVEL1
BY IN_DATE
BY OUT_DATE
BY PROP_NAME
BY PROP_ADDRESS
BY CITY_ST
BY REFUSE_CODE
ON TABLE HOLD AS XFOUR
END
-RUN
 
JOIN ROLL WITH PASSNGR_NAME IN XFOUR TO ROLLUP_CODE IN ROLLUP AS J3
-RUN
 
DEFINE FILE XFOUR
  NOWTOD/A8 WITH RANK_VALUE = HHMMSS (NOWTOD);
  ROLL/A8='&&ROLLUP.EVAL';
    LVL/A60='&&LVL_DSC.EVAL';
    RPHONE/A15=EDIT(ROLL_PHONE,'9-999-999-9999');
    RD/A10 = 'Run Date:';
    RT/A10 = 'Run Time:';
  -*   LEV/A25 = EDIT(LEVEL_DESC, '9999999999999999999999999');
    RN/A60=TRIM('B', '&&RPT_TTL1.EVAL', 60, ' ', 1, 'A60');
    RN1/A60=TRIM('B', '&&FIELD.EVAL', 60, ' ', 1, 'A60');
    TDATE1/A8MDYY='&MDYY';
    TDATE2/A10=EDIT(TDATE1,'99/99/9999');
    TIME1/A8=HHMMSS('A8');
TIME2/A8=EDIT(TIME1,'99$:99$:99');
END
-RUN
-TYPE Before Report
 
TABLE FILE XFOUR
HEADING
"<+0> Client:<ROLL_NAME"
"<+0> Contact:<ROLL_CONTACT"
"<+0> Phone:<RPHONE "
"<+0> &&DTDESC <+0> &FMM/&FMD/&FMY - &TOM/&TOD/&TOY"
"<+0> <RN"
"<+0> <RN1"
" "
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM
LEVEL1 AS 'EMPLOYEE,ID'
REFUSE_CODE AS 'RSN,CDE'
BOOKINGS AS 'COUNT'
NBR_DAYS1 AS 'NMBR,NIGHTS'
NBR_ROOMS AS 'NMBR,ROOMS'
NEW_TOTAL_AMT/D12.2CSM AS 'TOTAL,AMOUNT'
COMPUTE AVGDR/D12.2CSM = IF NEW_TOTAL_AMT LT 0 AND NBR_DAYS1 LT 0 THEN
                          ((NEW_TOTAL_AMT/NBR_DAYS1) * (-1)) ELSE
                        IF ((NBR_DAYS1 LT 0) AND (NBR_DAYS1 GT (-1))) THEN 0
                           ELSE (NEW_TOTAL_AMT/NBR_DAYS1);
                           AS 'AVG,DLY,RTE'
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY RANK_VALUE NOPRINT
BY PASSNGR_NAME AS 'PASSENGER,NAME'
BY LEVEL1 NOPRINT
BY IN_DATE AS 'IN,DATE'
BY OUT_DATE AS 'OUT,DATE'
BY PROP_NAME AS 'HOTEL'
BY PROP_ADDRESS AS 'ADDRESS'
BY CITY_ST AS 'CITY/STATE'
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON PASSNGR_NAME RECOMPUTE MULTILINES AS 'TOTAL FOR'
-*
-INCLUDE FOOTERDT
-*
ON TABLE PAGE-BREAK AND SUBFOOT
" "
-INCLUDE SBFOOT
 
 
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.030000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.030000, BOTTOMMARGIN=0.010000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
 
 
TYPE=REPORT, FOCEXEC=NONE, FONT=ARIAL, SIZE=8, COLOR=BLACK,
    BACKCOLOR=NONE, GRID=OFF, STYLE=NORMAL, $
 
TYPE=TITLE,
     JUSTIFY=LEFT,
     STYLE=BOLD+UNDERLINE,
     FONT=ARIAL,
     COLOR=RGB(5 76 149),
$
 
TYPE=HEADING, SIZE=10, GRID=OFF, STYLE=BOLD, $
 
TYPE=HEADING, HEADALIGN=BODY,$
TYPE=HEADING, LINE=1, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=1, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=2, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=3, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=4, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=4, ITEM=2, COLSPAN=4, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=5, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=5, ITEM=2, COLSPAN=10, JUSTIFY=CENTER, $
TYPE=HEADING, LINE=6, ITEM=1, COLSPAN=1, JUSTIFY=LEFT, $
TYPE=HEADING, LINE=6, ITEM=2, COLSPAN=10, JUSTIFY=CENTER, $
 
 
-* TYPE=REPORT, COLUMN=N5, WRAP=.5, $
-* TYPE=REPORT, COLUMN=N8, WRAP=.5, $
-* TYPE=REPORT, COLUMN=N11, WRAP=.5,$
 
TYPE=GRANDTOTAL, SIZE=8, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=8,   STYLE=BOLD, $
 
 
TYPE=FOOTING, FONT=ARIAL, SIZE=7, STYLE=BOLD,$
TYPE=REPORT, IMAGE=TSMHEAD.jpg, POSITION=(0 0), $
TYPE=REPORT, IMAGE=FOOTER.jpg, POSITION=(0 8.2), $
 
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
-*
-GOTO ENDIT;
 
-XXIT
-SKIPTHIS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THIS FIELD IS NOT DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO ENDIT;
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-ENDIT
-GOTO XXIT;
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
 
-XXIT
 
