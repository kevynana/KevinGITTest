-* 9/10/14 ADDED LIMO TO NEW TOTAL TRANSACTION REPORT BY ROLLUP AND CATEGORY
-* 8/23/16-S-22623-JEM-Added Business total/annualized/prior number of rental days

-INCLUDE SETECHO
SET ASNAMES = ON
-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED IN STEP3
-*========================================================================
 
DEFINE FILE LR_50

EDATE/YYMD=  &&TODT.EVAL ;
EOM_DATE/YYMD=DATEMOV(EDATE,'EOM');
SDATE/YYMD = &&FROMDT.EVAL;
BOM_DATE/YYMD=DATEMOV(SDATE,'BOM');
BOY_DATE1/YYMD=DATEMOV(SDATE, 'BOY');

ANNUAL_BEGIN/YYMD = &&FROMDT.EVAL;
P_ANNUAL_BEGIN/YYMD=DATEMOV(DATEADD(BOY_DATE1, 'M', (-12)), 'BOM');

P_ANNUAL_END1/YYMD=DATEMOV(DATEADD(P_ANNUAL_BEGIN, 'M', (+0)), 'EOM');

P_ANNUAL_END2/YYMD=DATEMOV(DATEADD(P_ANNUAL_END1, 'M', (+11)), 'EOY');
 
YD/A1 = IF (INVOICE_DATE GE  &&FROMDT.EVAL)  AND (INVOICE_DATE LE  &&TODT.EVAL ) THEN 'Y' ELSE 'N';

P_YD/A1 = IF (INVOICE_DATE GE P_ANNUAL_BEGIN ) AND
            (INVOICE_DATE LE P_ANNUAL_END2) THEN 'Y' ELSE 'N';

  NEW_RENTL_AMT/D14.2C = RENTAL_AMT;

  RENTAL_DAYS/D12 =  IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                  ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;  
  
-*  YTD

 YLAMT/D14.2C = IF YD EQ 'Y' THEN NEW_RENTL_AMT ELSE 0;
 YLRENTL/P12 = IF YD EQ 'Y' THEN RENTAL_DAYS ELSE 0;
  
-*  PRIOR  

 PYLAMT/D14.2C = IF P_YD EQ 'Y' THEN NEW_RENTL_AMT ELSE 0;
 PYLRENTL/P12 = IF P_YD EQ 'Y' THEN RENTAL_DAYS ELSE 0;

ROLLCD/A8 = '&&CURROLL.EVAL';
END

TABLE  FILE LR_50
SUM YLAMT YLRENTL
PYLAMT PYLRENTL
COMPUTE LDAYS/I6=(EOM_DATE - ANNUAL_BEGIN) + 1;

BY ROLLCD
BY CATEGORY
WHERE CATEGORY OMITS 'GLOBAL' OR 'TRAVELFUND' OR 'FINANCIAL'
-* &AIRDATES CANNOT BE USED FOR THIS REPORT B/C IT WILL RUN FOR CURRENT MONTH ONLY INSTEAD OF YTD,QTR, ETC
-* -INCLUDE &AIRDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS TLIMYD
END
-RUN
 

-IF &LINES EQ 0 THEN GOTO NEXTONE;
-*========================================================================
-*   PROCESS EXTRACT DATA FROM ABOVE
-*========================================================================

DEFINE FILE TLIMYD
NEW_CAT/A15 = IF CATEGORY EQ 'CONF PLANNING' OR 'CONFERENCE' OR 'MAPS' OR 'OTHER' OR 'SITE INSPEC' THEN 'MEETING' ELSE 
              IF CATEGORY EQ 'NITTOB' OR 'NONBILL' OR 'TOYOB' OR 'TTHUB' THEN 'BUSINESS' ELSE CATEGORY;
END
TABLE FILE TLIMYD
SUM YLAMT/D12.2 PYLAMT/D12.2 YLRENTL/P12 PYLRENTL/P12
COMPUTE NLDAYS/I6 = FST.LDAYS;
COMPUTE DIVBY/I6=IF NLDAYS GT 365 THEN NLDAYS ELSE 365; NOPRINT
COMPUTE LPROSP1/D12.2   = ((YLAMT/FST.LDAYS)*DIVBY) ;
COMPUTE LPRODY1/P12     = ((YLRENTL/FST.LDAYS)*DIVBY) ;

BY ROLLCD
BY NEW_CAT AS 'CATEGORY'
ON TABLE HOLD AS TLIMYD2
END
-RUN


DEFINE FILE TLIMYD2
CBUS_LAMT/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN YLAMT ELSE 0;
CBUS_LDAY/P12 = IF CATEGORY EQ 'BUSINESS' THEN YLRENTL ELSE 0;
CMTG_LAMT/D12.2 = IF CATEGORY EQ 'MEETING' THEN YLAMT ELSE 0;
PBUS_LAMT/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN PYLAMT ELSE 0;
PBUS_LDAY/P12    = IF CATEGORY EQ 'BUSINESS' THEN PYLRENTL ELSE 0;
PMTG_LAMT/D12.2 = IF CATEGORY EQ 'MEETING' THEN PYLAMT ELSE 0;
BLPROSP1/D12.2 = IF CATEGORY EQ 'BUSINESS' THEN LPROSP1 ELSE 0;
BLPRODY1/P12 = IF CATEGORY EQ 'BUSINESS' THEN LPRODY1 ELSE 0;
MLPROSP1/D12.2 = IF CATEGORY EQ 'MEETING' THEN LPROSP1 ELSE 0;
END
TABLE FILE TLIMYD2
SUM YLAMT YLRENTL PYLAMT PYLRENTL LPROSP1 LPRODY1
    CBUS_LAMT CBUS_LDAY PBUS_LAMT PBUS_LDAY BLPROSP1 BLPRODY1 CMTG_LAMT PMTG_LAMT  MLPROSP1
BY ROLLCD
ON TABLE HOLD AS TCLIM2
END
-RUN

DEFINE FILE TCLIM2
  NMB/I5 = NMB + 1;  
END
TABLE FILE TCLIM2
SUM YLAMT
    YLRENTL
    PYLAMT
    PYLRENTL
    LPROSP1
    LPRODY1
    CBUS_LAMT 
    CBUS_LDAY
    PBUS_LAMT
    PBUS_LDAY
    BLPROSP1 
    BLPRODY1
    CMTG_LAMT
    PMTG_LAMT  
    MLPROSP1
BY ROLLCD
BY NMB
ON TABLE HOLD AS TCLIM3
END
-RUN

MODIFY FILE TCLIMYD2
FIXFORM FROM TCLIM3
DATA ON TCLIM3
END
-RUN


-NEXTONE
