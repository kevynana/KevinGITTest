-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* Created for the new quarterly review - 
-* Changed to use CITYPAIR.INTL_DOM vs. AIR_MAIN - Steve - 02/26/2007 
-******************************************************************************
-INCLUDE SETECHO
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN

DEFINE FILE &&EXTRACT 

FARE_PAID/D12.2=SEG_AMT + SEG_TAX ;
MRK_CNT/D8     = IF (TICKET_CODE EQ '1') AND
                     (FARE_PAID GE 0) AND
                     (SEG_COUNT EQ 1) AND 
                     (CONJ_REL EQ 1) THEN 1 ELSE 
                  IF (TICKET_CODE EQ '1') AND
                     (EX_FLAG NE ' ') THEN (-1) ELSE 
                  IF (TICKET_CODE EQ '1') AND
                     (RF_FLAG NE ' ') THEN (-1) ELSE 0;     
NEW_SEG_MILES/D10 = SEG_MILES;

CLIENTCODE/A8 = '&&ROLLUP.EVAL';                       
-INCLUDE QRFTRANA

END

TABLEF FILE &&EXTRACT
PRINT CITYPAIR.INTL_DOM AS 'INT_DOM'
      AIRLINE
      AIRLINE_NAME
      CURRENT
      FARE_PAID
      MRK_CNT
      CLIENTCODE AS 'ROLLUP_CODE'  
      NEW_SEG_MILES
      PRIOR
      PYTD
      TRAN
      YEAR
      TRN_DATE
WHERE (TRN_DATE GE '&&FROMDT.EVAL') AND (TRN_DATE LE '&&TODT.EVAL') OR
      (TRN_DATE GE '&&FXDATE.EVAL') AND (TRN_DATE LE '&&TXDATE.EVAL')      
WHERE VOID.VOID_DATE EQ 0
-IF &&INTDOM EQ 'T' THEN GOTO SKIP;
WHERE CITYPAIR.INTL_DOM EQ '&&INTDOM.EVAL'
-SKIP
-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************

ON TABLE HOLD AS DOMCARIP 
END
-RUN


-IF &LINES EQ 0 THEN GOTO NEXTONE;

DEFINE FILE DOMCARIP
               
CMAMT/D12.2  = IF CURRENT EQ 'Y' THEN FARE_PAID ELSE 0;
PMAMT/D12.2  = IF PYTD EQ 'Y' THEN FARE_PAID ELSE 0;
CMCNT/D8  = IF CURRENT EQ 'Y' THEN MRK_CNT ELSE 0;
PMCNT/D8  = IF PYTD EQ 'Y' THEN MRK_CNT ELSE 0;
CMILE/D10  = IF CURRENT EQ 'Y' THEN NEW_SEG_MILES ELSE 0;
PMILE/D10  = IF PYTD EQ 'Y' THEN NEW_SEG_MILES ELSE 0;
AIRLINEN/A12 = LCWORD (12, AIRLINE_NAME, AIRLINEN);
END




TABLE FILE DOMCARIP 
 SUM CMAMT
     PMAMT
     CMCNT
     PMCNT
     CMILE
     PMILE 
 BY ROLLUP_CODE    
 BY AIRLINE
 BY AIRLINEN
ON TABLE HOLD AS DCARIP2
END
-RUN

DEFINE FILE DCARIP2
AIR/A2 = EDIT(AIRLINE, '99$');
AIR_NM/A15 = AIR | '-' | AIRLINEN;
END
TABLE FILE DCARIP2 
 SUM CMAMT
     CMCNT
     CMILE
     PMAMT
     PMCNT
     PMILE
 BY ROLLUP_CODE    
 BY AIRLINE
 BY AIR_NM AS 'AIRLINEN'
WHERE AIRLINE IN FILE TOPCARI
ON TABLE HOLD AS DCARIP3
END
-RUN


TABLE FILE DCARIP3
PRINT ROLLUP_CODE AIRLINEN CMAMT CMCNT CMILE PMAMT PMCNT PMILE
ON TABLE SAVE
END
-RUN

MODIFY FILE QRPCARIR
FIXFORM ROLLUP_CODE/8 AIRLINEN/15 CMAMT/12 CMCNT/8 CMILE/10 PMAMT/12 PMCNT/8 PMILE/10
MATCH ROLLUP_CODE AIRLINEN
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


DEFINE FILE DCARIP2
AIRLINEN/A15 = 'Other';
END
TABLE FILE DCARIP2
 SUM CMAMT
     PMAMT
     CMCNT
     PMCNT
     CMILE
     PMILE 
 BY ROLLUP_CODE    
 BY AIRLINEN
WHERE NOT AIRLINE IN FILE TOPCARI
ON TABLE HOLD AS DCARIP4
END
-RUN

TABLE FILE DCARIP4
PRINT ROLLUP_CODE AIRLINEN CMAMT CMCNT CMILE PMAMT PMCNT PMILE
ON TABLE SAVE
END
-RUN


MODIFY FILE QRPCARIR
FIXFORM ROLLUP_CODE/8 AIRLINEN/15 CMAMT/12 CMCNT/8 CMILE/10 PMAMT/12 PMCNT/8 PMILE/10
MATCH ROLLUP_CODE AIRLINEN
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN


-NEXTONE