-INCLUDE SETECHO
-* SETDONE.FEX - Executed at the end of RUN_SET.FEX to distribute the
-*               Reports.
-*

-DEFAULT &ZSKEY='X';
-DEFAULT &&SET_DONE = 'N';
-DEFAULT &&HFTP='N';
-DEFAULT &&HEMAIL='N';


-**********DETERMINE DATE AND TIME FOR FILE TO SAVE
-SET &NOWTM = HHMMSS ('A8');
-SET &NOWTM = EDIT(&NOWTM,'99$99$99');
-SET &NOWDT = &YYMD;
-SET &NOWDTTM = &NOWDT || '_' || &NOWTM;


-* SKIP ZIPPING CODE IF RUN IS TO BROWSER
-IF EDIT('&&OUTTO.EVAL','$9') EQ 'B' GOTO SKIP_DISTR;

EX CLEANUP
-RUN

-* If the Set is not complete skip the zipping code
-IF &&SET_DONE EQ 'N' GOTO SKIP_DISTR;


-* THIS IF STATEMENT WILL HANDLE THE REPORTS THAT WERE SCHEDULED FOR EMAIL
-* BUT WERE NOT PART OF A REPORT SET SCHEDULE.
-IF &&SETZIP EQ 'N'  AND EDIT(&&OUTTO,'9') EQ 'E' GOTO EMAIL_INST;


-*****************************************************************
-* FTP REPORTS
-*****************************************************************
-IF &&HFTP EQ 'N' GOTO SKIP_FTP;
-*-INCLUDE FTPFILE
-SKIP_FTP



-*****************************************************************
-* EMAIL REPORT SET
-*****************************************************************
-IF &&HEMAIL EQ 'N' GOTO SKIP_DISTR;
-* THIS REPEAT LOOP WILL CHANGE ALL NON-ALPHA-NUMERIC CHARACTERS TO
-* UNDERSCORES FOR THE REPORT SET NAME .  
-SET &RSLENX=&&RS.LENGTH;
-SET &RSLEN=ARGLEN(40, &&RS, 'I3');
-SET &RSLEN=IF &RSLENX LT &RSLEN THEN &RSLENX ELSE &RSLEN;
-SET &IRS=&&RS;
-REPEAT RSETLOOP &RSLEN TIMES;
-SET &RSCHK=CHKFMT(&RSLEN, &IRS, 
- 'XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX', 'I3');
-IF &RSCHK EQ 0 GOTO ENDRSET;
-SET &BADCHAR=SUBSTR(&RSLEN, &IRS, &RSCHK, &RSCHK, 1, 'A1');
-SET &BCDVAL=BYTVAL(&BADCHAR,'I3');
-SET &IRS = CTRAN(&RSLEN, &IRS, &BCDVAL, 122, 'A40');
-RSETLOOP 
-ENDRSET

-SET &GROUPNAME = CTRAN(&RSLEN, &IRS, 122, 95, 'A40') ||
- &NOWDTTM || '.ZIP';
-SET &&USEMPATH = '&&SETMPATH.EVAL';

-INCLUDE MAILFILE

-SET &&SET_DONE = 'N';

-GOTO SKIP_DISTR


-* EMAIL REPORTS THAT WERE NOT SCHEDULED AS A SET
-EMAIL_INST
-*-SET &GROUPNAME = &&IRN ||
-*- &NOWDTTM || '.ZIP';
-*Issue with version 7.7.03
-SET &GROUPNAME = '&&IRN.EVAL' || '&NOWDTTM.EVAL' || '.ZIP';

-SET &&USEMPATH = 
- &&MAILPATH;

-INCLUDE MAILFILE

-SET &&SET_DONE = 'N';

-SKIP_DISTR
