-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File AVGTKT.FEX
-**************************************************************************
-*DATE          NAME            DESCRIPTION
-*7/31/00     IBISTL-KR         ADDED DEFINE FOR NOWTOD AND
-*                              REPLACED &FOCSYSTOD WITH HHMMSS SUBROUTINE  
-*09/25/00    IBISTL-SB         REPLACED &BITMP3 STATEMENT WITHOUT OUTPUT
-*                              DESTINATION FIELD CONCATENATION
-*09/25/00    IBISTL-RJ         CHANGED THE FORMAT OF ON GRAPH STATEMENT
-*                              TO GIF FORMAT
-*09/25/00    IBISTL-SB         CHANGED THE DOS STATE STATEMENT WITH 
-*                              &&WEB_PATH FIELD TO LOCATE THE GIF FILE
-*09/25/00    IBISTL-SB         REPLACED THE IMAGE FIELD IN THE STYLE 
-*                              REPORT WITH THE &&WEB_PATH1 LOCATION
-*09/25/00    IBISTL-SB         ADDED LOGIC FOR THE DRILL-DOWN
-* 3/07/01   TANDT-SS           CHANGE ALL NUMERIC FORMATS FROM P (PACKED)
-*                              D (FLOATING-POINT DOUBLE PRECISION) TO 
-*                              CONSISTENCY IN ROUNDING AND PERCENTAGES.
-*07/09/01    TANDT-SAC         CHANGE SO THAT THE GRAPH DISPLAYS IN CORRECT
-*                              ORDER 
-*                              TESTED STRANGE QUARTER (NORSTAN), NO DATA
-*                              AND NO DATA MIDDLE (CETAC-R)
-*04/03/02     TANDT-SAC         CHANGED TIMER DEFINE TO MULTIPLY BY 20 (SORT)
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-**************************************************************************
-INCLUDE SETECHO
-RUN

-SET &&RPTSUF = 'AVGTKT';
-INCLUDE FDEFRPTS
-RUN

DEFINE FILE TSSHOLD
FARE_PAID/D12.2=SEG_AMT + SEG_TAX ;

CYRAMT/D12.2S = IF TRN_DATE GE &&CYFMDT.EVAL AND 
                 TRN_DATE LE &&CYTODT.EVAL 
                 THEN FARE_PAID ELSE 0;
 
PYRAMT/D12.2S = IF TRN_DATE GE &&PYFMDT.EVAL AND
                 TRN_DATE LE &&PYTODT.EVAL
                 THEN FARE_PAID ELSE 0;

NET_TKT_CNT/D8CS    =   IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  
  CYRTKT/D8CS = IF TRN_DATE GE &&CYFMDT.EVAL AND 
                   TRN_DATE LE &&CYTODT.EVAL 
                   THEN NET_TKT_CNT ELSE 0;

  PYRTKT/D8CS = IF TRN_DATE GE &&PYFMDT.EVAL AND
                    TRN_DATE LE &&PYTODT.EVAL
                    THEN NET_TKT_CNT ELSE 0;
  TKT_MON/MT = TRN_DATE;

  BITMAP/A12 WITH SEG_AMT='AVGTKT.SVG';
  CMO/YYMD = &&CYFMDT.EVAL;
  TMO/YYMD = &&CYTODT.EVAL;
  BEGD/I4YM = CMO;
  ENDD/I4YM = TMO;
  NOMONTHS/I3 = YM (BEGD, ENDD, 'I3') + 1;
  CFD/M = CMO;
  CMONTH/I2 = CFD;
  TD/M = TRN_DATE;
  TMONTH/I2 = TD;
  TIMER/I2 = IF (TD GE CFD) THEN ((TMONTH - CMONTH) + 1) ELSE
             IF (TD LT CFD) THEN (TMONTH * 20) ELSE 0;
END
-RUN

TABLE FILE TSSHOLD
SUM CYRAMT PYRAMT
    CYRTKT PYRTKT
    BITMAP
BY TIMER
BY TKT_MON
BY CMONTH
BY NOMONTHS
BY BEGD
BY ENDD
BY CMO
BY TMO
ON TABLE HOLD AS HOLD1 FORMAT ALPHA
END
-RUN

-IF &RECORDS EQ 0 GOTO SKIP_GRAPH;

DEFINE FILE HOLD1
CAVGCOST/D9.2= IF ((CYRTKT LT 1) AND (CYRTKT GT (-1))) THEN 0 ELSE
                  (CYRAMT/CYRTKT);
PAVGCOST/D9.2= IF ((PYRTKT LT 1) AND (PYRTKT GT (-1))) THEN 0 ELSE
                  (PYRAMT/PYRTKT);
END
-RUN

-READ HOLD1 &&TIMER.A2. &&TKT_MON.A2. &&CMONTH.A2. &&MAX.A3.
-RUN

GRAPH FILE HOLD1
SUM CAVGCOST AS 'CURRENT'
    PAVGCOST AS 'PRIOR' 
ACROSS TKT_MON AS ' ' COLUMNS &&CMONTH

-SET &CTR = 2;
-LOOP1
-IF &CTR GT &&MAX GOTO NOMORE;
AND
-SET &MO = IF (&&CMONTH + &CTR - 1) GT 12 THEN
- (&&CMONTH + &CTR - 13) ELSE
- (&&CMONTH + &CTR - 1);
&MO
-SET &CTR = &CTR + 1;
-GOTO LOOP1
-NOMORE
ON GRAPH SAVE AS AVGTKT FORMAT SVG
ON GRAPH SET GRAPHSTYLE *
setGraphType(17);
setLegendDisplay(true);
setLegendPosition(2);
setFontSizeAbsolute(getLegendText(),true);
setAutofit(getLegendText(),false);
setFontSize(getLegendText(),15);
setFontStyle(getLegendText(),0);
setLegendMarkerPosition(1);
setO1LabelDisplay(true);
setFontSizeAbsolute(getO1Label(),true); 
setAutofit(getO1Label(),false);
setFontStyle(getO1Label(),0);
setFontSize(getO1Label(),15);
setO1LabelRotate(0);
setO1MajorGridDisplay(false);
setO1MinorGridDisplay(true);
setFontSizeAbsolute(getY1Label(),true);
setAutofit(getY1Label(),false);
setY1LabelDisplay(true);
setY1LabelFormat(1);
setFontSize(getY1Label(),15);
setFontStyle(getY1Label(),0);
setY1AxisSide(0);
setTitleString(" ");
setSubtitleString(" ");
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 115 160 247));
ENDSTYLE 
END
-RUN
-TYPE "AFTER GRAPH"
-SKIP_GRAPH
-RUN

-IF &LINES GT 0 GOTO THIS;
-IF &LINES EQ 0 GOTO THAT;
-THIS
-IF &&OUTFMT NE 'PDF' THEN GOTO THAT;
-SET &THERE = 'THERE';
-RUN
-GOTO WHERE2
-THAT
-SET &THERE = 'NOT THERE';
-RUN
-WHERE2
-INCLUDE DTRANGE
-RUN


DEFINE FILE HOLD1
BITMAP/A12 WITH CYRAMT = 'AVGTKT.SVG';
-**************************************************************************
-*DATE          NAME            DESCRIPTION
-*7/31/00     IBISTL-KR         ADDED DEFINE FOR NOWTOD
-**************************************************************************
NOWTOD/A8 WITH TKT_MON = HHMMSS(NOWTOD);
TKT_MONX/I2=TKT_MON;
MON_DESC/A3=DECODE TKT_MONX(1 'JAN' 2 'FEB' 3 'MAR' 4 'APR'
                    5 'MAY' 6 'JUN' 7 'JUL' 8 'AUG'
                   9 'SEP' 10 'OCT' 11 'NOV' 12 'DEC' ELSE 'XXX');
END
-RUN


TABLE FILE HOLD1
ON TABLE SET PAGE-NUM OFF
SUM CYRAMT/D12S NOPRINT CYRTKT NOPRINT     
    PYRAMT/D12S NOPRINT PYRTKT NOPRINT
    BITMAP NOPRINT
    AND COMPUTE
    CYR/D9.2S= IF ((CYRTKT LT 1) AND (CYRTKT GT (-1))) THEN 0 ELSE
               (CYRAMT/CYRTKT); AS 'CURRENT'
    AND COMPUTE 
    PYR/D9.2S= IF ((PYRTKT LT 1) AND (PYRTKT GT (-1))) THEN 0 ELSE
               (PYRAMT/PYRTKT); AS 'PRIOR'
BY TIMER NOPRINT
BY TKT_MON NOPRINT
BY MON_DESC AS ''
ON MON_DESC RECOMPUTE MULTILINES AS ''
&&OUTLINE1
&&OUTLINE2
HEADING
"</5"
"<33 AVERAGE TICKET COST"
"<15 &&LVL_DSC </1"
"<31 Current: <37 &CYFMM/&CYFMD/&CYFMY - &CYTOM/&CYTOD/&CYTOY"
"<31   Prior: <37 &PYFMM/&PYFMD/&PYFMY - &PYTOM/&PYTOD/&PYTOY"
"</3"
-**************************************************************************
-*DATE          NAME            DESCRIPTION
-*7/31/00     IBISTL-KR         REPLACED &FOCSYSTOD WITH NOWTOD
-**************************************************************************
FOOTING BOTTOM
"&&FOOTER <41 PAGE 4"
"Average ticket cost includes tax
<41 AVGTKT <65 &&FOOTR"
"A REPORT FROM eTTek Review RUN ON &DATE AT <NOWTOD"
-* "&&CRIGHT"
ON TABLE SET STYLE *
-*************** DRILLDOWN-BEGIN **********************
-*TYPE=DATA, COLUMN=N8, \
TYPE=DATA, COLUMN=N9, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRAVGTKT' MONTH1=N2 FLAGPARM='CP' \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
-*TYPE=DATA, COLUMN=N9, \
TYPE=DATA, COLUMN=N10, \
URL=&&SRVR.EVAL/ibi_apps/WFServlet? \
(IBIF_ex='DRAVGTKT' MONTH1=N2 FLAGPARM='PP' \
SETFNM='&&DTTMSTRM.EVAL' \
FPARM='&&DTTMID.EVAL' \
ROLUP='&&ROLLUP' user='&&UNAME'), $
-*********************** DRILLDOWN-END **********************
 
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.60000, RIGHTMARGIN=0.250000, 
    TOPMARGIN=0.50000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT, FOCEXEC=NONE, FONT=TIMES NEW ROMAN, SIZE=9, COLOR=BLACK, 
    BACKCOLOR=NONE, STYLE=NORMAL, $
-IF &THERE EQ 'NOT THERE' GOTO SKIP_IT;
-***********************************************************************
-*09/25/00    IBISTL-SB         REPLACED THE IMAGE FIELD IN THE STYLE 
-*                              REPORT WITH THE &&WEB_PATH1 LOCATION
-***********************************************************************
TYPE=REPORT, 
 IMAGE=AVGTKT.SVG,
    POSITION=(+3.8 +2.0), SIZE=(5 4), $
-SKIP_IT
TYPE=TITLE, SIZE=10, STYLE=BOLD, $
TYPE=HEADING, LINE=7, SIZE=13, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=8, SIZE=13, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=10, OBJECT=TEXT, ITEM=1, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=10, OBJECT=TEXT, ITEM=2, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=11, OBJECT=TEXT, ITEM=1, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=HEADING, LINE=11, OBJECT=TEXT, ITEM=2, FONT=COURIER NEW,
    STYLE=BOLD+ITALIC, $
TYPE=REPORT, COLUMN=N7, POSITION=+1.0000, $
TYPE=REPORT, COLUMN=N8, POSITION=+1.0000, $
TYPE=GRANDTOTAL, STYLE=BOLD, $
TYPE=SUBTOTAL, STYLE=BOLD, $
TYPE=FOOTING, SIZE=7, $
TYPE=REPORT, IMAGE=&&TNTPIC,  POSITION=(&&TNTSMPOS), SIZE=(&&TNTSMSIZ), $
TYPE=REPORT, IMAGE=&&ROLLPIC, POSITION=(&&ROLSMPOS), SIZE=(&&ROLSMSIZ), $
ENDSTYLE
END
-RUN

