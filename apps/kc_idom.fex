-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*  02/02/2015  REJ  S-05681 Changes to the no data report to prepare for the Available
-*                           Report changes.  Rollup Code is being removed from BRPTINST in
-*                           ER5, so I'm removing the reference to the ROLLUP_CODE in that report.
-INCLUDE SETECHO
SET ASNAMES=ON
SET ALL=OFF
-* File KCINTL_DOM.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*11/5/04        JK            HOLD FILE NAME CHANGED IN VIEW REPORT WAS
-*                             ERRORING OUT.  FIXED TO USE CORRECT
-*                             HOLD FILE
-*4/25/2017-JEM-S-33318-updated logic for no records report

-**********************************************************************
EX KCDBLS SETFILE=&&SETF
-RUN
-INCLUDE OTHPARMS

-TYPE &&OUTPUTDEST
-IF &&OUTLINE2 EQ 'OFFLINE' THEN GOTO XOFFLINE ELSE GOTO XONLINE;

-XONLINE
-TYPE Before ONLINE
ONLINE
-TYPE After ONLINE
-RUN
-GOTO CONT

-XOFFLINE
-SET &&OUTLINE2 = '';
-TYPE Before OFFLINE
OFFLINE
-TYPE After OFFLINE
-RUN
-GOTO CONT

-CONT
-SET &&RPT_HOLD = 'RPTHOLD';

FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-INCLUDE SELCATQT
-RUN

-*EX CATQTR
EX &&CATQTRPR
-RUN

FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN

EX AIRUSE
-RUN

EX &&EXPARM
-RUN

-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW=&&EXPARM;
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-IF &RECORDS GT 0 THEN GOTO DOIT;

-NORECORDS
-IF &&FROMAVRP NE 'Y' GOTO :AVSF2;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF2

-INCLUDE NORECRPT
-RUN

-GOTO XXIT

-DOIT

DEFINE FILE MKTSHR1
  AIRSELECT/A3 = IF ('&&AIR1' EQ AIR_LINE) THEN AIR_LINE ELSE '0';
  LABEL1/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDO ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMO ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDO ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMO ELSE ' ';

 LABEL2/A25 = IF '&&ORGDST' EQ 'DIR_CITY_CD' THEN DIR_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CITY_NM' THEN DIR_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_CD' THEN DIR_APT_CDD ELSE
               IF '&&ORGDST' EQ 'DIR_CP_NM' THEN DIR_APT_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_CD' THEN ND_CTY_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CITY_NM' THEN ND_CTY_NMD ELSE
               IF '&&ORGDST' EQ 'ND_CP_CD' THEN ND_APT_CDD ELSE
               IF '&&ORGDST' EQ 'ND_CP_NM' THEN ND_APT_NMD ELSE ' ';
END
-RUN

TABLE FILE MKTSHR1
SUM COMM_SEG AS 'TCOMM'
    CP_FARE_PD AS 'FARE'
    MILES
    NET_COMM AS 'NCOMM'
    NET_TKT_CNT AS 'TICK_SEG'
    TOTAL_FARE AS 'TFARE'
BY AIRSELECT
BY AIRLINE_NAME
BY ORGDST
BY LABEL1
BY LABEL2
WHERE CP_AIRLINE EQ '&&AIR1'
ON TABLE HOLD AS CPHOLD1
END
-RUN

TABLE FILE CPHOLD1
PRINT AIRLINE_NAME
      AIRSELECT
      FARE
      MILES
      NCOMM
      TCOMM
      TFARE
      TICK_SEG
-*RANKED BY HIGHEST TICK_SEG
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD AS CPHOLD2
END
-RUN

TABLE FILE MKTSHR1
SUM COMM_SEG AS 'ACOMM'
    CP_FARE_PD AS 'AFARE'
    NET_COMM AS 'ANET'
    NET_TKT_CNT AS 'ATICK_SEG'
    TOTAL_FARE AS 'ATFARE'
BY ORGDST
BY LABEL1
BY LABEL2
WHERE CP_AIRLINE NE '&&AIR1'
ON TABLE HOLD AS HOLD1
END
-RUN

TABLE FILE HOLD1
SUM AFARE
    ANET ACOMM
    ATFARE
    ATICK_SEG
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD AS HOLD2
END
-RUN

MATCH FILE CPHOLD2
PRINT *
BY ORGDST
BY LABEL1
BY LABEL2
ON TABLE HOLD
RUN
FILE HOLD2
PRINT *
BY ORGDST
BY LABEL1
BY LABEL2
AFTER MATCH HOLD AS HOLD3 OLD-OR-NEW
END
-RUN

DEFINE FILE HOLD3
  BASE_FARE/D12.2CS = AFARE + FARE;
  FARE_PAID/D12.2CS = ATFARE + TFARE;
  NET_FARE/D12.2CS = ANET + NCOMM;
  NET_MKTS/D8CS = ATICK_SEG + TICK_SEG;
  TOTAL_COMM/D12.2 = ACOMM + TCOMM;
  
  ODTEST/A50=IF '&&CTYPR' EQ 'ALL' THEN ORGDST ELSE
             IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
             ELSE ORGDST;
  SUBTOT/A1=IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN '1' ELSE ' ';
  LBL1/A25= IF '&&CTYPR' EQ 'ALL' THEN LABEL1 ELSE
            IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
            ELSE LABEL1;
  LBL2/A25= IF '&&CTYPR' EQ 'ALL' THEN LABEL2 ELSE
            IF TICK_SEG EQ 0 AND FARE EQ 0 THEN 'ALL OTHER NOT IN MKT'
            ELSE LABEL2;   

END
-RUN

-* ************************************************
-* Start of new files to correct ranking problem.
-* ************************************************
TABLE FILE HOLD3
PRINT
  ACOMM
  AFARE
  ANET
  ATFARE
  ATICK_SEG
  BASE_FARE
  FARE
  FARE_PAID
  MILES
  NCOMM
  NET_FARE
  NET_MKTS
  TCOMM
  TFARE
  TICK_SEG
  TOTAL_COMM
RANKED BY &&RANK_ORDER &&RANK_METH
BY SUBTOT
BY ODTEST
BY LBL1
BY LBL2
WHERE ODTEST NE 'ALL OTHER NOT IN MKT'
ON TABLE HOLD AS HOLD4R
END
-RUN

TABLE FILE HOLD4R
PRINT *
BY RANK
BY TICK_SEG
BY SUBTOT
BY ODTEST
BY LBL1
BY LBL2
ON TABLE HOLD AS HOLD4 FORMAT FOCUS
END
-RUN

DEFINE FILE HOLD3    ADD
 RANK/I7 = 99999;
END
-RUN

TABLE FILE HOLD3
PRINT
  ACOMM 
  AFARE 
  ANET
  ATFARE    
  ATICK_SEG 
  BASE_FARE
  FARE
  FARE_PAID
  MILES
  NCOMM
  NET_FARE
  NET_MKTS  
  TCOMM
  TFARE 
  TICK_SEG
  TOTAL_COMM    
BY RANK
BY TICK_SEG
BY SUBTOT
BY ODTEST
BY LBL1
BY LBL2
WHERE ODTEST EQ 'ALL OTHER NOT IN MKT'
ON TABLE HOLD AS HOLD4S 
END
-RUN

TABLE FILE HOLD4S
PRINT *
BY RANK
BY TICK_SEG
BY SUBTOT
BY ODTEST
BY LBL1
BY LBL2
ON TABLE HOLD AS HOLD4S2 FORMAT FOCUS
END
-RUN

-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &HRANK=&HLDPATH || 'HOLD4.FOC';
-SET &HNORANK=&HLDPATH || 'HOLD4S2.FOC';
-RUN

USE CLEAR *
-RUN

EX SUUSES
-RUN

USE ADD 
&HRANK AS HOLD4
&HNORANK AS HOLD4
END
-RUN

-* **********************************
-* End of new additions
-* **********************************

-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN

DEFINE FILE HOLD4
  DRANK/D9 = RANK;
  ARANKX/A11 = FTOA(DRANK, '(D9)', 'A11');
  ARANK/A9 = EDIT(ARANKX,'$$999999999');
  LIST/D9 = LAST LIST + 1;
  CRANKX/A11 = FTOA(LIST, '(D9)', 'A11');
  CRANK/A9 = EDIT(CRANKX,'$$999999999');
  RANK_VALUE/A20 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN
                   'ALL OTHER NOT IN MKT' ELSE
                   IF &&RANK_LIMIT EQ 0 THEN ARANK ELSE
                   IF LIST GT &&RANK_LIMIT THEN 'ALL OTHER IN MKT'
                   ELSE CRANK;
  RANK_LABEL/A50 = IF ODTEST EQ 'ALL OTHER NOT IN MKT' THEN ' '
                   ELSE IF RANK_VALUE NE 'ALL OTHER IN MKT' THEN
                   ODTEST ELSE ' ';
    



-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH SUBTOT = HHMMSS (NOWTOD);
END
TABLE FILE HOLD4
-INCLUDE HEADER
"&&SUBHEAD </1"
   &&S_SUBJ1
   &&S_SUBJ2
   &&S_SUBJ3
   &&S_SUBJ4
   &&S_SUBJ5
   &&S_SUBJ6
   &&S_SUBJ7
   &&S_SUBJ8
   &&S_SUBJ9
   &&S_SUBJ10
   &&S_SUBJ11
   &&S_SUBJ12
   &&S_SUBJ13
   &&S_SUBJ14
   &&S_SUBJ15
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
-INCLUDE FOOTERSM
-*ON TABLE SET STYLE &&SMSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&SMSTY
ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-* REJ ADDED TO TRAP ERRORS
-SET &&FEXNOW='KC_IDOM';
-INCLUDE ERRORCHK
-IF &&RET GT 1 GOTO XXIT;
-* REJ ADDED TO TRAP ERRORS

-XXIT
