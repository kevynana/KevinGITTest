SET ASNAMES = ON;

-INCLUDE SETECHO

-DEFAULT &FY = 01;
-SET &&ROLLFY = &FY;
-SET &&ROLLFY = '01';
-SET &&FROMDT = '2017/01/01';
-SET &&TODT =   '2017/06/30';

-SET &&FXDATE = '2016/01/01';
-SET &&TXDATE = '2016/12/31';

-SET &&PYTDDATE = '2016/12/31';
-SET &&PQFMDT   = '2016/04/01';
-SET &&PQTODT   = '2016/06/30';
-SET &&CQFMDT   = '2016/04/01';
-SET &&CQTODT   = '2016/06/30';

-SET &&FYFLAG = '0';
-SET &&IQTR = 2;

-INCLUDE SETECHO

DEFINE FILE TOPAZ
  FROM_MYR/A8=GETTOK(PERIOD, 18, 1, '-', 8, FROM_MYR);
  TO_MYR/A8  =GETTOK(PERIOD, 18, -1, '-', 8, TO_MYR);

 
  TOYRX/A4 = GETTOK(TO_MYR,8, -1, ' ', 4, TOYRX);   
  TOYR/A4=IF TOYRX CONTAINS '20' THEN TOYRX ELSE EDIT(TO_MYR, '$$$$9999');
   
  FMMTH/A3 = EDIT(FROM_MYR, '999');  
  FMYR/A4=IF FROM_MYR CONTAINS '20' THEN EDIT(FROM_MYR,'$$$$9999')
    ELSE EDIT(TO_MYR,'$$$$9999');
   
  TOMTH/A3 = EDIT(TO_MYR,'999');   
  MTH1/A2=DECODE FMMTH ('Jan' '01' 'Feb' '02' 'Mar' '03' 'Apr' '04' 'May' '05' 
   'Jun' '06' 'Jul' 07 'Aug' '08' 'Sep' '09' 'Oct' '10' 'Nov' '11' 'Dec' '12');
   MTH2/A2=DECODE TOMTH ('Jan' '01' 'Feb' '02' 'Mar' '03' 'Apr' '04' 'May' '05' 
   'Jun' '06' 'Jul' 07 'Aug' '08' 'Sep' '09' 'Oct' '10' 'Nov' '11' 'Dec' '12');
  
-*Calculate FromData and ToDate  
  AFROMDATE/A6YYM = FMYR | MTH1;
  ATODATE/A6YYM   = TOYR | MTH2; 
  FROMDATE/YYMD = AFROMDATE;
  TODATE1/YYM   = ATODATE;
  TODATE2/YYM = TODATE1 + 1;
  TODATE3/YYMD = TODATE2;
  TODATE/YYMD = TODATE3 - 1;
     
  CURRQ/Q = FROMDATE;
  CY/Y = FROMDATE; 
  CM/M = FROMDATE;
  FM/M = &&ROLLFY;
  DM/I2 = CM - FM;
   
  THEYR/I1 = IF (FROMDATE GE '&&FROMDT.EVAL') AND (FROMDATE LE '&&TODT.EVAL') THEN 2
     ELSE IF (FROMDATE GE '&&FXDATE.EVAL') AND (FROMDATE LE '&&TXDATE.EVAL') THEN 1;
 
  TRANYR/YY= FROMDATE;
  AYEAR/A4YY = TRANYR;
 

-*first year as fiscal year label
  FMDATE1/YYMD='&&FXDATE.EVAL';
  FMDATE2/YYMD='&&FROMDT.EVAL';
  FYYR1/Y=FMDATE1;
  FYYR2/Y=FMDATE2;
 
 -*second year as fiscal year label
  Y2DATE/YYMD = DATEADD(FMDATE2, 'Y', 1);
  FYYR3/Y=Y2DATE;
 
  FY_YR/A4= IF '&&FYFLAG.EVAL' EQ '1' AND THEYR EQ 1 THEN 'FY' | EDIT(FYYR1) 
   ELSE IF '&&FYFLAG.EVAL' EQ '1' AND THEYR EQ 2 THEN 'FY' | EDIT(FYYR2) 
   ELSE IF '&&FYFLAG.EVAL' EQ '2' AND THEYR EQ 1 THEN 'FY' | EDIT(FYYR2) 
   ELSE IF '&&FYFLAG.EVAL' EQ '2' AND THEYR EQ 2 THEN 'FY' | EDIT(FYYR3) 
   ELSE AYEAR;
 
  YEAR/A4 = IF FM EQ 01 THEN AYEAR ELSE FY_YR;
   
  FTRAN1/A2 = IF FM EQ 01 THEN 'Q' | EDIT(CURRQ)  
   ELSE IF (DM GE 9 OR (DM GE -3 AND DM LE -1)) THEN 'Q4'
   ELSE IF (DM GE 6 OR (DM GE -6 AND DM LE -4)) THEN 'Q3' 
   ELSE IF (DM GE 3 OR (DM GE -9 AND DM LE -7))  THEN 'Q2'
   ELSE IF DM LE 0 AND FM GE 11 THEN 'Q1' 
   ELSE 'Q1';    
   
  FTRAN/A6 = IF FM EQ 01 THEN FTRAN1 | ' ' | EDIT(CY)
   ELSE FTRAN1 | FY_YR;
   
  TRAN/A7 = IF THEYR EQ 1 THEN ' ' | FTRAN ELSE FTRAN;

  FY/A1 = IF ('&&ROLLFY.EVAL' EQ '01' OR '04' OR '07' OR '10') AND (MTH1 EQ '01' OR '04' OR '07' OR '10') THEN 'Y'
      ELSE IF ('&&ROLLFY.EVAL' EQ '02' OR '05' OR '08' OR '11') AND (MTH1 EQ '02' OR '05' OR '08' OR '11') THEN 'Y'
      ELSE IF ('&&ROLLFY.EVAL' EQ '03' OR '06' OR '09' OR '12') AND (MTH1 EQ '03' OR '06' OR '09' OR '12') THEN 'Y'
      ELSE 'N';
  ROLLUP_CODE/A8 = 'TOPAZ-R';
  
-*Current: current YTD; PRIOR: prior year; PYTD: prior YTD flag; PQ: prior quarter; CQ: current quarter
  CURRENT/A1 = IF FROMDATE GE '&&FROMDT.EVAL'  AND TODATE LE '&&TODT.EVAL'     THEN 'Y' ELSE 'N';
  PRIOR/A1   = IF FROMDATE GE '&&FXDATE.EVAL'  AND TODATE LE '&&TXDATE.EVAL'   THEN 'Y' ELSE 'N'; 
  PYTD/A1    = IF FROMDATE GE '&&FXDATE.EVAL'  AND TODATE LE '&&PYTDDATE.EVAL' THEN 'Y' ELSE 'N';
  PQ/A1      = IF FROMDATE GE '&&PQFMDT.EVAL'  AND TODATE LE '&&PQTODT.EVAL'   THEN 'Y' ELSE 'N';	
  CQ/A1      = IF FROMDATE GE '&&CQFMDT.EVAL'  AND TODATE LE '&&CQTODT.EVAL'   THEN 'Y' ELSE 'N';	
  
  FIRST3/A3 WITH PERIOD=EDIT(PERIOD,'999');

  FIRST3X/A10 =FIRST3 | TRAN;

-* DATATYPE/I2 = IF FIRST3 EQ LAST FIRST3 THEN DATATYPE+1 ELSE 1;
  DATATYPE/I2 = IF FIRST3X EQ LAST FIRST3X THEN DATATYPE+1 ELSE 1;

  ISYTD/A1 = IF FROMDATE EQ '&&FROMDT.EVAL' OR FROMDATE EQ '&&FXDATE.EVAL' THEN 'Y' ELSE 'N';

  
END

-*Retrieve YTD data
TABLE FILE TOPAZ
PRINT DPRICE  IPRICE  
-*PRINT * TRAN FIRST3 DATATYPE ISYTD
BY YEAR 
WHERE DATATYPE EQ &&IQTR
WHERE FY EQ 'Y'
WHERE (FROMDATE GE '&&FROMDT.EVAL') AND (TODATE LE '&&TODT.EVAL') OR
      (FROMDATE GE '&&FXDATE.EVAL') AND (TODATE LE '&&TXDATE.EVAL')
WHERE ISYTD EQ 'Y'
WHERE WORKSHEET EQ 'QTR'
ON TABLE SAVE AS TOPAZYTD
END
-RUN


-SET &CNT = 0;
-REPEAT TOPAZYTD2 &LINES TIMES
-READ TOPAZYTD NOCLOSE &YEAR.4. &T1AVGD.6. &T1AVGI.6. 
-SET &CNT = &CNT + 1;
-SET &YEAR.&CNT = &YEAR;
-SET &T1AVGD.&CNT = &T1AVGD;
-SET &T1AVGI.&CNT = &T1AVGI;
-*-SET &T1AVGC.&CNT = &T1AVGC;
-SET &T1AVGDA.&CNT = FTOA(&T1AVGD, '(D6M)', 'A8');
-SET &T1AVGIA.&CNT = FTOA(&T1AVGI, '(D6M)', 'A8');
-TOPAZYTD2
-CLOSE TOPAZYTD

-*Retrieve Quarter data
TABLE FILE TOPAZ
PRINT DPRICE  
      IPRICE  
      CPRICE  
CURRENT PRIOR PYTD PQ CQ    
BY ROLLUP_CODE 
BY YEAR
BY TRAN
WHERE FY EQ 'Y'
WHERE DATATYPE EQ 1
WHERE (FROMDATE GE '&&FROMDT.EVAL') AND (TODATE LE '&&TODT.EVAL') OR
      (FROMDATE GE '&&FXDATE.EVAL') AND (TODATE LE '&&TXDATE.EVAL')
WHERE WORKSHEET EQ 'QTR'      
ON TABLE SAVE
END
-RUN

 


MODIFY FILE QRATP2
LOG DUPL MSG OFF
FIXFORM ROLLUP_CODE/8 YEAR/4 TRAN/7 AVGD/6 AVGI/6 AVGC/6 CURRENT/1 PRIOR/1 PYTD/1 PQ/1 CQ/1
MATCH ROLLUP_CODE YEAR TRAN
ON NOMATCH INCLUDE
DATA ON SAVE
END
-RUN

-QUIT
