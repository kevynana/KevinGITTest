-* SETHOTELDB.FEX

-SET &HTL_DB = 'H' | '&QTRNO.EVAL' || '&COMP.EVAL' ;
-TYPE FIND HOTEL DB: &HTL_DB 

-*backup the database to be updated to d:\lusheng\backups\data\
-SET &BACKUPPATH = 'd:\lusheng\backups\data\';
-SET &SRC = 'D:\TNT\TTRACKER\DATA\' | '&HTL_DB.EVAL' || '.FOC';
-SET &DST = '&BACKUPPATH.EVAL' | '&HTL_DB.EVAL' || '.FOC';
-DOS COPY &SRC &DST
-RUN


TABLE FILE PrefChainAccts
-*PRINT CHAIN_CODE START_DATE END_DATE ROLLUP_CODE
-*BY ROLLUP_CODE 
PRINT START_DATE END_DATE ROLLUP_CODE
BY CHAIN_CODE
WHERE ROLLUP_CODE EQ '&ROLLUP.EVAL'
ON TABLE HOLD AS HOLDPFCA FORMAT FOCUS INDEX CHAIN_CODE
END
-RUN

JOIN CLEAR *
JOIN CHAIN_CODE IN &HTL_DB TO UNIQUE CHAIN_CODE IN HOLDPFCA AS J1
END

TABLE FILE &HTL_DB
-*PRINT HTL_MAIN.CHAIN_CODE CHKIN_DATE HOLDPFCA.START_DATE HOLDPFCA.END_DATE CLIENT_PREF
-*BY ROLLUP_CODE
PRINT HTL_MAIN.CHAIN_CODE CLIENT_PREF
BY HTL_KEY
WHERE CHKIN_DATE IS-FROM HOLDPFCA.START_DATE TO HOLDPFCA.END_DATE
WHERE CLIENT_PREF EQ ''
ON TABLE HOLD
END
-RUN

-IF &LINES EQ 0 THEN GOTO SKIPUPDATE;

MODIFY FILE &HTL_DB
FIXFORM FROM HOLD
COMPUTE CLIENT_PREF = 'P';
MATCH HTL_KEY
  ON MATCH UPDATE CLIENT_PREF
  ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-SKIPUPDATE

-* Locate the hotel databases based on the rollup codes in your excel file, and then if the CHAIN_CODE in 
-* HR_50 matches the CHAIN_CODE field in your excel file for that rollup code, and if the field CHKIN_DATE 
-* in HR_50 falls within Effective Date and End Date in your excel file, I should update CLIENT_PREF in 
-* HR_50 to have value of "P" if it was blank.  If it was not blank, the value in CLIENT_PREF should not be changed.
-*
