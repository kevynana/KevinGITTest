-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-*7/26/04    JOY                Added/Changed Norecords logic
-*2/5/14 Updated the bookings define to use num_days instead of daily_rate - JK
-*  05/27/2016  REJ  S-19665 Changes for Available Reports.  Adding code so that the
-*                           program will run in current Review and in Available Reports.
-* 02/24/2017 - DLV - S-22486 CHANGED ettek Review to Post-trip Reporting
-* 05/18/17 JEM S-34624 - Updating heading/stylesheet to standardize
-* 07/12/2017 - RSB - S-29908 - Webfocus Upgrade - Reports that do not run in ER5 Sandbox
 
-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-DEFAULT &&CPFLAG = 'XX';
-DEFAULT &&CARC   = 'XX';
-DEFAULT &&VALUERANK   = 'XXXXXX';
 
-SET &&CPFLAG = &FLAGPARM;
-SET &&CARC   = &ROW_LABEL; 
-SET &&VALUERANK = &VALUERANK;
 
-**********************************************************************
-* EX RPTRESET TO RESET PARAMS
-**********************************************************************
-SET &&ROLLUP = &ROLUP;
-* -SET &&UNAME = &UID;
-SET &&UNAME = &user;
-**** the following two lines added 11/03/2003 ****
-DEFAULT &FPARM = ' ';
-SET &&FIPARM = &FPARM;
-SET &&DRILLED_RUN = 'Y';
 
EX RPTRESET
-RUN
-*
EX RESET
-RUN
 
-**********************************************************************
-* CHECK FOR PRESENCE OF ORIGINAL SET FILE
-*********************************************************************
-*****  USE VALUE PASSED AS &SETFNM TO DETERMINE FILE NAME
-IF &&DEPLOY EQ 'W' GOTO SETWEB;
-SET &&LOCPATH = '&&DATASRV.EVAL' || '\TT\ '|| &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&LOCPATH || &SETFNM || '.FTM';
-GOTO CHKFILE;
 
-SETWEB
-SET &&WEBPATH = '&&WEBSRV.EVAL' || '\TTWEB\ ' || &&ROLLUP || '\ '||&&UNAME.EVAL||'\';
-SET &FILEXIST = &&WEBPATH || &SETFNM || '.FTM';
 
-CHKFILE
STATE &FILEXIST
-RUN
-IF &RETCODE NE 0 GOTO NOFILE;
-GOTO CONT;
 
 
-*
-*
-**********************************************************************
-*
-**********************************************************************
-CONT
 
-SET &TMPDRIL = &&WEB_PATH || 'TEMPDRIL.FEX' ;
COPY &FILEXIST &TMPDRIL
-SET &&SETF='TEMPDRIL';
-RUN
 
EX OVSETDB SETFILE=&&SETF
-RUN
EX CNTLUPD
-RUN
 
-* REJ added for er5 
-IF &&FROMAVRP NE 'Y' GOTO :AVRP1;
APP PREPENDPATH REVIEW_50_SHARED REVIEW_50_AVRP ttapps_sql
-RUN
-INCLUDE GetForDrillDown
-RUN
-:AVRP1
 
-* -INCLUDE GetForDrillDown
-* -RUN
-INCLUDE OTHPARMS
 
-*****************************************************************
-*DATE           NAME                 DESCRIPTION OF CHANGE
-*9/13/00      IBISTL-KR              Drill Down functionality
-*****************************************************************
-SET &&RPTTYP = ' ';
-SET &&SMD = 'S';
EX TS_JAMM2
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :DDTA1;
-SET &&RPTGRP_OVERRIDE='CAR';
EX CODEGEN
-RUN
-:DDTA1
 
-INCLUDE SELCATQT
-RUN
 
 
-SET &&OUTLINE1 = 'ON TABLE SET ONLINE-FMT';
-SET &&OUTLINE2 = IF &&OUTLINE2 CONTAINS 'PDF' THEN 'PDF' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXCEL' THEN 'EXCEL' ELSE
-          IF &&OUTLINE2 CONTAINS 'EXL2K' THEN 'EXL2K' ELSE 'PDF';
 
 
-****
-* &&CPFLAG WILL EQUAL ONE OF THE FOLLOWING:
-* CY (CURRENT YEAR),  PY (PRIOR YEAR)
 
-****
-* &&FIELD WILL EQUAL ONE OF THE FIELD NAMES WITHIN THE REPORT
-****
 
-IF &&CPFLAG EQ 'CY' THEN GOTO CURYEAR
-  ELSE IF &&CPFLAG EQ 'PY' THEN GOTO PRYEAR
-  ELSE GOTO CURYEAR;
 
-CURYEAR
-SET &&DCHK='WHERE INV_DT GE '&&FROMDT.EVAL' AND INV_DT LE '&&TODT.EVAL'';
-SET &&FROMDT = &&FROMDT;
-SET &&TODT = &&TODT;
-SET &&SUBHEAD = '<NAMEC';
-GOTO ENDDATE
 
-PRYEAR
-SET &&DCHK='WHERE INV_DT GE '&&FXDATE.EVAL' AND INV_DT LE '&&TXDATE.EVAL'';
-SET &&FROMDT = &&FXDATE;
-SET &&TODT = &&TXDATE;
-SET &&SUBHEAD = '<NAMEC';
-GOTO ENDDATE
 
-ENDDATE
-SET &&RPT_TTL1 = '                 CAR SUMMARY DETAIL REPORT ';
 
FILEDEF AUSEINCL DISK &&AUSEPASS
-RUN
FILEDEF CUSEINCL DISK &&CUSEPASS
-RUN
FILEDEF HUSEINCL DISK &&HUSEPASS
-RUN
FILEDEF LUSEINCL DISK &&LUSEPASS
-RUN
 
 
-*EX CATQTR
EX &&CATQTRPR
-RUN
 
FILEDEF AUSEINCL CLEAR
-RUN
FILEDEF CUSEINCL CLEAR
-RUN
FILEDEF HUSEINCL CLEAR
-RUN
FILEDEF LUSEINCL CLEAR
-RUN
 
EX CARUSE
-RUN
 
-TYPE Before Extract
SET ASNAMES = ON
-SET HOLDATTR = ON;
-RUN
 
-****************************************************
-* All other Drilldown
-****************************************************
 
-IF &&CARC EQ 'All Other' THEN SKIPTHIS ELSE SKIPTHAT;
-SKIPTHAT
 
DEFINE FILE CR_50
BOOKINGS/D8 = IF NUM_DAYS GT 0 THEN 1 ELSE IF NUM_DAYS LT 0 THEN (-1) ELSE 0;
 
DROP_OFF_DT/MDYY = DROP_DATE;
PICK_UP_DT/MDYY = PICKUP_DATE;
NEW_RENTAL_AMT/D15.2 =  RENTAL_AMT;
 
NBR_DAYS/D10 = IF (NUM_DAYS LT 0) AND
                  (NUM_CAR LT 0) THEN
                  (NUM_DAYS * NUM_CAR) * (-1) ELSE
                  (NUM_DAYS * NUM_CAR);
 
INV_DT/YYMD = INVOICE_DATE;
VNDR/A12 = LCWORD(12, VENDOR_NAME, VNDR);
END
-RUN
 
TABLEF FILE CR_50
PRINT
  BOOKINGS
  CURR_DATE
  CAR_TYPE
  DROP_CTY.CITY_NAME AS 'DROP_CTY_NAME'
  DROP_OFF_DT
  INVOICE_DATE
  NBR_DAYS
  NEW_RENTAL_AMT
  PASSNGR_NAME
  PICK_CTY.CITY_NAME AS 'PICK_CTY_NAME'
  PICK_UP_DT
  REFUSE_KEY
  VENDOR_NAME
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
 
-IF '&&CARC.EVAL' EQ 'Total' THEN GOTO NOWHERE;
WHERE VNDR EQ '&&CARC.EVAL'
-NOWHERE;
&&DCHK
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
-IF &RECORDS EQ 0 THEN GOTO NORECORDS;
-RUN

-IF &&FROMAVRP NE 'Y' GOTO :AVSF1;
-INCLUDE OTHPARMS
-INCLUDE AVRP_SUBFOOT
-:AVSF1
 
TABLE FILE RPTHOLD
PRINT BOOKINGS
  CAR_TYPE
  DROP_CTY_NAME
  DROP_OFF_DT
  INVOICE_DATE
  NBR_DAYS
  NEW_RENTAL_AMT
  PASSNGR_NAME
  PICK_CTY_NAME
  PICK_UP_DT
  REFUSE_KEY
  VENDOR_NAME
BY CURR_DATE
ON TABLE HOLD AS RPTHOLD1 FORMAT FOCUS INDEX CURR_DATE
END
-RUN
 
-* Global Reporting JOIN
JOIN RPTHOLD1.CURR_DATE IN RPTHOLD1 TO GLOBAL.DATE IN GLOBAL AS J1
 
 
DEFINE FILE RPTHOLD1
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
NAMEC1/A30 = CNAME;
NAMEC/A30 = IF COUNTRYCODE EQ 'USD' THEN ' ' ELSE NAMEC1;
  CURR1X/D20.6 = 1 / USDPU ;
 
-* DEFINES ON PREVIOUS DEFINES
  CARAMT/D20.6 = NEW_RENTAL_AMT * CURR1X;
 
-* DEFINES ON PREVIOUS DEFINES
 
  CARAMTX/D20.6 = CARAMT;
 
-* DEFINES ON PREVIOUSLY DEFINED DEFINES
 
  CARAMT1/D15.2CS = CARAMTX;
  NOWTOD/A8 WITH NBR_DAYS = HHMMSS (NOWTOD);
END
-RUN
-TYPE Before Report

-SET &&PAHDR  = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADAIR' ELSE 'HDREXLS'; 
-SET &&PASTYD = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'DTEXL' ELSE &&DTSTY;
-RUN

 
TABLE FILE RPTHOLD1
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM
NBR_DAYS AS 'DAYS'
BOOKINGS AS 'BOOKINGS'
CARAMT1 AS 'RENTAL,AMOUNT'
COMPUTE AVGDR/P12.2CS = IF CARAMT1 LT 0 AND NBR_DAYS LT 0
 THEN ((CARAMT1/NBR_DAYS) * (-1))
 ELSE CARAMT1/NBR_DAYS; AS 'AVERAGE,DAILY,RATE'
 
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY VENDOR_NAME AS 'VENDOR'
BY CAR_TYPE AS 'CAR'
BY PICK_UP_DT AS 'PICKUP,DATE'
BY PICK_CTY_NAME AS 'PICKUP,CITY'
BY DROP_OFF_DT AS 'DROP,DATE'
BY DROP_CTY_NAME AS 'DROP,CITY'
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
-* ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME SUMMARIZE AS 'TOTAL FOR'
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
" "
"&&FOOTER"
"Post-trip Reporting &DATE AT <NOWTOD <90 &&FOOTR"
-* "&&CRIGHT"
 
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&PASTYD
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
END
-RUN
 
-GOTO XXIT;
 
-SKIPTHIS
-***************************************************************************
-* ALL OTHER DRILLDOWN
-***************************************************************************
-IF &&CPFLAG EQ 'CY' THEN GOTO CURYEARA
- ELSE IF &&CPFLAG EQ 'PY' THEN GOTO PRRYEARA
- ELSE GOTO CURYEARA;
 
-CURYEARA
-SET &&DCHK = 'WHERE INVOICE_DATE GE '&&FROMDT.EVAL' AND INVOICE_DATE LE '&&TODT.EVAL'';
-GOTO ENDDATEA
 
-PRRYEARA
-SET &&DCHK = 'WHERE INVOICE_DATE GE '&&FXDATE.EVAL' AND INVOICE_DATE LE '&&TXDATE.EVAL'';
-GOTO ENDDATEA
 
-ENDDATEA
 
 
DEFINE FILE CR_50
CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
 
BOOKINGS/D8 = IF DAILY_RATE GE 0 THEN 1 ELSE
                  IF DAILY_RATE LT 0 THEN (-1) ELSE 0;
DROP_OFF_DT/MDYY = DROP_DATE;
PICK_UP_DT/MDYY = PICKUP_DATE;
NEW_RENTAL_AMT/D15.2 =  RENTAL_AMT;
 
NBR_DAYS/P10CS = IF (NUM_DAYS LT 0) AND
                  (NUM_CAR LT 0) THEN
                  (NUM_DAYS * NUM_CAR) * (-1) ELSE
                  (NUM_DAYS * NUM_CAR);
CDAY/P10CS = IF CURRENT EQ 'Y' THEN NBR_DAYS ELSE 0;
 
INV_DT/YYMD = INVOICE_DATE;
VNDR/A12 = LCWORD(12, VENDOR_NAME, VNDR);
END
-RUN
 
TABLEF FILE CR_50
PRINT
  BOOKINGS
  CDAY
  CURR_DATE
  CAR_TYPE
  DROP_CTY.CITY_NAME AS 'DROP_CTY_NAME'
  DROP_OFF_DT
  INVOICE_DATE
  NBR_DAYS
  NEW_RENTAL_AMT
  PASSNGR_NAME
  PICK_CTY.CITY_NAME AS 'PICK_CTY_NAME'
  PICK_UP_DT
  REFUSE_KEY
  VENDOR_NAME
-***************************************************************
-* Where statements to select data specified in user profile
-***************************************************************
WHERE INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL OR
      INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL
&&WHERE1
&&WHERE2
&&WHERE3
 
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHOLD
END
-RUN
 
TABLE FILE RPTHOLD
SUM CDAY
BY VENDOR_NAME
ON TABLE HOLD AS OVC1
END
-RUN
 
TABLE FILE OVC1
PRINT *
RANKED BY HIGHEST CDAY NOPRINT
ON TABLE HOLD AS OVC2
END
-RUN
 
DEFINE FILE OVC2
LIST/D9 = IF VENDOR_NAME NE LAST VENDOR_NAME THEN (LAST LIST + 1) ELSE LAST LIST;
DRANK/D9 = RANK;
ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
ARANK1/A9 = EDIT(ARANK, '$$999999999');
RANK_VALUE/A9 = IF LIST GT 3 THEN 'OTHER' ELSE ARANK1;
RANK_LABEL/A30 = IF RANK_VALUE NE 'OTHER' THEN VENDOR_NAME ELSE 'ALL OTHER';
END
-RUN
 
TABLE FILE OVC2
SUM CDAY
BY VENDOR_NAME
WHERE RANK_VALUE EQ 'OTHER'
ON TABLE HOLD AS OVC3
END
-RUN
 
TABLE FILE RPTHOLD
PRINT BOOKINGS
  CAR_TYPE
  CURR_DATE
  DROP_CTY_NAME
  DROP_OFF_DT
  INVOICE_DATE
  NBR_DAYS
  NEW_RENTAL_AMT
  PASSNGR_NAME
  PICK_CTY_NAME
  PICK_UP_DT
  REFUSE_KEY
BY VENDOR_NAME
&&DCHK
ON TABLE HOLD AS RPTHOLD1
END
-RUN
 
MATCH FILE RPTHOLD1
PRINT BOOKINGS
  CAR_TYPE
  CURR_DATE
  DROP_CTY_NAME
  DROP_OFF_DT
  INVOICE_DATE
  NBR_DAYS
  NEW_RENTAL_AMT
  PASSNGR_NAME
  PICK_CTY_NAME
  PICK_UP_DT
  REFUSE_KEY
BY VENDOR_NAME
ON TABLE HOLD
RUN
FILE OVC3
SUM CDAY
BY VENDOR_NAME
AFTER MATCH HOLD AS RPTHOLD2 OLD-AND-NEW
END
-RUN
 
TABLE FILE RPTHOLD2
PRINT BOOKINGS
  CAR_TYPE
  DROP_CTY_NAME
  DROP_OFF_DT
  INVOICE_DATE
  NBR_DAYS
  NEW_RENTAL_AMT
  PASSNGR_NAME
  PICK_CTY_NAME
  PICK_UP_DT
  REFUSE_KEY
  VENDOR_NAME
BY CURR_DATE
ON TABLE HOLD AS RPTHOLD1 FORMAT FOCUS INDEX CURR_DATE
END
-RUN
 
-* Global Reporting JOIN
JOIN RPTHOLD1.CURR_DATE IN RPTHOLD1 TO GLOBAL.DATE IN GLOBAL AS J1
 
 
DEFINE FILE RPTHOLD1
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
NAMEC1/A30 = CNAME;
NAMEC/A30 = IF COUNTRYCODE EQ 'USD' THEN ' ' ELSE NAMEC1;
  CURR1X/D20.6 = 1 / USDPU ;
 
-* DEFINES ON PREVIOUS DEFINES
  CARAMT/D20.6 = NEW_RENTAL_AMT * CURR1X;
 
-* DEFINES ON PREVIOUS DEFINES
 
  CARAMTX/D20.6 = CARAMT;
 
-* DEFINES ON PREVIOUSLY DEFINED DEFINES
 
  CARAMT1/D15.2CS = CARAMTX;
  NOWTOD/A8 WITH NBR_DAYS = HHMMSS (NOWTOD);
END
-RUN
-TYPE Before Report
 
-IF &&CPFLAG EQ 'CY' THEN GOTO CYA ELSE GOTO PYA;
 
-CYA
-SET &&FROMDT = &&FROMDT;
-SET &&TODT = &&TODT;
-RUN
-GOTO RPTC;
 
-PYA
-SET &&FROMDT = &&FXDATE;
-SET &&TODT = &&TXDATE;
-RUN
 
-RPTC;
 
 
TABLE FILE RPTHOLD1
-INCLUDE HEADAIR
"&&SUBHEAD"
-*
-*******************************************************************
-* Sum statement with fields passed from the user profile database
-*******************************************************************
-*
SUM
NBR_DAYS AS 'DAYS'
BOOKINGS AS 'BOOKINGS'
CARAMT1 AS 'RENTAL,AMOUNT'
COMPUTE AVGDR/P12.2CS = IF CARAMT1 LT 0 AND NBR_DAYS LT 0
 THEN ((CARAMT1/NBR_DAYS) * (-1))
 ELSE CARAMT1/NBR_DAYS; AS 'AVERAGE,DAILY,RATE'
 
-*
-******************************************************************
-* By statements (one per field)
-******************************************************************
-*
BY PASSNGR_NAME AS 'PASSENGER NAME'
BY VENDOR_NAME AS 'VENDOR'
BY CAR_TYPE AS 'CAR'
BY PICK_UP_DT AS 'PICKUP,DATE'
BY PICK_CTY_NAME AS 'PICKUP,CITY'
BY DROP_OFF_DT AS 'DROP,DATE'
BY DROP_CTY_NAME AS 'DROP,CITY'
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
-*
-******************************************************************
-* On Table statements for code appropriate to On Table commands
-******************************************************************
-*
ON TABLE COLUMN-TOTAL
-* ON PASSNGR_NAME SKIP-LINE
ON PASSNGR_NAME SUMMARIZE AS 'TOTAL FOR'
-*
-******************************************************************
-* Report specific footer(s)
-******************************************************************
-*
FOOTING BOTTOM
" "
"&&FOOTER"
"eTTek Review &DATE AT <NOWTOD <90 &&FOOTR"
-* "&&CRIGHT"
 
-*
-*ON TABLE SET STYLE &&DTSTY
-**************************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT
-*                                  WITH AN INCLUDE
-**************************************************************************
ON TABLE SET STYLE *
-INCLUDE EODT
ENDSTYLE
 
-*
&&OUTLINE1
&&OUTPUTDEST
&&OUTLINE2
 
 
END
-RUN
-*
-TYPE After Report
-GOTO XXIT;
 
 
 
-NOFILE
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>THE REPORT IS NO LONGER DRILLABLE!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
-GOTO XXIT;
 
 
-NORECORDS
-HTMLFORM BEGIN
<HTML>
<HEAD>
 
</HEAD>
<BODY>
 
 
<TITLE> TRAVEL & TRANSPORT</TITLE>
<CENTER><H2>NO RECORDS FOUND FOR THIS SELECTION!!!</H2></CENTER>
 
<CENTER><H4>PLEASE GO BACK TO THE PREVIOUS PAGE USING </H4></CENTER>
<CENTER><H3>BACK BUTTON OF THE BROWSER</H3></CENTER>
 
</BODY>
</HTML>
-HTMLFORM END
 
-XXIT
 
