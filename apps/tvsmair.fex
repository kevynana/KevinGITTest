-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* STEVE - 05/23/2003 - CHANGED CADVTP TO CADVTPX - DEFINED TWICE
-* JOY   - 08/05/2003 - CHANGED TO DEFINE/TABLE OFF OF SR_50
-* JOY   - 02/24/2004 - ADDED CTAVGT DEFINE FOR PATTI 
-* JOY   - 02/36/2004 - CHANGED THE ONLINE PERCENTAGE DEFINE TO BACK OUT
-*                    - REFUNDS/EXCHANGES
-* JOY   - 10/27/04   - PER PATTI CHANGED THE CALCULATIONS FOR 0-6/7-14 %'S TO 
-*                      USE NET TICKETS INSTEAD OF TICKETS PURCHASED
-* DEB    - 11/5/04   - PER PATTI CHANGED CNVSA DEFINE TO EXCLUDE 5 ADDTL
-*                      CLIENT CODES - CHG CREDIT_CARD OMITS FROM 424604 
-*                      TO 4246
-*DEB   -  12/21/04   - ADDED DOD_TYPE 'J' TO CONL CDONL AND CONGONL DEFINE

-*DEB   -  10/11/05   - ADDED DOD_TYPE 'H' TO CONL CDONL AND CONGONL DEFINE
-*GSE   -  05/08/13   - Changed call to PREFAIR fields to match new fieldnames.
-*                      Added check against DEPART_EFFECTIVE_DATE and
-*                      DEPART_EXPIRATION_DATE vs FST_DPT_DATE to VA_FLG DEFINE.
-*1/31/14 UPDATED ONLINE/TRADITIONAL DEFINES TO USE ONLINE_TRADITIONAL FIELD INSTEAD OF DOD_TYPE

-INCLUDE SETECHO
SET ASNAMES = ON
SET EMPTYREPORT=ON
-SET HOLDATTR = ON;
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';
-RUN

DEFINE FILE &&EXTRACT
  CC/A2 = EDIT(CREDIT_CARD, '99');
  14ADV/A8 = IF ADV_PURCH GE 14 THEN 'Y' ELSE 'N';
  ONLA/A1 = IF ONLINE_TRADITIONAL EQ 'ONLINE' THEN 'Y' ELSE 'N';
  PREF_CC/A1 = IF CC EQ 'AX' THEN 'Y' ELSE 'N';
  LEVEL_DESC/A60 = &&LDESC;
  EX_FARE/D12.2CSM = IF SEG_NBR EQ '01' THEN EXCH_NET_FARE ELSE 0;
  
  FARE_PAID1/D12.2CM = SEG_AMT + SEG_TAX;
  FARE_PAID/D12.2CS = IF DOC_NUMBER NE '0000000000' THEN EX_FARE ELSE FARE_PAID1;
  
  NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID1 GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                        IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;
  RO_FLAG/A1 = IF RNDT_FLAG EQ 'Y' THEN 'R' ELSE 'O';                           
  TKT_PURCH/D8.2CS      = IF (TICKET_CODE EQ '1') AND
                             (SEG_NBR EQ '01') AND
                             (FARE_PAID GE 0) AND
                             (SEG_COUNT EQ 1) AND 
                             (CONJ_REL EQ 1) THEN 1 ELSE 0;
  TKT_TYPE/A4          = IF (SEG_COUNT LE 0) AND
                          (RF_FLAG EQ 'P') THEN 'P-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (RF_FLAG EQ 'F') THEN 'F-RF' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'P') THEN 'P-EX' ELSE
                        IF (SEG_COUNT LE 0) AND 
                          (EX_FLAG EQ 'F') THEN 'F-EX' ELSE ' ';                       
XTRAN_DT/MDYY = TRN_DATE;
TVL_DTE/MDYY = FST_DPT_DATE;
TKTS/A1 = EDIT(TKT_SORT, '$$$$$$$$$$9');

FLAG/A1 = 'A';

-* Defines on Previous Defines
  DISSAVINGS/D12.2CSM = IF EMBARK EQ 'DT1' THEN 0 ELSE
                   FARE_PAID-SEG_LOWEST;
-*   DECLINE_AMT/D12.2C   = IF EMBARK EQ 'DT1' THEN 0 ELSE
-*                          TKT_AMOUNT - LOWEST_AMT;
  VASAVINGS/D12.2CSM = SEG_DISCOUNT - FARE_PAID;
  NEW_STAY/D9S = STAY;
  
 

END
-RUN

TABLEF FILE &&EXTRACT
PRINT 14ADV
      AIR_KEY
      AIR_MAIN.INTL_DOM AS 'IDOM'
      BR_CL_IDX
      C_ITIN
      CATEGORY
      FARE_PAID
      FARE_PAID1
      FST_DPT_DATE
      ONLA
      DISSAVINGS 
      NET_TKT_CNT
      PASSNGR_NAME
      PNR_LOCATOR
      PREF_CC
      NEW_STAY
      TKT_NUM
      XTRAN_DT
      TVL_DTE
      FLAG
      ROLLUP_CODE
      VAL_AIRLINE AS 'VAL_ALN'     
      DOC_NUMBER
      EX_FLAG
      EXCHG_SALE
      EXCH_NET_FARE
      TKT_SORT
      SEG_NBR
      RF_FLAG
      EXCHG_SALE
      TRN_DATE
      RO_FLAG
-* WHERE (TRN_DATE GE &&FROMDT.EVAL) AND (TRN_DATE LE &&TODT.EVAL) 
WHERE VOID_DATE EQ 0
WHERE RF_FLAG NE 'P'
WHERE EX_FLAG NE 'P'
WHERE EMBARK NE 'DT1'
WHERE DOC_TYPE NE '1'
WHERE TKTS NE 'C'

-********************************************************************
-* Where statements to select data specified in user profile
-********************************************************************
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE &AIRDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS RPTHLD
END
-RUN


TABLE FILE RPTHLD
SUM   14ADV
      AIR_KEY
      IDOM
      BR_CL_IDX
      C_ITIN
      CATEGORY
      FARE_PAID
      FARE_PAID1
      FST_DPT_DATE
      ONLA
      DISSAVINGS 
      NET_TKT_CNT
      PREF_CC
      NEW_STAY
      XTRAN_DT
      TVL_DTE
      FLAG
      ROLLUP_CODE
      VAL_ALN
      DOC_NUMBER
      EX_FLAG
      RF_FLAG
      ROLLUP_CODE
      EXCHG_SALE
      RO_FLAG
BY PASSNGR_NAME
BY PNR_LOCATOR
BY TRN_DATE
BY TKT_NUM
BY TKT_SORT
ON TABLE HOLD AS RH1 FORMAT FOCUS
END
-RUN


TABLE FILE &&EXTRACT
SUM 14ADV
      AIR_KEY
      AIR_MAIN.INTL_DOM AS 'IDOM'
      BR_CL_IDX
      C_ITIN
      CATEGORY
      FARE_PAID
      FARE_PAID1
      FST_DPT_DATE
      ONLA
      DISSAVINGS 
      NET_TKT_CNT
      PREF_CC
      NEW_STAY
      XTRAN_DT
      TVL_DTE
      FLAG
      ROLLUP_CODE
      VAL_AIRLINE AS 'VAL_ALN'
      DOC_NUMBER
      EX_FLAG
      RF_FLAG
      ROLLUP_CODE
      EXCHG_SALE
      RO_FLAG
BY PASSNGR_NAME
BY PNR_LOCATOR
BY TRN_DATE
BY TKT_NUM
BY TKT_SORT
WHERE FST_DPT_DATE GE '20120901' 
WHERE FST_DPT_DATE LE TRN_DATE
WHERE FST_DPT_DATE LE &&TODT.EVAL
WHERE TRN_DATE GE &&FROMDT.EVAL
WHERE TRN_DATE LE &&TODT.EVAL
WHERE VOID_DATE EQ 0
WHERE (RF_FLAG EQ 'F') OR (EX_FLAG EQ 'F')
WHERE TKTS NE 'C'
ON TABLE HOLD AS RHRE FORMAT FOCUS
END
-RUN

-SET &HLDPATH=TEMPPATH(50,'A50');
-SET &HRANK1=&HLDPATH || 'RH1.FOC';
-SET &HRANK2=&HLDPATH || 'RHRE.FOC';
-RUN

USE ADD 
&HRANK1 AS RH1
&HRANK2 AS RH1
END
-RUN


TABLE FILE RH1
SUM 14ADV
      AIR_KEY
      IDOM
      BR_CL_IDX
      C_ITIN
      CATEGORY
      FARE_PAID
      FARE_PAID1
      FST_DPT_DATE
      ONLA
      DISSAVINGS 
      NET_TKT_CNT
      PREF_CC
      NEW_STAY
      XTRAN_DT
      TVL_DTE
      FLAG
      ROLLUP_CODE
      VAL_ALN
      DOC_NUMBER
      EX_FLAG
      RF_FLAG
      ROLLUP_CODE
      EXCHG_SALE
      RO_FLAG
BY PASSNGR_NAME
BY PNR_LOCATOR
BY TRN_DATE
BY TKT_NUM
BY TKT_SORT
ON TABLE HOLD AS RPTHLDA
END
-RUN

TABLE FILE RPTHLDA
PRINT 14ADV 
      AIR_KEY
      BR_CL_IDX
      C_ITIN
      CATEGORY
      FARE_PAID
      FARE_PAID1
      FST_DPT_DATE
      ONLA
      DISSAVINGS 
      IDOM
      NET_TKT_CNT
      PASSNGR_NAME
      PNR_LOCATOR
      PREF_CC
      NEW_STAY AS 'STAY'
      TKT_NUM
      XTRAN_DT
      TVL_DTE
      FLAG
      EX_FLAG
      RF_FLAG
      ROLLUP_CODE
      EXCHG_SALE
      TKT_SORT
      TRN_DATE
      RO_FLAG
BY VAL_ALN 
ON TABLE HOLD AS RPTHLD FORMAT FOCUS INDEX VAL_ALN
END
-RUN

DEFINE FILE PREFAIR
-* ROLLUP_CODE/A8 = TSROLL;
VAL_ALN/A3 = AIRLINE_CODE;
END
-RUN
TABLE FILE PREFAIR
PRINT AIRLINE_CODE AIRLINE_INT_DOM AIRLINE_CATEGORY BRH_CUSN TICKET_EFFECTIVE_DATE TICKET_EXPIRATION_DATE GROUP_NUMBER
      DEPART_EFFECTIVE_DATE DEPART_EXPIRATION_DATE
BY VAL_ALN
WHERE '&&ROLLUP.EVAL' EQ TSROLL
WHERE TICKET_EFFECTIVE_DATE LE &&FROMDT.EVAL AND TICKET_EXPIRATION_DATE GE &&TODT.EVAL
ON TABLE HOLD AS JOY FORMAT FOCUS INDEX VAL_ALN
END
-RUN

JOIN VAL_ALN IN RPTHLD TO VAL_ALN IN JOY AS J1
-RUN

DEFINE FILE RPTHLD
-* EFF_FLG/A1 = IF TICKET_EFFECTIVE_DATE LE FST_DPT_DATE AND TICKET_EXPIRATION_DATE GE FST_DPT_DATE THEN 'Y' ELSE 'N';
VA_FLG/A1 = IF ((VAL_ALN EQ AIRLINE_CODE AND CATEGORY EQ AIRLINE_CATEGORY) AND 
                (DEPART_EFFECTIVE_DATE LE FST_DPT_DATE AND DEPART_EXPIRATION_DATE GE FST_DPT_DATE))
                THEN 'Y' ELSE
            IF ((VAL_ALN EQ AIRLINE_CODE AND CATEGORY EQ AIRLINE_CATEGORY) AND 
	        (DEPART_EFFECTIVE_DATE EQ ' '))
                THEN 'Y' ELSE 'N';
ID_FLG/A1 = IF AIRLINE_INT_DOM EQ 'B' THEN 'Y' ELSE 
            IF IDOM EQ AIRLINE_INT_DOM THEN 'Y' ELSE 'N';
CUS_FLG/A1 = IF BRH_CUSN EQ ' ' THEN 'Y' ELSE
             IF BRH_CUSN EQ BR_CL_IDX THEN 'Y' ELSE 'N';
END
-RUN
TABLE FILE RPTHLD
PRINT 14ADV 
      AIR_KEY
      BR_CL_IDX
      C_ITIN
      FARE_PAID
      FARE_PAID1
      GROUP_NUMBER
      ONLA
      DISSAVINGS 
      NET_TKT_CNT
      PREF_CC
      STAY
      XTRAN_DT
      TVL_DTE
      VAL_ALN
      AIRLINE_CODE
      AIRLINE_INT_DOM
      CATEGORY
      AIRLINE_CATEGORY
      BRH_CUSN
      TICKET_EFFECTIVE_DATE
      TICKET_EXPIRATION_DATE
      FST_DPT_DATE
      DEPART_EFFECTIVE_DATE
      DEPART_EXPIRATION_DATE
      VA_FLG
      ID_FLG
      CUS_FLG
      PASSNGR_NAME
      PNR_LOCATOR
      TKT_NUM
      FLAG   
      EX_FLAG
      RF_FLAG
      ROLLUP_CODE
      EXCHG_SALE
      TKT_SORT
      TRN_DATE
      RO_FLAG
BY AIR_KEY
ON TABLE HOLD AS RPTHLDAA FORMAT FOCUS INDEX AIR_KEY
END
-RUN

TABLE FILE AIRUDID
PRINT AUD_DATA AS 'UID'
BY AIR_KEY 
WHERE UDID EQ 'UID'
ON TABLE HOLD AS AIRUDID1
END
-RUN

TABLE FILE AIRUDID
PRINT AUD_DATA AS 'GNUM'
BY AIR_KEY
WHERE UDID EQ 'GN-'
ON TABLE HOLD AS AIRUDID2
END
-RUN

JOIN AIR_KEY IN RPTHLDAA TO AIR_KEY IN AIRUDID1 AS J2
-RUN

DEFINE FILE RPTHLDAA
UID_DSC/A64 = IF UID EQ ' ' THEN 'NONE GIVEN' ELSE UID;
END
-RUN
TABLE FILE RPTHLDAA
PRINT 14ADV 
      AIR_KEY
      BR_CL_IDX
      C_ITIN
      FARE_PAID
      FARE_PAID1
      GROUP_NUMBER
      ONLA
      DISSAVINGS 
      NET_TKT_CNT
      PREF_CC
      STAY
      XTRAN_DT
      TVL_DTE
      VAL_ALN
      AIRLINE_CODE
      AIRLINE_INT_DOM
      CATEGORY
      AIRLINE_CATEGORY
      BRH_CUSN
      TICKET_EFFECTIVE_DATE
      TICKET_EXPIRATION_DATE
      VA_FLG
      ID_FLG
      CUS_FLG
      PASSNGR_NAME
      PNR_LOCATOR
      TKT_NUM
      FLAG   
      UID_DSC
      EX_FLAG
      RF_FLAG
      ROLLUP_CODE
      EXCHG_SALE
      TKT_SORT
      TRN_DATE
      RO_FLAG
BY AIR_KEY 
ON TABLE HOLD AS AIRHLDA FORMAT FOCUS INDEX AIR_KEY
END
-RUN

JOIN AIR_KEY IN AIRHLDA TO AIR_KEY IN AIRUDID2 AS J3
-RUN

DEFINE FILE AIRHLDA
GN_FLG/A1 = IF GROUP_NUMBER EQ ' ' THEN 'Y' ELSE
            IF GNUM EQ GROUP_NUMBER THEN 'Y' ELSE 'N';
PREF_ALN/A1 = IF VA_FLG EQ 'Y' AND ID_FLG EQ 'Y' AND CUS_FLG EQ 'Y' AND GN_FLG EQ 'Y' THEN 'Y' ELSE 'N';
LS/A1 = IF DISSAVINGS NE 0 THEN 'Y' ELSE 'N';

END
-RUN
TABLE FILE AIRHLDA
PRINT   14ADV 
      C_ITIN
      FARE_PAID
      FARE_PAID1
      ONLA
      DISSAVINGS 
      LS
      NET_TKT_CNT
      PREF_CC
      STAY
      XTRAN_DT
      TVL_DTE
      VAL_ALN
      PREF_ALN 
      ROLLUP_CODE
      EXCHG_SALE
      UID_DSC
      RO_FLAG
BY PASSNGR_NAME
BY PNR_LOCATOR
BY TRN_DATE
BY TKT_NUM
BY TKT_SORT
BY FLAG
ON TABLE HOLD AS AIRHLD
END
-RUN

-IF &RECORDS NE 0 THEN GOTO SETA ELSE GOTO SETA1;

-SETA
-SET &&AIRDATA='Y';
-GOTO XXIT;

-SETA1
-SET &&AIRDATA='N';
-GOTO XXIT;


-XXIT;



 

 

















