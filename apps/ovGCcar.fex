-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-INCLUDE SETECHO
SET ASNAMES=ON

DEFINE FILE CR_50
NBR_DAYS/D10 = IF (NUM_DAYS LT 0) AND
                  (NUM_CAR LT 0) THEN      
                  (NUM_DAYS * NUM_CAR) * (-1) ELSE
                  (NUM_DAYS * NUM_CAR);

TC_AMT/D15.2 = RENTAL_AMT;

CURRENT/A1 = IF INVOICE_DATE GE &&FROMDT.EVAL AND
                INVOICE_DATE LE &&TODT.EVAL THEN 'Y' ELSE 'N';
PRIOR/A1 =  IF INVOICE_DATE GE &&FXDATE.EVAL AND
               INVOICE_DATE LE &&TXDATE.EVAL THEN 'Y' ELSE 'N';

-* Defines on Previous Defines
CDAY/D10 = IF CURRENT EQ 'Y' THEN NBR_DAYS ELSE 0;
PDAY/D10 = IF PRIOR EQ 'Y' THEN NBR_DAYS ELSE 0;
CAMT/D15.2 = IF CURRENT EQ 'Y' THEN TC_AMT ELSE 0;
PAMT/D15.2 = IF PRIOR EQ 'Y' THEN TC_AMT ELSE 0;
END
-RUN

TABLEF FILE CR_50
PRINT CAMT
      CDAY
      CURRENT
      CURR_DATE
      PAMT
      PDAY
      PRIOR
      NBR_DAYS
      VENDOR_NAME
WHERE (INVOICE_DATE GE &&FROMDT.EVAL AND INVOICE_DATE LE &&TODT.EVAL) OR
      (INVOICE_DATE GE &&FXDATE.EVAL AND INVOICE_DATE LE &&TXDATE.EVAL)
&&WHERE1
&&WHERE2
&&WHERE3


-INCLUDE RPTPARMS

ON TABLE HOLD AS OVCHOLD 
END

TABLE FILE OVCHOLD
PRINT CAMT
      CDAY
      CURRENT
      PAMT
      PDAY
      PRIOR
      NBR_DAYS
      VENDOR_NAME
BY CURR_DATE
ON TABLE HOLD AS OVCHOLD1 FORMAT FOCUS INDEX CURR_DATE
END
-RUN


-* Global Reporting JOIN
JOIN OVCHOLD1.CURR_DATE IN OVCHOLD1 TO GLOBAL.DATE IN GLOBAL AS J1

DEFINE FILE OVCHOLD1
-******************************************************************************
-*                         Global Currency Conversion Defines
-******************************************************************************
 CNAME1/A30 = LCWORD(30, CNAME, CNAME1);
 NAMEC/A30 = IF GLOBAL.COUNTRYCODE EQ 'USD' THEN ' ' ELSE CNAME1;
  CURR1X/D20.6 = 1 / USDPU ;

-* DEFINES ON PREVIOUS DEFINES
  CCARAMT/D20.6 = CAMT * CURR1X;
  PCARAMT/D20.6 = PAMT * CURR1X;
 
-* DEFINES ON PREVIOUSLY DEFINES DEFINES

  CCAMTX/D20.6 = CCARAMT;
  PCAMTX/D20.6 = PCARAMT;

-* DEFINES ON PREVIOUSLY DEFINED DEFINES

 CCAMT1/D15.2 = CCAMTX;
 PCAMT1/D15.2 = PCAMTX;
END
-RUN

TABLE FILE OVCHOLD1
SUM CDAY 
    CCAMT1
    NAMEC
BY VENDOR_NAME
WHERE CURRENT EQ 'Y'
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS CHOLD1 
END
-RUN

TABLE FILE OVCHOLD1
SUM PDAY 
    PCAMT1
BY VENDOR_NAME
WHERE PRIOR EQ 'Y'
WHERE GLOBAL.COUNTRYCODE EQ '&&CURRENCY'
ON TABLE HOLD AS CHOLD2 
END
-RUN

-* JOIN CLEAR *
-RUN

MATCH FILE CHOLD1
PRINT CDAY
      CCAMT1
      NAMEC
BY VENDOR_NAME
ON TABLE HOLD
RUN
FILE CHOLD2
PRINT PDAY
      PCAMT1
BY VENDOR_NAME
AFTER MATCH HOLD AS CHOLD3 OLD-OR-NEW
END

TABLE FILE CHOLD3
SUM CDAY
    CCAMT1
    PDAY
    PCAMT1
    NAMEC
BY VENDOR_NAME
ON TABLE HOLD AS CHOLD4 
END
-RUN

TABLE FILE CHOLD4
PRINT *
RANKED BY HIGHEST CDAY NOPRINT
ON TABLE HOLD AS CHOLD5 
END
-RUN

-SET &RECFOUND = &RECORDS;

DEFINE FILE CHOLD5
  LIST/D9 = IF VENDOR_NAME NE LAST VENDOR_NAME THEN (LAST LIST + 1) ELSE 
  LAST LIST;
  DRANK/D9 = RANK;
  ARANK/A11 = FTOA(DRANK, '(D9)', ARANK);
  ARANK1/A9 = EDIT(ARANK, '$$999999999');
  RANK_VALUE/A9 = IF LIST GT 3 THEN 'OTHER' ELSE ARANK1;
  RANK_LABEL1/A30 = IF RANK_VALUE NE 'OTHER' THEN VENDOR_NAME
                      ELSE 'All Other';
  RANK_LABEL/A30 = LCWORD(30, RANK_LABEL1, RANK_LABEL);
END
-RUN

TABLE FILE CHOLD5
SUM CDAY
    CCAMT1
    PDAY
    PCAMT1
    NAMEC
BY RANK_VALUE NOPRINT
BY RANK_LABEL
ON TABLE HOLD AS CHOLD6 
END
-RUN

GRAPH FILE CHOLD6
SUM CDAY AS ' '
ACROSS RANK_LABEL
ON GRAPH SET GRAPHSTYLE *
setGraphType(56);
setLegendDisplay(true);
setAutofit(getLegendText(),true);
setLegendPosition(1);
setLegendMarkerPosition(2);
setFontSizeAbsolute(getTitle(),true);
setAutofit(getTitle(),false);
setTextJustHoriz(getTitle(),1);
setFontStyle(getTitle(),1);
setFontSize(getTitle(),15);
setTitleString("Car Booking Summary");
setTextJustHoriz(getTitle(), 1);
setSubtitleString("Based Upon Number of Rental Days");
setTextJustHoriz(getSubtitle(), 1);
setFontSizeAbsolute(getSubtitle(),true); 
setAutofit(getSubtitle(),true);
setFontStyle(getSubtitle(),1);
setFontSize(getSubtitle(),15);
setPieFeelerTextDisplay(2);
setPieFeelerTextFormat(1);
setPieLabelDisplay(0);
setAutofit(getPieLabelDisplay(),true);
setFontStyle(getPieLabelDisplay(),1);
setSeriesFillColor(0, new Color, 0 64 128));
setSeriesFillColor(1, new Color, 115 160 247));
ENDSTYLE
ON GRAPH SAVE AS CAR FORMAT SVG
END
-RUN

-IF &&OUTFMT NE 'PDF' GOTO NOGRAPH1 ELSE GOTO GRAPH1;
-NOGRAPH1;
-SET &&CAR_GIF = 'N';
-GOTO GRAPHN1;
-GRAPH1;
-SET &&CAR_GIF=IF &LINES GT 0 THEN 'Y' ELSE 'N';
-GRAPHN1;

DEFINE FILE CHOLD6
LABEL/A30 = 'Car Vendors';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 5;
END
-RUN

TABLE FILE CHOLD6
PRINT CATEGORY/A61 AND RANK_LABEL/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CCAMT1/P15C  AS 'VOL_CURR' AND 
CDAY/P9CS AS 'TKT_CURR' AND PCAMT1/P15C AS 'VOL_PRIOR' 
AND PDAY/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = ROW_ORDER + 1;    
ON TABLE HOLD AS CHOLD7 
END
-RUN

TABLE FILE CHOLD7
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS CHOLD8 
END
MODIFY FILE OV1_GC
FIXFORM FROM CHOLD8
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON CHOLD8
END
-RUN

-******************************************************************************
-* Start of New Code for adding Total Line
-******************************************************************************

DEFINE FILE CHOLD6
LABEL/A30 = 'Car Vendors';
CATEGORY/A61 = IF NAMEC EQ ' ' THEN LABEL ELSE LABEL||'-'||NAMEC;
CAT_ORDER/I2 = 5;
RANK_LABEL1/A30 = 'Total';
END
-RUN

TABLE FILE CHOLD6
SUM CDAY
    CCAMT1
    PDAY
    PCAMT1
    NAMEC
BY CATEGORY
BY CAT_ORDER
BY RANK_LABEL1
ON TABLE HOLD AS CHOLDT
END
-RUN

TABLE FILE CHOLDT
PRINT CATEGORY/A61 AND RANK_LABEL1/A30 AS 'ROW_LABEL' AND
CAT_ORDER/I2 AND CCAMT1/P15C  AS 'VOL_CURR' AND 
CDAY/P9CS AS 'TKT_CURR' AND PCAMT1/P15C AS 'VOL_PRIOR' 
AND PDAY/P9CS AS 'TKT_PRIOR' AND 
COMPUTE ROW_ORDER/I2 = 5;    
ON TABLE HOLD AS CHOLDT1
END
-RUN

TABLE FILE CHOLDT1
PRINT CATEGORY AND ROW_LABEL AND CAT_ORDER
      AND ROW_ORDER AND VOL_CURR AND TKT_CURR
      AND VOL_PRIOR AND TKT_PRIOR
ON TABLE HOLD AS CHOLDT2
END
MODIFY FILE OV1_GC
FIXFORM FROM CHOLDT2
MATCH CATEGORY ROW_LABEL
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA ON CHOLDT2
END
-RUN


