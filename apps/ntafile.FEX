SET ASNAMES=ON
-INCLUDE SETECHO

USE ADD
D:\TNT\TTRACKER\DATA\S_NATIX.FOC AS SR_50
D:\TNT\TTRACKER\DATA\A_NATIX.FOC AS AIR_MAIN
D:\TNT\TTRACKER\DATA\T_NATIX.FOC AS TKT_MAIN
END

DEFINE FILE SR_50
XDPT_DT/MDYY  = DPT_DATE;
SEGA/P12.2M = SEG_AMT;
SEGC/P12.2M = SEG_COM;
END
TABLE FILE SR_50
PRINT ORG_APC DST_APC SEG_MILES SEGA SEGC CLASS FARE_BAS FLIGHT XDPT_DT AIRLINE VAL_AIRLINE AROLL_LEV3
BY TKT_SORT
BY TKT_NUM
BY SEG_NBR
WHERE DPT_DATE GE '20100601' AND DPT_DATE LE '20101101'
WHERE VOID_DATE EQ 0
WHERE AROLL_LEV3 EQ 'DEFENT'
WHERE ORG_APC NE 'DT1' AND DST_APC NE 'DT1'
ON TABLE HOLD AS HOLD1
END
-RUN



TABLE FILE SR_50
SUM SEG_MILES AS 'TMILE'
BY TKT_SORT
BY TKT_NUM
ON TABLE HOLD AS HOLD2
END
-RUN

MATCH FILE HOLD1
PRINT ORG_APC DST_APC SEG_MILES SEGA SEGC CLASS FARE_BAS FLIGHT XDPT_DT AIRLINE VAL_AIRLINE AROLL_LEV3 
BY TKT_SORT
BY TKT_NUM
ON TABLE HOLD
RUN
FILE HOLD2
SUM TMILE
BY TKT_SORT
BY TKT_NUM
AFTER MATCH HOLD AS HOLD3 OLD
END

DEFINE FILE HOLD3
SEG_CNT/P12.2 = IF SEG_MILES LT 0 THEN ((SEG_MILES/TMILE)*(-1)) ELSE (SEG_MILES/TMILE);
ORGDST/A6 = IF ORG_APC LT DST_APC THEN ORG_APC|DST_APC ELSE DST_APC|ORG_APC;
CBKG/A2 = 'US';
END
TABLE FILE HOLD3
HEADING CENTER
"<AROLL_LEV3"
PRINT CBKG AS 'Country of,Booking' ORGDST AS 'Origin/,Destination' TKT_NUM AS 'Ticket,Number' AIRLINE AS 'Flown,Airline' 
      VAL_AIRLINE AS 'Validating,Airline' SEG_CNT AS 'Segments' SEGA AS 'Net,Revenue' SEGC AS 'Net,Commission'  
      CLASS AS 'Class,of,Service' FARE_BAS AS 'Fare,Basis,Code' FLIGHT AS 'Flight,Number' XDPT_DT AS 'Departure,Date' 
      SEG_MILES AS 'Miles,Flown' 
BY CBKG NOPRINT
BY ORGDST NOPRINT
BY TKT_SORT NOPRINT
BY TKT_NUM NOPRINT
ON TABLE COLUMN-TOTAL
ON TABLE SET STYLE *

UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.100000, RIGHTMARGIN=0.030000,
    TOPMARGIN=0.030000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $

TYPE=REPORT, FOCEXEC=NONE, GRID=OFF, FONT=ARIAL, SIZE=9, COLOR=BLACK,
             BACKCOLOR=NONE, STYLE=NORMAL, $
             
TYPE=HEADING, SIZE=12, COLOR=BLACK, STYLE=BOLD, JUSTIFY=CENTER, $             
TYPE=TITLE, STYLE=BOLD, COLOR=BLACK, JUSTIFY=CENTER, $ 
-* TYPE=DATA, JUSTIFY=LEFT, $
ENDSTYLE             
ON TABLE PCHOLD FORMAT EXL2K
-* ON TABLE SAVE AS NATIXGAUS FORMAT EXL2K
END
-RUN
-QUIT

