-****************************************************************************************
-*DVRAirPortCodes.fex - get airport codes from AirHold3 for Air Detail (DVRAIRDT.FEX)
-*DATE:  11/25/2008
-*03/09/17 - DLV - S-22486 - Updated IMAGE define to use server report is being ran from
-****************************************************************************************
-INCLUDE SETECHO
SET ASNAME = ON

-SET &HOLDFILE = '&&WEB_PATH.EVAL' || 'APCODE.ftm';
-*-SET &HOLDFILE = 'APCODE.ftm';
-DOS DEL &HOLDFILE
FILEDEF &HOLDFILE CLEAR
FILEDEF APCODE DISK &HOLDFILE (APPEND
-RUN
 
TABLE FILE AIRHOLD3
BY EMB_APT_CD AS 'AIRPORT_CODE' 
ON TABLE HOLD AS APCODE
END
TABLE FILE AIRHOLD3
BY RTE_APT_CD AS 'AIRPORT_CODE' 
ON TABLE HOLD AS APCODE
END
-RUN

TABLE FILE APCODE 
BY AIRPORT_CODE
ON TABLE HOLD
END
-RUN

-SET &RPTTITLE = 'AIRPORT CITY CODE';
DEFINE FILE AIRPORT
  NOWTOD/A8 = HHMMSS (NOWTOD);
-*IMAGE/A80 = '<img src=HTTPS://www.ettekreview.com/asp/NEW_HDR.jpg>';
IMAGE/A80 = '<img src=' || '&&SRVR.EVAL' || '/asp/NEW_HDR.PNG>';
END
TABLE FILE AIRPORT
HEADING
"<IMAGE>"
"</5"
"&RPTTITLE.EVAL"
"&&DTDESC.EVAL &&FROMDT - &&TODT " 

PRINT AIRPORT_NAME CITY_CODE AS 'City' STATE_CODE AS 'State' COUNTRY_CODE AS 'Country'
BY AIRPORT_CODE AS 'Code'
WHERE AIRPORT_CODE IN FILE HOLD

&&OUTLINE1
&&OUTLINE2

ON TABLE SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.200000, RIGHTMARGIN=0.200000, 
    TOPMARGIN=0.500000, BOTTOMMARGIN=0.250000, SQUEEZE=ON, 
    ORIENTATION=LANDSCAPE, $
TYPE=REPORT,FONT=TIMES NEW ROMAN, SIZE=8, COLOR=BLACK, GRID=ON, STYLE=NORMAL,WRAP= OFF, $
TYPE=TITLE, SIZE=9, STYLE=BOLD+UNDERLINE,  $
TYPE=HEADING, SIZE=10, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=14, LINE=8, STYLE=BOLD, FONT=TIMES NEW ROMAN,$
TYPE=HEADING, SIZE=10, LINE=9, STYLE=BOLD, FONT=TIMES NEW ROMAN,$

TYPE=FOOTING, SIZE=7, STYLE=BOLD, COLOR=GREY, $
TYPE=TITLE, STYLE=BOLD, $
TYPE=SUBFOOT, SIZE=14, STYLE=BOLD, $
TYPE=SUBTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=GRANDTOTAL, SIZE=9, STYLE=BOLD, $
TYPE=REPORT, TITLETEXT=&DSC,$

ENDSTYLE

END
-RUN
