-*  This program will purge Air, Car, and Hotel records from all
-*    associated databases, based on a purge date and trigger file
-*    of target rollup environments
-* FOR QUARTERED COMPANIES PURGE - USE PRGEQTR
-* 04/05/2002 - CHANGED TO PURGE AL, CL AND HL - STEVE CORDS
-* 04/05/2002 - TOOK OUT PURGE OF AH, CH AND HH - STEVE CORDS

-* This is the path where Air, Car, and Hotel databases reside
-SET &&DATPATH = 'D:\TNT\TTRACKER\DATA\';
-* This is the path where the Rollup database resides
-SET &&SYSPATH = 'D:\TNT\TTRACKER\SYSTEM\DATA\';
-SET &&DBUSE = '';
-SET &&PRGTARG = '        ';

-* This is the purge date, which will also be the Start_Date
-*   updated in Rollup following the purge
-SET &&PRGDATE = '''1999/07/01''';

-PRGLOOP

-* This is the trigger file containing the target rollups for purge
-*   action - each rollup shall be identified by a single entry
-*   consisting of the segment database target - e.g. S1METX or
-*   S_BAIR - the last record in this trigger file must be END
FILEDEF PRGLIST DISK D:\TNT\TTRACKER\TEMP\PRGLIST.TXT (LRECL 80
-RUN
FILEDEF PRGNEW DISK D:\TNT\TTRACKER\TEMP\PRGNEW.TXT (LRECL 80
-RUN

-READ PRGLIST &&PRGTARG
-IF &&PRGTARG EQ 'END' THEN GOTO XFER;
-TYPE PRGTARG &&PRGTARG

-REPEAT BNEW WHILE &IORETURN EQ 0;
-READ PRGLIST &&SLINE.80
-IF &IORETURN NE 0 THEN GOTO BNEW;
-WRITE PRGNEW &&SLINE
-BNEW

FILEDEF PRGLIST CLEAR
-RUN
FILEDEF PRGNEW CLEAR
-RUN

COPY D:\TNT\TTRACKER\TEMP\PRGNEW.TXT D:\TNT\TTRACKER\TEMP\PRGLIST.TXT
-RUN

-TYPE PRGTARG &&PRGTARG

-SET &&PRGQTR = SUBSTR(8, &&PRGTARG, 2, 2, 1, 'A1');
-SET &&PRGCOMP = SUBSTR(8, &&PRGTARG, 3, 8, 6, 'A6');
-SET &&PRGROLL = &&PRGCOMP || '-R';

-TYPE PRGQTR &&PRGQTR
-TYPE PRGCOMP &&PRGCOMP
-TYPE PRGROLL &&PRGROLL

-SET &&PRGSEG = 'S' || &&PRGQTR || &&PRGCOMP;
-SET &&PRGMKT = 'M' || &&PRGQTR || &&PRGCOMP;
-SET &&PRGDST = 'D' || &&PRGQTR || &&PRGCOMP;
-SET &&PRGAIR = 'A' || &&PRGQTR || &&PRGCOMP;
-SET &&PRGAL = 'AL' || &&PRGCOMP;
-SET &&PRGCAR = 'C' || &&PRGQTR || &&PRGCOMP;
-SET &&PRGCL = 'CL' || &&PRGCOMP;
-SET &&PRGHTL = 'H' || &&PRGQTR || &&PRGCOMP;
-SET &&PRGHL = 'HL' || &&PRGCOMP;

-TYPE PRGSEG &&PRGSEG
-TYPE PRGMKT &&PRGMKT
-TYPE PRGDST &&PRGDST
-TYPE PRGAIR &&PRGAIR
-TYPE PRGAL &&PRGAL
-TYPE PRGCAR &&PRGCAR
-TYPE PRGCL &&PRGCL
-TYPE PRGHTL &&PRGHTL
-TYPE PRGHL &&PRGHL

-NOTQTR
-TYPE BEGINNING NOT QUARTERED UPDATE

-SET &&DBUSE = &&DATPATH || &&PRGSEG || '.FOC';
USE
&&DBUSE
END
-RUN

TABLE FILE &&PRGSEG
PRINT TRN_DATE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
WHERE TRN_DATE LT &&PRGDATE.EVAL;
ON TABLE HOLD AS SEGTRIG1
END
-RUN

TABLE FILE &&PRGSEG
PRINT TRN_DATE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
WHERE TRN_DATE GE &&PRGDATE.EVAL;
ON TABLE HOLD AS SEGTRIG2
END
-RUN

MODIFY FILE &&PRGSEG
FIXFORM AIR_KEY/A45 EMBARK/A3 ROUTE/A3 TKT_SORT/A11
FIXFORM SEG_NBR/A2 X8
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
DATA ON SEGTRIG1
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGMKT || '.FOC';
USE
&&DBUSE
END
-RUN

TABLE FILE &&PRGMKT
PRINT TRN_DATE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
WHERE TRN_DATE LT &&PRGDATE.EVAL;
ON TABLE HOLD AS MKTTRIG
END
-RUN

MODIFY FILE &&PRGMKT
FIXFORM AIR_KEY/A45 EMBARK/A3 ROUTE/A3 TKT_SORT/A11
FIXFORM SEG_NBR/A2 X8
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
DATA ON MKTTRIG
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGDST || '.FOC';
USE
&&DBUSE
END
-RUN

TABLE FILE &&PRGDST
PRINT TRN_DATE
BY AIR_KEY
BY EMBARK
BY ROUTE
BY TKT_SORT
BY SEG_NBR
WHERE TRN_DATE LT &&PRGDATE.EVAL;
ON TABLE HOLD AS DSTTRIG
END
-RUN

MODIFY FILE &&PRGDST
FIXFORM AIR_KEY/A45 EMBARK/A3 ROUTE/A3 TKT_SORT/A11
FIXFORM SEG_NBR/A2 X8
MATCH AIR_KEY EMBARK ROUTE TKT_SORT SEG_NBR
ON MATCH DELETE
DATA ON DSTTRIG
END
-RUN

-TYPE SEG MATCH

-MATCH

MATCH FILE SEGTRIG1
PRINT AIR_KEY TRN_DATE
BY AIR_KEY NOPRINT
RUN
FILE SEGTRIG2
PRINT AIR_KEY
BY AIR_KEY NOPRINT
AFTER MATCH HOLD AS SEGTRIG OLD-NOT-NEW
END
-RUN

TABLE FILE SEGTRIG
SUM TRN_DATE
BY AIR_KEY
ON TABLE HOLD AS AIRTRIG
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGAIR || '.FOC';
USE
&&DBUSE
END
-RUN

MODIFY FILE &&PRGAIR
FIXFORM AIR_KEY/A45
MATCH AIR_KEY
ON MATCH DELETE
DATA ON AIRTRIG
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGAL || '.FOC';
USE
&&DBUSE
END
-RUN

MODIFY FILE &&PRGAL
FIXFORM AIR_KEY/A45
MATCH AIR_KEY
ON MATCH DELETE
DATA ON AIRTRIG
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGCAR || '.FOC';
USE
&&DBUSE
END
-RUN

TABLE FILE &&PRGCAR
PRINT PNR_LOCATOR
BY CAR_KEY
WHERE INVOICE_DATE LT &&PRGDATE.EVAL;
ON TABLE HOLD AS CARTRIG1
END
-RUN

TABLE FILE &&PRGCAR
PRINT PNR_LOCATOR
BY CAR_KEY
WHERE INVOICE_DATE GE &&PRGDATE.EVAL;
ON TABLE HOLD AS CARTRIG2
END
-RUN

-TYPE CAR MATCH

-MATCH

MATCH FILE CARTRIG1
PRINT CAR_KEY PNR_LOCATOR
BY CAR_KEY NOPRINT
RUN
FILE CARTRIG2
PRINT CAR_KEY
BY CAR_KEY NOPRINT
AFTER MATCH HOLD AS CARTRIG OLD-NOT-NEW
END
-RUN

MODIFY FILE &&PRGCAR
FIXFORM CAR_KEY/A82
MATCH CAR_KEY
ON MATCH DELETE
DATA ON CARTRIG
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGCL || '.FOC';
USE
&&DBUSE
END
-RUN

MODIFY FILE &&PRGCL
FIXFORM CAR_KEY/A82
MATCH CAR_KEY
ON MATCH DELETE
DATA ON CARTRIG
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGHTL || '.FOC';
USE
&&DBUSE
END
-RUN

TABLE FILE &&PRGHTL
PRINT PROP_KEY
BY HTL_KEY
WHERE INVOICE_DATE LT &&PRGDATE.EVAL;
ON TABLE HOLD AS HTLTRIG1
END
-RUN

TABLE FILE &&PRGHTL
PRINT PROP_KEY
BY HTL_KEY
WHERE INVOICE_DATE GE &&PRGDATE.EVAL;
ON TABLE HOLD AS HTLTRIG2
END
-RUN

-TYPE HOTEL MATCH

-MATCH

MATCH FILE HTLTRIG1
PRINT HTL_KEY PROP_KEY
BY HTL_KEY NOPRINT
RUN
FILE HTLTRIG2
PRINT HTL_KEY
BY HTL_KEY NOPRINT
AFTER MATCH HOLD AS HTLTRIG OLD-NOT-NEW
END
-RUN

MODIFY FILE &&PRGHTL
FIXFORM HTL_KEY/A69
MATCH HTL_KEY
ON MATCH DELETE
DATA ON HTLTRIG
END
-RUN

-SET &&DBUSE = &&DATPATH || &&PRGHL || '.FOC';
USE
&&DBUSE
END
-RUN

MODIFY FILE &&PRGHL
FIXFORM HTL_KEY/A69
MATCH HTL_KEY
ON MATCH DELETE
DATA ON HTLTRIG
END
-RUN

-SET &&DBUSE = &&SYSPATH || 'ROLLUP.FOC';
USE
&&DBUSE
END
-RUN

TABLE FILE ROLLUP
PRINT ROLLUP_CODE
IF COMP EQ &&PRGCOMP
ON TABLE HOLD AS ROLLUPD
END
-RUN

MODIFY FILE ROLLUP
FIXFORM ROLLUP_CODE/A8
MATCH ROLLUP_CODE
ON MATCH COMPUTE START_DATE = &&PRGDATE;
ON MATCH UPDATE START_DATE
DATA ON ROLLUPD
END
-RUN

EX PRGERBLD
-RUN

-GOTO PRGLOOP

-XFER
-TYPE EXITING
