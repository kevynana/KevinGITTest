-*get all records from AIR_MAIN with FST_DTP_DATE blank
TABLE FILE &AIRMAINDB
PRINT FST_DPT_DATE
BY AIR_KEY 
WHERE FST_DPT_DATE EQ ' '
ON TABLE HOLD AS HOLDAM FORMAT FOCUS INDEX AIR_KEY
END
-RUN

-*get all segments if DPT_DATE NE blank
-SET &MYJOIN = 'JOIN AIR_KEY IN HOLDAM TO ALL AIR_KEY IN &SRDB.EVAL ';
-*JOIN AIR_KEY IN HOLDAM TO ALL AIR_KEY IN SR_50
&MYJOIN
TABLE FILE HOLDAM
PRINT DPT_DATE 
BY AIR_KEY
BY SEG_NBR
BY EMBARK
BY ROUTE
WHERE DPT_DATE NE ' ' AND EMBARK NE 'DT1'
ON TABLE HOLD AS HOLDSR FORMAT FOCUS INDEX AIR_KEY
END
-RUN
-IF &LINES EQ 0 THEN GOTO NOUPDATE;
-*get the lowest segment with the ticket
SET ASNAME = ON
TABLE FILE HOLDSR
PRINT DPT_DATE AS 'FST_DPT_DATE'
BY AIR_KEY
WHERE AIR_KEY NE LAST AIR_KEY
ON TABLE HOLD
END
MODIFY FILE &AIRMAINDB
FIXFORM AIR_KEY/A48 FST_DPT_DATE/YYMD
MATCH AIR_KEY
  ON MATCH UPDATE FST_DPT_DATE
  ON NOMATCH REJECT
DATA ON HOLD
END
-RUN
-NOUPDATE