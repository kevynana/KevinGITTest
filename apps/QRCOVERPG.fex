-**********************************************************************
-* Print the cover page of Quarterly Review.
-**********************************************************************
-INCLUDE SETECHO

TABLE FILE BRPTFLDS
PRINT REVIEW.COVERPAGE 
IF RECORDLIMIT EQ 1
ON TABLE SAVE
END
-RUN
-READ SAVE &&CP.1.
-IF &&CP EQ 0 THEN GOTO NOCOVERPG;

DEFINE FILE BRPTINST
  CENTERIT/A1  =' ';
END
TABLE FILE BRPTINST
HEADING CENTER
"</11"
"<RPT_TITLE1"
"<RPT_TITLE2"
"</1 <39 From <FM_DATE to <TO_DATE"
"</3 "
"<43 Presented by"
PRINT CENTERIT AS '' IN 0 INST_KEY  AS '' NOPRINT
RPT_NAME AS '' NOPRINT RPT_TITLE1 AS ' ' NOPRINT RPT_TITLE2 AS ' ' NOPRINT CENTERIT AS '' IN 110
FOOTING
"</5"
-*"<42 Powered by Employee Owners."
"</2 "
"<28 No need too large...no detail too small"

BY INST_KEY NOPRINT
WHERE RECORDLIMIT EQ 1
&&OUTLINE1
&&OUTLINE2
ON TABLE SET PAGE OFF   
ON TABLE SET STYLE *
UNITS=IN, PAGESIZE='Letter', LEFTMARGIN=0.050000, RIGHTMARGIN=0.0500000,
    TOPMARGIN=0.250000, BOTTOMMARGIN=0.050000, SQUEEZE=ON,
    ORIENTATION=LANDSCAPE, $
DEFMACRO=MC01,FONT=TIMES NEW ROMAN,STYLE=BOLD, $

TYPE=HEADING, LINE=13, SIZE=48, MACRO=MC01, $
TYPE=HEADING, LINE=14, SIZE=36, MACRO=MC01, $
TYPE=HEADING, LINE=16, SIZE=18, MACRO=MC01, $
TYPE=HEADING, LINE=21, SIZE=32, MACRO=MC01, $

TYPE=FOOTING, SIZE=18, LINE=6, OBJECT=TEXT, STYLE=NORMAL,COLOR=BLACK, 
FONT=TIMES NEW ROMAN,$    
TYPE=FOOTING, SIZE=24,LINE=10, OBJECT=TEXT, STYLE=BOLD+ITALIC,
COLOR=BLACK, FONT=TIMES NEW ROMAN,$
   
TYPE=REPORT, IMAGE=&&LOGO,POSITION=(&&SXPOS 1.60), SIZE=(&&NEWSIZE), $        
TYPE=REPORT, IMAGE=TNTLogo.GIF,POSITION=(3.5 5.3), SIZE=(4.0 1.6), $    
TYPE=REPORT, IMAGE=none.GIF,POSITION=(0.1 .5), SIZE=(10.75 0.95), $
ENDSTYLE

END
-RUN

-NOCOVERPG