-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**

-INCLUDE SETECHO
-* File TKTSMRY.FEX
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-*07/03/02   STEVE              CHANGED TO eTTek Review and Copyright
-*1/19/05    DEB                ADDED CODE FOR GLOBAL CURRENCY
-*12/09/15 - JEM - S-13286 Updated to allow reports to sort/subtotal by id-levels

-**********************************************************************

SET ASNAMES = ON
-RUN

-IF &&CURRENCY EQ 'USD' THEN GOTO NEXT;
-SET &&SUBHEAD = <NAMEC;
-SET &TAMT_CUR = 'FARE,PAID, (' | &&CURRENCY || ')';
-SET &RSN_CUR = 'MISC,FARE, (' | &&CURRENCY || ')';
-SET &MSSV_CUR = 'MISC,SAVINGS, (' | &&CURRENCY || ')';
-SET &&S_SUBJ3='PAID_FAREX/D16.2CS AS ' | '''&TAMT_CUR.EVAL''';
-SET &&S_SUBJ4='RSN_SEGX/D16.2CS AS ' | '''&RSN_CUR.EVAL''';
-SET &&S_SUBJ5='SAV_MISCX/D16.2CS AS ' | '''&MSSV_CUR.EVAL'''; 






-NEXT;


TABLEF FILE MSSAV
PRINT   
    AIR_KEY
    INTL_DOM
    AIRLINE    
    ARR_TIME
    BR_CL_IDX
    EMB_APT_CD
    RTE_APT_CD
    CLASS	
    COMM_SEG	
    DOC_TYPE	
    DPT_TIME
    EMB_APT_NM	
    EMB_CTY_NM	
    EMB_CTY_CD	
    FARE_PAID	
    INT_CODE	
    LEVEL_DESC
    MISC_SAV
    MISC_SAV2
    MRK_TKT_CNT
    NAMEC
    NET_FARE
    NET_TKT_CNT	
    NEW_SEG_DISC
    PAID_FAREX/D16.2CS	
    REF_EXCH_CNT	
    REFUSE_KEY        	
    REFUSE_CODE        	
    RESV_BRANCH
    RSN_SEGX/D16.2CS  	
    RTE_APT_NM	
    RTE_CTY_NM	
    RTE_CTY_CD
    SAV_MISCX/D16.2CS 
    SEG_DESIG
    SEG_NBR	
    SEG_REASON
    SEG_RSN
    TAX
    TKT_NUM
    TKT_PURCH
    TKT_SORT
    TKT_TYPE
    TRN_DATE
    VAL_AIR	
    VASAVINGS	
    XDPT_DT	
    XTRAN_DT	
    XPSNGR_NM
ON TABLE HOLD AS HLD1
END
-RUN


-* JOIN VAL_AIR IN AIR_MAIN TO AIRLINE_NAME IN AIRLINE

JOIN HLD1.VAL_AIR IN HLD1 TO ALL AIRLINE.AIRLINE IN AIRLINE

TABLE FILE HLD1
SUM FARE_PAID/D12.2CS AS 'TFARE'
    PAID_FAREX/D16.2CS AS 'GTFARE'
    MISC_SAV AS 'TMISC'
    MISC_SAV2 AS 'TMISC2'
    SAV_MISCX/D16.2CS AS 'GTMISC'
    SEG_RSN AS 'TSRSN'
    RSN_SEGX/D16.2CS AS 'GTSRSN'
    NET_TKT_CNT AS 'NTTKT'
    MRK_TKT_CNT AS 'MTKT'
BY LEVEL_DESC  
ON TABLE HOLD AS HOLDONE
END
-RUN


TABLE FILE HLD1
PRINT BR_CL_IDX 
  FARE_PAID
  MISC_SAV
  MISC_SAV2
  MRK_TKT_CNT
  NET_TKT_CNT
  PAID_FAREX/D16.2CS
  RSN_SEGX/D16.2CS
  SAV_MISCX/D16.2CS
  SEG_RSN
  RESV_BRANCH
  NAMEC
BY LEVEL_DESC
BY AIRLINE_NAME
ON TABLE HOLD AS XTWO
END
-RUN

MATCH FILE XTWO
PRINT *
BY LEVEL_DESC
BY AIRLINE_NAME 
ON TABLE HOLD
RUN
FILE HOLDONE
SUM GTFARE
    GTMISC
    GTSRSN
    MTKT
    NTTKT
    TFARE
    TMISC
    TMISC2
    TSRSN  
 BY LEVEL_DESC
 AFTER MATCH HOLD AS XTHREE OLD-OR-NEW 
 END
-RUN

DEFINE FILE XTHREE
  PCTFARE/D8.1S =   IF ((TFARE LT 1) AND (TFARE GT (-1))) THEN 0
                    ELSE ((FARE_PAID/TFARE)*100);
  PCTNETTKT/D8.1S = IF ((NTTKT LT 1) AND (NTTKT GT (-1))) THEN 0
                    ELSE ((NET_TKT_CNT/NTTKT)*100);

-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS 
-*                                  USED IN FOOTERSM
-**********************************************************************
  NOWTOD/A8 WITH LEVEL_DESC = HHMMSS (NOWTOD);
END


-SET &&PAHDR = IF '&&OUTFMT.EVAL' EQ 'PDF' OR 'PPT' OR 'COM' THEN 'HEADER' ELSE 'HDREXLS'; 
-SET &&PASTYS = IF '&&OUTFMT.EVAL' NE 'PDF' OR 'PPT' OR 'COM' THEN 'SMEXL' ELSE &&SMSTY;
-RUN

TABLE FILE XTHREE
-INCLUDE &&PAHDR
"&&SUBHEAD"
-IF &&PAHDR EQ 'HDREXLS' GOTO NOSPC;
"</2"
-NOSPC;

    &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
    
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
&&BREAKOPTION
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10

-INCLUDE FOOTERSM
-* ON TABLE COLUMN-TOTAL
-*ON TABLE SET STYLE &&SMSTY
-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/11/00      IBISTL-SB            CHANGED THE SET STYLE STATEMENT 
-*                                  WITH AN INCLUDE
-**********************************************************************
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT

ON TABLE SET STYLE *

-INCLUDE &&PASTYS

-**********************************************************************
-*DATE           NAME               DESCRIPTION OF CHANGE
-*----           ----               ---------------------
-*8/18/00      IBISTL-KR            INCLUDE STYLESHEET PARAMS FOR 
-*                                  DRILLABLE REPORTS.
-**********************************************************************
-IF &&DRILLABLE EQ 'N' GOTO SKIP_DRILL;
-INCLUDE &&DSTY

-SKIP_DRILL

ENDSTYLE
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN

-IF &&DRILLABLE EQ 'Y' GOTO SKIP_R3;

EX REPORT_3

-SKIP_R3
