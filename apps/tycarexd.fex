-* 1/30/13 - DEB changed due to expansion of car database to
-* include trip purpose
-* CHANGED CODE DUE TO DRILL DOWN NOT WORKING - DEB 11/18/13

SET ASNAMES=ON

-INCLUDE SETECHO

FILEDEF RPTPARMS CLEAR
-RUN

EX CARUSE
-RUN

-SET &&RPTGRP = 'CAR';
EX UDIDGEN
-RUN

DEFINE FILE BRPTINST
  RT/A3 = 'CAR';
END
TABLE FILE BRPTINST
PRINT RT AS 'RPT_GROUP'
BY INST_KEY
ON TABLE SAVE AS AIRACH00
END
-RUN
MODIFY FILE BRPTINST
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 RPT_GROUP/3
MATCH INST_KEY
  ON MATCH UPDATE RPT_GROUP
DATA ON AIRACH00
END
-RUN

MODIFY FILE BRPTFLDS
LOG DUPL MSG OFF
FIXFORM INST_KEY/7 CAR.ID/1 CAR.INCL1/1 
REPOSITION INST_KEY
MATCH INST_KEY
  ON MATCH CONTINUE
  ON NOMATCH INCLUDE
MATCH * KEYS SEG CAR
  ON NOMATCH INCLUDE
  ON MATCH UPDATE CAR.INCL1
DATA ON AIRACH00
END
-RUN

EX CODEGEN
-RUN


TABLE FILE CARUDID
SUM AUD_DATA AS 'CBKD_BY' 
BY CAR_KEY 
WHERE UDID EQ 'BB-' 
ON TABLE HOLD AS CUDID1
END
-RUN

TABLE FILE CARUDID
SUM AUD_DATA AS 'CD_BILL'
BY CAR_KEY
WHERE UDID EQ 'DBC'
ON TABLE HOLD AS CUDID2
END
-RUN

MATCH FILE CUDID1
PRINT CBKD_BY
BY CAR_KEY
ON TABLE HOLD
RUN
FILE CUDID2
PRINT CD_BILL
BY CAR_KEY
AFTER MATCH HOLD AS CUDID3 OLD-OR-NEW
END
-RUN

TABLE FILE CUDID3
PRINT CBKD_BY CD_BILL
BY CAR_KEY
ON TABLE HOLD AS CUDID4 FORMAT FOCUS INDEX CAR_KEY
END
-RUN


-SET &CARDATES = &&WEB_PATH || 'CARDATES.fex';

DEFINE FILE &&EXTRACT
LEVEL_DESC/A60      = &&LDESC;
CBOOK/P8CS = IF DAILY_RATE GE 0 THEN 1 ELSE (-1);
CAR_AMT/D12.2CS = RENTAL_AMT;

NBR_DAYS/D12 = NUM_DAYS;
CAR_DAYS/P12CS = IF (NUM_DAYS LT 0) AND (NUM_CARS LT 0) THEN
                   ((NUM_DAYS * NUM_CARS) * (-1)) ELSE NUM_DAYS * NUM_CARS;
CAR_DT/MDYY = PICKUP_DATE;                   
TRN_DATE/YYMD = INVOICE_DATE;
CC_ITIN/A67 = PICK_CTY.CITY_NAME;
XPSNGR_NM/A15 = EDIT(PASSNGR_NAME,'999999999999999$$$$$$$$$$$$$$$$$$');
TRIP_KEY/A2   = EDIT(TRP_KEY, '$$$$$$$$99');
TRIPRSN/A10 = IF TRIP_KEY NE ' ' THEN TRIP_KEY ELSE 'ZZZZZZZZZZ';
TRIP_RSN/A10 = IF TRIPRSN EQ 'ZZZZZZZZZZ' THEN 'NONE GIVEN' ELSE TRIPRSN;
TKTNC/A10 = IF TKT_NUM NE '0000000000' THEN TKT_NUM ELSE 'CAR ONLY';
XCINV_DT/MDYY  = INVOICE_DATE;
FLAG/A1 = 'C';
REFUSE_CD/A2=EDIT(REFUSE_KEY,'$$$$$$$$99');
CRSNCDE/A48=REFUSE_CD|| '-' ||REFUSAL_DESC;
CCOM/A3 = IF REFUSE_CD EQ 'P' THEN 'NO' ELSE 'YES';
END                
TABLE FILE &&EXTRACT
PRINT AROLL_DSC3 AROLL_DSC4 CBOOK CAR_DAYS CAR_AMT LEVEL_DESC CC_ITIN XPSNGR_NM TKT_NUM CAR_DT VENDOR_NAME TRIP_KEY TRIP_RSN XCINV_DT TRP_REF.TRP_DESC 
      INVOICE_NUM AS 'CINV_NUM' FLAG CCOM CRSNCDE TKTNC
BY CAR_KEY
-INCLUDE &CARDATES
-INCLUDE RPTPARMS
ON TABLE HOLD AS CARHLD
END
-RUN



JOIN CAR_KEY IN CARHLD TO CAR_KEY IN CUDID4
-RUN




