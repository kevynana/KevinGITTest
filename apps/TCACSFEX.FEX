-*10/26/15 - DV - S-12172 CREATED FOR TCCSFTSF TOTAL-R Client Service Fund
-*                                     Travel and Transport Service Fund
-INCLUDE SETECHO
SET ASNAMES = ON

-*========================================================================
-*           PERFORM DATA EXTRACT FOR CLIENT DETERMINED
-*========================================================================
-SET &AIRDATES = &&WEB_PATH || 'AIRDATES.fex';

DEFINE FILE &&EXTRACT
FARE_PAID/D12.2CS   = SEG_AMT + SEG_TAX;
NET_TKT_CNT/D8CS    = IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (FARE_PAID GE 0) AND
                           (SEG_COUNT EQ 1) AND 
                           (CONJ_REL EQ 1) THEN 1 ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND 
                           (EX_FLAG EQ 'F') THEN (-1) ELSE 
                      IF (TICKET_CODE EQ '1') AND
                           (SEG_NBR EQ '01') AND
                           (RF_FLAG EQ 'F') THEN (-1) ELSE 0;


END

TABLE FILE &&EXTRACT
PRINT 
  FARE_PAID
  NET_TKT_CNT 
  AGENT_NUM
  TKT_NUM
BY AIR_KEY  
-INCLUDE &AIRDATES
WHERE VOID_DATE EQ 0
&&WHERE1
&&WHERE2
&&WHERE3
&&WHERE4
&&WHERE5
&&WHERE6
&&WHERE7
&&WHERE8
&&WHERE9
&&WHERE10
-INCLUDE RPTPARMS
ON TABLE HOLD AS TCCSFTSF 
END
-RUN

-IF &LINES EQ 0 THEN GOTO NEXTONE;

DEFINE FILE TCCSFTSF
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCCSFTSF
PRINT FARE_PAID/D14.2CS
    NET_TKT_CNT
    ROLLCD
    AGENT_NUM
    TKT_NUM
BY AIR_KEY
ON TABLE HOLD AS TCCSF FORMAT FOCUS INDEX AIR_KEY
END

-INCLUDE TCUDAUSE
-RUN

TABLE FILE &&EXTRACT2
SUM NUD_DATA/P12.2CS AS 'CSD'  AUD_DATA/A64 AS 'CSR' AUD_DATA/A64 AS 'CSF'
BY AIR_KEY
WHERE UDID EQ 'CSD' OR 'CSR' OR 'CSF'
ON TABLE HOLD AS TCUDCSF1
END
-RUN


TABLE FILE &&EXTRACT2
SUM NUD_DATA/P12.2CS AS 'TSD'  AUD_DATA/A64 AS 'TSR' AUD_DATA/A64 AS 'TSF' 
BY AIR_KEY
WHERE UDID EQ 'TSD' OR 'TSR' OR 'TSF'
ON TABLE HOLD AS TCUDTSF1
END
-RUN
 
 
MATCH FILE TCUDCSF1
PRINT CSD CSR CSF
BY AIR_KEY
ON TABLE HOLD
RUN
FILE TCUDTSF1
PRINT TSD TSR TSF
BY AIR_KEY 
AFTER MATCH HOLD AS TCUDXONE OLD-OR-NEW
END
-RUN

 

 
JOIN AIR_KEY IN TCCSF TO AIR_KEY IN TCUDXONE AS J1
-RUN

DEFINE FILE TCCSF
AIR_CSCSAV/P12.2 = CSD;
AIR_CSF/P12.2CSM = AIR_CSCSAV;
CSF_CNT/P12S = IF AIR_CSF NE 0 THEN NET_TKT_CNT ELSE 0;
CSF_RSN/A48 = EDIT (CSR, '999999999999999999999999999999999999999999999999');
CSFA/A30 = EDIT(CSF, '999999999999999999999999999999');
AIR_TSCSAV/P12.2 = TSD;
AIR_TSF/P12.2CSM = AIR_TSCSAV;
TSF_CNT/P12S = IF AIR_TSF NE 0 THEN NET_TKT_CNT ELSE 0;
TSF_RSN/A48 = EDIT (TSR, '999999999999999999999999999999999999999999999999');
TSFA/A30 = EDIT(TSF, '999999999999999999999999999999');
ATSF_AMT/P12.2CSM = IF TKT_NUM NE LAST TKT_NUM  THEN AIR_TSF ELSE 0;
ACSF_AMT/P12.2CSM = IF TKT_NUM NE LAST TKT_NUM  THEN AIR_CSF ELSE 0;

TSF_AMT/P12.2CSM = IF NET_TKT_CNT LT 0 THEN 0 ELSE ATSF_AMT;
-*                    IF TKT_TYPE NE ' ' THEN 0 ELSE  
CSF_AMT/P12.2CSM = IF NET_TKT_CNT LT 0 THEN 0 ELSE ACSF_AMT;
-*                    IF TKT_TYPE NE ' ' THEN 0 ELSE ACSF_AMT; 
END
TABLE FILE TCCSF
PRINT NET_TKT_CNT
      FARE_PAID
      AIR_CSF
      CSF_AMT
      CSF_CNT
      CSF_RSN
      CSFA
      AIR_TSF
      TSF_AMT
      TSF_CNT
      TSF_RSN
      TSFA
      TKT_NUM
      AGENT_NUM
BY TKT_NUM
WHERE TSR NE ' ' OR CSR NE ' '
ON TABLE HOLD AS TCCSF2
END
-RUN
 


DEFINE FILE TCCSF2
ROLLCD/A8 = '&&ROLL';
END
TABLE FILE TCCSF2
SUM CSF_AMT
    CSF_CNT
    CSF_RSN
    TSF_AMT
    TSF_CNT
    TSF_RSN
BY ROLLCD
BY AGENT_NUM
ON TABLE HOLD AS TCCSF3
END
-RUN



DEFINE FILE TCCSF3
 NMB/I5 = NMB + 1;
END
TABLE FILE TCCSF3
SUM CSF_AMT
    CSF_CNT
    CSF_RSN
    TSF_AMT
    TSF_CNT
    TSF_RSN
BY ROLLCD
BY AGENT_NUM
BY NMB
ON TABLE HOLD AS TCCSF4
END
-RUN


MODIFY FILE TCACSF
FIXFORM FROM TCCSF4
MATCH ROLLCD  NMB AGENT_NUM
ON NOMATCH INCLUDE
ON MATCH REJECT
DATA ON TCCSF4
END
-RUN
 

-NEXTONE
