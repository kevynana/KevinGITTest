-**************************************************************
-* Date       Author    Modification
-**************************************************************
-* 01/24/2012 GSE       Removed parens from around CLIMIT to
-*                      prevent errors in 7.7.03
-* 6/11/14    JEK       ADDED GZONE
-*08/12/16 - DLV - S-23151 - Changed extract routine from TCL102EX to TCL102VEX to add
-*                           VENDOR and GEO back into TCLVND
-*  08/23/2016  REJ  S-17914 Changes for ER5.
-*  12/29/2016  REJ  S-28553 Move ER5 My Reports to production.

-**************************************************************
-INCLUDE SETECHO
 
CREATE FILE TCLVND
-RUN
 
-INCLUDE TCGETROLL
-RUN
 
-SET &CNT = 1;
-REPEAT Loop2 &TOTROLLS TIMES
-SET &&ROLL = &ROLL.&CNT;
EX TCLIMDB
-RUN
- IF &FOCERRNUM EQ 36 THEN GOTO IncLoop2;
 
EX TCL102VEX
-RUN
-IncLoop2
-SET &CNT = &CNT + 1;
-Loop2
 
-INCLUDE SetParmtime
-RUN
 
 
-*========================================================================
-*  GET THE FINAL REPORT
-*========================================================================
-SET &&RANK_METH = IF &&RANK_METH EQ 'CAR_AMT' THEN 'CAR_AMT'
- ELSE IF &&RANK_METH EQ 'NMBR_DAYS' THEN 'NMBR_DAYS'
- ELSE 'NMBR_DAYS';
 
TABLE FILE TCLVND
SUM NBR_DAYS AS 'NMBR_DAYS'
BY VENDOR_CODE
BY VENDOR_NAME
ON TABLE HOLD AS TLHOLD1
END
-RUN
 
TABLE FILE TCLVND
PRINT BOOKINGS
      NBR_DAYS AS 'NUM_DAYS'
      RENTAL_AMT
BY VENDOR_CODE
BY VENDOR_NAME
BY GZONE
WHERE NBR_DAYS NE 0
ON TABLE HOLD AS LHOLD1
END
-RUN
 
MATCH FILE LHOLD1
PRINT BOOKINGS NUM_DAYS RENTAL_AMT
BY VENDOR_CODE
BY VENDOR_NAME
BY GZONE
ON TABLE HOLD
RUN
FILE TLHOLD1
SUM NMBR_DAYS
BY VENDOR_CODE
BY VENDOR_NAME
AFTER MATCH HOLD AS LHOLD2 OLD-OR-NEW
END
-RUN
 
 
DEFINE FILE LHOLD2
VNAME/A12 = IF VENDOR_CODE EQ '  ' THEN 'UNKNOWN' ELSE VENDOR_NAME;
END
TABLE FILE LHOLD2
SUM BOOKINGS/P8
    NUM_DAYS
    NMBR_DAYS
    RENTAL_AMT/D12.2 AS 'CAR_AMT'
BY VENDOR_CODE
BY VNAME AS 'VENDOR_NAME'
BY GZONE
ON TABLE HOLD AS LHOLD3
END
-RUN
 
 
 
TABLE FILE LHOLD3
SUM BOOKINGS
    CAR_AMT
    NUM_DAYS
    NMBR_DAYS
RANKED BY HIGHEST &&RANK_METH NOPRINT
BY VENDOR_CODE
BY VENDOR_NAME
BY GZONE
ON TABLE HOLD AS LHOLD6
END
-RUN
 
 
-SET &&RANK_LIMIT = 50;
 
DEFINE FILE LHOLD6
  CTY_LIMIT/I2 = &&RANK_LIMIT;
  CLIMIT/A2    = EDIT(CTY_LIMIT) ;
  CRANK/D5     = RANK;
  BRANK/A6     = FTOA(CRANK, '(D5)', 'A6');
  LIST/D6 = IF VENDOR_NAME NE LAST VENDOR_NAME THEN
                        (LAST LIST + 1) ELSE LAST LIST;
  FRANKX/A7 = FTOA(LIST, '(D6)', 'A7');
  FRANK/A6=EDIT(FRANKX,'$999999');
  VNDR_VALUE/A6 = IF CTY_LIMIT EQ 0 THEN BRANK ELSE
                  IF RANK GT CTY_LIMIT THEN 'OTHER' ELSE BRANK;
  VNDR/A30 = IF VNDR_VALUE NE 'OTHER' THEN VENDOR_NAME ELSE  'OTHER';
  VNDR_CODE/A6 = IF VNDR_VALUE NE 'OTHER' THEN VENDOR_CODE ELSE 'OTHER';
  GZ/A5 = IF VNDR_VALUE NE 'OTHER' THEN GZONE ELSE 'OTHER';
  FLAG/A1 = 'A';
 
END
-RUN
 
 
TABLE FILE LHOLD6
PRINT BOOKINGS
    CAR_AMT
    NUM_DAYS
BY FLAG
BY VNDR_VALUE
BY VNDR_CODE
BY VNDR
BY GZ
ON TABLE HOLD AS LHOLD7
END
-RUN
 
 
TABLE FILE LHOLD6
SUM NMBR_DAYS AS 'TDAYS'
BY FLAG
ON TABLE HOLD AS LHOLD8
END
-RUN
 
 
MATCH FILE LHOLD7
PRINT BOOKINGS
      NUM_DAYS AS 'NMBR_DAYS'
      CAR_AMT
      VNDR_VALUE
      VNDR_CODE
      VNDR
      GZ
BY FLAG
ON TABLE HOLD
RUN
FILE LHOLD8
SUM TDAYS
BY FLAG
AFTER MATCH HOLD AS LHOLD9 OLD-OR-NEW
END
-RUN
 
-IF &&FROMAVRP NE 'Y' GOTO :AVSF0;
-INCLUDE AVRP_SUBFOOT
-:AVSF0
 
-SET &&RPTSUF = 'SMY';
-INCLUDE FDEFRPTS
-RUN
-**********************************************************************
-*DATE           NAME               DESCRIPTION
-*7/31/00      IBISTL-KR            ADDED DEFINE FOR NOWTOD WHICH IS
-*                                  USED IN FOOTERSM
-**********************************************************************
 
DEFINE FILE LHOLD9
NOWTOD/A8 WITH VNDR_VALUE = HHMMSS (NOWTOD);
END
TABLE FILE LHOLD9
-INCLUDE HEADER
"&&SUBHEAD"
"</2"
 &&S_SUBJ1
    &&S_SUBJ2
    &&S_SUBJ3
    &&S_SUBJ4
    &&S_SUBJ5
    &&S_SUBJ6
    &&S_SUBJ7
    &&S_SUBJ8
    &&S_SUBJ9
    &&S_SUBJ10
 
-*BY
   &&S_TARG1
   &&S_TARG2
   &&S_TARG3
   &&S_TARG4
   &&S_TARG5
   &&S_TARG6
   &&S_TARG7
   &&S_TARG8
   &&S_TARG9
   &&S_TARG10
-*ON
   &&SUMM_ON1
   &&SUMM_ON2
   &&SUMM_ON3
   &&SUMM_ON4
   &&SUMM_ON5
   &&SUMM_ON6
   &&SUMM_ON7
   &&SUMM_ON8
   &&SUMM_ON9
   &&SUMM_ON10
 
 
FOOTING BOTTOM
"&&FOOTER1"
"&&FOOTER2"
-*"Blank vendor codes with $0 rate excluded from this report"
" "
 
ON TABLE PAGE-BREAK AND SUBFOOT
-INCLUDE SBFOOT
ON TABLE SET STYLE *
-INCLUDE &&SMSTY
ENDSTYLE
 
&&OUTLINE1
&&SUMMDEST
&&OUTLINE2
END
-RUN
 
-IF &LINES GT 0 THEN GOTO XXIT;
EX TCEMPTY
-RUN
-XXIT
