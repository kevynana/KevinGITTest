-*****************THIS HEADER REPRESENTS THE 80 CHARACTER MAXIUM FOR PROGRAMS**
-* File RUN_SET2.FEX
-* DATE          NAME                DESCRIPTION
-* 8/31/00     IBISTL-KR             MODIFIED CODE TO REFLECT FIXED
-*                                   KEY LENGTH OF 50. 
-* 9/13/00     IBISTL-KR             ADDED NEW PARAMETERS TO TEST
-*                                   FOR DRILLABLE REPORTS.   
-* 3/19/01     TANDT-SS              ADDED CODE FOR DUAL DATE RANGE.     
-* 1/31/03     LS                    ADDED CODE TO CLEAN HRPTFLDS
-* 10/22/03    LS                    Made changes as SU databases changes
-* 02/24/04    WT                    ADDED STATUS CODE FOR AUTO PROCESS
-*********************************************************************** 
-SET &&RCRUN='N';
-INCLUDE SETECHO
-*SET TEMPERASE = OFF
-* Subroutine returns the TEMPDIR path
-SET &&WEB_PATH=
- TEMPPATH(50,'A50');
SET EDAPATH=+&&WEB_PATH;
-*-SET &&EXECUTED = 0;

EX SUUSES
-RUN

-RELOAD_BQUE
-* Subroutine returns the TEMPDIR path
-SET &&WEB_PATH=
- TEMPPATH(50,'A50');
-SET &&AUSEPASS = 
- '&&WEB_PATH.EVAL' || 'AUSEINCL.FTM';
-SET &&CUSEPASS = 
- '&&WEB_PATH.EVAL' || 'CUSEINCL.FTM';
-SET &&HUSEPASS = 
- '&&WEB_PATH.EVAL' || 'HUSEINCL.FTM';

-SET &&WRRUN = 'N';
-SET &&HLDIKEY=' ';
-SET &&HLDITIME=' ';
-SET &&HEMAIL='N';
-SET &&HFTP='N';
-SET &&SET_DONE = 'N';

-*========================================================================
-*   SET STATUS FLAG FILE TO 'WAIT' STATE FOR AUTOMATIC SYSTEM CHECKS
-*========================================================================
-* EX COPYSTATUS
-* -RUN

TABLE FILE RPT_QUE
PRINT SET_KEY USER_NAME
BY INTIME NOPRINT
WHERE RPT_SEQ NE 999
WHERE EX_FLAG NE 'M'
WHERE ROLLUP_CODE EQ 'ABCD-R'
IF RECORDLIMIT EQ 1
ON TABLE SAVE AS SAVE1
END
-RUN
-IF &LINES EQ 0 THEN GOTO XXIT;
-READ SAVE1 &ZIPSETKEY.7. &ZIPUSER.A20.
-TYPE ZIPSETKEY &ZIPSETKEY &ZIPUSER
-CLOSE SAVE1

-* FEX WILL LOAD THE NEXT SCHEDULED SET INTO THE BRPT_QUE FILE.
-INCLUDE LOADBQUE
-RUN
-IF &QUEDONE EQ 'Y' GOTO XXIT;

-INCLUDE LOADBINS
-RUN

-RECHECK_QUE

-IF &&HLDIKEY EQ ' ' GOTO CONT_RECHECK;

TABLE FILE RPT_QUE
PRINT INST_KEY INTIME
WHERE INST_KEY EQ  &&HLDIKEY
WHERE INTIME EQ '&&HLDITIME.EVAL'
WHERE EX_FLAG EQ 'M'
ON TABLE HOLD AS HLDQUEDL
END
-RUN

MODIFY FILE RPT_QUE
FIXFORM FROM HLDQUEDL
MATCH INST_KEY INTIME
  ON NOMATCH REJECT
  ON MATCH DELETE
DATA ON HLDQUEDL
END
-RUN

MODIFY FILE HRPTINST
FIXFORM FROM HLDQUEDL
MATCH INST_KEY INTIME
  ON NOMATCH REJECT
  ON MATCH DELETE
DATA ON HLDQUEDL
END
-RUN

MODIFY FILE HRPTFLDS
FIXFORM FROM HLDQUEDL
MATCH INST_KEY INTIME
  ON NOMATCH REJECT
  ON MATCH DELETE
DATA ON HLDQUEDL
END
-RUN

MATCH FILE HRPT_SET
BY SET_KEY
BY INTIME
RUN
FILE RPT_QUE
BY SET_KEY
BY INTIME
AFTER MATCH HOLD OLD-NOT-NEW
END
-RUN

TABLE FILE HOLD
PRINT SET_KEY INTIME
ON TABLE HOLD AS HLDSKEY
END
-RUN
-IF &LINES EQ 0 THEN GOTO CONT_RECHECK; 

MODIFY FILE HRPT_SET
FIXFORM FROM HLDSKEY
MATCH SET_KEY INTIME
 ON NOMATCH REJECT
 ON MATCH DELETE
DATA ON HLDSKEY
END
-RUN 

-CONT_RECHECK

-SET &&DRILLED_RUN = 'N';
-SET &&DRILLSTREAM = 'N';       

FILEDEF INSTPASS CLEAR
FILEDEF INSTPASS DISK INSTPASS.FTM
-RUN

EX CYC_CHK
-RUN

-READ INSTPASS &&STREAM.8 &&SETF.8 &&RPTTYP.3 &&IKEY.7 &&RN.50 &&RS.40,
-, &&OUTFMT.10 &&ROLLUP.8 &&UNAME.20 &&SKEY.7,
-, &&OUTTO.2 &&SETZIP.1 &&ITIME.18

-IF &&STREAM EQ 'xxxxxxxx' OR &&ROLLUP EQ ' ' THEN GOTO RELOAD_BQUE;

-TYPE ROLLUP ==> &&ROLLUP
-SET &&IDKEY = &&IKEY;

-SET &&HLDIKEY= &&IKEY;
-SET &&HLDITIME='&&ITIME.EVAL';
-SET &&HEMAIL=IF EDIT(&&OUTTO,'9') EQ 'E ' THEN 'Y' 
- ELSE '&&HEMAIL.EVAL';
-SET &&HFTP=IF EDIT(&&OUTTO,'9') EQ 'F ' THEN 'Y' 
- ELSE '&&HFTP.EVAL';

EX RPTRESET
-RUN


-SET &&RN = 
- LJUST(50, &&RN, &&RN);
-SET &&RS = 
- LJUST(40, &&RS, &&RS);

-SET &&INSTID = &&IKEY;
-SET &&VARFILE = 'D:\TNT\TTRACKER\DATA\SETFILES\' || &&SETF || '.FEX';
-SET &&VARFWEB = &&WEB_PATH || &&SETF || '.FEX';
-RUN

-SET &&READSET = 
- &&WEB_PATH || 'READSET.FTM';
FILEDEF READSET DISK &&READSET
FILEDEF OLDSET DISK &&VARFWEB 
-* WORKAROUND TO PROBLEM WITH -READ AND (MAINTAIN AND FIX FILES)
COPY &&VARFILE &&READSET 
-RUN

TABLE FILE READSET
PRINT *
ON TABLE HOLD AS READSET FORMAT ALPHA
END
-RUN 
-* END OF WORKAROUND

-VARS
FILEDEF READSET CLEAR
FILEDEF OLDSET CLEAR
FILEDEF OUTFILE DISK &&VARFWEB
FILEDEF INSTPASS DISK INSTPASS.FTM
FILEDEF READSET DISK &&READSET
-RUN

EX VARSETS
-RUN

FILEDEF INSTPASS DISK INSTPASS.FTM
-RUN
-READ INSTPASS &&XSTREAM.8 
-CLOSE INSTPASS
FILEDEF INSTPASS CLEAR
-RUN

-IF &&XSTREAM EQ 'AGAIN' THEN GOTO VARS; 

FILEDEF READSET CLEAR
FILEDEF OUTFILE CLEAR
-RUN

-EXSTRM

-*Generate the screening conditions
EX CODEGEN
-RUN

-SET &&NOW_CYC = 'CYC';
-TYPE Executing &&STREAM
EX &&STREAM
-RUN

-TYPE Afer Executing &&STREAM
TABLE FILE RPT_QUE
PRINT INST_KEY
BY SET_KEY NOPRINT
BY USER_NAME NOPRINT
WHERE SET_KEY EQ &ZIPSETKEY 
WHERE USER_NAME EQ '&ZIPUSER.EVAL' 
ON TABLE HOLD
END
-RUN

-SET &&SET_DONE = IF &LINES EQ 1 THEN 'Y' ELSE 'N';

-SETNOTDONE
EX SETDONE
-RUN
-GOTO RECHECK_QUE

-XXIT
-*========================================================================
-*   SET STATUS FLAG FILE TO 'OK' STATE FOR AUTOMATIC SYSTEM CHECKS
-*========================================================================
-* EX DELETESTATUS
-* -RUN
