-**********************************************************************
-* This program will retrieve all selected ROLLUP_CODEs from BRPTFLDS.
-* If no company was selected, it assumes all companies selected.
-* Date: Feb 09, 2004
-*  10/13/2016  REJ  S-25863 Updated to exclude DEMO-R.
-**********************************************************************
-* -INCLUDE SETECHO
 
TABLE FILE BRPTFLDS
PRINT ROLLUP
BY INST_KEY NOPRINT
WHERE INST_KEY EQ &&IKEY
WHERE ROLLUP NE ' '
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
-SET &WHATROLLS = '';
-SET &OFACCT2 = 0;
 
-*check to see if it is included or excluded, OF ACCOUNT is checked
TABLE FILE BRPTFLDS
PRINT ROLL1.INCL1 OF_ACCT
BY INST_KEY NOPRINT
WHERE INST_KEY EQ &&IKEY
ON TABLE SAVE AS ROLL111
END
-RUN
-IF &LINES EQ 0 GOTO NOROLL1;
-READ ROLL111 &INCL1.1. &OFACCT2.1.
-CLOSE ROLL111
-NOROLL1
 
-SET &WHATROLLS = IF &OFACCT2 EQ 1 THEN 'WHERE OF_ACCOUNT EQ ' | '''Y''' ELSE ' ';
 
-IF &TOTROLLS EQ 0 THEN GOTO SelectAll;
-IF &INCL1 EQ 1 THEN GOTO GetCount;
-*Exclude condition
DEFINE FILE BRPTFLDS
ROLL/A10 = '''' | ROLLUP | '''';
END
 
TABLE FILE BRPTFLDS
PRINT ROLL
BY ROLLUP NOPRINT
ON TABLE SAVE AS ROLLEXCL
END
-RUN
 
TABLE FILE ROLLUP
PRINT ROLLUP_CODE
BY ROLLUP_CODE NOPRINT
WHERE UPD_ENABLE NE 'Z'
WHERE ROLLUP_CODE NE 'TOTAL-R' OR 'ABCD-R' OR 'ABCD2-R' OR 'ABCD3-R' OR 'DEMO-R'
WHERE COMP NE ' '
&WHATROLLS
WHERE NOT ROLLUP_CODE IN FILE ROLLEXCL
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
-GOTO GetCount;
 
-SelectAll
TABLE FILE ROLLUP
PRINT ROLLUP_CODE
BY ROLLUP_CODE NOPRINT
IF UPD_ENABLE NE 'Z'
-*IF UPD_ENABLE EQ 'X'
WHERE ROLLUP_CODE NE 'TOTAL-R' OR 'ABCD-R' OR 'ABCD2-R' OR 'ABCD3-R' OR 'DEMO-R'
WHERE COMP NE ' '
&WHATROLLS
ON TABLE SAVE AS ROLLSEL
END
-RUN
-SET &TOTROLLS = &LINES;
 
 
-GetCount
 
-SET &CNT = 0;
-REPEAT Loop1 &TOTROLLS TIMES
-READ ROLLSEL NOCLOSE &VAL.8.
-IF &VAL EQ ' ' THEN GOTO Loop1;
-SET &CNT = &CNT + 1;
-SET &ROLL.&CNT = &VAL;
-Loop1
